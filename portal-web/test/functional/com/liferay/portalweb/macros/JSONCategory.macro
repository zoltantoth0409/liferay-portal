definition {

	@summary = "Directly calls the JSON WS API to add a category"
	macro _addCategory {
		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		if (!(isSet(parentCategoryId))) {
			var parentCategoryId = "0";
		}

		if (isSet(titleMap)) {
			var titleMap = JSONUtil2._convertLocalizedListToMap(listMap = "${titleMap}");
		}
		else {
			var titleMap = '''{"en_US":"${title}"}''';
		}

		if (isSet(descriptionMap)) {
			var descriptionMap = JSONUtil2._convertLocalizedListToMap(listMap = "${descriptionMap}");
		}
		else if (isSet(description)) {
			var descriptionMap = '''{"en_US":"${description}"}''';
		} else {
			var descriptionMap = "{}";
		}

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/add-category \
			-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d parentCategoryId=${parentCategoryId} \
				-d titleMap=${titleMap} \
				-d descriptionMap=${descriptionMap} \
				-d vocabularyId=${vocabularyId} \
				-d categoryProperties=
		''';

		echo("## Assign a 'Category' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Parent Category Id: ${categoryId}");

		echo("## * Vocabulary Id: ${vocabularyId}");

		echo("## * Title Map: ${titleMap}");

		echo("## * Description Map: ${descriptionMap}");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to add a vocabulary"
	macro _addVocabulary {
		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		if (isSet(titleMap)) {
			var titleMap = JSONUtil2._convertLocalizedListToMap(listMap = "${titleMap}");
		}
		else {
			var titleMap = '''{"en_US":"${title}"}''';
		}

		if (isSet(descriptionMap)) {
			var descriptionMap = JSONUtil2._convertLocalizedListToMap(listMap = "${descriptionMap}");
		}
		else if (isSet(description)) {
			var descriptionMap = '''{"en_US":"${description}"}''';
		} else {
			var descriptionMap = "{}";
		}

		var curl = '''
			${portalURL}/api/jsonws/assetvocabulary/add-vocabulary \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d title= \
				-d titleMap=${titleMap} \
				-d descriptionMap=${descriptionMap} \
				-d Settings=
		''';

		echo("## Assign a 'Vocabulary' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Title Map: ${titleMap}");

		echo("## * Description Map: ${descriptionMap}");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to get categoryId"
	macro _getCategoryIdByName {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		if (!(isSet(categoryName))) {
			fail("Please set 'categoryName'.");
		}

		if (!(isSet(vocabularyId))) {
			fail("Please set 'vocabularyId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/get-vocabulary-categories \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d name=${categoryName} \
				-d vocabularyId=${vocabularyId} \
				-d <CURL_DATA[start=-1]CURL_DATA> \
				-d <CURL_DATA[end=-1]CURL_DATA> \
				-d -obc=
		''';

		echo("## Get a 'Category' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Category Name: ${categoryName}");

		var categoryId = JSONCurlUtil.post("${curl}", "$.[?(@['titleCurrentValue'] == '${categoryName}')]['categoryId']");
		var categoryId = StringUtil.replace("${categoryId}", "[&quot;", "");
		var categoryId = StringUtil.replace("${categoryId}", "&quot;]", "");

		if ("${categoryId}" == "[]") {
			fail("FAIL. Cannot find category.");
		}
		else {
			echo("FOUND. Returning categoryId: ${categoryId}");
		}

		return "${categoryId}";
	}

	@summary = "Get the categoryId from any categoryby using groupId and vocabularyName"
	macro _getVocabularyIdByName {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetvocabulary/get-group-vocabularies \
				-u test@liferay.com:test \
				-d groupId=${groupId}
		''';

		echo("## Get a 'Vocabulary' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Vocabulary Name: ${vocabularyName}");

		var vocabularyId = JSONCurlUtil.post("${curl}", "$.[?(@['titleCurrentValue'] == '${vocabularyName}')]['vocabularyId']");
		var vocabularyId = StringUtil.replace("${vocabularyId}", "[&quot;", "");
		var vocabularyId = StringUtil.replace("${vocabularyId}", "&quot;]", "");

		if ("${vocabularyId}" == "[]") {
			fail("FAIL. Cannot find vocabulary.");
		}
		else {
			echo("FOUND. Returning vocabularyId: ${vocabualryId}");
		}

		return "${vocabularyId}";
	}

	@summary = "Add a basic category"
	macro addCategory {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		JSONUtil2._exclusiveOrCheck(
			A = "${title}",
			B = "${titleMap}",
			failMessage = "Please only set 'title' or only 'titleMap' but not both or none.");

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var vocabularyId = JSONCategory._getVocabularyIdByName(
			groupId = "${groupId}",
			vocabularyName = "${vocabularyName}"
		);

		JSONCategory._addCategory(
			description = "${description}",
			descriptionMap = "${descriptionMap}",
			groupId = "${groupId}",
			title = "${title}",
			titleMap = "${titleMap}",
			vocabularyId = "${vocabularyId}");
	}

	@summary = "Add a basic subcategory"
	macro addSubcategory {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(parentCategoryName))) {
			fail("Please set 'parentCategoryName'.");
		}

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		JSONUtil2._exclusiveOrCheck(
			A = "${title}",
			B = "${titleMap}",
			failMessage = "Please only set 'title' or only 'titleMap' but not both or none.");

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var vocabularyId = JSONCategory._getVocabularyIdByName(
			groupId = "${groupId}",
			vocabularyName = "${vocabularyName}"
		);

		var parentCategoryId = JSONCategory._getCategoryIdByName(
			categoryName = "${parentCategoryName}",
			groupId = "${groupId}",
			vocabularyId = "${vocabularyId}"
		);

		JSONCategory._addCategory(
			groupId = "${groupId}",
			parentCategoryId = "${parentCategoryId}",
			title = "${title}",
			titleMap = "${titleMap}",
			vocabularyId = "${vocabularyId}");
	}

	@summary = "Add a basic vocabulary"
	macro addVocabulary {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		JSONUtil2._exclusiveOrCheck(
			A = "${title}",
			B = "${titleMap}",
			failMessage = "Please only set 'title' or only 'titleMap' but not both or none.");

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		JSONCategory._addVocabulary(
			description = "${description}",
			descriptionMap = "${descriptionMap}",
			groupId = "${groupId}",
			title = "${title}",
			titleMap = "${titleMap}");
	}

	@summary = "Get the categoryId from any category by using categoryName, groupId, and vocabularyId"
	macro getCategoryId {
		if (!(isSet(categoryName))) {
			fail("Please set 'categoryName'.");
		}

		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var vocabularyId = JSONCategory._getVocabularyIdByName(
			groupId = "${groupId}",
			vocabularyName = "${vocabularyName}"
		);

		var categoryId = JSONCategory._getCategoryIdByName(
			categoryName = "${categoryName}",
			groupId = "${groupId}",
			vocabularyId = "${vocabularyId}"
		);

		return "${categoryId}";
	}

}