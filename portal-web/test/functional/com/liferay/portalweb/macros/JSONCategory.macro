definition {

	@summary = "Directly calls the JSON WS API to add a category"
	macro _addCategory {
		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/add-category \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d title=${title} \
				-d vocabularyId=${vocabularyId}
		''';

		echo("## Assign a 'Category' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Vocabulary Id: ${vocabularyId}");

		echo("## * Title: ${title}");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to add a subcategory"
	macro _addSubcategory {
		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		var subcategoryMap = '''{"en_US":"${subcategoryName}"}''';

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/add-category \
			-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d parentCategoryId=${categoryId} \
				-d titleMap=${subcategoryMap} \
				-d descriptionMap={} \
				-d vocabularyId=${vocabularyId} \
				-d categoryProperties=
			''';

		echo("## Assign a 'Sub-Category' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Parent Category Id: ${categoryId}");

		echo("## * Vocabulary Id: ${vocabularyId}");

		echo("## * Title Map: ${subcategoryMap}");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to add a vocabulary"
	macro _addVocabulary {
		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		var curl = '''
			${portalURL}/api/jsonws/assetvocabulary/add-vocabulary \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d title=${title}
		''';

		echo("## Assign a 'Vocabulary' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Title: ${title}");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to get categoryId"
	macro _getCategoryIdByName {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		if (!(isSet(categoryName))) {
			fail("Please set 'categoryName'.");
		}

		if (!(isSet(vocabularyId))) {
			fail("Please set 'vocabularyId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/get-vocabulary-categories \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d name=${categoryName} \
				-d vocabularyId=${vocabularyId} \
				-d <CURL_DATA[start=-1]CURL_DATA> \
				-d <CURL_DATA[end=-1]CURL_DATA> \
				-d -obc=
		''';

		echo("## Get a 'Category' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Category Name: ${categoryName}");

		var categoryId = JSONCurlUtil.post("${curl}", "$.[?(@['titleCurrentValue'] == '${categoryName}')]['categoryId']");
		var categoryId = StringUtil.replace("${categoryId}", "[&quot;", "");
		var categoryId = StringUtil.replace("${categoryId}", "&quot;]", "");

		if ("${categoryId}" == "[]") {
			fail("FAIL. Cannot find category.");
		}
		else {
			echo("FOUND. Returning categoryId: ${categoryId}");
		}

		return "${categoryId}";
	}

	@summary = "Get the categoryId from any categoryby using groupId and vocabularyName"
	macro _getVocabularyIdByName {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetvocabulary/get-group-vocabularies \
				-u test@liferay.com:test \
				-d groupId=${groupId}
		''';

		echo("## Get a 'Vocabulary' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Vocabulary Name: ${vocabularyName}");

		var vocabularyId = JSONCurlUtil.post("${curl}", "$.[?(@['titleCurrentValue'] == '${vocabularyName}')]['vocabularyId']");
		var vocabularyId = StringUtil.replace("${vocabularyId}", "[&quot;", "");
		var vocabularyId = StringUtil.replace("${vocabularyId}", "&quot;]", "");

		if ("${vocabularyId}" == "[]") {
			fail("FAIL. Cannot find vocabulary.");
		}
		else {
			echo("FOUND. Returning vocabularyId: ${vocabualryId}");
		}

		return "${vocabularyId}";
	}

	@summary = "Add a basic category"
	macro addCategory {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(title))) {
			fail("Please set 'title'.");
		}

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var vocabularyId = JSONCategory._getVocabularyIdByName(
			groupId = "${groupId}",
			vocabularyName = "${vocabularyName}"
		);

		JSONCategory._addCategory(
			groupId = "${groupId}",
			title = "${title}",
			vocabularyId = "${vocabularyId}");
	}

	@summary = "Add a basic subcategory"
	macro addSubcategory {
		if (!(isSet(categoryName))) {
			fail("Please set 'categoryName'.");
		}

		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(subcategoryName))) {
			fail("Please set 'subcategoryName'.");
		}

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var vocabularyId = JSONCategory._getVocabularyIdByName(
			groupId = "${groupId}",
			vocabularyName = "${vocabularyName}"
		);

		var categoryId = JSONCategory._getCategoryIdByName(
			categoryName = "${categoryName}",
			groupId = "${groupId}",
			vocabularyId = "${vocabularyId}"
		);

		JSONCategory._addSubcategory(
			categoryId = "${categoryId}",
			groupId = "${groupId}",
			subcategoryName = "${subcategoryName}",
			vocabularyId = "${vocabularyId}");
	}

	@summary = "Add a basic vocabulary"
	macro addVocabulary {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(title))) {
			fail("Please set 'title'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		JSONCategory._addVocabulary(
			groupId = "${groupId}",
			title = "${title}");
	}

	@summary = "Get the categoryId from any category by using categoryName, groupId, and vocabularyId"
	macro getCategoryId {
		if (!(isSet(categoryName))) {
			fail("Please set 'categoryName'.");
		}

		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(vocabularyName))) {
			fail("Please set 'vocabularyName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var vocabularyId = JSONCategory._getVocabularyIdByName(
			groupId = "${groupId}",
			vocabularyName = "${vocabularyName}"
		);

		var categoryId = JSONCategory._getCategoryIdByName(
			categoryName = "${categoryName}",
			groupId = "${groupId}",
			vocabularyId = "${vocabularyId}"
		);

		return "${categoryId}";
	}
}