definition {

	macro addField {
		if (IsElementNotPresent(key_tabName = "Builder", locator1 = "Sidebar#ACTIVE_TAB")) {
			Navigator.gotoSidebarTab(tabName = "Builder");
		}

		if (IsElementNotPresent(key_tab = "Fields", locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			Navigator.gotoNavTab(navTab = "Fields");
		}

		var key_fieldFieldLabel = "${fieldFieldLabel}";
		var key_fieldName = "${fieldName}";

		if (isSet(fieldLocation)) {
			DragAndDrop.javaScriptDragAndDropToObject(
				key_fieldLocation = "${fieldLocation}",
				locator1 = "Sidebar#ADD_FIELD_NAME",
				locator2 = "Sidebar#ADD_FIELD_POSITION",
				value1 = "${fieldName}");
		}
		else {
			DragAndDrop.javaScriptDragAndDropToObject(
				locator1 = "Sidebar#ADD_FIELD_NAME",
				locator2 = "Sidebar#ADD_FIELD_TARGET",
				value1 = "${fieldName}");
		}

		Pause(locator1 = "3000");

		if (IsElementNotPresent(key_tab = "Basic", locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			Navigator.gotoNavTab(navTab = "Basic");
		}

		Type(
			key_fieldLabel = "Label",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "${fieldFieldLabel}");

		AssertTextEquals(
			key_fieldLabel = "Label",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "${fieldFieldLabel}");

		AssertElementPresent(
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL",
			value1 = "${fieldFieldLabel}");

		if ("${fieldName}" == "Text") {
			if (isSet(fieldType)) {
				Check.checkToggleSwitch(
					key_toggleSwitchLabel = "${fieldType}",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
		}
		else if ("${fieldName}" == "Numeric") {
			if (isSet(numericType)) {
				Check.checkToggleSwitch(
					key_toggleSwitchLabel = "${numericType}",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
		}

		Click(locator1 = "Sidebar#BACK");
	}

	macro addFieldNested {
		if (IsElementNotPresent(key_tabName = "Builder", locator1 = "Sidebar#ACTIVE_TAB")) {
			Navigator.gotoSidebarTab(tabName = "Builder");
		}

		if (IsElementNotPresent(key_tab = "Fields", locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			Navigator.gotoNavTab(navTab = "Fields");
		}

		DragAndDrop.javaScriptDragAndDropToObject(
			key_fieldFieldLabel = "${targetFieldLabel}",
			key_fieldName = "${fieldName}",
			locator1 = "Sidebar#ADD_FIELD_NAME",
			locator2 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${fieldName}");

		Pause(locator1 = "3000");

		if (IsElementNotPresent(key_tab = "Basic", locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			Navigator.gotoNavTab(navTab = "Basic");
		}

		Type(
			key_fieldLabel = "Label",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "${fieldFieldLabel}");

		AssertTextEquals(
			key_fieldLabel = "Label",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "${fieldFieldLabel}");

		AssertElementPresent(
			key_fieldFieldLabel = "${fieldFieldLabel}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL",
			value1 = "${fieldFieldLabel}");

		if ("${fieldName}" == "Text") {
			if (isSet(fieldType)) {
				Check.checkToggleSwitch(
					key_toggleSwitchLabel = "${fieldType}",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
		}
		else if ("${fieldName}" == "Numeric") {
			if (isSet(numericType)) {
				Check.checkToggleSwitch(
					key_toggleSwitchLabel = "${numericType}",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
		}

		Click(locator1 = "Sidebar#BACK");
	}

	macro addFields {
		if (IsElementNotPresent(key_tabName = "Builder", locator1 = "Sidebar#ACTIVE_TAB")) {
			Navigator.gotoSidebarTab(tabName = "Builder");
		}

		if (IsElementNotPresent(key_tab = "Fields", locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			Navigator.gotoNavTab(navTab = "Fields");
		}

		for (var fieldName : list "${fieldNameList}") {
			var key_fieldName = "${fieldName}";

			DoubleClick(locator1 = "Sidebar#ADD_FIELD_NAME");

			Pause(locator1 = "3000");

			AssertElementPresent(
				key_fieldFieldLabel = "${fieldName}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL",
				value1 = "${fieldName}");

			Click(locator1 = "Sidebar#BACK");
		}
	}

	macro addRepeatableField {
		var key_fieldName = "${fieldName}";

		Click(locator1 = "DDMField#FIELD_DUPLICATE");
	}

	macro deleteField {
		AssertClick.assertPartialTextClickAt(
			key_fieldFieldLabel = "${fieldFieldLabel}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${fieldFieldLabel}");

		Click(
			key_text = "${fieldFieldLabel}",
			locator1 = "Icon#ANY_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Delete");
	}

	macro editFieldRepeatable {
		AssertClick.assertPartialTextClickAt(
			key_fieldFieldLabel = "${fieldFieldLabel}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${fieldFieldLabel}");

		if (IsElementNotPresent(locator1 = "NavTab#ACTIVE_TAB_LINK", key_tab = "Advanced")) {
			Navigator.gotoNavTab(navTab = "Advanced");
		}

		Check.checkToggleSwitch(
			checkboxName = "Repeatable",
			locator1 = "Checkbox#ANY_CHECKBOX");
	}

	macro editFieldRequired {
		AssertClick.assertPartialTextClickAt(
			key_fieldFieldLabel = "${fieldFieldLabel}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${fieldFieldLabel}");

		if (IsElementNotPresent(locator1 = "NavTab#ACTIVE_TAB_LINK", key_tab = "Basic")) {
			Navigator.gotoNavTab(navTab = "Basic");
		}

		Check.checkToggleSwitch(
			checkboxName = "Required Field",
			locator1 = "Checkbox#ANY_CHECKBOX");
	}

	macro editFieldTip {
		AssertClick.assertPartialTextClickAt(
			key_fieldFieldLabel = "${fieldFieldLabel}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${fieldFieldLabel}");

		if (isSet(fieldPlaceholder)) {
			var key_fieldFieldLabel = "${fieldFieldLabel}";
			var key_text = "Placeholder";

			Click(locator1 = "Sidebar#DDM_ANY");

			Type(
				locator1 = "Sidebar#DDM_ANY",
				value1 = "${fieldPlaceholder}");

			AssertTextEquals(
				locator1 = "DDMEditStructure#SETTINGS_TEXT",
				value1 = "${fieldPlaceholder}");
		}

		if (isSet(fieldHelp)) {
			var key_fieldFieldLabel = "${fieldFieldLabel}";
			var key_text = "Help";

			Click(locator1 = "Sidebar#DDM_ANY");

			Type(
				locator1 = "Sidebar#DDM_ANY",
				value1 = "${fieldHelp}");

			AssertTextEquals(
				locator1 = "DDMEditStructure#SETTINGS_TEXT",
				value1 = "${fieldHelp}");
		}

		Click(locator1 = "Sidebar#BACK");
	}

	macro viewField {
		var key_fieldFieldLabel = "${fieldFieldLabel}";
		var key_text = "${fieldName}";

		AssertClick.assertPartialTextClickAt(
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${fieldFieldLabel}");

		AssertTextEquals(
			locator1 = "Button#ANY",
			value1 = "${fieldName}");

		AssertTextEquals.assertValue(
			locator1 = "Sidebar#DDM_LABEL",
			value1 = "${fieldFieldLabel}");
	}

	macro viewNestedField {
		AssertTextEquals(
			key_fieldName = "${fieldName}",
			key_nestedFieldName = "${nestedField}",
			locator1 = "DDMField#NESTED_DE",
			value1 = "${nestedFieldData}");
	}

}