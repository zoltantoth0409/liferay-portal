definition {

	macro addContent {
		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#GOOGLE_DOC");

		if (IsElementPresent(locator1 = "GoogleDoc#DISMISS")) {
			Click(locator1 = "GoogleDoc#DISMISS");

			AssertElementPresent(locator1 = "GoogleDoc#INSERT_LINK_BUTTON");
		}

		if (isSet(insertLink)) {
			Click(locator1 = "GoogleDoc#INSERT_LINK_BUTTON");

			Click(locator1 = "GoogleDoc#INSERT_LINK_LINK");

			Type(
				locator1 = "GoogleDoc#INSERT_LINK_LINK",
				value1 = "${linkAddress}");

			Click(locator1 = "GoogleDoc#INSERT_LINK_TEXT");

			Type(
				locator1 = "GoogleDoc#INSERT_LINK_TEXT",
				value1 = "${linkText}");

			Click(locator1 = "GoogleDoc#INSERT_LINK_APPLY_BUTTON");

			var key_googleDocContent = "${googleDocContent}";

			AssertElementPresent(locator1 = "GoogleDoc#INSERT_LINK_DOCUMENT_CONTENT");
		}

		SelectFrameTop();
	}

	macro discardChanges {
		var key_text = "Discard Changes";

		AssertElementPresent(locator1 = "Button#ANY");

		AssertClick(
			locator1 = "Button#ANY",
			value1 = "Discard Changes");
	}

	macro editTitleInGoogleDoc {
		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#GOOGLE_DOC");

		Click(locator1 = "GoogleDoc#TITLE");

		Type(
			locator1 = "GoogleDoc#TITLE",
			value1 = "${dmDocumentTitle}");

		KeyPress(
			locator1 = "GoogleDoc#TITLE",
			value1 = "\RETURN");

		SelectFrameTop();

		Refresh();
	}

	macro gotoEdit {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit in ${dmDocumentTypeName}");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		Pause(locator1 = "5000");

		if (IsElementNotPresent(locator1 = "Button#ANY", key_text = "Save and Return to ${siteName}")) {
			Refresh();

			Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS");

			MenuItem.click(menuItem = "Edit in ${dmDocumentTypeName}");
		}

		AssertElementPresent(
			key_text = "Save and Return to ${siteName}",
			locator1 = "Button#ANY");
	}

	macro saveAndExit {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		var key_text = "Save and Return to ${siteName}";

		AssertElementPresent(locator1 = "Button#ANY");

		Pause(locator1 = "3000");

		AssertClick(
			locator1 = "Button#ANY",
			value1 = "Save and Return to ${siteName}");

		Pause(locator1 = "5000");

		Navigator.openURL();
	}

}