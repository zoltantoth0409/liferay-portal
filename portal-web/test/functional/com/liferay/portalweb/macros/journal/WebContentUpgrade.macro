definition {

	macro addWithStructureCP {
		AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

		PortletEntry.inputTitle(title = "${webContentTitle}");

		if (isSet(webContentBoolean)) {
			Check(
				key_fieldName = "Boolean",
				locator1 = "DDMField#BOOLEAN");
		}

		if (isSet(webContentColor)) {
			if (!(isSet(webContentColorFieldLabel))) {
				var webContentColorFieldLabel = "Color";
			}

			Click(
				key_fieldFieldLabel = "${webContentColorFieldLabel}",
				locator1 = "WCEditWebContent#COLOR_PICKER");

			Click(
				key_colorSwatchItem = "${webContentColor}",
				locator1 = "Button#COLOR_SWATCH_ITEM");
		}

		if (isSet(webContentDate)) {
			if (!(isSet(webContentDateFieldLabel))) {
				var webContentDateFieldLabel = "Date";
			}

			Type(
				key_fieldFieldLabel = "${webContentDateFieldLabel}",
				locator1 = "WCEditWebContent#DATE_INPUT",
				value1 = "${webContentDate}");
		}

		if (isSet(webContentDecimal)) {
			Type(
				key_fieldFieldLabel = "Decimal",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentDecimal}");
		}

		if (isSet(webContentDocumentsAndMedia)) {
			AssertClick(
				key_fieldName = "${fieldName}",
				locator1 = "Button#SELECT_CONTENT",
				value1 = "Select");

			if (isSet(depotName)) {
				ItemSelector.selectRepositoryImage(
					available = "${available}",
					contentName = "${webContentSelectWCFile}",
					depotName = "${depotName}",
					folderName = "${folderName}",
					imageFileName = "${webContentDocumentsAndMedia}");
			}

			else {
				SelectFrame(locator1 = "IFrame#MODAL_BODY");

				AssertClick(
					key_dmDocumentTitle = "${webContentDocumentsAndMedia}",
					locator1 = "WC#ICON_DOCUMENT_TITLE",
					value1 = "${webContentDocumentsAndMedia}");
			}
		}

		if (isSet(webContentHTML)) {
			CKEditor.addContentInWebContent(
				content = "${webContentHTML}",
				fieldLabel = "HTML");
		}

		if (isSet(webContentSourceHTML)) {
			Click(
				key_fieldFieldLabel = "HTML",
				locator1 = "WCEditWebContent#SOURCE_BUTTON");

			Type.typeCodeMirrorEditorNoError(
				locator1 = "CKEditor#BODY_FIELD_SOURCE_ON",
				value1 = "${content}");
		}

		if (isSet(webContentImage)) {
			AssertClick(
				key_fieldFieldLabel = "Image",
				locator1 = "WCEditWebContent#SELECT_BUTTON",
				value1 = "Select");

			if ("${navTab}" == "Documents and Media") {
				if (isSet(depotName)) {
					ItemSelector.selectRepositoryImage(
						depotName = "${depotName}",
						imageFileName = "${webContentImage}",
						navTab = "${navTab}");
				}
				else if (isSet(imageFileName)) {
					ItemSelector.selectDMImage(
						imageFileName = "${webContentImage}",
						navTab = "${navTab}");
				}
				else {
					ItemSelector.uploadFile(
						navTab = "${navTab}",
						uploadFileName = "${webContentImage}");
				}
			}
			else if ("${navTab}" == "Web Content Images") {
				ItemSelector.uploadFile(
					navTab = "${navTab}",
					uploadFileName = "${webContentImage}");
			}

			if (isSet(webContentImageDescription)) {
				Type(
					key_fieldFieldLabel = "Image",
					locator1 = "WCEditWebContent#IMAGE_DESCRPTION",
					value1 = "${webContentImageDescription}");
			}
		}

		if (isSet(webContentImageUnderDocumentsAndMedia)) {
			AssertClick(
				key_fieldFieldLabel = "Image",
				locator1 = "WCEditWebContent#SELECT_BUTTON",
				value1 = "Select");

			if (isSet(imageFileName)) {
				ItemSelector.selectDMImage(
					imageFileName = "${imageFileName}",
					navTab = "Documents and Media");
			}
			else {
				ItemSelector.uploadFile(
					navTab = "Documents and Media",
					uploadFileName = "${webContentImageUnderDocumentsAndMedia}");
			}
		}

		if (isSet(webContentInteger)) {
			Type(
				key_fieldFieldLabel = "Integer",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentInteger}");
		}

		if (isSet(linkedPageName)) {
			if (!(isSet(webContentLinkToPageFieldLabel))) {
				var webContentLinkToPageFieldLabel = "Link to Page";
			}

			var key_pageName = "${linkedPageName}";

			AssertClick(
				key_fieldFieldLabel = "${webContentLinkToPageFieldLabel}",
				locator1 = "WCEditWebContent#SELECT_BUTTON",
				value1 = "Select");

			while (IsElementNotPresent(locator1 = "WC#LINK_TO_PAGE_SELECT_PAGE_RADIO")) {
				Click(locator1 = "WC#LINK_TO_PAGE_NEXT_PAGE");
			}

			Click(locator1 = "WC#LINK_TO_PAGE_SELECT_PAGE_RADIO");

			ClickNoError(locator1 = "Button#SELECT");
		}

		if (isSet(webContentNumber)) {
			Type(
				key_fieldFieldLabel = "Number",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentNumber}");
		}

		if (isSet(webContentRadio)) {
			Select(
				locator1 = "DDMField#RADIO",
				value1 = "${webContentRadio}");
		}

		if (isSet(webContentSelect)) {
			Select(
				locator1 = "DDMField#SELECT",
				value1 = "${webContentSelect}");
		}

		if (isSet(webContentWebContent)) {
			if (!(isSet(webContentWebContentFieldLabel))) {
				var webContentWebContentFieldLabel = "Web Content";
			}

			AssertClick(
				key_fieldFieldLabel = "${webContentWebContentFieldLabel}",
				locator1 = "WCEditWebContent#SELECT_BUTTON",
				value1 = "Select");

			if (isSet(depotName)) {
				WebContent.selectRepositoryWC(
					available = "${available}",
					contentName = "${webContentWebContent}",
					depotName = "${depotName}",
					folderName = "${folderName}");
			}
			else {
				SelectFrame(locator1 = "IFrame#MODAL_BODY");

				AssertClick.assertPartialTextClickAt(
					key_listEntry = "${webContentWebContent}",
					locator1 = "LexiconList#LIST_ENTRY",
					value1 = "${webContentWebContent}");

				SelectFrame(value1 = "relative=top");
			}
		}

		if (isSet(webContentText)) {
			Type(
				key_fieldFieldLabel = "Text",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentText}");
		}

		if (isSet(webContentTextBox)) {
			if (isSet(fieldName)) {
				Type(
					key_fieldName = "${fieldName}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
			else {
				Type(
					key_fieldName = "TextBox",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
		}

		if (isSet(webContentTextBox2)) {
			Type(
				key_fieldName = "${fieldName2}",
				locator1 = "DDMField#TEXT_BOX",
				value1 = "${webContentTextBox2}");
		}

		if (isSet(webContentTextBoxList)) {
			var count = "1";

			for (var webContentTextBox : list "${webContentTextBoxList}") {
				Type(
					key_fieldName = "heading${count}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");

				var count = ${count} + 1;
			}
		}

		if (isSet(description)) {
			CKEditor.addContentInWebContent(
				content = "${description}",
				fieldLabel = "Summary");
		}

		if (isSet(templateName)) {
			if (IsTextNotEqual(locator1 = "WCEditWebContent#SELECTED_TEMPLATE", value1 = "${templateName}")) {
				WebContent.selectTemplateCP(
					structureName = "${structureName}",
					templateName = "${templateName}");
			}
		}

		if ("${saveAsDraft}" == "true") {
			PortletEntry.saveAsDraft();

			AssertTextEquals(
				key_status = "Draft",
				locator1 = "WCEditWebContent#WORKFLOW_STATUS",
				value1 = "Draft");
		}
	}

	macro viewWithStructureCP {
		if (!(isSet(predefinedValue))) {
			LexiconEntry.changeDisplayStyle(displayStyle = "list");

			WebContentNavigator.gotoEditCP(
				webContentTitle = "${webContentTitle}",
				webContentTitleEscapeCharacter = "${webContentTitleEscapeCharacter}");

			AssertTextEquals(
				locator1 = "TextInput#TITLE",
				value1 = "${webContentTitle}");

			if (isSet(description)) {
				SelectFrame.selectFrameNoLoading(
					key_fieldLabel = "Summary",
					locator1 = "CKEditor#BODY_FIELD_IFRAME_WEB_CONTENT_ARTICLE");

				AssertTextEquals(
					locator1 = "CKEditor#BODY",
					value1 = "${description}");

				SelectFrameTop(value1 = "relative=top");
			}
		}

		if (isSet(webContentBoolean)) {
			if ("${webContentBoolean}" == "true") {
				FormFields.viewCheckboxChecked(fieldName = "Boolean");
			}
			else if ("${webContentBoolean}" == "false") {
				FormFields.viewCheckboxNotChecked(fieldName = "Boolean");
			}
		}

		if (isSet(webContentColor)) {
			AssertTextEquals.assertValue(
				key_fieldFieldLabel = "Color",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentColor}");
		}

		if (isSet(webContentDate)) {
			AssertTextEquals(
				attribute1 = "value",
				key_fieldFieldLabel = "Date",
				locator1 = "WCEditWebContent#DATE_INPUT",
				value1 = "${webContentDate}");
		}

		if (isSet(webContentDecimal)) {
			AssertTextEquals.assertValue(
				key_fieldFieldLabel = "Decimal",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentDecimal}");
		}

		if (isSet(webContentDocumentsAndMedia)) {
			AssertAttributeValue(
				attribute1 = "value",
				key_fieldFieldLabel = "${webContentDocumentsAndMediaFieldName}",
				locator1 = "TextInput#SELECTOR_INPUT",
				value1 = "${webContentDocumentsAndMedia}");
		}

		if (isSet(webContentHTML)) {
			SelectFrame.selectFrameNoLoading(
				key_fieldLabel = "HTML",
				locator1 = "CKEditor#BODY_FIELD_IFRAME_WEB_CONTENT_ARTICLE");

			AssertTextEquals(
				locator1 = "CKEditor#BODY",
				value1 = "${webContentHTML}");

			SelectFrameTop();
		}

		if (isSet(webContentImage)) {
			if (!(isSet(webContentImageFieldLabel))) {
				var webContentImageFieldLabel = "Image";
			}

			AssertTextEquals(
				key_fieldFieldLabel = "${webContentImageFieldLabel}",
				locator1 = "TextInput#SELECTOR_INPUT",
				value1 = "${webContentImage}");

			AssertElementPresent(
				key_fieldFieldLabel = "${webContentImageFieldLabel}",
				key_image = "/documents/",
				locator1 = "WCEditWebContent#IMAGE_PICKER_PREVIEW");
		}

		if (isSet(webContentInteger)) {
			AssertTextEquals.assertValue(
				key_fieldFieldLabel = "Integer",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentInteger}");
		}

		if (isSet(webContentLinkToPage)) {
			AssertTextEquals(
				key_fieldFieldLabel = "${webContentLinkToPageFieldName}",
				locator1 = "TextInput#SELECTOR_INPUT",
				value1 = "${webContentLinkToPage}");
		}
		if (isSet(webContentNumber)) {
			AssertTextEquals.assertValue(
				key_fieldFieldLabel = "Number",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentNumber}");
		}

		if (isSet(webContentRadio)) {
			FormFields.viewRadioValue(
				fieldName = "Radio",
				fieldValue = "${webContentRadio}");
		}

		if (isSet(webContentSeparator)) {
			AssertVisible(
				key_fieldName = "Separator",
				locator1 = "DDMField#SEPARATOR");
		}

		if (isSet(webContentSelect)) {
			AssertSelectedLabel(
				key_fieldLabel = "Select",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${webContentSelect}");
		}

		if (isSet(webContentText)) {
			AssertTextEquals.assertValue(
				key_fieldFieldLabel = "Text",
				locator1 = "WCEditWebContent#TEXT_INPUT",
				value1 = "${webContentText}");
		}

		if (isSet(webContentTextBox)) {
			if (isSet(fieldName)) {
				AssertTextEquals(
					key_fieldName = "${fieldName}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
			else if (isSet(webContentTextBoxFieldName)) {
				AssertTextEquals(
					key_fieldName = "${webContentTextBoxFieldName}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
			else {
				AssertTextEquals(
					key_fieldName = "TextBox",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
		}

		if (isSet(webContentTextBox2)) {
			var key_fieldName = "${fieldName2}";

			AssertTextEquals(
				locator1 = "DDMField#TEXT_BOX",
				value1 = "${webContentTextBox2}");
		}

		if (isSet(webContentTextBoxList)) {
			var count = "1";

			for (var webContentTextBox : list "${webContentTextBoxList}") {
				AssertTextEquals(
					key_fieldName = "heading${count}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");

				var count = ${count} + 1;
			}
		}

		if (isSet(webContentWebContent)) {
			AssertTextEquals(
				key_fieldFieldLabel = "${webContentWebContentFieldName}",
				locator1 = "TextInput#SELECTOR_INPUT",
				value1 = "${webContentWebContent}");
		}
	}

}