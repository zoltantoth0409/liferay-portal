definition {
	macro addCP {
		var key_webContentTitle = "${folderName}";

		LexiconEntry.gotoAdd();

		if (IsElementPresent(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "Folder")) {
			AssertClick(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Folder",
				key_menuItem = "Folder"
			);
		}

		PortletEntry.inputName(name = "${folderName}");

		PortletEntry.inputDescription(description = "${folderDescription}");

		PortletEntry.save();

		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		AssertElementPresent.pauseAssertVisible(
			locator1 = "WC#ENTRY_DESCRIPTIVE_THUMBNAIL"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE",
			value1 = "${folderName}"
		);
	}

	macro addSubfolderCP {
		var key_webContentTitle = "${folderName}";

		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		Click(locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE");

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "${folderName}");

		LexiconEntry.gotoAddMenuItem(menuItem = "Subfolder");

		PortletEntry.inputName(name = "${subfolderName}");

		PortletEntry.inputDescription(description = "${subfolderDescription}");

		Button.clickSave();

		var key_webContentTitle = "${subfolderName}";

		Alert.viewSuccessMessage();

		AssertElementPresent.pauseAssertVisible(
			locator1 = "WC#ENTRY_DESCRIPTIVE_THUMBNAIL"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE",
			value1 = "${subfolderName}"
		);
	}

	macro checkSubfolderPermissions {
		var key_webContentTitle = "${folderName}";

		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		Click(locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE");

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "${folderName}");

		MouseOver(locator1 = "Button#PLUS");

		if ("${subfolder}" == "false") {
			AssertElementNotPresent(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				key_menuItem = "Subfolder"
			);
		}
		else {
			AssertElementPresent(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				key_menuItem = "Subfolder"
			);
		}
	}

	macro configureWorkflowCP {
		WebContentFolder.gotoEditCP(
			folderName = "${folderName}",
			webContentTitle = "${folderName}"
		);

		if (IsElementNotPresent.isNotVisible(locator1 = "WCEditFolder#WORKFLOW_PARENT_FOLDER_RADIO")) {
			Click(locator1 = "WCEditFolder#WORKFLOW_STRUCTURE_RESTRICTIONS_TOGGLE");
		}

		if ("${workflowRestriction}" == "Parent Folder") {
			Click(locator1 = "WCEditFolder#WORKFLOW_PARENT_FOLDER_RADIO");
		}
		else if ("${workflowRestriction}" == "Specific Restrictions") {
			Click(locator1 = "WCEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_RADIO");

			AssertClick(
				locator1 = "WCEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_CHOOSE_STRUCTURE_BUTTON",
				value1 = "Choose Structure"
			);

			SelectFrame(value1 = "relative=top");

			IFrame.selectDialogFrame();

			var key_ddlDataDefinitionName = "${structureName}";

			AssertClickNoError(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
				value1 = "${structureName}"
			);

			Confirm.assertConfirmationNoWait(
				value1 = "Selecting a new structure deletes all unsaved content. Do you want to proceed?"
			);

			SelectFrame(value1 = "relative=top");

			Select(
				locator1 = "WCEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_STRUCTURE_WORKFLOW_SELECT",
				value1 = "${workflowDefinition}"
			);
		}
		else if ("${workflowRestriction}" == "Default Workflow") {
			Click(locator1 = "WCEditFolder#WORKFLOW_DEFAULT_WORKFLOW_RADIO");

			Select(
				locator1 = "WCEditFolder#WORKFLOW_DEFAULT_WORKFLOW_WORKFLOW_SELECT",
				value1 = "${workflowDefinition}"
			);
		}

		PortletEntry.save();
	}

	macro gotoCP {
		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		var key_webContentTitle = "${webContentTitle}";

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE",
			value1 = "${folderName}"
		);

		AssertClick(locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE", value1 = "${folderName}");

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "${folderName}");
	}

	macro gotoEditCP {
		var key_webContentTitle = "${folderName}";

		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE",
			value1 = "${webContentTitle}"
		);

		Click(locator1 = "WC#ENTRY_DESCRIPTIVE_MENULIST_ICON");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);
	}

	macro moveToRecycleBinCP {
		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		var key_webContentTitle = "${folderName}";

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE",
			value1 = "${folderName}"
		);

		Click(locator1 = "WC#ENTRY_DESCRIPTIVE_MENULIST_ICON");

		PortletEntry.clickMoveToRecycleBin();

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}
}