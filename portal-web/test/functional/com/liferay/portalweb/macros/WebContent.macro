definition {

	macro _gotoAddPGViaWCD {
		MouseOver.mouseOverNotVisible(locator1 = "WCD#ADD_WEB_CONTENT");

		Click(locator1 = "WCD#ADD_WEB_CONTENT");

		var key_structureName = "Basic Web Content";

		AssertClick(
			locator1 = "WCD#ADD_STRUCTURED_WEB_CONTENT",
			value1 = "Basic Web Content");

		SelectFrame(locator1 = "IFrame#EDIT_ASSET");
	}

	macro addArticleCP {
		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		PortletEntry.publish();
	}

	macro addCP {
		if (isSet(webContentTitle)) {
			Click(locator1 = "TextInput#TITLE");

			PortletEntry.inputTitle(title = "${webContentTitle}");
		}

		if (isSet(webContentContent)) {
			AlloyEditor.addContent(content = "${webContentContent}");
		}

		if (isSet(viewableBy)) {
			Panel.expandPanel(panel = "Permissions");

			Select(
				locator1 = "Select#PERMISSIONS",
				value1 = "${viewableBy}");
		}

		if (isSet(webContentId)) {
			Type(
				locator1 = "WCEditWebContent#WEB_CONTENT_ID",
				value1 = "${webContentId}");
		}
	}

	macro addDraft {
		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		Button.clickSaveAsDraft();
	}

	macro addDraftPGViaAP {
		AssetPublisherPortlet.addAsset(assetType = "Basic Web Content");

		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		WebContent.saveAsDraftViaAP();
	}

	macro addDraftPGViaWCD {
		WebContent._gotoAddPGViaWCD();

		WebContent.addDraft(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");
	}

	macro addExistingRelatedAssetCP {
		var key_webContentTitle = "${webContentTitle}";

		Panel.expandPanel(panel = "Related Assets");

		AssertClick(
			locator1 = "AssetRelatedAssets#RELATED_ASSETS_SELECT",
			value1 = "Select");

		AssertClick(
			key_assetType = "${assetType}",
			locator1 = "APConfiguration#ASSET_SELECTION_MANUAL_ASSET_ENTRIES_ASSET",
			value1 = "${assetType}");

		SelectFrame(locator1 = "AssetRelatedAssets#SELECT_ASSET_IFRAME");

		if (isSet(scopeSite)) {
			Click(locator1 = "Dropdown#FILTER_AND_ORDER");

			MenuItem.click(menuItem = "${scopeSite}");
		}

		Search.searchCP(searchTerm = "${assetTitle}");

		var key_assetTitle = "${assetTitle}";

		AssertClick(
			locator1 = "AssetRelatedAssets#ASSET_TABLE_CHOOSE",
			value1 = "${assetTitle}");

		SelectFrameTop();

		AssertTextEquals(
			locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_TYPE",
			value1 = "${assetType}");

		AssertTextEquals(
			locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_TITLE",
			value1 = "${assetTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_SCOPE",
			value1 = "${siteName}");

		AssertElementPresent(locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_REMOVE_LINK");

		PortletEntry.publish();
	}

	macro addExpirationDateCP {
		Panel.expandPanel(panel = "Schedule");

		AssertChecked(
			locator1 = "Checkbox#NEVER_EXPIRE",
			value1 = "Never Expire");

		Uncheck(
			locator1 = "Checkbox#NEVER_EXPIRE",
			value1 = "Never Expire");

		Type.clickAtType(
			locator1 = "TextInput#EXPIRATION_DATE",
			value1 = "${expirationDate}");
	}

	macro addFailWithoutRequiredCategoryAssetTypeCP {
		PortletEntry.inputTitle(title = "Web Content Title");

		AlloyEditor.addContent(content = "Web Content Content");

		AssertClick(
			locator1 = "Button#PUBLISH",
			value1 = "Publish");

		AssertElementPresent(locator1 = "Icon#ASTERISK");

		Alert.viewRequestFailedToComplete();

		Panel.expandPanel(panel = "Metadata");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "Please select at least one category for ${vocabularyName}.");
	}

	macro addFeed {
		PortletEntry.inputName(name = "${feedTitle}");

		PortletEntry.inputDescription(description = "${description}");

		Type(
			locator1 = "TextInput#URL",
			value1 = "${targetFriendlyUrl}");

		Panel.expandPanel(panel = "Web Content Constraints");

		AssertClick(
			locator1 = "WCEditWebContent#SELECT_STRUCTURE",
			value1 = "Select");

		SelectFrame(value1 = "relative=top");

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertClickNoError(
			key_ddlDataDefinitionName = "${structureName}",
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = "${structureName}");

		AssertConfirm.assertConfirmationNoWait(value1 = "Selecting a new structure changes the available templates and available feed item content. Do you want to proceed?");

		SelectFrame(value1 = "relative=top");

		Panel.expandPanel(panel = "Presentation Settings");

		if (isSet(feedContent)) {
			Select(
				locator1 = "Select#FEED_ITEM_CONTENT",
				value1 = "${feedContent}");
		}

		if (isSet(feedType)) {
			Select(
				locator1 = "Select#FEED_TYPE",
				value1 = "${feedType}");
		}

		PortletEntry.save();
	}

	macro addLocalizedPGViaWCD {
		WebContentNavigator.gotoAddPGViaWCD(structureName = "${localizedBasicWebContent}");

		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		Click(locator1 = "Button#PUBLISH_LOCALIZED");

		SelectFrame(value1 = "relative=top");
	}

	macro addMultipleArticlesCP {
		var articleCount = ${articleCount} + 1;
		var count = "1";

		while ("${count}" != "${articleCount}") {
			WebContent.addArticleCP(
				webContentContent = "WC Content ${count}",
				webContentTitle = "WC Title ${count}");

			var count = ${count} + 1;
		}
	}

	macro addNullCP {
		if (isSet(webContentContent)) {
			AlloyEditor.addContent(content = "${webContentContent}");
		}

		AssertClick(
			locator1 = "Button#PUBLISH",
			value1 = "Publish");

		Alert.viewRequiredField();

		if (isSet(webContentContent)) {
			AssertTextEquals(
				key_editor = "content",
				locator1 = "AlloyEditor#EDITOR",
				value1 = "${webContentContent}");
		}
	}

	macro addPGViaAP {
		AssetPublisherPortlet.addAsset(assetType = "Basic Web Content");

		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		AssertClickNoError(
			locator1 = "Button#PUBLISH",
			value1 = "Publish");

		Pause(locator1 = "5000");

		SelectFrame(value1 = "relative=top");

		var key_assetTitle = "${webContentTitle}";

		AssertTextEquals(
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "AP#ASSET_ABSTRACT_DESCRIPTION",
			value1 = "${webContentContent}");
	}

	macro addPGViaWCD {
		WebContentNavigator.gotoAddPGViaWCD();

		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		Button.clickPublish();

		SelectFrame(value1 = "relative=top");
	}

	macro addPriority {
		Panel.expandPanel(panel = "Metadata");

		Type(
			locator1 = "WC#PRIORITY",
			value1 = "${priority}");
	}

	macro addSmallImageViaURL {
		Panel.expandPanel(panel = "Small Image");

		Check(locator1 = "WCEditWebContent#USE_SMALL_IMAGE_CHECKBOX");
		Click(locator1 = "WCEditWebContent#SMALL_IMAGE_URL");

		Type(
			locator1 = "WCEditWebContent#SMALL_IMAGE_URL_INPUT",
			value1 = "${imageSourceURL}");
	}

	macro addStructuredPGViaWCD {
		WebContentNavigator.gotoAddPGViaWCD(structureName = "${structureName}");

		PortletEntry.inputTitle(title = "${webContentTitle}");

		if (isSet(webContentText)) {
			Type(
				key_fieldName = "${structureFieldName}",
				locator1 = "TextInput#DDM",
				value1 = "${webContentText}");
		}

		if (isSet(webContentTextBox)) {
			Type(
				key_fieldName = "${textBoxFieldName}",
				locator1 = "DDMField#TEXT_BOX",
				value1 = "${webContentTextBox}");
		}

		Click(locator1 = "Button#PUBLISH");

		SelectFrame(value1 = "relative=top");
	}

	macro addTagPGViaWCD {
		Panel.expandPanel(panel = "Metadata");

		Type(
			locator1 = "AssetCategorization#TAGS_FIELD",
			value1 = "${tagName}");

		AssertClick(
			locator1 = "Button#ADD_TAGS",
			value1 = "Add");

		AssertTextEquals(
			key_tagName = "${tagName}",
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}");

		AssertClick(
			locator1 = "Button#PUBLISH",
			value1 = "Publish");

		SelectFrame(value1 = "relative=top");
	}

	macro addViaCKEditor {
		PortletEntry.inputTitle(title = "${webContentTitle}");

		Type.typeCKEditorWaitForCKEditor(
			locator1 = "CKEditor#BODY_FIELD",
			value1 = "${webContentContent}");
	}

	macro addWithCategoryCP {
		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		WebContent.selectCategory(
			categoryName = "${categoryName}",
			userSegmentDetailTitle = "${userSegmentDetailTitle}",
			vocabularyName = "${vocabularyName}");

		PortletEntry.publish();
	}

	macro addWithDocumentCP {
		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		ItemSelector.gotoItemSelectorViaAlloyEditor();

		SelectFrame(locator1 = "IFrame#METADATA_SETS_IFRAME");

		var key_imageFileName = "${dmDocumentTitle}";
		var key_imageFileName = StringUtil.replace("${dmDocumentTitle}", " ", "+");

		Pause(locator1 = "1000");

		if (isSet(siteName)) {
			Click(locator1 = "ItemSelector#SITES_NAVIGATION");

			AssertClick(
				key_siteName = "${siteName}",
				locator1 = "ItemSelector#SITE_SELECTOR",
				value1 = "${siteName}");
		}

		Click(locator1 = "ItemSelector#SELECT_FILE_IMAGE_PREVIEW");

		SelectFrameTop(value1 = "relative=top");

		AssertClick(
			locator1 = "ItemSelector#ADD_BUTTON",
			value1 = "Add");

		PortletEntry.publish();
	}

	macro addWithFriendlyURL {
		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		Panel.expandPanel(panel = "Friendly URL");

		Type(
			locator1 = "TextInput#FRIENDLY_URL",
			value1 = "${friendlyUrl}");

		PortletEntry.publish();
	}

	macro addWithGlobalStructurePGViaWCD {
		WebContentNavigator.gotoAddPGViaWCD(structureName = "${selectStructureName}");

		WebContent.addCP(webContentTitle = "${webContentTitle}");

		Panel.expandPanel(panel = "Structure and Template");

		AssertClick(
			locator1 = "WCEditWebContent#SELECT_STRUCTURE",
			value1 = "Select");

		SelectFrame(value1 = "relative=top");

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertClickNoError(
			key_ddlDataDefinitionName = "${structureName}",
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = "${structureName}");

		AssertConfirm.assertConfirmationNoWait(value1 = "Editing the current structure deletes all unsaved content.");

		SelectFrame(value1 = "relative=top");

		SelectFrame(locator1 = "IFrame#EDIT_ASSET");

		Panel.expandPanel(panel = "Structure and Template");

		AssertTextEquals(
			locator1 = "WCEditWebContent#SELECTED_STRUCTURE",
			value1 = "${structureName}");

		AssertTextEquals(
			locator1 = "WCEditWebContent#SELECTED_TEMPLATE",
			value1 = "${templateName}");

		PortletEntry.inputTitle(title = "${webContentTitle}");

		Type(
			key_fieldName = "${fieldName}",
			locator1 = "TextInput#DDM",
			value1 = "${webContentContent}");

		Click(locator1 = "Button#PUBLISH");

		SelectFrame(value1 = "relative=top");

		if ("${webContentJavascript}" == "true") {
			AssertTextEquals(
				locator1 = "//div[@class='journal-content-article']//span[contains(@class,'portlet-title-text')]",
				value1 = "${embeddedPortletTitle}");
		}
		else {
			AssertTextEquals(
				locator1 = "WCD#WEB_CONTENT_CONTENT",
				value1 = "${webContentContent}");
		}
	}

	macro addWithMultipleCategories {
		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		Panel.expandPanel(panel = "Metadata");

		AssetCategorization.addMultipleCategories(
			categoryNameList = "${categoryNameList}",
			vocabularyName = "${vocabularyName}");

		PortletEntry.publish();
	}

	macro addWithStructureAndWorkflowCP {
		WebContent.addCP(webContentTitle = "${webContentTitle}");

		if (isSet(webContentText)) {
			Type(
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "${webContentText}");
		}

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Alert.viewSuccessMessage();
	}

	macro addWithStructureCP {
		Click(locator1 = "TextInput#TITLE");

		PortletEntry.inputTitle(title = "${webContentTitle}");

		if (isSet(webContentBoolean)) {
			Check(locator1 = "DDMField#BOOLEAN");
		}

		if (isSet(webContentColor)) {
			Click(
				key_fieldName = "Color",
				locator1 = "TextInput#DDM");

			Click(
				key_colorCode = "${webContentColor}",
				locator1 = "DDMField#COLOR_PICKER_COLOR");
		}

		if (isSet(webContentDate)) {
			Type(
				locator1 = "TextInput#DDM",
				value1 = "${webContentDate}");
		}

		if (isSet(webContentDecimal)) {
			Type(
				key_fieldName = "Decimal",
				locator1 = "TextInput#DDM",
				value1 = "${webContentDecimal}");
		}

		if (isSet(webContentDocumentsAndMedia)) {
			AssertClick(
				key_fieldName = "${fieldName}",
				locator1 = "Button#SELECT_CONTENT",
				value1 = "Select");

			SelectFrame(locator1 = "IFrame#DIALOG");

			AssertClick.assertPartialTextClickAt(
				key_dmDocumentTitle = "${webContentDocumentsAndMedia}",
				locator1 = "WC#ICON_DOCUMENT_TITLE",
				value1 = "${webContentDocumentsAndMedia}");

			SelectFrameTop(value1 = "relative=top");

			Click(locator1 = "Button#ADD");
		}

		if (isSet(webContentHTML)) {
			Type.typeEditor(
				key_fieldName = "HTML",
				locator1 = "DDMField#HTML",
				value1 = "${webContentHTML}");
		}

		if (isSet(webContentImage)) {
			Click(locator1 = "Button#SELECT");

			ItemSelector.uploadFile(
				navTab = "Web Content Images",
				uploadFileName = "${webContentImage}");
		}

		if (isSet(webContentImageUnderDocumentsAndMedia)) {
			AssertClick(
				key_fieldName = "Image",
				locator1 = "Button#SELECT_CONTENT",
				value1 = "Select");

			ItemSelector.uploadFile(
				navTab = "Documents and Media",
				uploadFileName = "${webContentImageUnderDocumentsAndMedia}");
		}

		if (isSet(webContentInteger)) {
			Type(
				key_fieldName = "Integer",
				locator1 = "TextInput#DDM",
				value1 = "${webContentInteger}");
		}

		if (isSet(linkedPageName)) {
			var key_pageName = "${linkedPageName}";

			AssertClick(
				key_fieldName = "${linkFieldName}",
				locator1 = "Button#SELECT_CONTENT",
				value1 = "Select");

			while (IsElementNotPresent(locator1 = "WC#LINK_TO_PAGE_SELECT_PAGE_RADIO")) {
				Click(locator1 = "WC#LINK_TO_PAGE_NEXT_PAGE");
			}

			Click(locator1 = "WC#LINK_TO_PAGE_SELECT_PAGE_RADIO");

			ClickNoError(locator1 = "Button#SELECT");
		}

		if (isSet(webContentNumber)) {
			Type(
				key_fieldName = "Number",
				locator1 = "TextInput#DDM",
				value1 = "${webContentNumber}");
		}

		if (isSet(webContentRadio)) {
			Select(
				locator1 = "DDMField#RADIO",
				value1 = "${webContentRadio}");
		}

		if (isSet(webContentSelect)) {
			Select(
				locator1 = "DDMField#SELECT",
				value1 = "${webContentSelect}");
		}

		if (isSet(webContentSelectWCFile)) {
			AssertClick(
				key_fieldName = "${fieldName}",
				locator1 = "Button#SELECT_CONTENT",
				value1 = "Select");

			SelectFrame(locator1 = "IFrame#DIALOG");

			AssertClick(
				key_webContentTitle = "${webContentSelectWCFile}",
				locator1 = "Link#WEB_CONTENT_ARTICLE",
				value1 = "${webContentSelectWCFile}");

			SelectFrame(value1 = "relative=top");
		}

		if (isSet(webContentText)) {
			Type(
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "${webContentText}");
		}

		if (isSet(webContentTextBox)) {
			if (isSet(fieldName)) {
				Type(
					key_fieldName = "${fieldName}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
			else {
				Type(
					key_fieldName = "TextBox",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
		}

		if (isSet(webContentTextBox2)) {
			Type(
				key_fieldName = "${fieldName2}",
				locator1 = "DDMField#TEXT_BOX",
				value1 = "${webContentTextBox2}");
		}

		if (isSet(webContentTextBoxList)) {
			var count = "1";

			for (var webContentTextBox : list "${webContentTextBoxList}") {
				Type(
					key_fieldName = "heading${count}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");

				var count = ${count} + 1;
			}
		}

		if (isSet(description)) {
			AlloyEditor.addDescription(description = "${description}");
		}

		if ("${saveAsDraft}" == "true") {
			PortletEntry.saveAsDraft();

			AssertTextEquals(
				locator1 = "AssetWorkflow#WORKFLOW_STATUS",
				value1 = "Draft");
		}
	}

	macro addWithTagCP {
		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		Panel.expandPanel(panel = "Metadata");

		for (var tagName : list "${tagNameList}") {
			Click(locator1 = "Button#ADD_TAGS");

			Type.clickAtType(
				locator1 = "AssetCategorization#TAGS_FIELD",
				value1 = "${tagName}");
		}

		Click(locator1 = "Button#ADD_TAGS");

		PortletEntry.publish();
	}

	macro addWithThreeTranslations {
		WebContent.addCP(
			webContentContent = "WC Content",
			webContentTitle = "WC Title");

		PortletEntry.changeLocale(
			fieldName = "Title",
			locale = "es-ES");

		WebContent.addCP(
			webContentContent = "WC Contenido",
			webContentTitle = "WC Titulo");

		PortletEntry.changeLocale(
			fieldName = "Title",
			locale = "de-DE");

		WebContent.addCP(
			webContentContent = "WC Inhalt",
			webContentTitle = "WC Titel");

		PortletEntry.changeLocale(
			fieldName = "Title",
			locale = "fi-FI");

		WebContent.addCP(
			webContentContent = "WC Pitoisuus",
			webContentTitle = "WC Otsikko");
	}

	macro addWithWorkflowCP {
		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		Button.clickSubmitForPublication();

		Alert.viewSuccessMessage();
	}

	macro addWithWorkflowPGViaAP {
		AssetPublisherPortlet.addAsset(assetType = "Basic Web Content");

		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Pause(locator1 = "5000");

		SelectFrame();

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "There are no results.");
	}

	macro addWithWorkflowPGViaWCD {
		WebContentNavigator.gotoAddPGViaWCD(structureName = "${selectStructureName}");

		WebContent.addCP(
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		SelectFrame(value1 = "relative=top");

		AssertTextEquals(
			locator1 = "Message#ERROR_3",
			value1 = "${webContentTitle} is not approved.");
	}

	macro copyCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Copy",
			rowEntry = "${webContentTitle}");

		Alert.viewSuccessMessage();
	}

	macro deleteCP {
		WebContent.moveToRecycleBinCP(
			assetType = "Web Content Article",
			webContentTitle = "${webContentTitle}",
			webContentTitleEscapeCharacter = "${webContentTitleEscapeCharacter}");

		RecycleBin.openRecycleBinAdmin(siteURLKey = "${siteURLKey}");

		RecycleBin.emptyCP();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content");

		AssertTextNotPresent(value1 = "${webContentTitle}");
	}

	macro deleteStagedCP {
		WebContent.moveToRecycleBinCP(
			assetType = "Web Content Article",
			webContentTitle = "${webContentTitle}",
			webContentTitleEscapeCharacter = "${webContentTitleEscapeCharacter}");

		Navigator.gotoStagedSitePage(
			pageName = "${pageName}",
			siteName = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Recycle Bin",
			panel = "Site Administration",
			portlet = "Recycle Bin");

		RecycleBin.emptyCP();

		Navigator.gotoStagedSitePage(
			pageName = "${pageName}",
			siteName = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content");

		AssertTextNotPresent(value1 = "${webContentTitle}");
	}

	macro deleteWithDisabledRB {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${entryTitle}");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro editCP {
		WebContent.addCP(webContentTitle = "${webContentTitleEdit}");

		if (isSet(webContentContentEdit)) {
			AlloyEditor.addContent(content = "${webContentContentEdit}");
		}

		if ("${saveAsDraft}" == "true") {
			PortletEntry.saveAsDraft();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#WEB_CONTENT_STATUS",
				value1 = "Draft");
		}
		else {
			PortletEntry.publish();

			LexiconEntry.changeDisplayStyle(displayStyle = "list");

			var key_webContentTitle = "${webContentTitleEdit}";

			AssertElementPresent.pauseAssertVisible(locator1 = "WC#ENTRY_LIST_THUMBNAIL");

			AssertTextEquals.assertPartialText(
				locator1 = "WC#ENTRY_LIST_TITLE",
				value1 = "${webContentTitleEdit}");
		}
	}

	macro editDisplayPage {
		Panel.expandPanel(panel = "Display Page");

		Click(locator1 = "WCSelectDisplayPage#USE_SPECIFIC_DISPLAY_PAGE");

		AssertClick(
			locator1 = "Button#CHOOSE",
			value1 = "Choose");

		Pause(locator1 = "5000");

		SelectFrameTop();

		SelectFrame(locator1 = "WCSelectDisplayPage#DISPLAY_PAGE_CONFIGURATION_IFRAME");

		if ("${pageType}" == "Public Pages") {
			Navigator.gotoNavItem(navItem = "Public Pages");

			Portlet.expandTree();

			Portlet.selectTreeNode(nodeName = "${pageName}");
		}
		else {
			Navigator.gotoNavItem(navItem = "${pageType}");

			AssertClick(
				key_cardText = "${pageName}",
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${pageName}");
		}

		SelectFrameTop();

		Button.clickDone();

		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "IFrame#EDIT_ASSET")) {
			SelectFrame(locator1 = "IFrame#EDIT_ASSET");
		}

		AssertTextEquals.assertPartialText(
			locator1 = "WCEditWebContent#DISPLAY_PAGE_SELECTION",
			value1 = "${pageName}");

		Button.clickPublish();
	}

	macro editDisplayPagePGViaAP {
		AssetPublisherPortlet.gotoEditAssetPG(assetTitle = "${assetTitle}");

		WebContent.editDisplayPage(
			pageName = "${displayPageName}",
			pageType = "${pageType}");

		Pause(locator1 = "3000");

		SelectFrame(value1 = "relative=top");
	}

	macro editFriendlyURL {
		Panel.expandPanel(panel = "Friendly URL");

		Type(
			locator1 = "TextInput#FRIENDLY_URL",
			value1 = "${webContentFriendlyURL}");
	}

	macro editInReview {
		WebContent.addCP(webContentTitle = "${webContentTitleEdit}");

		if (isSet(webContentImage)) {
			Click(locator1 = "Button#SELECT");

			SelectFrameTop(value1 = "relative=top");

			ItemSelector.uploadFile(
				navTab = "Web Content Images",
				uploadFileName = "${webContentImage}");
		}

		SelectFrame(locator1 = "IFrame#DIALOG");

		PortletEntry.save();
	}

	macro editNullCP {
		WebContent.addCP(webContentTitle = "");

		if (isSet(webContentContent)) {
			AlloyEditor.addContent(content = "${webContentContent}");
		}

		AssertClick(
			locator1 = "Button#PUBLISH",
			value1 = "Publish");

		Alert.viewRequiredField();
	}

	macro editPGViaWCD {
		WebContent.addCP(webContentTitle = "${webContentTitleEdit}");

		if (isSet(webContentText)) {
			Type(
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "${webContentText}");
		}
		else {
			AlloyEditor.addContent(content = "${webContentContentEdit}");
		}

		Click(
			locator1 = "Button#PUBLISH",
			value1 = "Publish");

		SelectFrame(value1 = "relative=top");

		if ("${webContentJavascript}" == "true") {
			AssertTextEquals(
				locator1 = "//div[@class='journal-content-article']//span[contains(@class,'portlet-title-text')]",
				value1 = "${embeddedPortletTitle}");
		}
		else {
			AssertTextEquals(
				locator1 = "WCD#WEB_CONTENT_CONTENT",
				value1 = "${webContentContentEdit}");
		}
	}

	macro expireCP {
		for (var webContentTitle : list "${webContentTitleList}") {
			Check(
				key_webContentTitle = "${webContentTitle}",
				locator1 = "WC#ENTRY_LIST_CHECKBOX");
		}

		Click(locator1 = "Icon#EXPIRE");

		Alert.viewSuccessMessage();
	}

	macro expireSpecificVersionCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "View History",
			rowEntry = "${webContentTitle}");

		if (isSet(webContentList)) {
			for (var webContentTitle : list "${webContentList}") {
				Check(
					key_webContentTitle = "${webContentTitle}",
					locator1 = "WCEditWebContent#VIEW_HISTORY_VERSION_CHECKBOX");
			}
		}
		else {
			Check(
				key_webContentTitle = "${webContentTitle}",
				locator1 = "WCEditWebContent#VIEW_HISTORY_VERSION_CHECKBOX");
		}

		ClickNoError(locator1 = "Icon#EXPIRE");

		AssertConfirm(value1 = "Are you sure you want to expire the selected version?");
	}

	macro getAssetId {
		if (isSet(webContentAsset)) {
			var webContentId = selenium.getAttribute("//li[contains(@data-title,'${webContentAsset}')]//input@value");

			return "${webContentId}";
		}

		if (isSet(folderAsset)) {
			var folderId = selenium.getAttribute("//li[contains(@data-title,'${folderAsset}')]@data-folder-id");

			return "${folderId}";
		}
	}

	macro moveArticlesToFolderCP {
		for (var webContentTitle : list "${webContentArticlesList}") {
			Check(
				key_webContentTitle = "${webContentTitle}",
				locator1 = "WC#ENTRY_LIST_CHECKBOX");
		}

		Click(locator1 = "Icon#MOVE");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Move Web Content");

		Click(locator1 = "Button#PARENT_FOLDER_SELECT");

		SelectFrame(locator1 = "IFrame#DIALOG");

		Portlet.expandTree();

		Portlet.selectTreeNode(nodeName = "${folderName}");

		SelectFrameTop();

		Button.clickDone();

		AssertClick(
			locator1 = "Button#MOVE",
			value1 = "Move");

		Alert.viewSuccessMessage();
	}

	macro moveMultipleToFolderCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		for (var rowEntry : list "${rowEntryCheckList}") {
			Check(
				key_rowEntry = "${rowEntry}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");
		}

		Click(locator1 = "Icon#MOVE");

		AssertClick(
			locator1 = "Button#SELECT",
			value1 = "Select");

		SelectFrame(locator1 = "IFrame#DIALOG");

		Portlet.expandTree();

		Portlet.selectTreeNode(nodeName = "${folderName}");

		SelectFrame(value1 = "relative=top");

		Pause(locator1 = "5000");

		Button.clickDone();

		AssertClick(
			locator1 = "Button#MOVE",
			value1 = "Move");

		Alert.viewSuccessMessage();
	}

	macro moveToFolderCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move",
			rowEntry = "${webContentTitle}");

		AssertTextEquals(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#MOVE_WEB_CONTENT_ARTICLE",
			value1 = "${webContentTitle}");

		AssertClick(
			locator1 = "Button#SELECT",
			value1 = "Select");

		SelectFrame(locator1 = "IFrame#DIALOG");

		Portlet.expandTree();

		Portlet.selectTreeNode(nodeName = "${folderName}");

		SelectFrame(value1 = "relative=top");

		Pause(locator1 = "5000");

		Button.clickDone();

		AssertClick(
			locator1 = "Button#MOVE",
			value1 = "Move");

		Alert.viewSuccessMessage();
	}

	macro moveToRecycleBinCP {
		WebContent.viewTitle(
			webContentTitle = "${webContentTitle}",
			webContentTitleEscapeCharacter = "${webContentTitleEscapeCharacter}");

		if (!(contains("${webContentTitleEscapeCharacter}", "{webContentTitleEscapeCharacter}"))) {
			var key_webContentTitle = "${webContentTitleEscapeCharacter}";
		}
		else {
			var key_webContentTitle = "${webContentTitle}";
		}

		Click(locator1 = "WC#ENTRY_LIST_MENULIST_ICON");

		AssertClick.assertPartialTextClickAt(
			key_menuItem = "Move to Recycle Bin",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Move to Recycle Bin");

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}

	macro moveToRecycleBinViaDeleteIconCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		PortletEntry.selectAll();

		Click(locator1 = "Icon#MOVE_TO_RECYCLE_BIN");

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}

	macro openWebContentAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_journal_web_portlet_JournalPortlet");
	}

	macro paginateRelatedAssets {
		WebContentNavigator.gotoEditCP(webContentTitle = "${webContentTitle}");

		Panel.expandPanel(panel = "Related Assets");

		AssertClick(
			locator1 = "AssetRelatedAssets#RELATED_ASSETS_SELECT",
			value1 = "Select");

		AssertClick(
			key_assetType = "${assetType}",
			locator1 = "APConfiguration#ASSET_SELECTION_MANUAL_ASSET_ENTRIES_ASSET",
			value1 = "${assetType}");

		SelectFrame(locator1 = "AssetRelatedAssets#SELECT_ASSET_IFRAME");

		AssertClick.assertPartialTextClickAt(
			locator1 = "Pagination#ITEMS_PER_PAGE_SELECT",
			value1 = "20 Entries");

		MenuItem.click(menuItem = "${itemsPerPage}");
	}

	macro preview {
		AssertTextEquals(
			locator1 = "WCPreview#PREVIEW_TITLE",
			value1 = "${webContentTitle}");

		SelectFrame(locator1 = "WCPreview#PREVIEW_IFRAME");

		if (isSet(fieldName)) {
			var webContentText = "${fieldName}: ${webContentText}";
		}

		AssertTextEquals(
			locator1 = "WCPreview#BODY_FIELD",
			value1 = "${webContentText}");
	}

	macro publishViaWCD {
		Button.clickPublish();

		SelectFrameTop();

		AssertVisible(locator1 = "WCD#WEB_CONTENT_CONTENT");
	}

	macro saveAsDraftViaAP {
		Button.clickSaveAsDraft();

		Pause(locator1 = "5000");

		SelectFrame(value1 = "relative=top");

		Refresh();

		AssertElementNotPresent(locator1 = "AP#ASSET_ABSTRACT_GENERIC");
	}

	macro searchCP {
		Search.searchCP(searchTerm = "${webContentTitle}");

		AssertTextEquals(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${webContentTitle}");
	}

	macro searchNoResultsCP {
		Search.searchCP(searchTerm = "${searchQuery}");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No web content was found.");

		AssertTextNotPresent(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${webContentTitle}");

		Search.searchCP(searchTerm = "");

		AssertTextPresent(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${webContentTitle}");
	}

	macro selectCategory {
		Panel.expandPanel(panel = "Metadata");

		if (isSet(userSegmentDetailTitle)) {
			AssertTextEquals(
				key_userSegmentDetailTitle = "${userSegmentDetailTitle}",
				locator1 = "WCEditWebContent#USER_SEGMENT_DETAIL_TITLE",
				value1 = "${userSegmentDetailTitle}");
		}

		AssetCategorization.addCategory(
			categoryName = "${categoryName}",
			vocabularyName = "${vocabularyName}");
	}

	macro selectTemplateCP {
		ScrollBy(value1 = "0, 50");

		Panel.expandPanel(panel = "Structure and Template");

		AssertTextEquals(
			locator1 = "WCEditWebContent#SELECTED_STRUCTURE",
			value1 = "${structureName}");

		AssertClick(
			locator1 = "WCEditWebContent#SELECT_TEMPLATE",
			value1 = "Select");

		IFrame.selectTopFrame();

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertClickNoError(
			key_rowEntry = "${templateName}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${templateName}");

		AssertConfirm.assertConfirmationNoWait(value1 = "Editing the current template deletes all unsaved content.");

		IFrame.selectTopFrame();

		if (IsElementPresent(locator1 = "IFrame#EDIT_ASSET")) {
			IFrame.selectEditAssetFrame();
		}

		Panel.expandPanel(panel = "Structure and Template");

		AssertTextEquals(
			locator1 = "WCEditWebContent#SELECTED_TEMPLATE",
			value1 = "${templateName}");
	}

	macro subscribeToCurrentFolderCP {
		Click.clickAt(locator1 = "Icon#INFO");

		Pause(locator1 = "1000");

		Click(locator1 = "Icon#SUBSCRIBE");

		Alert.viewSuccessMessage();
	}

	macro subscribeToSpecificArticle {
		Click(
			key_rowEntry = "${webContentTitle}",
			locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Subscribe");

		Alert.viewSuccessMessage();
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		if (IsElementPresent.pauseIsElementPresent(locator1 = "WC#ENTRY_LIST_THUMBNAIL_GENERIC")) {
			PortletEntry.selectAll();

			Click(locator1 = "Icon#MOVE_TO_RECYCLE_BIN");

			AssertElementPresent(locator1 = "Message#SUCCESS");
		}

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Recycle Bin",
			panel = "Site Administration",
			portlet = "Recycle Bin");

		RecycleBin.emptyCP();
	}

	macro uploadAudio {
		Click.pauseClickAt(locator1 = "WCEditWebContent#AUDIO_BUTTON");

		ItemSelector.addFromURL(
			imageSourceURL = "${dmDocumentURL}",
			navTab = "${navTab}");

		SelectFrame(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		AssertElementPresent(locator1 = "CKEditor#CONTENT_AUDIO_LINK_IMAGE");

		IFrame.selectTopFrame();
	}

	macro uploadVideo {
		Click.pauseClickAt(locator1 = "WCEditWebContent#VIDEO_BUTTON");

		ItemSelector.addFromURL(
			imageSourceURL = "${dmDocumentURL}",
			navTab = "${navTab}");

		SelectFrame(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		AssertElementPresent(locator1 = "CKEditor#CONTENT_VIDEO_LINK_IMAGE");

		IFrame.selectTopFrame();
	}

	macro viewApprovedStatus {
		var key_webContentTitle = "${webContentTitle}";

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "WC#ENTRY_TABLE_WORKFLOW_STATUS",
			value1 = "Approved");
	}

	macro viewAudioViaWCD {
		AssertElementPresent(locator1 = "WCD#WEB_CONTENT_AUDIO");
	}

	macro viewAvailableLanguagesPGViaWCD {
		Click(
			key_fieldName = "${fieldName}",
			locator1 = "Button#LOCALIZATION");

		AssertElementPresent(
			key_locale = StringUtil.replace("${locale1}", "-", "_"),
			locator1 = "Translation#LOCALIZATION_NAME");

		AssertElementNotPresent(
			key_locale = StringUtil.replace("${locale2}", "-", "_"),
			locator1 = "Translation#LOCALIZATION_NAME");
	}

	macro viewCategorizationCategoryCP {
		Panel.expandPanel(panel = "Metadata");

		for (var categoryName : list "${categoryNameList}") {
			AssertTextEquals(
				key_categoryName = "${categoryName}",
				locator1 = "AssetCategorization#CATEGORY",
				value1 = "${categoryName}");
		}
	}

	macro viewCategorizationCategoryNotVisibleCP {
		Panel.expandPanel(panel = "Metadata");

		AssertElementNotPresent(
			key_categoryName = "${categoryName}",
			locator1 = "AssetCategorization#CATEGORY");
	}

	macro viewCategorizationVocabularyCP {
		Panel.expandPanel(panel = "Metadata");

		var count = "1";

		for (var vocabularyName : list "${vocabularyNameList}") {
			AssertTextEquals(
				key_count = "${count}",
				locator1 = "AssetCategorization#VOCABULARY_LIST_ITEM",
				value1 = "${vocabularyName}");

			var count = ${count} + 1;
		}
	}

	macro viewCompareVersions {
		SelectFrame(locator1 = "IFrame#DIALOG");

		LexiconEntry.gotoEntry(rowEntry = "${version}");

		SelectFrameTop();

		AssertTextEquals(
			locator1 = "WCViewHistory#VIEW_COMPARE_VERSIONS_CONTENT",
			value1 = "${newContent}");
	}

	macro viewConversionLinksPGViaAP {
		WebContent.viewPGViaAP(
			author = "${author}",
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		for (var documentType : list "${documentTypes}") {
			var documentTypeUppercase = StringUtil.upperCase("${documentType}");
			var key_documentType = "${documentTypeUppercase}";

			AssertTextEquals.assertPartialText(
				locator1 = "APAsset#DOCUMENT_CONVERSION_NAME",
				value1 = "${documentTypeUppercase}");

			AssertElementPresent(locator1 = "APAsset#DOCUMENT_CONVERSION_NAME_ICON");
		}
	}

	macro viewCP {
		if (isSet(webContentTitleEscapeCharacter)) {
			var key_webContentTitle = "${webContentTitleEscapeCharacter}";
		}
		else {
			var key_webContentTitle = "${webContentTitle}";
		}

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");

		if (isSet(webContentText)) {
			AssertTextEquals(
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "${webContentText}");
		}

		if (isSet(webContentDocumentsAndMedia)) {
			AssertTextEquals(
				key_fieldName = "DocumentsAndMedia",
				locator1 = "TextInput#DDM",
				value1 = "${webContentDocumentsAndMedia}");
		}
		else {
			AssertTextEquals(
				key_editor = "content",
				locator1 = "AlloyEditor#EDITOR",
				value1 = "${webContentContent}");

			SelectFrame(value1 = "relative=top");
		}
	}

	macro viewCPWithoutContentField {
		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");

		if ("${field}" == "Web Content") {
			var key_fieldData = "${fieldData}";
			var key_fieldFieldLabel = "${fieldFieldLabel}";
			var key_fieldName = "${fieldName}";

			AssertTextEquals(
				locator1 = "DDMField#WEB_CONTENT",
				value1 = "${fieldFieldLabel}");

			AssertTextEquals(
				locator1 = "DDMField#WEB_CONTENT_TITLE_SELECTED",
				value1 = "${fieldData}");
		}

		if ("${field}" == "Geolocation") {
			AssertTextEquals(
				key_fieldFieldLabel = "${fieldFieldLabel}",
				key_fieldName = "${fieldName}",
				locator1 = "DDMField#GEOLOCATION",
				value1 = "${fieldFieldLabel}");
		}

		if ("${field}" == "Image") {
			AssertTextEquals(
				locator1 = "WCEditWebContent#SELECTED_IMAGE",
				value1 = "${webContentImage}");
		}
	}

	macro viewDefaultCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No web content was found.");

		var key_webContentTitle = "${webContentTitle}";

		AssertElementNotPresent(locator1 = "WC#ENTRY_LIST_THUMBNAIL");

		AssertElementNotPresent(locator1 = "WC#ENTRY_LIST_TITLE");
	}

	macro viewDisplayStyleCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		AssertElementPresent.pauseAssertVisible(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_CARDS_THUMBNAIL");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertElementPresent.pauseAssertVisible(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_THUMBNAIL");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertElementPresent(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_TABLE_TITLE");
	}

	macro viewEditWCViaWCD {
		AssertElementPresent(locator1 = "WCD#WEB_CONTENT_CONTENT");

		WebContentNavigator.gotoArticleMenuPGViaWCD(webContentTitle = "${webContentTitle}");

		AssertTextEquals(
			key_menuItem = "Edit Web Content",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit Web Content");
	}

	macro viewEmailConfigurationCP {
		SiteAdmin.gotoPortletConfiguration();

		AssertTextEquals(
			locator1 = "NavTab#EMAIL_FROM",
			value1 = "Email From");

		AssertTextEquals(
			locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_ADDED_EMAIL",
			value1 = "Web Content Added Email");

		AssertTextEquals(
			locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_REVIEW_EMAIL",
			value1 = "Web Content Review Email");

		AssertTextEquals(
			locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_UPDATED_EMAIL",
			value1 = "Web Content Updated Email");

		if ("${webContentWorkflowEnabled}" == "true") {
			AssertTextEquals(
				locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_DENIED_EMAIL",
				value1 = "Web Content Denied Email");

			AssertTextEquals(
				locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_GRANTED_EMAIL",
				value1 = "Web Content Granted Email");

			AssertTextEquals(
				locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_REQUESTED_EMAIL",
				value1 = "Web Content Requested Email");
		}
		else {
			AssertElementNotPresent(locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_DENIED_EMAIL");

			AssertElementNotPresent(locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_GRANTED_EMAIL");

			AssertElementNotPresent(locator1 = "WCConfiguration#NAVIGATION_WEB_CONTENT_REQUESTED_EMAIL");
		}
	}

	macro viewEntryAsWorkflowAsset {
		SiteAdmin.viewPortletTitle(portletTitle = "${webContentTitle}");

		AssertTextEquals(
			key_webContentContent = "${webContentContent}",
			locator1 = "WCD#WEB_CONTENT_CONTENT",
			value1 = "${webContentContent}");
	}

	macro viewExpirationDateCP {
		Panel.expandPanel(panel = "Schedule");

		AssertNotChecked(
			locator1 = "Checkbox#NEVER_EXPIRE",
			value1 = "Never Expire");

		AssertTextEquals(
			locator1 = "TextInput#EXPIRATION_DATE",
			value1 = "${expirationDate}");
	}

	macro viewFieldRepeated {
		AssertTextEquals(
			locator1 = "xpath=(//div[@data-fieldname='${fieldName}'][1]//a[contains(@class,'ddm-repeatable-add-button')]/../div//input)",
			value1 = "${ddlRecordFieldData}");

		AssertTextEquals(
			locator1 = "xpath=(//div[@data-fieldname='${fieldName}'][2]//a[contains(@class,'ddm-repeatable-add-button')]/../div//input)",
			value1 = "${ddlRecordFieldData} 2");

		var i = "3";

		while (IsElementPresent(locator1 = "xpath=(//div[@data-fieldname='${fieldName}'])[${i}]")) {
			AssertTextEquals(
				locator1 = "xpath=(//div[@data-fieldname='${fieldName}'][${i}]//a[contains(@class,'ddm-repeatable-add-button')]/../div//input)",
				value1 = "${ddlRecordFieldData} ${i}");

			var i = ${i} + 1;
		}
	}

	macro viewFriendlyURLCP {
		Panel.expandPanel(panel = "Friendly URL");

		AssertTextEquals(
			locator1 = "TextInput#FRIENDLY_URL",
			value1 = "${friendlyUrl}");
	}

	macro viewLinkedAssetPGViaAP {
		AssertClick.assertPartialTextClickAt(
			key_assetTitle = "${webContentTitle}",
			locator1 = "APAsset#ASSET_TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals(
			locator1 = "APAsset#ASSET_TITLE_HEADER",
			value1 = "${webContentTitle}");

		AssertTextEquals.assertPartialText(
			key_assetContent = "${webContentContent}",
			locator1 = "APAsset#ASSET_CONTENT",
			value1 = "${webContentContent}");
	}

	macro viewNestedField {
		AssertTextEquals(
			key_fieldName = "${fieldName}",
			key_nestedFieldName = "${nestedField}",
			locator1 = "DDMField#NESTED",
			value1 = "${nestedFieldData}");
	}

	macro viewNoContentAsGuestPGViaWCD {
		AssertTextNotPresent(
			locator1 = "Message#ERROR",
			value1 = "The selected web content no longer exists.");

		AssertElementNotPresent(
			key_portletName = "Web Content Display",
			locator1 = "Portlet#BODY");
	}

	macro viewNoConversionLinksPGViaAP {
		WebContent.viewPGViaAP(
			author = "${author}",
			webContentContent = "${webContentContent}",
			webContentTitle = "${webContentTitle}");

		for (var documentType : list "${documentTypes}") {
			var documentTypeUppercase = StringUtil.upperCase("${documentType}");

			AssertElementNotPresent(
				key_documentType = "${documentTypeUppercase}",
				locator1 = "APAsset#DOCUMENT_CONVERSION_NAME");
		}
	}

	macro viewNoEditWCCP {
		var key_webContentTitle = "${webContentTitle}";

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(locator1 = "WC#ENTRY_LIST_MENULIST_ICON");

		AssertElementNotPresent(
			key_menuItem = "Edit",
			locator1 = "MenuItem#ANY_MENU_ITEM");
	}

	macro viewNoEditWCViaAP {
		AssertElementPresent(
			key_assetTitle = "${assetTitle}",
			locator1 = "APAsset#ASSET_TITLE");

		AssertElementNotPresent(
			key_assetTitle = "${assetTitle}",
			locator1 = "AP#ASSET_ELLIPSIS_ICON");
	}

	macro viewNoEditWCViaWCD {
		AssertElementPresent(locator1 = "WCD#WEB_CONTENT_CONTENT");

		AssertElementNotPresent(
			key_portletName = "Web Content Display",
			locator1 = "Portlet#SPECIFIC_BORDERLESS_ELLIPSIS_ICON");
	}

	macro viewNoPermissionCP {
		SiteAdmin.viewPortletTitle(portletTitle = "Web Content");

		AssertElementPresent(locator1 = "NavBar#CONTAINER");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No web content was found.");
	}

	macro viewNotPresentCP {
		AssertElementNotPresent(locator1 = "TextInput#TITLE");
	}

	macro viewPageBreak {
		AssertTextEquals.assertPartialText(
			locator1 = "Pagination#PAGES",
			value1 = "Pages");
	}

	macro viewPaginateRelatedAssets {
		AssertTextEquals.assertPartialText(
			locator1 = "Pagination#ITEMS_PER_PAGE_SELECT",
			value1 = "${itemsPerPage}");

		AssertTextEquals(
			locator1 = "Pagination#CURRENT_PAGE_SELECT",
			value1 = "${numberOfPages}");
	}

	macro viewPGViaAP {
		var key_assetContent = "${webContentContent}";
		var key_assetTitle = "${webContentTitle}";

		AssertTextEquals(
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "AP#ASSET_ABSTRACT_DESCRIPTION",
			value1 = "${webContentContent}");

		if (isSet(author)) {
			AssertTextEquals.assertPartialText(
				key_author = "${author}",
				locator1 = "AP#METADATA_AUTHOR",
				value1 = "${author}");
		}

		AssertClick(
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "${webContentTitle}");

		AssertVisible(locator1 = "Icon#SHARE");

		AssertTextEquals(
			locator1 = "APAsset#ASSET_TITLE_HEADER",
			value1 = "${webContentTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "APAsset#ASSET_VIEW_CONTENT",
			value1 = "${webContentContent}");
	}

	macro viewPGViaWCD {
		if ("${draft}" == "true") {
			AssertTextEquals(
				locator1 = "Message#ERROR_3",
				value1 = "${webContentTitle} is not approved.");
		}
		else if ("${expiredContent}" == "true") {
			AssertTextEquals(
				locator1 = "Message#ERROR_3_ALT",
				value1 = "${webContentTitle} is expired.");
		}
		else if ("${image}" == "true") {
			AssertElementPresent(locator1 = "WCD#WEB_CONTENT_CONTENT_IMAGE");
		}
		else if ("${imageOnLive}" == "true") {
			AssertElementPresent(locator1 = "WCD#WEB_CONTENT_CONTENT_IMAGE_ON_LIVE");
		}
		else if ("${linkToPage}" == "true") {
			var key_contentText = "${linkName}";

			AssertTextEquals.assertPartialText(
				locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL",
				value1 = "${linkName}");
		}
		else {
			AssertTextEquals(
				locator1 = "WCD#WEB_CONTENT_CONTENT",
				value1 = "${webContentContent}");
		}
	}

	macro viewRelatedAssetsPGViaWCD {
		AssertTextEquals(
			locator1 = "Relatedassets#ASSET_TITLE_1",
			value1 = "${assetTitle}");
	}

	macro viewRTLLayout {
		AssertCssValue(
			locator1 = "TextInput#TITLE",
			locator2 = "direction",
			value1 = "rtl");
	}

	macro viewSelectedTemplateCP {
		Panel.expandPanel(panel = "Structure and Template");

		AssertTextEquals(
			locator1 = "WCEditWebContent#SELECTED_TEMPLATE",
			value1 = "${templateName}");
	}

	macro viewSpecificVersionHistoryCP {
		var key_webContentTitle = "${webContentTitle}";

		AssertTextEquals(
			locator1 = "WCEditWebContent#VIEW_HISTORY_VERSION_TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals(
			locator1 = "WCEditWebContent#VIEW_HISTORY_VERSION_STATUS",
			value1 = "${webContentStatus}");
	}

	macro viewStatus {
		AssertTextEquals(
			locator1 = "AssetWorkflow#WORKFLOW_STATUS",
			value1 = "${webContentStatus}");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals(
			key_editor = "content",
			locator1 = "AlloyEditor#EDITOR",
			value1 = "${webContentContent}");

		SelectFrameTop();
	}

	macro viewStructureAndTemplateLocalization {
		if (isSet(structureLocalizationName)) {
			Panel.expandPanel(panel = "${panel}");

			AssertTextEquals(
				locator1 = "WCEditWebContent#SELECTED_STRUCTURE",
				value1 = "${structureLocalizationName}");
		}

		if (isSet(templateLocalizationName)) {
			Panel.expandPanel(panel = "${panel}");

			AssertTextEquals(
				locator1 = "WCEditWebContent#SELECTED_TEMPLATE",
				value1 = "${templateLocalizationName}");
		}
	}

	macro viewStructureViaAddMenuCP {
		LexiconEntry.gotoAdd();

		AssertTextEquals(
			key_menuItem = "${structureName}",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "${structureName}");
	}

	macro viewStructureWithWorkflowCP {
		WebContentNavigator.gotoViewStatus(
			webContentStatus = "Pending",
			webContentTitle = "${webContentTitle}");

		AssertTextEquals(
			locator1 = "AssetWorkflow#WORKFLOW_STATUS",
			value1 = "Pending (${workflowTask})");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");
	}

	macro viewTableEntryCP {
		var key_feedName = "${feedName}";

		AssertTextEquals(
			locator1 = "DDMSelectFeed#FEED_TABLE_NAME",
			value1 = "${feedName}");
	}

	macro viewTitle {
		if (!(contains("${webContentTitleEscapeCharacter}", "{webContentTitleEscapeCharacter}"))) {
			var key_webContentTitle = "${webContentTitleEscapeCharacter}";
		}
		else {
			var key_webContentTitle = "${webContentTitle}";
		}

		if ("${onLive}" == "true") {
			AssertTextEquals.assertPartialText(
				locator1 = "WC#ENTRY_LIST_TITLE_ON_LIVE",
				value1 = "${webContentTitle}");
		}
		else {
			AssertTextEquals.assertPartialText(
				locator1 = "WC#ENTRY_LIST_TITLE",
				value1 = "${webContentTitle}");
		}
	}

	macro viewUnlocalizableTextBox {
		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");

		FormFields.viewTextMultilineValue(
			fieldName = "TextBox",
			fieldValue = "${webContentTextBox}");

		PortletEntry.changeLocale(
			fieldName = "Title",
			locale = "es-ES");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${localizedTitle}");

		FormFields.viewTextValue(
			fieldName = "Text",
			fieldValue = "${localizedText}");

		FormFields.viewTextMultilineValue(
			fieldName = "TextBox",
			fieldValue = "${localizedTextBox}");
	}

	macro viewUserEditCP {
		AssertTextEquals.assertPartialText(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_LAST_MODIFIED",
			value1 = "${userFirstLast}");

		WebContent.viewVersion(
			version = "1.1",
			webContentTitle = "${webContentTitle}");
	}

	macro viewVersion {
		WebContentNavigator.gotoEditCP(webContentTitle = "${webContentTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "WCEditWebContent#VERSION",
			value1 = "${version}");
	}

	macro viewVideoViaWCD {
		AssertElementPresent(locator1 = "WCD#WEB_CONTENT_VIDEO");
	}

	macro viewWCViewableByPermissions {
		PortletEntry.inputTitle(title = "Web Content Title");

		Panel.expandPanel(panel = "Permissions");

		AssertSelectedLabel(
			locator1 = "Select#PERMISSIONS",
			value1 = "Site Members");
	}

	macro viewWithStructureCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		WebContentNavigator.gotoEditCP(
			webContentTitle = "${webContentTitle}",
			webContentTitleEscapeCharacter = "${webContentTitleEscapeCharacter}");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");

		if (isSet(description)) {
			AssertTextEquals(
				key_editor = "description",
				locator1 = "AlloyEditor#EDITOR",
				value1 = "${description}");
		}

		if (isSet(webContentImage)) {
			Click(locator1 = "Button#PREVIEW");

			SelectFrameTop(value1 = "relative=top");

			SikuliAssertElementPresent(locator1 = "DDMField#IMAGE_PREVIEW_PNG");
		}

		if (isSet(webContentText)) {
			AssertTextEquals(
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "${webContentText}");
		}

		if (isSet(webContentTextBox)) {
			if (isSet(fieldName)) {
				AssertTextEquals(
					key_fieldName = "${fieldName}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
			else {
				AssertTextEquals(
					key_fieldName = "TextBox",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");
			}
		}

		if (isSet(webContentTextBox2)) {
			var key_fieldName = "${fieldName2}";

			AssertTextEquals(
				locator1 = "DDMField#TEXT_BOX",
				value1 = "${webContentTextBox2}");
		}

		if (isSet(webContentTextBoxList)) {
			var count = "1";

			for (var webContentTextBox : list "${webContentTextBoxList}") {
				AssertTextEquals(
					key_fieldName = "heading${count}",
					locator1 = "DDMField#TEXT_BOX",
					value1 = "${webContentTextBox}");

				var count = ${count} + 1;
			}
		}
	}

	macro viewWithWorkflowCP {
		WebContentNavigator.gotoViewStatus(
			webContentStatus = "Pending",
			webContentTitle = "${webContentTitle}");

		AssertTextEquals(
			locator1 = "AssetWorkflow#WORKFLOW_STATUS",
			value1 = "Pending (${workflowTask})");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals(
			key_editor = "content",
			locator1 = "AlloyEditor#EDITOR",
			value1 = "${webContentContent}");

		SelectFrameTop();
	}

	macro viewWithWorkflowPGViaAP {
		AssertTextNotPresent(value1 = "${webContentTitle}");
	}

	macro viewWithWorkflowPGViaWCD {
		AssertTextEquals(
			locator1 = "Message#ERROR_3",
			value1 = "${webContentTitle} is not approved.");

		AssertClick(
			locator1 = "Message#ERROR_3_LINK",
			value1 = "${webContentTitle} is not approved.");

		AssertTextEquals(
			locator1 = "AssetWorkflow#WORKFLOW_STATUS",
			value1 = "Pending (${workflowTask})");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals(
			key_editor = "content",
			locator1 = "AlloyEditor#EDITOR",
			value1 = "${webContentContent}");
	}

}