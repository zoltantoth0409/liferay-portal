definition {
	var ddlDataDefinitionDescription = "Data Definition Description";
	var ddlDataDefinitionName = "Data Definition Name";
	var ddlListName = "List Name";

	macro _add {
		if (IsElementNotPresent(locator1 = "Portlet#HEADER")) {
			var key_iframeTitle = "Data Definitions";

			AssertTextEquals(locator1 = "IFrame#IFRAME_TITLE", value1 = "Data Definitions");

			IFrame.selectDialogFrame();

			var key_assetTitle = "Data Definitions";

			AssertTextEquals(locator1 = "NavBar#ASSET_TITLE", value1 = "Data Definitions");
		}
		else if (IsTextEqual(locator1 = "Portlet#HEADER", value1 = "Data Definitions")) {
			AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Data Definitions");
		}
		else {
			AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Structures");
		}

		LexiconEntry.gotoAdd();

		DDLDataDefinition.editName(ddlDataDefinitionName = "${ddlDataDefinitionName}");

		DDLDataDefinition._showDetails();

		if (isSet(ddlDataDefinitionDescription)) {
			Type.clickAtType(
				locator1 = "TextInput#DESCRIPTION",
				value1 = "${ddlDataDefinitionDescription}"
			);
		}
	}

	macro _select {
		IFrame.selectDialogFrame();

		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		AssertClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = "${ddlDataDefinitionName}"
		);
	}

	macro _showDetails {
		if (IsElementNotPresent.isNotVisible(locator1 = "TextInput#DESCRIPTION")) {
			Click(locator1 = "DDMEditStructure#DETAILS_TOGGLE");

			if (IsElementNotPresent.isNotVisible(locator1 = "TextInput#DESCRIPTION")) {
				Click(locator1 = "DDMEditStructure#DETAILS_TOGGLE");
			}
		}

		AssertVisible(locator1 = "TextInput#DESCRIPTION");
	}

	macro addCP {
		DDLNavigator.gotoManageDataDefinitions();

		DDLDataDefinition._add(
			ddlDataDefinitionDescription = "${ddlDataDefinitionDescription}",
			ddlDataDefinitionName = "${ddlDataDefinitionName}"
		);
	}

	macro addViaDDLDisplayPG {
		DDLDataDefinition._add(
			ddlDataDefinitionDescription = "${ddlDataDefinitionDescription}",
			ddlDataDefinitionName = "${ddlDataDefinitionName}"
		);

		SelectFrame(value1 = "relative=top");

		DynamicDataMapping.selectDynamicDataMappingFrame();
	}

	macro clickSave {
		AssertClick(locator1 = "DDMEditStructure#SAVE_BUTTON", value1 = "Save");
	}

	macro copyCP {
		DDLNavigator.gotoManageDataDefinitions();

		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		Click.waitForMenuToggleJSClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS"
		);

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Copy",
			key_menuItem = "Copy"
		);

		PortletEntry.inputName(name = "${ddlDataDefinitionCopyName}");

		if ("${copyFormTemplates}" == "true") {
			Check(locator1 = "DDMSelectTemplate#COPY_FORM_TEMPLATES_CHECKBOX");
		}

		if ("${copyDisplayTemplates}" == "true") {
			Check(locator1 = "DDMSelectTemplate#COPY_DISPLAY_TEMPLATES_CHECKBOX");
		}

		AssertClick(locator1 = "Button#COPY", value1 = "Copy");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		DDLDataDefinition.viewName(
			ddlDataDefinitionName = "${ddlDataDefinitionCopyName}"
		);
	}

	macro deleteCP {
		DDLNavigator.gotoManageDataDefinitions();

		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		Click.waitForMenuToggleJSClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS"
		);

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);

		DDLNavigator.gotoManageDataDefinitions();

		AssertElementNotPresent(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS",
			key_ddlDataDefinitionName = "${ddlDataDefinitionName}"
		);
	}

	macro deleteNoCP {
		DDLNavigator.gotoManageDataDefinitions();

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${ddlDataDefinitionName}"
		);

		Click(locator1 = "Icon#BASIC_SEARCH");

		Click.waitForMenuToggleJSClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1"
		);

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_2",
			value1 = "The structure cannot be deleted because it is required by one or more structure links."
		);
	}

	macro editCP {
		DDLNavigator.gotoManageDataDefinitions();

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${ddlDataDefinitionName}"
		);

		Click(locator1 = "Icon#BASIC_SEARCH");

		Click.waitForMenuToggleJSClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1"
		);

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "${ddlDataDefinitionNameEdit}"
		);

		DDLDataDefinition._showDetails();

		Type.clickAtType(
			locator1 = "TextInput#DESCRIPTION",
			value1 = "${ddlDataDefinitionDescriptionEdit}"
		);
	}

	macro editName {
		Type.clickAtType(
			locator1 = "DDMEditStructure#NAME_FIELD",
			value1 = "${ddlDataDefinitionName}"
		);
	}

	macro editPermissionsCP {
		IFrame.selectDialogFrame();

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "${addOrRemoveViewPermission}",
			roleList = "${roleList}"
		);
	}

	macro save {
		DDLDataDefinition.clickSave();

		Alert.viewSuccessMessage();
	}

	macro saveViaDDLDisplayPG {
		DDLDataDefinition.clickSave();

		Alert.viewSuccessMessage();

		SelectFrame(value1 = "relative=top");
	}

	macro selectCP {
		DDLDataDefinition._select(ddlDataDefinitionName = "${ddlDataDefinitionName}");

		SelectFrameTop();

		AssertTextEquals(
			locator1 = "TextInput#DATA_DEFINITION",
			value1 = "${ddlDataDefinitionName}"
		);
	}

	macro selectViaDDLDisplayPG {
		DDLDataDefinition._select(ddlDataDefinitionName = "${ddlDataDefinitionName}");

		SelectFrameTop();

		IFrame.selectDialogFrame();

		AssertTextEquals.assertValue(
			locator1 = "TextInput#DATA_DEFINITION",
			value1 = "${ddlDataDefinitionName}"
		);
	}

	macro setInputFieldLocalization {
		var key_languageValue = "${languageValue}";

		Click(locator1 = "FieldLocalization#OPTION");
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);

		DDLNavigator.gotoManageDataDefinitions();

		var ddlDataDefinitionName = "Data Definition";

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${ddlDataDefinitionName}"
		);

		Click(locator1 = "Icon#BASIC_SEARCH");

		AssertTextNotEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_1",
			value1 = "Contacts"
		);

		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		if (IsElementPresent(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			if ("${kfProcessExists}" == "true") {
				Alert.viewRequestFailedToComplete();

				AssertTextEquals.assertPartialText(
					locator1 = "Message#ERROR_2",
					value1 = "The structure cannot be deleted because it is required by one or more structure links."
				);
			}
			else {
				Alert.viewSuccessMessage();
			}
		}
	}

	macro viewCP {
		DDLNavigator.gotoManageDataDefinitions();

		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "${ddlDataDefinitionName}"
		);

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${ddlDataDefinitionName}"
		);

		if (isSet(ddlDataDefinitionDescription)) {
			DDLDataDefinition._showDetails();

			AssertTextEquals(
				locator1 = "TextInput#DESCRIPTION",
				value1 = "${ddlDataDefinitionDescription}"
			);
		}

		if (isSet(fieldFieldLabel)) {
			var key_fieldFieldLabel = "${fieldFieldLabel}";

			AssertElementPresent(locator1 = "DDMEditStructure#FORM_FIELD");
		}
	}

	macro viewName {
		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
			value1 = "${ddlDataDefinitionName}"
		);
	}

	macro viewParentStructureHeader {
		Click(locator1 = "DDMEditStructure#DETAILS_TOGGLE");

		AssertClick(locator1 = "Button#SELECT", value1 = "Select");

		IFrame.selectDialogFrame();

		var key_assetTitle = "${assetTitle}";

		AssertTextEquals(locator1 = "NavBar#ASSET_TITLE", value1 = "${assetTitle}");
	}
}