definition {
	macro changeLocale {
		Click(locator1 = "Button#LOCALIZATION", key_fieldName = "${fieldName}");

		Click(
			locator1 = "Translation#LOCALIZATION_NAME",
			key_locale = StringUtil.replace("${locale}", "-", "_")
		);
	}

	macro clickEditFromEllipsis {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");
	}

	macro clickMoveToRecycleBin {
		MenuItem.click(menuItem = "Move to Recycle Bin");
	}

	macro deleteViaMenuItem {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${rowEntry}"
		);

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);
	}

	macro deleteViaToolbar {
		for (var rowEntry : list "${rowEntries}") {
			LexiconEntry.selectEntry(key_rowEntry = "${rowEntry}");
		}

		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);
	}

	macro inputDescription {
		Click(locator1 = "TextArea#DESCRIPTION");

		Type(locator1 = "TextArea#DESCRIPTION", value1 = "${description}");
	}

	macro inputDescriptionTextInput {
		Click(locator1 = "TextInput#DESCRIPTION");

		Type(locator1 = "TextInput#DESCRIPTION", value1 = "${description}");
	}

	macro inputLocalizedName {
		PortletEntry.changeLocale(fieldName = "Name", locale = "${language}");

		PortletEntry.inputName(name = "${localizedName}");
	}

	macro inputName {
		Click(locator1 = "TextInput#NAME");

		Type(locator1 = "TextInput#NAME", value1 = "${name}");
	}

	macro inputText {
		Click(
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			key_fieldLabel = "${fieldLabel}"
		);

		Type(
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "${text}",
			key_fieldLabel = "${fieldLabel}"
		);
	}

	macro inputTextArea {
		Click(locator1 = "TextArea#ANY", key_text = "${fieldId}");

		Type(locator1 = "TextArea#ANY", value1 = "${text}", key_text = "${fieldId}");
	}

	macro inputTitle {
		Click(locator1 = "TextInput#TITLE");

		Type(locator1 = "TextInput#TITLE", value1 = "${title}");
	}

	macro publish {
		Button.clickPublish();

		Alert.viewSuccessMessage();
	}

	macro publishAndWait {
		Button.clickPublish();

		Pause(locator1 = "3000");

		Alert.viewSuccessMessage();
	}

	macro save {
		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro saveAsDraft {
		Button.clickSaveAsDraft();

		Alert.viewSuccessMessage();
	}

	macro selectAll {
		if (IsNotChecked.isNotCheckedNotVisible(locator1 = "Checkbox#LEXICON_SELECT_ALL_INPUT")) {
			Click(locator1 = "Checkbox#LEXICON_SELECT_ALL_CONTAINER");
		}
		else if (IsNotChecked(locator1 = "Checkbox#SELECT_ALL")) {
			Click(locator1 = "Checkbox#SELECT_ALL");
		}

		AssertChecked.assertCheckedNotVisible(
			locator1 = "Checkbox#LEXICON_SELECT_ALL_INPUT"
		);
	}

	macro submitForPublication {
		Button.clickSubmitForPublication();

		Alert.viewSuccessMessage();
	}

	macro update {
		Button.clickUpdate();

		Alert.viewSuccessMessage();
	}

	macro viewEmptyMessage {
		var entity = StringUtil.toLowerCase("${entity}");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no ${entity}."
		);
	}

	macro viewLocale {
		AssertElementPresent(
			locator1 = "Translation#CURRENT_LOCALE",
			key_locale = "${locale}"
		);
	}

	macro viewNoNavItemLink {
		AssertElementNotPresent(locator1 = "NavBar#NAV_ITEM_LINK");
	}

	macro viewWorkflowStatus {
		AssertTextEquals(
			locator1 = "Card#GENERIC_SMALL_TEXT",
			value1 = "${workflowStatus}",
			key_rowEntry = "${rowEntry}"
		);
	}
}