definition {

	macro activateStagingCP {
		ProductMenuHelper.openProductMenu();

		AssertTextEquals.assertPartialText(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
			value1 = "${siteName}");

		if ("${remoteStaging}" == "true") {
			Check(locator1 = "Radio#REMOTE_LIVE");

			Type(
				locator1 = "SiteSettingsStaging#REMOTE_STAGING_IP",
				value1 = "localhost");

			if (isSet(remotePort)) {
				Type(
					locator1 = "SiteSettingsStaging#REMOTE_STAGING_PORT",
					value1 = "${remotePort}");
			}
			else {
				Type(
					locator1 = "SiteSettingsStaging#REMOTE_STAGING_PORT",
					value1 = "8080");
			}

			Paste(
				locator1 = "SiteSettingsStaging#REMOTE_STAGING_SITE_ID",
				value1 = "${siteID}");
		}
		else {
			Check(locator1 = "Radio#LOCAL_LIVE");
		}

		if ("${pageVersioningEnabled}" == "true") {
			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PUBLIC_PAGES");

			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PRIVATE_PAGES");
		}

		if (isSet(noContent)) {
			Check(locator1 = "Checkbox#SELECT_ALL");

			Uncheck(locator1 = "Checkbox#SELECT_ALL");
		}
		else if (isSet(contentName)) {
			Check(locator1 = "Checkbox#SELECT_ALL");

			Uncheck(locator1 = "Checkbox#SELECT_ALL");

			Check.checkHiddenToggleSwitch(
				key_contentName = "${contentName}",
				locator1 = "Checkbox#STAGED_CONTENT");
		}
		else if (isSet(contentByContent)) {
			for (var contentName : list "Documents and Media,Mobile Device Families,Web Content") {
				var key_contentName = "${contentName}";

				Uncheck.uncheckToggleSwitchNoErrorNoAssert(locator1 = "Checkbox#STAGED_CONTENT");
			}

			for (var contentName : list "Application Display Template,Blogs,Bookmarks,Calendar,Documents and Media,Forms,Knowledge Base,Message Boards,Mobile Device Families,Polls,Web Content,Wiki") {
				var key_contentName = "${contentName}";

				Check.checkToggleSwitch(locator1 = "Checkbox#STAGED_CONTENT");
			}
		}
		else {
			Check(locator1 = "Checkbox#SELECT_ALL");
		}

		if (isSet(contentNameList)) {
			Check(locator1 = "Checkbox#SELECT_ALL");

			Uncheck(locator1 = "Checkbox#SELECT_ALL");

			for (var contentName : list "${contentNameList}") {
				Check.checkToggleSwitch(
					key_contentName = "${contentName}",
					locator1 = "Checkbox#STAGED_CONTENT");
			}
		}

		for (var contentName : list "Audience Targeting") {
			var key_contentName = "${contentName}";

			if (IsElementPresent(locator1 = "Checkbox#STAGED_CONTENT")) {
				Check.checkToggleSwitch(locator1 = "Checkbox#STAGED_CONTENT");
			}
		}

		if (isSet(uncheckContent)) {
			var key_contentName = "${uncheckContent}";

			Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#STAGED_CONTENT");
		}

		if (isSet(isNotEditable)) {
			var key_contentName = "${isNotEditable}";

			AssertNotEditable(locator1 = "SiteSettingsStaging#CONTENT_CHECKBOX");
		}

		if ("${editActivateStaging}" == "true") {
			Button.clickSave();
		}
		else if ("${remoteStaging}" == "true") {
			AssertClickNoError(
				locator1 = "Button#SAVE",
				value1 = "Save");

			Confirm(value1 = "Are you sure you want to activate remote staging for ${siteName}?");
		}
		else {
			AssertClickNoError(
				locator1 = "Button#SAVE",
				value1 = "Save");

			Confirm(value1 = "Are you sure you want to activate local staging for ${siteName}?");

			AssertTextEquals(
				locator1 = "Staging#INITIAL_PROCESS_SUCCESSFUL",
				value1 = "Successful");
		}

		Pause(locator1 = "5000");

		AssertConsoleTextNotPresent(value1 = "javax.portlet.PortletException: java.lang.NullPointerException");
	}

	macro addEventRepeatDaily {
		Click(locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON");

		IFrame.selectDialogFrame();

		Click(locator1 = "StagingPublishToLive#SWITCH_TO_ADVANCED_PUBLICATION");

		Check(locator1 = "StagingPublishToLive#DATE_SCHEDULE_RADIO");

		Type(
			locator1 = "StagingPublishToLive#DATE_SCHEDULE_TITLE_FIELD",
			value1 = "${scheduleTitleName}");

		Check(locator1 = "StagingPublishToLive#DATE_SCHEDULE_END_DATE_NO_END_DATE_RADIO");

		Check(
			key_frequency = "Daily",
			locator1 = "StagingPublishToLive#DATE_SCHEDULE_REPEAT_RADIO");

		Check(locator1 = "Select#CONTENT_ALL_CONTENT");

		AssertClick(
			locator1 = "StagingPublishToLive#ADD_EVENT_BUTTON",
			value1 = "Add Event");

		Alert.viewSuccessMessage();

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		AssertClick(
			locator1 = "Staging#SCHEDULED",
			value1 = "Scheduled");

		var key_scheduleTitleName = "${scheduleTitleName}";

		AssertTextEquals(
			locator1 = "StagingPublishToLive#SCHEDULED_TABLE_TITLE",
			value1 = "${scheduleTitleName}");

		AssertTextEquals(
			locator1 = "StagingPublishToLive#SCHEDULED_TABLE_END_DATE",
			value1 = "No End Date");
	}

	macro addPageVariationPG {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		Click(locator1 = "Staging#STAGING_MENU_MANAGE_PAGE_VARIATION");

		SelectFrame(locator1 = "IFrame#PAGE_VARIATION_IFRAME");

		AssertClick(
			locator1 = "Button#ADD_PAGE_VARIATION",
			value1 = "Add Page Variation");

		Type(
			locator1 = "StagingManageVariations#MANAGE_VARIATION",
			value1 = "${pageVariationName}");

		AssertClick(
			locator1 = "StagingManageVariations#ADD",
			value1 = "Add");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro addPublishTemplate {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Publish Templates");

		Pause(locator1 = "3000");

		LexiconEntry.gotoAdd();

		Type(
			locator1 = "TextInput#NAME",
			value1 = "${publishTemplateName}");

		PortletEntry.inputDescription(description = "${publishTemplateDescription}");
	}

	macro addSitePagesVariationPG {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		Click(locator1 = "Staging#MANAGE_VARIATIONS");

		var key_sitePagesVariationName = "${sitePagesVariationName}";

		SelectFrame(locator1 = "IFrame#SITE_VARIATION_IFRAME");

		AssertClick(
			locator1 = "StagingManageVariations#ADD_SITE_PAGES_VARIATIONS_BUTTON",
			value1 = "Add Site Pages Variation");

		Type(
			locator1 = "StagingManageVariations#MANAGE_VARIATION",
			value1 = "${sitePagesVariationName}");

		if (isSet(copySitePagesVariationName)) {
			Select(
				locator1 = "StagingManageVariations#ADD_SITE_PAGES_VARIATION_COPY_PAGES_SELECT",
				value1 = "${copySitePagesVariationName}");
		}

		AssertClick(
			locator1 = "StagingManageVariations#ADD",
			value1 = "Add");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro changePageVariationInHistory {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		Click(locator1 = "Staging#HISTORY_BUTTON");

		IFrame.selectDialogFrame();

		Select(
			key_pageVariationName = "${pageVariationNameMenulistName}",
			locator1 = "Staging#VARIATIONS_SELECTOR",
			value1 = "${pageVariationNameMenulistName}");
	}

	macro changePageVariationPG {
		var key_pageVariationName = "${pageVariationName}";

		Click(locator1 = "Staging#PAGE_VARIATION_DROPDOWN");

		var key_pageVariationName = "${pageVariationNameMenulistName}";

		AssertClick(
			locator1 = "Staging#PAGE_VARIATION_MENULIST_NAME",
			value1 = "${pageVariationNameMenulistName}");
	}

	macro changeSitePageVariationPG {
		var key_sitePagesVariationName = "${sitePagesVariationName}";

		Click(locator1 = "Staging#SITE_PAGES_VARIATION_DROPDOWN");

		var key_sitePagesVariationName = "${sitePagesVariationMenulistName}";

		AssertClick(
			locator1 = "Staging#SITE_PAGES_VARIATION_MENULIST_NAME",
			value1 = "${sitePagesVariationMenulistName}");
	}

	macro configurePublishToLive {
		if (IsElementPresent(locator1 = "StagingPublishToLive#SWITCH_TO_ADVANCED_PUBLICATION")) {
			AssertClick.assertPartialTextClickAt(
				locator1 = "StagingPublishToLive#SWITCH_TO_ADVANCED_PUBLICATION",
				value1 = "Switch to Advanced Publication");
		}

		if ("${deleteMissingPages}" == "true") {
			if (IsElementPresent(locator1 = "StagingPublishToLive#PAGES_CHANGE_LINK")) {
				AssertClick(
					locator1 = "StagingPublishToLive#PAGES_CHANGE_LINK",
					value1 = "Change");

				Check(locator1 = "PagesConfiguration#PAGES_DELETE_MISSING_PAGES_CHECKBOX");

				AssertClick(
					locator1 = "Button#OK",
					value1 = "OK");
			}
			else {
				Check(locator1 = "PagesConfiguration#PAGES_DELETE_MISSING_PAGES_CHECKBOX");
			}
		}

		if ("${uncheckLogo}" == "true") {
			if (IsElementPresent(locator1 = "StagingPublishToLive#PAGES_CHANGE_LINK")) {
				AssertClick(
					locator1 = "StagingPublishToLive#PAGES_CHANGE_LINK",
					value1 = "Change");

				Uncheck(locator1 = "PagesConfiguration#PAGES_LOGO_CHECKBOX");

				AssertClick(
					locator1 = "Button#OK",
					value1 = "OK");
			}
			else {
				Uncheck(locator1 = "PagesConfiguration#PAGES_LOGO_CHECKBOX");
			}
		}

		if (isSet(checkPageName)) {
			Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");

			Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");

			for (var pageName : list "${checkPageName}") {
				var key_pageName = "${pageName}";

				if (IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_CHECKED")) {
					var key_pageName = "${pageName}";

					Click(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_NAME");
				}
			}
		}

		if ("${contentDateRange}" == "All") {
			Click(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

			Click(locator1 = "Radio#CHOOSE_CONTENT_DATE_RANGE_CHANGE");

			Check(locator1 = "Radio#RANGE_ALL");

			Click(locator1 = "Link#REFRESH_COUNTS");
		}

		if ("${contentDateRange}" == "Date Range") {
			Click(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

			Click(locator1 = "Radio#CHOOSE_CONTENT_DATE_RANGE_CHANGE");

			var key_contentDateRange = "${contentDateRange}";

			Type(
				locator1 = "Select#DATE_RANGE_START_DATE",
				value1 = "${dateRangeStartDate}");

			Type(
				locator1 = "Select#DATE_RANGE_END_DATE",
				value1 = "${dateRangeEndDate}");

			Click(locator1 = "Link#REFRESH_COUNTS");
		}

		if ("${contentDateRange}" == "Last") {
			Click(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

			Click(locator1 = "Radio#CHOOSE_CONTENT_DATE_RANGE_CHANGE");

			Click(locator1 = "Radio#RANGE_LAST");

			Click(locator1 = "Link#REFRESH_COUNTS");
		}

		if ("${contentDeletion}" == "true") {
			Click(locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_DELETION_HEADER");

			Check.checkToggleSwitch(locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_DELETION_CHECKBOX");
		}

		if ("${pageDeletion}" == "true") {
			Click(locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_DELETION_HEADER");

			Check.checkToggleSwitch(locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_DELETION_CHECKBOX");

			AssertElementPresent(locator1 = "StagingPublishToLive#PAGES_CHOOSE_PAGE_DELETION_CHECKBOX");

			Click(locator1 = "StagingPublishToLive#PAGES_CHOOSE_PAGE_DELETION_CHECKBOX");
		}

		if ("${checkAllContent}" == "true") {
			Check(locator1 = "Select#CONTENT_ALL_CONTENT");
		}

		if (isSet(checkContentNameList)) {
			Check(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

			for (var contentName : list "${checkContentNameList}") {
				var key_contentName = "${contentName}";

				Check(locator1 = "PagesConfiguration#CONTENT_CHOOSE_CONTENT_CONTENT_CHECKBOX");
			}
		}

		if (isSet(uncheckContentNameList)) {
			Check(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

			for (var contentName : list "${uncheckContentNameList}") {
				var key_contentName = "${contentName}";

				Uncheck(locator1 = "PagesConfiguration#CONTENT_CHOOSE_CONTENT_CONTENT_CHECKBOX");
			}
		}

		if (isSet(uncheckSubContent)) {
			Click(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

			var key_uncheckContent = "${uncheckSubContent}";

			AssertClick(
				locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_CONTENT_CHANGE_LINK",
				value1 = "Change");

			Uncheck(locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_SUBCONTENT_CHECKBOX");

			AssertClick(
				locator1 = "Button#OK",
				value1 = "OK");
		}

		if (isSet(uncheckSubSubContent)) {
			Click(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

			var key_contentName = "${contentName}";

			Check(locator1 = "PagesConfiguration#CONTENT_CHOOSE_CONTENT_CONTENT_CHECKBOX");

			var key_uncheckContent = "${contentName}";

			AssertClick(
				locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_CONTENT_CHANGE_LINK",
				value1 = "Change");

			var key_subContent = "${subContent}";
			var key_subSubContent = "${uncheckSubSubContent}";

			Uncheck(locator1 = "ExportImport#CONTENT_CHOOSE_CONTENT_SUB_SUBCONTENT_CHECKBOX");

			AssertClick(
				locator1 = "Button#OK",
				value1 = "OK");
		}

		if (isSet(checkPrivatePages)) {
			if (IsElementPresent(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX")) {
				Click(locator1 = "PagesConfiguration#CHANGE_TO_PRIVATE_PAGES");
			}

			Click(locator1 = "PagesConfiguration#PAGES_ALL_PRIVATE_PAGES_CHECKBOX");

			Click(locator1 = "PagesConfiguration#PAGES_ALL_PRIVATE_PAGES_CHECKBOX");

			for (var pageName : list "${checkPrivatePages}") {
				var key_pageName = "${pageName}";

				if (IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_CHECKED")) {
					var key_pageName = "${pageName}";

					Click(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_NAME");
				}
			}
		}

		if (isSet(checkPublicPages)) {
			if (IsElementPresent(locator1 = "PagesConfiguration#PAGES_ALL_PRIVATE_PAGES_CHECKBOX")) {
				Click(locator1 = "PagesConfiguration#CHANGE_TO_PUBLIC_PAGES");
			}

			Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");

			Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");

			for (var pageName : list "${checkPublicPages}") {
				var key_pageName = "${pageName}";

				if (IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_CHECKED")) {
					var key_pageName = "${pageName}";

					Click(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_NAME");
				}
			}
		}

		if ((!(isSet(checkContentNameList))) && (!(isSet(uncheckContentNameList)))) {
			Check(locator1 = "Select#CONTENT_ALL_CONTENT");
		}
	}

	macro copyFromLive {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "${portletOption}");

		SelectFrame(locator1 = "IFrame#DIALOG");

		Click(locator1 = "Staging#COPY_FROM_LIVE_TAB");

		ClickNoError(locator1 = "Staging#COPY_FROM_LIVE");

		Confirm(value1 = "Are you sure you want to copy from live and update the existing staging portlet information?");

		AssertTextEquals(
			locator1 = "Staging#STAGING_STATUS_PORTLET",
			value1 = "Successful");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro deactivateStagingCP {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Staging Configuration");

		if (IsElementPresent(locator1 = "Checkbox#ENABLED_ON_PUBLIC_PAGES")) {
			Check(locator1 = "SiteSettingsStaging#NONE_RADIO");

			AssertClickNoError(
				locator1 = "Button#SAVE",
				value1 = "Save");

			Confirm(value1 = "Are you sure you want to deactivate staging for ${siteName}?");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#SUCCESS_3",
				value1 = "Staging is successfully disabled.");
		}

		AssertNotVisible(locator1 = "Checkbox#ENABLED_ON_PUBLIC_PAGES");
	}

	macro deletePageVariationPG {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		Click(locator1 = "Staging#STAGING_MENU_MANAGE_PAGE_VARIATION");

		SelectFrame(locator1 = "IFrame#PAGE_VARIATION_IFRAME");

		LexiconEntry.gotoEntryMenuItemNoWaitNoError(
			menuItem = "Delete",
			rowEntry = "${pageVariationName}");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro deletePublishTemplate {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Publish Templates");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${publishTemplateName}");
	}

	macro deleteSitePagesVariationPG {
		Staging.navigateToSitePageVariation();

		LexiconEntry.gotoEntryMenuItemNoWaitNoError(
			menuItem = "Delete",
			rowEntry = "${sitePagesVariationName}");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro editPublishTemplate {
		Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		Navigator.gotoNavItem(navItem = "Publish Templates");

		var key_publishTemplateName = "${publishTemplateName}";

		AssertTextEquals(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_NAME",
			value1 = "${publishTemplateName}");

		AssertClick(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_NAME",
			value1 = "${publishTemplateName}");

		Type(
			locator1 = "TextInput#NAME",
			value1 = "${publishTemplateNameEdit}");

		PortletEntry.inputDescription(description = "${publishTemplateDescriptionEdit}");
	}

	macro editSitePageVariationPG {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		Click(locator1 = "Staging#MANAGE_VARIATIONS");

		SelectFrame(locator1 = "IFrame#SITE_VARIATION_IFRAME");

		LexiconEntry.gotoEntryMenuItemNoWait(
			menuItem = "Edit",
			rowEntry = "${sitePagesVariationName}");

		Type(
			locator1 = "StagingManageVariations#MANAGE_VARIATION",
			value1 = "${sitePagesVariationName} Edit");

		AssertClick(
			locator1 = "StagingManageVariations#UPDATE_BUTTON",
			value1 = "Update");

		SelectFrameTop(value1 = "relative=top");

		SelectFrame(locator1 = "IFrame#SITE_VARIATION_IFRAME");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		SelectFrameTop(value1 = "relative=top");
	}

	macro editStagingConfiguration {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Staging Configuration");

		if ("${pageVersioningEnabled}" == "true") {
			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PUBLIC_PAGES");

			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PRIVATE_PAGES");
		}

		if ("${publicPageVersioningEnabled}" == "true") {
			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PUBLIC_PAGES");
		}

		if ("${privatePageVersioningEnabled}" == "true") {
			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PRIVATE_PAGES");
		}

		if (isSet(isNotEditable)) {
			AssertNotVisible(
				key_contentName = "${isNotEditable}",
				locator1 = "Checkbox#STAGED_CONTENT");
		}

		Button.clickSave();

		Pause(locator1 = "5000");
	}

	macro enablePageInSitePagesVariation {
		AssertTextEquals.assertPartialText(
			locator1 = "Staging#STAGING_MENU_REVISION_DETAILS_MESSAGE",
			value1 = "The page ${pageName} is not enabled in ${sitePagesVariationName}, but is available for other pages variations.");

		AssertClick(
			locator1 = "Staging#STAGING_MENU_ENABLE_PAGE_BUTTON",
			value1 = "Enable in ${sitePagesVariationName}");

		Page.view(pageName = "${pageName}");
	}

	macro gotoPublishToLive {
		if ((IsElementNotPresent(locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON")) && (IsElementPresent(locator1 = "Icon#STAGING"))) {
			Click(locator1 = "Icon#STAGING");

			AssertElementPresent(locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON");
		}

		if ((IsElementPresent(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION")) && (!(isSet(pleaseDoNotMarkAsReady)))) {
			Check.toggleSwitch(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION");
		}

		if ((IsElementPresent(locator1 = "SitePages#NAVIGATION_PUBLIC_PAGES")) && (IsElementPresent(locator1 = "Staging#STAGING_BUTTON"))) {
			AssertClick(
				locator1 = "Staging#STAGING_BUTTON",
				value1 = "Staging");
		}

		Click(locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON");

		IFrame.selectDialogFrame();
	}

	macro markAsReadyForPublication {
		if (IsElementPresent(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION")) {
			AssertNotChecked(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION");

			Check.toggleSwitch(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION");
		}
	}

	macro mergeSitePagesVariation {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		Click(locator1 = "Staging#MANAGE_VARIATIONS");

		SelectFrame(locator1 = "IFrame#SITE_VARIATION_IFRAME");

		LexiconEntry.gotoEntryMenuItemNoWait(
			menuItem = "Merge",
			rowEntry = "${sitePagesVariationName}");

		var key_sitePagesVariationMergeName = "${sitePagesVariationMergeName}";

		AssertTextEquals(
			locator1 = "StagingManageVariations#MERGE_SITE_PAGES_VARIATION_TABLE_BRANCH",
			value1 = "${sitePagesVariationMergeName}");

		AssertClickNoError(
			locator1 = "StagingManageVariations#MERGE_SITE_PAGES_VARIATION_TABLE_SELECT_LINK",
			value1 = "Select");

		Confirm(value1 = "Are you sure you want to merge changes from ${sitePagesVariationMergeName}?");

		Staging.viewVariationSuccessMessage();

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro modifyVariation {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Staging Configuration");

		if (isSet(enableOnPublicPages)) {
			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PUBLIC_PAGES");

			AssertClickNoError(
				locator1 = "Button#SAVE",
				value1 = "Save");
		}

		if (isSet(enableOnPrivatePages)) {
			Check.checkToggleSwitch(locator1 = "Checkbox#ENABLED_ON_PRIVATE_PAGES");

			AssertClickNoError(
				locator1 = "Button#SAVE",
				value1 = "Save");
		}
	}

	macro moveToTheRecycleBinPublishTemplate {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Publish Templates");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move to the Recycle Bin",
			rowEntry = "${publishTemplateName}");

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}

	macro navigateToSitePageVariation {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		Click(locator1 = "Staging#MANAGE_VARIATIONS");

		SelectFrame(locator1 = "IFrame#SITE_VARIATION_IFRAME");
	}

	macro openStagingAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_staging_processes_web_portlet_StagingProcessesPortlet");
	}

	macro publishCustomPublication {
		LexiconEntry.gotoAdd();

		if ("${remoteStaging}" == "true") {
			AssertClickNoError(
				locator1 = "Staging#STAGING_PUBLISH_TO_REMOTE_LIVE_BUTTON",
				value1 = "Publish to Remote Live");
		}
		else {
			AssertClickNoError(
				locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON",
				value1 = "Publish to Live");
		}

		Pause(locator1 = "30000");

		if (isSet(failureExpected)) {
			AssertTextEquals(
				locator1 = "Staging#PROCESS_RESULT",
				value1 = "Failed");
		}
		else {
			AssertTextEquals(
				locator1 = "Staging#PROCESS_RESULT",
				value1 = "Successful");
		}
	}

	macro publishToLive {
		if (isSet(publicationName)) {
			PortletEntry.inputName(name = "${publicationName}");
		}

		AssertClickNoError.assertPartialTextClickAt(
			locator1 = "Button#SUBMIT",
			value1 = "Publish");

		Pause(locator1 = "5000");

		if (isSet(failureExpected)) {
			AssertTextEquals(
				locator1 = "Staging#PROCESS_FAILED",
				value1 = "Failed");
		}
		else {
			AssertTextEquals(
				locator1 = "Staging#PROCESS_SUCCESSFUL",
				value1 = "Successful");
		}

		if (isSet(validateErrorMessage)) {
			Staging.viewValidationError(validateErrorMessage = "${validateErrorMessage}");
		}

		SelectFrameTop();

		Click(locator1 = "Icon#CLOSE");
	}

	macro publishToLiveNowViaPortletCP {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Staging");

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		AssertClickNoError(
			locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON",
			value1 = "Publish to Live");

		Confirm(value1 = "Are you sure you want to publish to live and update the existing portlet data?");

		Pause(locator1 = "5000");

		AssertTextEquals(
			locator1 = "Staging#STAGING_STATUS_PORTLET",
			value1 = "Successful");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro publishToLiveNowViaPortletPG {
		Portlet.gotoPortletOptions(portletOption = "Staging");

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		if (isSet(uncheckConfiguration)) {
			AssertClick(
				locator1 = "StagingPublishToLive#APPLICATION_CONFIGURATION_APPLICATION_CHANGE_LINK",
				value1 = "Change");

			Uncheck(locator1 = "StagingPublishToLive#APPLICATION_CONFIGURATION_APPLICATION_SETUP_CHECK");

			AssertClick(
				locator1 = "Button#OK",
				value1 = "OK");
		}

		AssertClickNoError(
			locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON",
			value1 = "Publish to Live");

		Confirm(value1 = "Are you sure you want to publish to live and update the existing portlet data?");

		Pause(locator1 = "5000");

		AssertTextEquals(
			locator1 = "Staging#STAGING_STATUS_PORTLET",
			value1 = "Successful");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro publishToLivePublishTemplate {
		if (IsElementPresent(locator1 = "StagingPublishToLive#SWITCH_TO_ADVANCED_PUBLICATION")) {
			AssertClick.assertPartialTextClickAt(
				locator1 = "StagingPublishToLive#SWITCH_TO_ADVANCED_PUBLICATION",
				value1 = "Switch to Advanced Publication");
		}

		AssertClick(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_BUTTON",
			value1 = "Publish Templates");

		Click(locator1 = "Icon#HISTORY_VERTICAL_ELLIPSIS");

		Click(locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_PUBLISH_TO_LIVE_ARROW_BUTTON");

		AssertClick(
			locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON",
			value1 = "Publish to Live");

		Pause(locator1 = "5000");

		AssertTextEquals(
			locator1 = "Staging#PROCESS_SUCCESSFUL",
			value1 = "Successful");

		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "Icon#CLOSE")) {
			SelectFrame(value1 = "relative=top");

			Click(locator1 = "Icon#CLOSE");
		}

		var key_siteName = "${siteName}";

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		AssertTextEquals(
			locator1 = "Staging#STAGING_STATUS",
			value1 = "Successful");
	}

	macro redoStagingPageVersioningPG {
		Click(locator1 = "Icon#HISTORY_VERTICAL_ELLIPSIS");

		ClickNoError(locator1 = "Staging#STAGING_MENU_REDO_BUTTON");

		Confirm(value1 = "Are you sure you want to redo your last changes?");
	}

	macro remoteStagingSetUp {
		if (isSet(remotePort)) {
			User.loginPG(
				nodePort = "${remotePort}",
				password = "test",
				userEmailAddress = "test@liferay.com");
		}
		else {
			Navigator.openURL();
		}

		var siteID = "${siteID}";

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addBlankCP(siteName = "Remote Site");

		if (isSet(remotePort)) {
			Navigator.openSpecificURL(url = "http://localhost:${remotePort}/group/remote-site/~/control_panel/manage?p_p_id=com_liferay_journal_web_portlet_JournalPortlet");
		}
		else {
			Navigator.openSiteLanding(siteURLKey = "Remote Site");
		}

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Site Settings");

		var siteID = "siteID";

		Copy(
			locator1 = "SitesEditSite#SITE_ID",
			value1 = "siteID");

		if (isSet(remotePort)) {
			User.loginPG(
				nodePort = "8080",
				password = "test",
				userEmailAddress = "test@liferay.com");
		}

		Navigator.openSiteLanding(siteURLKey = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		Staging.activateStagingCP(
			contentName = "${contentName}",
			editActivateStaging = "${editActivateStaging}",
			pageVersioningEnabled = "${pageVersioningEnabled}",
			remotePort = "${remotePort}",
			remoteStaging = "true",
			siteID = "${siteID}",
			siteName = "${siteName}",
			uncheckContent = "${uncheckContent}");
	}

	macro savePublishTemplate {
		Button.clickSave();

		var key_publishTemplateName = "${publishTemplateName}";

		AssertTextEquals(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_NAME",
			value1 = "${publishTemplateName}");

		AssertTextEquals(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_DESCRIPTION",
			value1 = "${publishTemplateDescription}");
	}

	macro schedulePublishToLivePG {
		if (IsElementPresent(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION")) {
			Check.toggleSwitch(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION");
		}

		Click(locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON");

		IFrame.selectDialogFrame();

		Click(locator1 = "StagingPublishToLive#SWITCH_TO_ADVANCED_PUBLICATION");

		Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");

		Check(locator1 = "StagingPublishToLive#DATE_SCHEDULE_RADIO");

		Type(
			locator1 = "StagingPublishToLive#DATE_SCHEDULE_TITLE_FIELD",
			value1 = "${scheduleTitleName}");

		var displayDateHour = selenium.getAttribute("//input[contains(@id,'schedulerStartDateHour')]@value");
		var displayDateHourLength = StringUtil.length("${displayDateHour}");

		if ("${displayDateHourLength}" == "1") {
			var displayDateHour = "0${displayDateHour}";
		}

		var displayDateAMPM = selenium.getAttribute("//input[contains(@id,'schedulerStartDateAmPm')]@value");
		var displayDateAMPM = StringUtil.replace("${displayDateAMPM}", "0", "AM");
		var displayDateAMPM = StringUtil.replace("${displayDateAMPM}", "1", "PM");
		var displayDateMinute = selenium.getAttribute("//input[contains(@id,'schedulerStartDateMinute')]@value");
		var displayDateMinuteFuture = ${displayDateMinute} + 2;
		var displayDateMinuteFutureLength = StringUtil.length("${displayDateMinuteFuture}");

		if ("${displayDateMinuteFutureLength}" == "1") {
			var displayDateMinuteFuture = "0${displayDateMinuteFuture}";
		}

		var displayDate = "${displayDateHour}:${displayDateMinuteFuture} ${displayDateAMPM}";

		Type(
			locator1 = "StagingPublishToLive#DATE_SCHEDULE_TIME_INPUT",
			value1 = "${displayDate}");

		if (isSet(endByDate)) {
			Check(locator1 = "StagingPublishToLive#DATE_SCHEDULE_END_DATE_END_BY_RADIO");

			var endByDateDay = selenium.getAttribute("//input[contains(@id,'schedulerEndDateDay')]@value");

			if ("${endByDateDayLength}" == "1") {
				var endByDateDay = "0${endByDateDay}";
			}

			var endByDateMonth = selenium.getAttribute("//input[contains(@id,'schedulerEndDateMonth')]@value");

			if ("${endByDateMonthLength}" == "1") {
				var endByDateMonth = "0${endByDateMonth}";
			}

			var endByDateYear = selenium.getAttribute("//input[contains(@id,'schedulerEndDateYear')]@value");
			var endByDateYearFuture = ${endByDateYear} + 5;
			var endByDay = "${endByDateMonth}/${endByDateDay}/${endByDateYearFuture}";

			Type(
				locator1 = "StagingPublishToLive#DATE_SCHEDULE_END_DATE_INPUT",
				value1 = "${endByDay}");
		}

		if (isSet(frequency)) {
			Check(
				key_frequency = "${frequency}",
				locator1 = "StagingPublishToLive#DATE_SCHEDULE_REPEAT_RADIO");
		}

		AssertClick(
			locator1 = "StagingPublishToLive#ADD_EVENT_BUTTON",
			value1 = "Add Event");

		Alert.viewSuccessMessage();

		SelectFrameTop(value1 = "relative=top");

		Pause(locator1 = "5000");
	}

	macro searchPublishTemplate {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Publish Templates");

		Pause(locator1 = "3000");

		Search.searchCP(searchTerm = "${searchTerm}");

		var key_publishTemplateName = "${searchTerm}";

		AssertTextEquals.assertPartialText(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_NAME",
			value1 = "${searchTerm}");
	}

	macro setContentDateRange {
		if (IsElementPresent(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION")) {
			Check.toggleSwitch(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION");
		}

		Click(locator1 = "Staging#STAGING_PUBLISH_TO_LIVE_BUTTON");

		IFrame.selectDialogFrame();

		AssertClick.assertPartialTextClickAt(
			locator1 = "StagingPublishToLive#SWITCH_TO_ADVANCED_PUBLICATION",
			value1 = "Switch to Advanced Publication");

		Check(locator1 = "Select#CONTENT_CHOOSE_CONTENT");

		Click(locator1 = "Radio#CHOOSE_CONTENT_DATE_RANGE_CHANGE");

		var key_contentDateRange = "DateRange";

		Type(
			locator1 = "Select#DATE_RANGE_START_DATE",
			value1 = "${dateRangeStartDate}");

		Type(
			locator1 = "Select#DATE_RANGE_END_DATE",
			value1 = "${dateRangeEndDate}");

		Click(locator1 = "Link#REFRESH_COUNTS");
	}

	macro submitForPublicationPageRevision {
		if (IsTextEqual(locator1 = "Message#WORKFLOW_STATUS", value1 = "Draft")) {
			AssertClick(
				locator1 = "Staging#STAGING_MENU_SUBMIT_FOR_PUBLICATION",
				value1 = "Submit for Publication");
		}

		AssertElementNotPresent(locator1 = "Staging#STAGING_MENU_SUBMIT_FOR_PUBLICATION");

		if (IsTextEqual(locator1 = "Message#WORKFLOW_STATUS", value1 = "Pending (Created)")) {
			AssertTextEquals(
				locator1 = "Message#WORKFLOW_STATUS",
				value1 = "Pending (Created)");
		}
		else {
			AssertTextEquals(
				locator1 = "Message#WORKFLOW_STATUS",
				value1 = "Pending (Review)");
		}
	}

	macro undoStagingPageVersioningPG {
		Click(locator1 = "Icon#HISTORY_VERTICAL_ELLIPSIS");

		ClickNoError(locator1 = "Staging#STAGING_MENU_UNDO_BUTTON");

		Confirm(value1 = "Are you sure you want to undo your last changes?");
	}

	macro viewAddButtonDoesNotExist {
		AssertElementNotPresent(locator1 = "Button#PLUS");
	}

	macro viewAddButtonExists {
		AssertVisible(locator1 = "Button#PLUS");
	}

	macro viewDateRange {
		AssertElementPresent(
			key_dateRangeStartDate = "${dateRangeStartDate}",
			locator1 = "Select#DATE_RANGE_START_DATE_VALUE");

		AssertElementPresent(
			key_dateRangeEndDate = "${dateRangeEndDate}",
			locator1 = "Select#DATE_RANGE_END_DATE_VALUE");
	}

	macro viewEditedSitePagesVariationPG {
		var key_sitePagesVariationName = "${sitePagesVariationName}";

		AssertTextEquals.assertPartialText(
			locator1 = "Staging#SITE_PAGES_VARIATION",
			value1 = "${sitePagesVariationName}");
	}

	macro viewHistoryVersionPG {
		if (IsElementPresent(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS")) {
			Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

			Click(locator1 = "Staging#HISTORY_BUTTON");

			SelectFrame(locator1 = "IFrame#DIALOG");
		}

		if ("${modifiedVersion}" == "true") {
			AssertTextNotEquals(
				locator1 = "Staging#HISTORY_VERSION",
				value1 = "${historyVersion}");
		}
		else if (isSet(historyVersionName)) {
			var key_historyVersionName = "${historyVersionName}";

			AssertTextEquals.assertPartialText(
				locator1 = "Staging#HISTORY_VERSION_NAME",
				value1 = "${historyVersionName}");
		}
		else if (IsElementPresent.isVisible(locator1 = "Staging#HISTORY_VERSION")) {
			AssertTextEquals.assertPartialText(
				locator1 = "Staging#HISTORY_VERSION",
				value1 = "${historyVersion}");
		}
		else {
			AssertTextEquals.assertPartialText(
				locator1 = "Staging#HISTORY_VERSION_ANCHOR",
				value1 = "${historyVersion}");
		}

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro viewIncompletePageViaStagingPageVariation {
		var key_page = "${pageName}";

		AssertTextEquals.assertPartialText(
			locator1 = "SitePages#INCOMPLETE_STAGING_PAGE_VARIATION_INFO_MESSAGE",
			value1 = "The page ${pageName} is not enabled in ${sitePagesVariationName}, but is available for other pages variations.");

		AssertTextEquals(
			locator1 = "SitePages#INCOMPLETE_STAGING_PAGE_VARIATION_ENABLE_BUTTON",
			value1 = "Enable in ${sitePagesVariationName}");

		AssertTextEquals(
			locator1 = "SitePages#INCOMPLETE_STAGING_PAGE_VARIATION_DELETE_BUTTON",
			value1 = "Delete in All Pages Variations");
	}

	macro viewNoPublishContentCountViaPortletStagingCP {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Staging");

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		AssertElementNotPresent(locator1 = "Staging#STAGING_CONTENT_COUNT");

		AssertElementNotPresent(locator1 = "Staging#STAGING_CONTENT_INFO");

		SelectFrameTop();

		Click(locator1 = "Icon#CLOSE");
	}

	macro viewNotAvailableMenuItem {
		var key_page = "${pageName}";

		Click.mouseOverClick(locator1 = "SitePages#SPECIFIC_PAGE_ELLIPSIS");

		AssertNotVisible(locator1 = "SitePages#DELETE_SPECIFIC_PAGE");
	}

	macro viewNotStagedAssetMessageOnStaging {
		AssertTextEquals.assertPartialText(
			locator1 = "Staging#NOT_STAGED_ASSET_MESSAGE",
			value1 = "The data of this portlet is not staged.");
	}

	macro viewPageVariationPG {
		var key_pageVariationName = "${pageVariationName}";

		AssertTextEquals(
			locator1 = "Staging#PAGE_VARIATION",
			value1 = "${pageVariationName}");
	}

	macro viewPublishContentCountViaPortletStagingCP {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Staging");

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		AssertTextEquals(
			locator1 = "Staging#STAGING_CONTENT_COUNT",
			value1 = "${contentCount}");

		AssertTextEquals.assertPartialText(
			locator1 = "Staging#STAGING_CONTENT_INFO",
			value1 = "${contentInformation}");

		SelectFrameTop();

		Click(locator1 = "Icon#CLOSE");
	}

	macro viewPublishTemplate {
		if (IsElementPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS")) {
			Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");
		}

		MenuItem.click(menuItem = "Publish Templates");

		var key_publishTemplateName = "${publishTemplateName}";

		AssertTextEquals(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_NAME",
			value1 = "${publishTemplateName}");

		AssertTextEquals(
			locator1 = "StagingPublishToLive#PUBLISH_TEMPLATES_TABLE_DESCRIPTION",
			value1 = "${publishTemplateDescription}");
	}

	macro viewSitePagesVariationPG {
		var key_sitePagesVariationName = "${sitePagesVariationName}";

		AssertTextEquals.assertPartialText(
			locator1 = "Staging#SITE_PAGES_VARIATION",
			value1 = "${sitePagesVariationName}");
	}

	macro viewStagingLocalLivePG {
		AssertElementPresent(locator1 = "Staging#STAGING_VIEW");
	}

	macro viewStagingLocalLiveVariationMessagePG {
		if (IsElementNotPresent(locator1 = "Staging#LIVE_MENU_MESSAGE")) {
			AssertClick(
				locator1 = "Staging#LIVE_DROP_DOWN_BUTTON",
				value1 = "Live");
		}

		AssertTextEquals.assertPartialText(
			locator1 = "Staging#LIVE_MENU_MESSAGE",
			value1 = "Page ${pageName} was last published from ${sitePagesVariationName}.");
	}

	macro viewStagingWorkflowStatusPG {
		if ("${stagingWorkflowStatus}" == "Ready for Publication") {
			AssertElementPresent(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION");
		}
	}

	macro viewValidationError {
		Click(locator1 = "Message#SEE_MORE_DETAILS_LINK");

		SelectFrameTop();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR",
			value1 = "${validateErrorMessage}");
	}

	macro viewVariationSuccessMessage {
		SelectFrameTop(value1 = "relative=top");

		SelectFrame(locator1 = "IFrame#SITE_VARIATION_IFRAME");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		SelectFrameTop(value1 = "relative=top");
	}

}