definition {

	macro addDocumentum {
		Panel.expandPanel(panel = "Repository Configuration");

		Select(
			locator1 = "DocumentsAndMediaEditRepository#REPOSITORY_TYPE_SELECT",
			value1 = "Documentum");

		Type(
			locator1 = "DocumentsAndMediaEditRepository#CABINET_FIELD",
			value1 = "${dmCabinetName}");

		Type(
			locator1 = "DocumentsAndMediaEditRepository#REPOSITORY_FIELD",
			value1 = "${dmRepositoryFieldName}");
	}

	macro addSharepoint {
		Panel.expandPanel(panel = "Repository Configuration");

		Select(
			locator1 = "DocumentsAndMediaEditRepository#REPOSITORY_TYPE_SELECT",
			value1 = "${dmRepositoryName}");

		if (isSet(dmServerVersion)) {
			Type(
				key_fieldLabel = "Server Version",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${dmServerVersion}");
		}

		if (isSet(dmRepositorySiteAbsoluteURL)) {
			Type(
				key_fieldLabel = "Site Absolute URL",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${dmRepositorySiteAbsoluteURL}");

			Type(
				key_fieldLabel = "Library Path",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${dmRepositoryLibraryPath}");
		}
		else {
			Type(
				locator1 = "DocumentsAndMediaEditRepository#SITE_URL_FIELD",
				value1 = "${dmRepositorySiteURL}");

			Type(
				locator1 = "DocumentsAndMediaEditRepository#LIBRARY_NAME_FIELD",
				value1 = "${dmRepositoryLibraryName}");
		}
	}

	macro edit {
		PortletEntry.inputName(name = "${dmRepositoryName}");
	}

	macro gotoCP {
		var key_dmRepositoryName = "${dmRepositoryName}";

		AssertClick.assertTextClickAtAndPause(
			locator1 = "DocumentsAndMedia#REPOSITORY_FOLDER_NAME_DESCRIPTIVE_VIEW",
			value1 = "${dmRepositoryName}");

		var key_dmFolderName = "${dmRepositoryName}";

		AssertVisible(locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "${dmRepositoryName}");
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Documents and Media");

		while (IsElementPresent(locator1 = "DocumentsAndMedia#FILTER_REPOSITORY_GENERIC")) {
			Click(locator1 = "DocumentsAndMedia#FILTER_REPOSITORY_GENERIC");

			Pause(locator1 = "1000");

			if (IsElementPresent(locator1 = "DocumentsAndMedia#ICON_DOCUMENT_GENERIC")) {
				Check(locator1 = "DocumentsAndMedia#TOOLBAR_SELECT_ALL_CHECKBOX");

				Pause(locator1 = "1000");

				AssertClick.waitForDMHomeAssertTextEqualsClick(
					locator1 = "DocumentsAndMedia#TOOLBAR_ACTIONS",
					value1 = "Actions");

				MenuItem.clickNoError(menuItem = "Delete");

				AssertConfirm(value1 = "Are you sure you want to delete the selected entries? They will be deleted immediately.");

				Alert.viewSuccessMessage();
			}

			Click(locator1 = "DocumentsAndMedia#FILTER_REPOSITORY_ACTIONS_GENERIC");

			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
	}

	macro viewBreadcrumb {
		var key_dmFolderName = "${dmRepositoryName}";

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME",
			value1 = "${dmRepositoryName}");
	}

}