definition {

	macro _addRole {
		Variables.assertDefined(parameterList = "${roleTitle}");

		var roleTitleEncoded = URLUtil.encodeUTF8("${roleTitle}");

		if (isSet(roleDescription)) {
			var roleDescriptionEncoded = URLUtil.encodeUTF8("${roleDescription}");

			var roleDescriptionMap = '''{"en_US":"${roleDescriptionEncoded}"}''';
		}
		else {
			var roleDescriptionMap = "{}";
		}

		if (isSet(roleKey)) {
			var roleKeyEncoded = URLUtil.encodeUTF8("${roleKey}");

			var roleKeyMap = '''{"en_US":"${roleKeyEncoded}"}''';
		}
		else {
			var roleKeyMap = "{}";
		}

		var roleTitleEncoded = URLUtil.encodeUTF8("${roleTitle}");

		if ("${roleType}" == "regular") {
			var roleTypeId = "1";
		}
		else if ("${roleType}" == "site") {
			var roleTypeId = "2";
		}
		else if ("${roleType}" == "organization") {
			var roleTypeId = "3";
		}
		else {
			fail("Invalid 'roleType', '${roleType}' must be 'regular/site/organization'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/add-role \
				-u test@liferay.com:test \
				-d className=com.liferay.portal.kernel.model.Role \
				-d classPK=0 \
				-d name=${roleTitleEncoded} \
				-d titleMap=${roleKeyMap} \
				-d descriptionMap=${roleDescriptionMap} \
				-d type=${roleTypeId} \
				-d subtype=
		''';

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	macro _assignRoleToUser {
		Variables.assertDefined(parameterList = "${roleId},${userId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-role-users \
				-u test@liferay.com:test \
				-d roleId=${roleId} \
				-d userIds=${userId}
		''';

		var roleTitle = JSONRole.getRoleName(roleId = "${roleId}");

		var userEmailAddress = JSONUser.getUserEmailAddress(userId = "${userId}");

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	macro addOrganizationRole {
		Variables.assertDefined(parameterList = "${roleTitle}");

		JSONRole._addRole(
			roleDescription = "${roleDescription}",
			roleKey = "${roleKey}",
			roleTitle = "${roleTitle}",
			roleType = "organization");
	}

	macro addRegularRole {
		Variables.assertDefined(parameterList = "${roleTitle}");

		JSONRole._addRole(
			roleDescription = "${roleDescription}",
			roleKey = "${roleKey}",
			roleTitle = "${roleTitle}",
			roleType = "regular");
	}

	macro addSiteRole {
		Variables.assertDefined(parameterList = "${roleTitle}");

		JSONRole._addRole(
			roleDescription = "${roleDescription}",
			roleKey = "${roleKey}",
			roleTitle = "${roleTitle}",
			roleType = "site");
	}

	macro assignRoleToUser {
		Variables.assertDefined(parameterList = "${roleTitle},${userEmailAddress}");

		var roleId = JSONRole.getRoleIdByName(roleTitle = "${roleTitle}");

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONRole._assignRoleToUser(
			roleId = "${roleId}",
			userId = "${userId}");
	}

	macro getRoleIdByName {
		Variables.assertDefined(parameterList = "${roleTitle}");

		var roleTitleEncoded = URLUtil.encodeUTF8("${roleTitle}");

		var roleTitleEncoded = StringUtil.replace("${roleTitleEncoded}", "+", "%20");

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/get-role/company-id/${companyId}/name/${roleTitleEncoded} \
				-u test@liferay.com:test
		''';
		var roleId = JSONCurlUtil.get("${curl}", "$['roleId']");

		return "${roleId}";
	}

	macro getRoleName {
		Variables.assertDefined(parameterList = "${roleId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/get-role/role-id/${roleId} \
				-u test@liferay.com:test
		''';
		var roleTitle = JSONCurlUtil.get("${curl}", "$['name']");

		return "${roleTitle}";
	}

}