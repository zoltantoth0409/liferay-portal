definition {

	macro _addUserGroup {
		Variables.assertDefined(parameterList = "${userGroupName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/usergroup/add-user-group \
				-u test@liferay.com:test \
				-d name=${userGroupName} \
				-d description=${userGroupDescription}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to assign a user group a role"
	macro _assignGroupRoleToUserGroup {
		Variables.assertDefined(parameterList = "${groupId},${roleId},${userGroupId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/usergroupgrouprole/add-user-group-group-roles \
				-u test@liferay.com:test \
				-d roleId=${roleId} \
				-d groupId=${groupId} \
				-d userGroupIds=${userGroupId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Get the userGroupId from any existing site by using userGroupName"
	macro _getUserGroupIdByName {
		Variables.assertDefined(parameterList = "${userGroupName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/usergroup/get-user-group \
				-u test@liferay.com:test \
				-d name=${userGroupName}
		''';

		var userGroupId = JSONCurlUtil.post("${curl}", "$.userGroupId");
		var userGroupId = StringUtil.replace("${userGroupId}", "[&quot;", "");
		var userGroupId = StringUtil.replace("${userGroupId}", "&quot;]", "");

		if ("${groupId}" == "[]") {
			fail("FAIL. Cannot find user group.");
		}

		return "${userGroupId}";
	}

}