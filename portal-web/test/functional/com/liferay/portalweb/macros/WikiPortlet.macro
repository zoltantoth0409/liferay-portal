definition {

	macro configureDisplaySettings {
		IFrame.selectDialogFrame();

		for (var SettingName : list "${uncheckSetting}") {
			Uncheck(locator1 = "Checkbox#${SettingName}");
		}

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro configureSharingAllowUsersToAddWikiToAnyWebsite {
		IFrame.selectConfigurationFrame();

		Navigator.gotoNavItem(navItem = "Sharing");

		if ("${enabled}" == "true") {
			Check.checkToggleSwitch(
				locator1 = "WikiConfiguration#ALLOW_USERS_TO_ADD_WIKI_TO_ANY_WEBSITE_CHECKBOX"
			);
		}
		else {
			Uncheck.uncheckToggleSwitch(
				locator1 = "WikiConfiguration#ALLOW_USERS_TO_ADD_WIKI_TO_ANY_WEBSITE_CHECKBOX"
			);
		}

		Button.clickSave();

		SelectFrame(value1 = "relative=top");
	}

	macro subscribeToNodeCP {
		var key_wikiNodeName = "${wikiNodeName}";

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Subscribe");

		Alert.viewSuccessMessage();

		Click.waitForMenuToggleJSClick(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.viewPresent(menuItem = "Unsubscribe");
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Wiki"
		);

		var key_wikiNodeName = "Main";

		LexiconEntry.openEntryMenu(rowEntry = "Main");

		MenuItem.click(menuItem = "Unsubscribe");

		Alert.viewSuccessMessage();

		LexiconEntry.openEntryMenu(rowEntry = "Main");

		MenuItem.viewPresent(menuItem = "Subscribe");
	}

	macro viewConfigurationTabs {
		IFrame.selectDialogFrame();

		for (var isPresent : list "${isPresent}") {
			AssertElementPresent(locator1 = "Configuration#${isPresent}");
		}

		for (var isNotPresent : list "${isNotPresent}") {
			AssertElementNotPresent(locator1 = "Configuration#${isNotPresent}");
		}
	}

	macro viewConfigureSharingAllowUsersToAddWikiToAnyWebsite {
		IFrame.selectConfigurationFrame();

		Navigator.gotoNavItem(navItem = "Sharing");

		if ("${enabled}" == "true") {
			AssertChecked.assertCheckedNotVisible(
				locator1 = "WikiConfiguration#ALLOW_USERS_TO_ADD_WIKI_TO_ANY_WEBSITE_CHECKBOX"
			);
		}
		else {
			AssertNotChecked.assertNotCheckedNotVisible(
				locator1 = "WikiConfiguration#ALLOW_USERS_TO_ADD_WIKI_TO_ANY_WEBSITE_CHECKBOX"
			);
		}

		SelectFrame(value1 = "relative=top");
	}

	macro viewDisplaySettings {
		IFrame.selectDialogFrame();

		for (var SettingName : list "${uncheckedSetting}") {
			AssertNotChecked(locator1 = "Checkbox#${SettingName}");
		}
	}

	macro viewNoPermissionCP {
		SiteAdmin.viewPortletTitle(portletTitle = "Wiki");

		AssertElementNotPresent(locator1 = "Button#ADD_WIKI");

		AssertElementNotPresent(locator1 = "Button#PERMISSIONS");
	}

}