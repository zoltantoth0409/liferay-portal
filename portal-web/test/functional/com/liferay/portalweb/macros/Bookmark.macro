definition {
	var assetType = "Bookmarks Entry";

	macro addBookmarkViaAP {
		AssetPublisherPortlet.addAsset(assetType = "Bookmarks Entry");

		if (isSet(bookmarkFolderName)) {
			AssertClick.pauseAssertTextClickAt(
				locator1 = "Button#SELECT",
				value1 = "Select"
			);

			SelectFrameTop();

			SelectFrame(locator1 = "IFrame#SELECT_FOLDER");

			AssertClick(
				locator1 = "WC#FOLDER_TABLE_CHOOSE",
				value1 = "Choose",
				key_folderName = "${bookmarkFolderName}"
			);
		}

		SelectFrameTop();

		SelectFrame(locator1 = "IFrame#EDIT_ASSET");

		PortletEntry.inputName(name = "${bookmarkName}");

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURL}");

		PortletEntry.inputDescription(description = "${bookmarkDescription}");

		AssertClick.pauseAssertTextClickAt(locator1 = "Button#SAVE", value1 = "Save");
	}

	macro addCP {
		LexiconEntry.gotoAddMenuItem(menuItem = "Bookmark");

		if (isSet(bookmarkName)) {
			PortletEntry.inputName(name = "${bookmarkName}");
		}
		else {
			var bookmarkName = "${bookmarkURL}";
		}

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURL}");

		if (isSet(bookmarkDescription)) {
			PortletEntry.inputDescription(description = "${bookmarkDescription}");
		}

		PortletEntry.save();

		Bookmark.viewBookmark(
			bookmarkName = "${bookmarkName}",
			bookmarkURL = "${bookmarkURL}",
			trailingSlash = "${trailingSlash}"
		);
	}

	macro addIncorrectURLCP {
		LexiconEntry.gotoAddMenuItem(menuItem = "Bookmark");

		PortletEntry.inputName(name = "${bookmarkName}");

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURLInvalid}");

		PortletEntry.inputDescription(description = "${bookmarkDescription}");

		Button.clickSave();

		AssertTextEquals.assertPartialText(
			locator1 = "TextInput#URL_REQUIRED",
			value1 = "URL"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "TextInput#URL_REQUIRED",
			value1 = "Required"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "TextInput#URL_REQUIRED",
			value1 = "Please enter a valid URL."
		);
	}

	macro addInvalidURLPG {
		Portlet.clickPortletPlusIconPG(portletName = "Bookmarks");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Bookmark",
			key_menuItem = "Bookmark"
		);

		PortletEntry.inputName(name = "${bookmarkName}");

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURLInvalid}");

		PortletEntry.inputDescription(description = "${bookmarkDescription}");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "TextInput#URL_REQUIRED",
			value1 = "URL asterisk Required Please enter a valid URL."
		);
	}

	macro addNullCP {
		LexiconEntry.gotoAddMenuItem(menuItem = "Bookmark");

		if (isSet(bookmarkName)) {
			PortletEntry.inputName(name = "${bookmarkName}");
		}

		if (isSet(bookmarkDescription)) {
			PortletEntry.inputDescription(description = "${bookmarkDescription}");
		}

		Button.clickSave();

		AssertTextEquals.assertPartialText(
			locator1 = "TextInput#URL_REQUIRED",
			value1 = "URL"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "TextInput#URL_REQUIRED",
			value1 = "Required"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "TextInput#URL_REQUIRED",
			value1 = "This field is required."
		);
	}

	macro addPG {
		Portlet.clickPortletPlusIconPG(portletName = "Bookmarks");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Bookmark",
			key_menuItem = "Bookmark"
		);

		if (isSet(bookmarkName)) {
			PortletEntry.inputName(name = "${bookmarkName}");
		}
		else {
			var bookmarkName = "${bookmarkURL}";
		}

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURL}");

		if (isSet(bookmarkDescription)) {
			PortletEntry.inputDescription(description = "${bookmarkDescription}");
		}

		if (isSet(bookmarkViewableBy)) {
			Panel.expandPanel(panelHeading = "Permissions");

			Select(
				locator1 = "AssetPermissions#VIEWABLE_AS_SELECT",
				value1 = "${bookmarkViewableBy}"
			);
		}

		PortletEntry.save();

		Bookmark.viewBookmark(
			bookmarkName = "${bookmarkName}",
			bookmarkURL = "${bookmarkURL}"
		);
	}

	macro addRelatedAssetBookmarksEntryPG {
		SelectFrame(locator1 = "AssetRelatedAssets#SELECT_ASSET_IFRAME");

		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${bookmarkName}");

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURL}");

		Button.clickSave();

		SelectFrame(value1 = "relative=top");
	}

	macro addToSubfolderCP {
		var key_assetName = "${folderName}";

		AssertClick(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${folderName}"
		);

		var key_assetName = "${subfolderName}";

		AssertClick(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${subfolderName}"
		);

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "${subfolderName}");

		LexiconEntry.gotoAddMenuItem(menuItem = "Bookmark");

		PortletEntry.inputName(name = "${bookmarkName}");

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURL}");

		PortletEntry.inputDescription(description = "${bookmarkDescription}");

		PortletEntry.save();

		Bookmark.viewBookmark(
			bookmarkName = "${bookmarkName}",
			bookmarkURL = "${bookmarkURL}"
		);
	}

	macro deletePG {
		AssertTextEquals(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${bookmarkName}",
			key_assetName = "${bookmarkName}"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move to the Recycle Bin",
			rowEntry = "${bookmarkName}"
		);

		AssertElementPresent(locator1 = "Message#SUCCESS");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Recycle Bin"
		);

		RecycleBin.emptyCP();
	}

	macro editCP {
		LexiconEntry.gotoEntryMenuItem(menuItem = "Edit", rowEntry = "${bookmarkName}");

		PortletEntry.inputName(name = "${editBookmarkName}");

		Type(locator1 = "TextInput#URL", value1 = "${editBookmarkURL}");

		if (isSet(bookmarkDescription)) {
			PortletEntry.inputDescription(description = "${editBookmarkDescription}");
		}

		PortletEntry.save();

		Bookmark.viewBookmark(
			bookmarkName = "${editBookmarkName}",
			bookmarkURL = "${editBookmarkURL}"
		);
	}

	macro editPG {
		LexiconEntry.gotoEntryMenuItem(menuItem = "Edit", rowEntry = "${bookmarkName}");

		PortletEntry.inputName(name = "${bookmarkNameEdit}");

		Type(locator1 = "TextInput#URL", value1 = "${bookmarkURLEdit}");

		PortletEntry.save();

		Bookmark.viewBookmark(
			bookmarkName = "${bookmarkNameEdit}",
			bookmarkURL = "${bookmarkURLEdit}"
		);
	}

	macro gotoPG {
		AssertTextEquals(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${bookmarkName}",
			key_assetName = "${bookmarkName}"
		);
	}

	macro moveToFolderPG {
		if (isSet(folderName)) {
			BookmarksFolder.gotoPG(folderName = "${folderName}");
		}

		LexiconEntry.gotoEntryMenuItem(menuItem = "Move", rowEntry = "${bookmarkName}");

		AssertClick(locator1 = "Button#SELECT", value1 = "Select");

		IFrame.selectDialogFrame();

		AssertClick(
			locator1 = "WC#FOLDER_TABLE_CHOOSE",
			value1 = "Choose",
			key_folderName = "${folderName2}"
		);

		SelectFrame(value1 = "relative=top");

		AssertClick(locator1 = "Button#MOVE", value1 = "Move");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no bookmarks in this folder."
		);
	}

	macro moveToHomeCP {
		AssertClick(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${folderName}",
			key_assetName = "${folderName}"
		);

		if (isSet(subfolderName)) {
			AssertClick(
				locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
				value1 = "${subfolderName}",
				key_assetName = "${subfolderName}"
			);

			AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "${subfolderName}");
		}

		LexiconEntry.gotoEntryMenuItem(menuItem = "Move", rowEntry = "${bookmarkName}");

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Move Entries");

		AssertClick(locator1 = "Button#SELECT", value1 = "Select");

		IFrame.selectDialogFrame();

		AssertClick(
			locator1 = "Button#CHOOSE_THIS_FOLDER",
			value1 = "Choose This Folder"
		);

		SelectFrame(value1 = "relative=top");

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Move Entries");

		AssertClick(locator1 = "Button#MOVE", value1 = "Move");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no bookmarks in this folder."
		);

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Bookmarks"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${bookmarkName}",
			key_assetName = "${bookmarkName}"
		);
	}

	macro moveToRecycleBinCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Bookmarks"
		);

		AssertTextEquals(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${bookmarkName}",
			key_assetName = "${bookmarkName}"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move to the Recycle Bin",
			rowEntry = "${bookmarkName}"
		);

		AssertElementPresent(locator1 = "Message#SUCCESS");

		AssertTextNotPresent(
			locator1 = "Bookmarks#BOOKMARKS_TABLE_URL",
			value1 = "${bookmarkURL}",
			key_bookmarkURL = "${bookmarkName}"
		);
	}

	macro moveToRecycleBinPG {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move to the Recycle Bin",
			rowEntry = "${bookmarkName}"
		);

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}

	macro searchNullCP {
		Search.searchCP(searchTerm = "${nullBookmarkName}");

		var key_bookmarkName = "${nullBookmarkName}";

		AssertTextNotPresent(
			locator1 = "Bookmarks#SEARCH_TABLE_ENTRY_NAME",
			value1 = "${bookmarkName}"
		);

		AssertTextNotPresent(
			locator1 = "Bookmarks#SEARCH_TABLE_ENTRY_NAME",
			value1 = "${bookmar2Name}"
		);
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Bookmarks"
		);

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			Click(locator1 = "Icon#MOVE_TO_RECYCLE_BIN");

			AssertElementPresent(locator1 = "Message#SUCCESS");

			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Recycle Bin"
		);

		RecycleBin.emptyCP();
	}

	macro viewBookmark {
		AssertClickNoError(
			locator1 = "Bookmarks#DESCRIPTIVE_ENTRY_TITLE",
			value1 = "${bookmarkName}",
			key_assetName = "${bookmarkName}"
		);

		if ("${trailingSlash}" == "false") {
			AssertLocation(value1 = "${bookmarkURL}");
		}
		else {
			AssertLocation(value1 = "${bookmarkURL}/");
		}
	}

	macro viewBookmarkViaAP {
		AssertClick(
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "${bookmarkName}",
			key_assetTitle = "${bookmarkName}"
		);

		AssertTextEquals(
			locator1 = "APAsset#ASSET_TITLE_HEADER",
			value1 = "${bookmarkName}"
		);

		Click(locator1 = "AP#ASSET_ABSTRACT_BOOKMARK_DETAIL_LINK");

		SelectWindow(
			locator1 = "title=Digital Experience Software Tailored to Your Needs | Liferay"
		);

		AssertElementPresent.assertElementPresent(
			locator1 = "AP#ASSET_ABSTRACT_BOOKMARK_LIFERAY_LOGO"
		);

		Close.closeWindow(
			locator1 = "title=Digital Experience Software Tailored to Your Needs | Liferay"
		);
	}

	macro viewEntry {
		Click(locator1 = "Icon#DISPLAY_STYLE_LIST");

		AssertTextEquals(
			locator1 = "Bookmarks#BOOKMARKS_TABLE_NAME",
			value1 = "${bookmarkName}",
			key_bookmarkName = "${bookmarkName}"
		);

		AssertTextEquals(
			locator1 = "Bookmarks#BOOKMARKS_TABLE_URL",
			value1 = "${bookmarkURL}",
			key_bookmarkURL = "${bookmarkURL}"
		);
	}

	macro viewNoCategoryCP {
		LexiconEntry.gotoAddMenuItem(menuItem = "Bookmark");

		Panel.expandPanel(panelHeading = "Categorization");

		AssertElementNotPresent(locator1 = "AssetCategorization#VOCABULARY");
	}

	macro viewNoPG {
		AssertTextNotPresent(value1 = "${bookmarkName}");

		AssertTextNotPresent(value1 = "${bookmarkURL}");
	}
}