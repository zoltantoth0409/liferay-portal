definition {
	var orgName = "Organization Name";

	macro addAdditionalEmailAddressesCP {
		Organization.gotoIdentificationCP(panelHeading = "Additional Email Addresses");

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDITIONAL_EMAIL_ADDRESSES_EMAIL_ADDRESS_FIELD",
			value1 = "${orgAdditionalEmailAddress}"
		);

		Click(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDITIONAL_EMAIL_ADDRESSES_EMAIL_ADDRESS_FIELD"
		);

		PortletEntry.save();

		Organization.gotoIdentificationCP(panelHeading = "Additional Email Addresses");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDITIONAL_EMAIL_ADDRESSES_EMAIL_ADDRESS_FIELD",
			value1 = "${orgAdditionalEmailAddress}"
		);
	}

	macro addAddressCP {
		Organization.gotoIdentificationCP();

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_STREET_FIELD_1",
			value1 = "${orgAddressStreet}"
		);

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_CITY_FIELD",
			value1 = "${orgAddressCity}"
		);

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_POSTAL_CODE_FIELD",
			value1 = "${orgAddressPostal}"
		);

		Select(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_COUNTRY_SELECT",
			value1 = "${orgAddressCountry}"
		);

		Select(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_REGION_SELECT",
			value1 = "${orgAddressRegion}"
		);

		Select(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_TYPE_SELECT",
			value1 = "${orgAddressType}"
		);

		Check(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_MAILING_CHECKBOX"
		);

		Click(
			locator1 = "UsersAndOrganizationsEditOrganization#ADDRESSES_PRIMARY_RADIO"
		);

		PortletEntry.save();
	}

	macro addCategorizationCP {
		Organization.editOrganizationInformationCP(
			orgName = "${orgName}",
			panelHeading = "Categorization"
		);

		var key_tagName = "${tagName}";

		Click(
			locator1 = "UsersAndOrganizationsEditOrganization#CATEGORIZATION_TAGS_FIELD"
		);

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#CATEGORIZATION_TAGS_FIELD",
			value1 = "${tagName}"
		);

		AssertClick(locator1 = "Button#ADD_TAGS", value1 = "Add");

		AssertElementPresent(
			locator1 = "UsersAndOrganizationsEditOrganization#CATEGORIZATION_TAG"
		);

		PortletEntry.save();

		Organization.gotoOrganizationInformationCP(panelHeading = "Categorization");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#CATEGORIZATION_TAG",
			value1 = "${tagName}"
		);
	}

	macro addCommentCP {
		Organization.gotoMiscellaneousCP();

		Type(locator1 = "TextArea#COMMENTS", value1 = "${orgComment}");

		PortletEntry.save();

		Organization.gotoMiscellaneousCP();

		AssertTextEquals(locator1 = "TextArea#COMMENTS", value1 = "${orgComment}");
	}

	macro addCP {
		AssertClick(locator1 = "NavBar#ORGANIZATIONS", value1 = "Organizations");

		var key_navItem = "Organizations";

		AssertElementPresent(locator1 = "NavBar#ACTIVE_NAV_ITEM_LINK");

		LexiconEntry.gotoAdd();

		if ("${orgType}" == "Location") {
			AssertClick(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Location",
				key_menuItem = "Location"
			);
		}
		else if (IsElementPresent(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "Organization")) {
			AssertClick(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Organization",
				key_menuItem = "Organization"
			);
		}

		Type(locator1 = "TextInput#NAME", value1 = "${orgName}");

		Button.clickSave();

		if ("${organizationName}" == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The organization name is already taken."
			);
		}
		else {
			var key_orgType = "${orgType}";

			Alert.viewSuccessMessage();

			AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${orgName}");

			AssertTextEquals.assertValue(
				locator1 = "UsersAndOrganizationsEditOrganization#DETAILS_TYPE",
				value1 = "${orgType}"
			);
		}
	}

	macro addMemberCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Assign Users",
			key_menuItem = "Assign Users"
		);

		Navigator.gotoNavTab(navTab = "Available");

		var key_userScreenName = "${userScreenName}";

		if (IsElementNotPresent(locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME")) {
			Type(locator1 = "TextInput#SITE_SEARCH_KEYWORDS", value1 = "${userScreenName}");

			AssertClick(locator1 = "Button#SEARCH", value1 = "Search");
		}

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}"
		);

		Check(locator1 = "UsersAndOrganizationsAssignUsers#USER_TABLE_CHECKBOX");

		AssertClick(
			locator1 = "Button#UPDATE_ASSOCIATIONS",
			value1 = "Update Associations"
		);

		Alert.viewSuccessMessage();

		Navigator.gotoNavTab(navTab = "Current");

		AssertChecked(locator1 = "UsersAndOrganizationsAssignUsers#USER_TABLE_CHECKBOX");

		if (isSet(userMiddleName)) {
			AssertTextEquals(
				locator1 = "UsersAndOrganizations#USER_TABLE_USER_NAME",
				value1 = "${userFirstName} ${userMiddleName} ${userLastName}"
			);
		}
		else {
			AssertTextEquals(
				locator1 = "UsersAndOrganizations#USER_TABLE_USER_NAME",
				value1 = "${userFirstName} ${userLastName}"
			);
		}

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}"
		);
	}

	macro addPhoneNumbersCP {
		Organization.gotoIdentificationCP(panelHeading = "Phone Numbers");

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#PHONE_NUMBERS_NUMBER_FIELD",
			value1 = "${orgPhoneNumber}"
		);

		Click(
			locator1 = "UsersAndOrganizationsEditOrganization#PHONE_NUMBERS_PRIMARY_RADIO"
		);

		PortletEntry.save();

		Organization.gotoIdentificationCP(panelHeading = "Phone Numbers");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#PHONE_NUMBERS_NUMBER_FIELD",
			value1 = "${orgPhoneNumber}"
		);
	}

	macro addReminderQueriesCP {
		Organization.gotoMiscellaneousCP(panelHeading = "Reminder Queries");

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#REMINDER_QUERIES_DEFAULT_LANGUAGE_FIELD",
			value1 = "${orgReminderQueriesDefaultLanguage}"
		);

		Select(
			locator1 = "UsersAndOrganizationsEditOrganization#REMINDER_QUERIES_LOCALIZED_LANGUAGE_SELECT",
			value1 = "${orgLocalizedLanguage}"
		);

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#REMINDER_QUERIES_TEXT_FIELD",
			value1 = "${orgReminderQuery}"
		);

		PortletEntry.save();

		Organization.gotoMiscellaneousCP(panelHeading = "Reminder Queries");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#REMINDER_QUERIES_DEFAULT_LANGUAGE_FIELD",
			value1 = "${orgReminderQueriesDefaultLanguage}"
		);

		Select(
			locator1 = "UsersAndOrganizationsEditOrganization#REMINDER_QUERIES_LOCALIZED_LANGUAGE_SELECT",
			value1 = "${orgLocalizedLanguage}"
		);

		AssertSelectedLabel(
			locator1 = "UsersAndOrganizationsEditOrganization#REMINDER_QUERIES_LOCALIZED_LANGUAGE_SELECT",
			value1 = "${orgLocalizedLanguage}"
		);
	}

	macro addServicesCP {
		Organization.gotoIdentificationCP(panelHeading = "Services");

		Select(
			locator1 = "UsersAndOrganizationsEditOrganization#SERVICES_MONDAY_OPEN_SELECT",
			value1 = "${orgServicesOpen}"
		);

		Select(
			locator1 = "UsersAndOrganizationsEditOrganization#SERVICES_MONDAY_CLOSE_SELECT",
			value1 = "${orgServicesClosed}"
		);

		PortletEntry.save();

		Organization.gotoIdentificationCP(panelHeading = "Services");

		AssertSelectedLabel(
			locator1 = "UsersAndOrganizationsEditOrganization#SERVICES_MONDAY_OPEN_SELECT",
			value1 = "${orgServicesOpen}"
		);

		AssertSelectedLabel(
			locator1 = "UsersAndOrganizationsEditOrganization#SERVICES_MONDAY_CLOSE_SELECT",
			value1 = "${orgServicesClosed}"
		);
	}

	macro addSiteCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		Pause(locator1 = "3000");

		Panel.expandPanel(panelHeading = "Organization Site");

		Check(
			locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_CREATE_SITE_CHECKBOX"
		);

		if (("${organizationSite}" == "Community Site") || ("${organizationSite}" == "Intranet Site")) {
			Select(
				locator1 = "UserGroupsEditUserGroup#PUBLIC_PAGES_SELECT",
				value1 = "${organizationSite}"
			);

			if ("${checkEnablePropagationCheckbox}" == "true") {
				AssertChecked(
					locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX"
				);
			}
			else {
				Uncheck(
					locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX"
				);
			}
		}

		PortletEntry.save();

		Panel.expandPanel(panelHeading = "Organization Site");

		if (IsElementPresent(locator1 = "UserGroupsEditUserGroup#PUBLIC_PAGES_SELECT")) {
			AssertChecked(
				locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_CREATE_SITE_CHECKBOX"
			);
		}
	}

	macro addSiteCPWithPrivatePage {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		Pause(locator1 = "3000");

		Panel.expandPanel(panelHeading = "Organization Site");

		Check(
			locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_CREATE_SITE_CHECKBOX"
		);

		if (("${organizationSite}" == "Community Site") || ("${organizationSite}" == "Intranet Site")) {
			Select(
				locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_PRIVATE_PAGES_SELECT",
				value1 = "${organizationSite}"
			);

			if ("${checkEnablePropagationCheckbox}" == "true") {
				AssertChecked(
					locator1 = "UsersAndOrganizationsEditOrganization#ENABLE_PROPAGATION_PRIVATE_PAGE_CHECKBOX"
				);
			}
			else {
				Uncheck(
					locator1 = "UsersAndOrganizationsEditOrganization#ENABLE_PROPAGATION_PRIVATE_PAGE_CHECKBOX"
				);
			}
		}

		PortletEntry.save();

		Panel.expandPanel(panelHeading = "Organization Site");

		if (IsElementPresent(locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_PRIVATE_PAGES_SELECT")) {
			AssertChecked(
				locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_CREATE_SITE_CHECKBOX"
			);
		}
	}

	macro addWebsitesCP {
		Organization.gotoIdentificationCP(panelHeading = "Websites");

		Type(
			locator1 = "UsersAndOrganizationsEditOrganization#WEBSITES_URL_FIELD",
			value1 = "${orgWebsite}"
		);

		Click(locator1 = "UsersAndOrganizationsEditOrganization#WEBSITES_PRIMARY_RADIO");

		PortletEntry.save();

		Organization.gotoIdentificationCP(panelHeading = "Websites");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#WEBSITES_URL_FIELD",
			value1 = "${orgWebsite}"
		);
	}

	macro addWithLocalizedCountryCP {
		Navigator.gotoNavItem(navItem = "${navItem}");

		LexiconEntry.gotoAdd();

		if (IsElementPresent(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "${localizedOption}")) {
			AssertClick(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "${localizedOption}",
				key_menuItem = "${localizedOption}"
			);
		}

		Type(locator1 = "TextInput#NAME", value1 = "${orgName}");

		Select(
			locator1 = "UsersAndOrganizations#ORGANIZATION_COUNTRY_NAME",
			value1 = "${countryName}",
			key_countryName = "${countryName}"
		);

		AssertClick(
			locator1 = "Button#SAVE_LOCALIZED",
			value1 = "${saveButton}",
			key_saveButton = "${saveButton}"
		);

		Alert.viewSuccessMessage();
	}

	macro assignOrgRoleCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		Pause(locator1 = "5000");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Assign Organization Roles",
			key_menuItem = "Assign Organization Roles"
		);

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertTextEquals(
			locator1 = "//tr[contains(.,'${roleName}')]/td[1]/span/a",
			value1 = "${roleName}"
		);

		Click(locator1 = "//tr[contains(.,'${roleName}')]/td[1]/span/a");

		Navigator.gotoNavItem(navItem = "Available");

		Search.searchCP(searchTerm = "${userScreenName}");

		var key_userScreenName = "${userScreenName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}"
		);

		Check(locator1 = "UsersAndOrganizationsAssignUsers#USER_TABLE_CHECKBOX");

		AssertClick(
			locator1 = "Button#UPDATE_ASSOCIATIONS",
			value1 = "Update Associations"
		);

		Alert.viewSuccessMessage();

		Navigator.gotoNavItem(navItem = "Current");

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_USER_NAME",
			value1 = "${userFirstName} ${userLastName}"
		);

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}"
		);

		IFrame.selectTopFrame();

		Click(locator1 = "Icon#CLOSE");
	}

	macro deleteCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${orgName}");
	}

	macro editOrganizationCreateSite {
		Organization.editOrganizationInformationCP(
			orgName = "${orgName}",
			panelHeading = "Organization Site"
		);

		Check(
			locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_CREATE_SITE_CHECKBOX"
		);

		PortletEntry.save();

		Organization.gotoOrganizationInformationCP(panelHeading = "Organization Site");

		AssertChecked(
			locator1 = "UsersAndOrganizationsEditOrganization#ORGANIZATION_SITE_CREATE_SITE_CHECKBOX"
		);
	}

	macro editOrganizationInformationCP {
		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		Organization.gotoOrganizationInformationCP(panelHeading = "${panelHeading}");
	}

	macro editSiteCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		Type(locator1 = "TextInput#NAME", value1 = "${orgNameEdit}");

		PortletEntry.save();

		AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${orgNameEdit}");
	}

	macro gotoActionsCmd {
		Pause(locator1 = "1000");

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "${orgName}");

		Click(locator1 = "Button#SUBMIT");

		AssertVisible(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC"
		);

		var key_orgName = "${orgName}";

		if (IsElementNotPresent(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME")) {
			Pagination.changePagination(itemsPerPage = "30");
		}

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);

		Click(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_ACTIONS");
	}

	macro gotoCP {
		Type(locator1 = "TextInput#SEARCH", value1 = "${searchToken}");

		Click(locator1 = "Button#SUBMIT");

		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);

		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);

		Breadcrumb.viewActiveEntry(breadcrumbName = "${orgName}");
	}

	macro gotoIdentificationCP {
		AssertClick(locator1 = "NavTab#IDENTIFICATION", value1 = "Identification");

		if (isSet(panelHeading)) {
			Panel.expandPanel(panelHeading = "${panelHeading}");
		}
	}

	macro gotoMiscellaneousCP {
		AssertClick(
			locator1 = "UsersAndOrganizations#MISCELLANEOUS",
			value1 = "Miscellaneous"
		);

		if (isSet(panelHeading)) {
			Panel.expandPanel(panelHeading = "${panelHeading}");
		}
	}

	macro gotoOrganizationInformationCP {
		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_INFORMATION",
			value1 = "Organization Information"
		);

		if (isSet(panelHeading)) {
			Panel.expandPanel(panelHeading = "${panelHeading}");
		}
	}

	macro manageMembershipOptionsInSites {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Manage Site",
			key_menuItem = "Manage Site"
		);

		if ("${selectMembershipType}" == "true") {
			AssertTextEquals(
				locator1 = "SitesEditSite#MEMBERSHIP_OPTIONS_MEMBERSHIP_TYPE_LABEL",
				value1 = "Membership Type"
			);

			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_OPTIONS_MEMBERSHIP_TYPE",
				value1 = "${membershipType}"
			);
		}

		if ("${allowManualMembershipManagement}" == "true") {
			Check.checkToggleSwitch(
				locator1 = "Checkbox#ALLOW_MANUAL_MEMBERSHIP_MANAGEMENT"
			);
		}

		PortletEntry.save();
	}

	macro removeMemberCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Assign Users",
			key_menuItem = "Assign Users"
		);

		var key_userScreenName = "${userScreenName}";

		AssertClick(
			locator1 = "UsersAndOrganizationsAssignUsers#CURRENT_TAB",
			value1 = "Current"
		);

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}"
		);

		Uncheck(locator1 = "UsersAndOrganizationsAssignUsers#USER_TABLE_CHECKBOX");

		AssertClick(
			locator1 = "Button#UPDATE_ASSOCIATIONS",
			value1 = "Update Associations"
		);

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No users were found."
		);
	}

	macro searchCP {
		Type(locator1 = "TextInput#SEARCH", value1 = "${searchTerm}");

		Click(locator1 = "Button#SUBMIT");

		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);
	}

	macro selectParentOrganizationCP {
		var key_orgName = "${orgName}";

		Click(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_ACTIONS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		AssertClick(
			locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_SELECT_BUTTON",
			value1 = "Select"
		);

		SelectFrame(
			locator1 = "UsersAndOrganizationsSelectOrganization#SELECT_ORGANIZATION_IFRAME"
		);

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditUser#ORGANIZATIONS_TABLE_NAME",
			value1 = "${orgName}"
		);

		var key_orgName = "${parentOrgName}";

		AssertClick(
			locator1 = "UsersAndOrganizationsSelectOrganization#ORGANIZATION_TABLE_CHOOSE_BUTTON",
			value1 = "Choose"
		);

		SelectFrame(value1 = "relative=top");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_TABLE_NAME",
			value1 = "${parentOrgName}"
		);

		PortletEntry.save();
	}

	macro tearDownCP {
		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations"
		);

		while (IsElementPresent(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC", value1 = "${orgName}")) {
			Click(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC");

			AssertClick(
				locator1 = "UsersAndOrganizationsOrganization#MENU_ASSIGN_USERS",
				value1 = "Assign Users"
			);

			AssertClick(
				locator1 = "UsersAndOrganizationsAssignUsers#CURRENT_TAB",
				value1 = "Current"
			);

			if ((IsElementPresent(locator1 = "Checkbox#SELECT_ALL")) && (IsVisible(locator1 = "Checkbox#SELECT_ALL"))) {
				Uncheck(locator1 = "Checkbox#SELECT_ALL");

				AssertClick(
					locator1 = "Button#UPDATE_ASSOCIATIONS",
					value1 = "Update Associations"
				);

				AssertTextEquals(locator1 = "Message#INFO", value1 = "No users were found.");
			}

			AssertClick.assertPartialTextClickAt(locator1 = "Link#BACK", value1 = "Back");

			AssertClickNoError(
				locator1 = "UsersAndOrganizationsOrganization#MENU_DELETE",
				value1 = "Delete"
			);

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);
		}
	}

	macro viewAssignMemberCP {
		var key_userScreenName = "${userScreenName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsOrganization#USER_TABLE_NAME",
			value1 = "${userName}"
		);

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsOrganization#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}"
		);
	}

	macro viewCP {
		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_TYPE",
			value1 = "${orgType}"
		);

		Click(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_ACTIONS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		var key_orgType = "${orgType}";

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Edit ${orgName}");

		AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${orgName}");

		AssertTextEquals.assertValue(
			locator1 = "UsersAndOrganizationsEditOrganization#DETAILS_TYPE",
			value1 = "${orgType}"
		);
	}

	macro viewLocalizedCountryCP {
		AssertClick(
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "${navItem}",
			key_navItem = "${navItem}"
		);

		Click(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_ACTIONS",
			key_orgName = "${orgName}"
		);

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "${editLocalized}",
			key_menuItem = "${editLocalized}"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "UsersAndOrganizations#ORGANIZATION_COUNTRY_NAME",
			value1 = "${countryName}",
			key_countryName = "${countryName}"
		);
	}

	macro viewMembersNotFoundCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "This organization does not have any users."
		);
	}

	macro viewNoOrganizationsFoundCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No organizations were found."
		);
	}

	macro viewParentOrganizationCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		AssertClick(
			locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_SELECT_BUTTON",
			value1 = "Select"
		);

		SelectFrame(
			locator1 = "UsersAndOrganizationsSelectOrganization#SELECT_ORGANIZATION_IFRAME"
		);

		if ("${parentOrgExist}" == "true") {
			var key_orgName = "${subOrgName}";

			AssertTextEquals(
				locator1 = "UsersAndOrganizationsEditUser#ORGANIZATIONS_TABLE_NAME",
				value1 = "${subOrgName}"
			);

			AssertTextEquals(
				locator1 = "UsersAndOrganizationsSelectOrganization#ORGANIZATION_TABLE_PARENT_ORGANIZATION",
				value1 = "${parentOrgName}"
			);
		}
		else if ("${parentOrgExist}" == "false") {
			AssertTextEquals(
				locator1 = "UsersAndOrganizationsEditUser#ORGANIZATIONS_TABLE_NAME",
				value1 = "${orgName}"
			);

			AssertTextEquals(
				locator1 = "UsersAndOrganizationsSelectOrganization#ORGANIZATION_TABLE_PARENT_ORGANIZATION",
				value1 = ""
			);
		}

		IFrame.closeFrame();
	}

	macro viewRemoveMemberCP {
		Type(locator1 = "TextInput#SEARCH", value1 = "${orgName}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);

		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);

		AssertTextNotPresent(value1 = "${userFirstName}");
	}

	macro viewSubOrganizationCP {
		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_TYPE",
			value1 = "${orgType}"
		);

		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}"
		);

		var key_suborgName = "${suborgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsOrganization#SUBORGANIZATION_TABLE_NAME",
			value1 = "${suborgName}"
		);

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsOrganization#SUBORGANIZATION_TABLE_TYPE",
			value1 = "${suborgType}"
		);

		AssertElementPresent(
			locator1 = "UsersAndOrganizationsOrganization#SUBORGANIZATION_TABLE_ACTIONS"
		);
	}
}