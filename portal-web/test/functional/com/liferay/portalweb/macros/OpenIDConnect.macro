definition {

	macro acceptConditions {
		if (IsElementPresent.pauseIsElementPresent(locator1 = "GoogleLogin#ALLOW")) {
			Click(locator1 = "GoogleLogin#ALLOW");
		}

		if (IsElementPresent.pauseIsElementPresent(locator1 = "Button#I_AGREE")) {
			Click(locator1 = "Button#I_AGREE");
		}

		if (IsElementPresent.pauseIsElementPresent(locator1 = "TextInput#ANSWER")) {
			Type(
				locator1 = "TextInput#ANSWER",
				value1 = "test");

			KeyPress(
				locator1 = "TextInput#ANSWER",
				value1 = "\RETURN");
		}
	}

	macro accountSignIn {
		if (isSet(specificURL)) {
			Navigator.openSpecificURL(url = "${specificURL}");
		}
		else {
			Navigator.openURL();
		}

		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		Click(locator1 = "Icon#OPENID_CONNECT");

		Click(locator1 = "Button#SIGN_IN");

		Type(
			locator1 = "GoogleLogin#LOGIN_EMAIL_FIELD",
			value1 = "${userEmailAddress}");

		Click(locator1 = "GoogleLogin#LOGIN_NEXT_BUTTON");

		Type(
			locator1 = "GoogleLogin#LOGIN_PASSWORD_FIELD",
			value1 = "${userPassword}");

		if (IsElementPresent(locator1 = "GoogleLogin#LOGIN_SIGN_IN_BUTTON")) {
			Click(locator1 = "GoogleLogin#LOGIN_SIGN_IN_BUTTON");
		}
		else {
			Click(locator1 = "GoogleLogin#LOGIN_NEXT_BUTTON");
		}
	}

	macro addProvider {
		Button.clickAdd();

		FormFields.editTextMultiline(
			fieldName = "providerName",
			fieldValue = "${providerName}");

		FormFields.editTextMultiline(
			fieldName = "openIdConnectClientId",
			fieldValue = "${openIDConnectClientID}");

		FormFields.editTextMultiline(
			fieldName = "openIdConnectClientSecret",
			fieldValue = "${openIDConnectClientSecret}");

		FormFields.editTextMultiline(
			fieldName = "discoveryEndPoint",
			fieldValue = "https://accounts.google.com/.well-known/openid-configuration");

		PortletEntry.save();
	}

	macro viewProvider {
		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		AssertElementPresent(locator1 = "Icon#OPENID_CONNECT");

		Click(locator1 = "Icon#OPENID_CONNECT");

		AssertElementPresent(locator1 = "OpenIDConnectLogin#OPENID_CONNECT_PROVIDER_NAME_LABEL");

		Click(locator1 = "OpenIDConnectLogin#OPENID_CONNECT_DROPDOWN_MENU");

		AssertElementPresent(
			key_providerNname = "Google",
			locator1 = "OpenIDConnectLogin#OPENID_CONNECT_DROPDOWN_OPTION");
	}

}