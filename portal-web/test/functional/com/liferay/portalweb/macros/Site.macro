definition {

	var siteMember = "User";
	var siteType = "Open";

	macro addAllMembersCP {
		Navigator.gotoNavItem(navItem = "Users");

		LexiconEntry.gotoAdd();

		IFrame.selectDialogFrame();

		Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

		SelectFrameTop();

		AssertClick(
			locator1 = "Button#DONE",
			value1 = "Done");

		Alert.viewSuccessMessage();
	}

	macro addBlankCP {
		Site.addCP(
			membershipType = "${membershipType}",
			parentSiteName = "${parentSiteName}",
			singleSiteTypePermission = "${singleSiteTypePermission}",
			siteName = "${siteName}",
			siteTemplateName = "${siteTemplateName}",
			siteType = "Blank",
			uncheckPropagation = "${uncheckPropagation}");
	}

	macro addCommunityCP {
		Site.addCP(
			membershipType = "${membershipType}",
			parentSiteName = "${parentSiteName}",
			siteName = "${siteName}",
			siteTemplateName = "${siteTemplateName}",
			siteType = "Community",
			uncheckPropagation = "${uncheckPropagation}");
	}

	macro addCP {
		LexiconEntry.gotoAdd();

		if ("${singleSiteTypePermission}" != "true") {
			if ("${siteType}" == "Blank") {
				AssertClick(
					key_menuItem = "Blank Site",
					locator1 = "MenuItem#ANY_MENU_ITEM",
					value1 = "Blank Site");
			}
			else if ("${siteType}" == "Community") {
				AssertClick(
					key_menuItem = "Community Site",
					locator1 = "MenuItem#ANY_MENU_ITEM",
					value1 = "Community Site");
			}
			else if ("${siteType}" == "Intranet") {
				AssertClick(
					key_menuItem = "Intranet Site",
					locator1 = "MenuItem#ANY_MENU_ITEM",
					value1 = "Intranet Site");
			}
			else if ("${siteType}" == "Site Template") {
				MenuItem.click(menuItem = "${siteTemplateName}");
			}
			else {
				fail("Invalid var siteType. Must be (Blank|Community|Intranet|Site Template).");
			}
		}

		PortletEntry.inputName(name = "${siteName}");

		if (("${siteType}" == "Community") || ("${siteType}" == "Site Template")) {
			if ("${uncheckPropagation}" == "true") {
				Panel.expandPanel(panelHeading = "Pages");

				Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ENABLE_PROPAGATION");
			}
		}

		if ("${membershipType}" == "Private") {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Private");
		}
		else if ("${membershipType}" == "Restricted") {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Restricted");
		}
		else {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Open");
		}

		if (isSet(parentSiteName)) {
			AssertClick(
				locator1 = "Button#SELECT",
				value1 = "Select");

			var key_parentSiteName = "${parentSiteName}";

			SelectFrame(locator1 = "SitesSelectSite#SELECT_SITE_IFRAME");

			Type(
				locator1 = "NavBar#BASIC_SEARCH_FIELD",
				value1 = "${parentSiteName}");

			KeyPress(
				locator1 = "NavBar#BASIC_SEARCH_FIELD",
				value1 = "\RETURN");

			var key_site = "${parentSiteName}";

			AssertTextEquals(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_TD_1_SITE_LINK",
				value1 = "${parentSiteName}");

			Click(locator1 = "ContentRow#ENTRY_CONTENT_ROW_TD_1_SITE_LINK");

			var key_parentSiteName = "${parentSiteName}";

			SelectFrameTop();
		}

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "SitesEditSite#DETAILS_NAME",
			value1 = "${siteName}");
	}

	macro addIntranetCP {
		Site.addCP(
			membershipType = "${membershipType}",
			parentSiteName = "${parentSiteName}",
			siteName = "${siteName}",
			siteTemplateName = "${siteTemplateName}",
			siteType = "Intranet",
			uncheckPropagation = "${uncheckPropagation}");
	}

	macro addSiteTemplateCP {
		Site.addCP(
			membershipType = "${membershipType}",
			parentSiteName = "${parentSiteName}",
			siteName = "${siteName}",
			siteTemplateName = "${siteTemplateName}",
			siteType = "Site Template",
			uncheckPropagation = "${uncheckPropagation}");
	}

	macro addVirtualHostsURLCP {
		Panel.expandPanel(panelHeading = "Site URL");

		if (isSet(publicVirtualHosts)) {
			AssertTextEquals(
				locator1 = "SiteSettingsSiteURL#SITE_URL_VIRTUAL_HOSTS_PUBLIC_PAGES_LABEL",
				value1 = "Public Pages");

			Type(
				locator1 = "TextInput#PUBLIC_VIRTUAL_HOST_URL",
				value1 = "${publicPageVirtualHostURL}");
		}

		if (isSet(privateVirtualHosts)) {
			AssertTextEquals(
				locator1 = "SiteSettingsSiteURL#SITE_URL_VIRTUAL_HOSTS_PRIVATE_PAGES_LABEL",
				value1 = "Private Pages");

			Type(
				locator1 = "TextInput#PRIVATE_VIRTUAL_HOST_URL",
				value1 = "${privatePageVirtualHostURL}");
		}

		PortletEntry.save();
	}

	macro approveJoinSiteRestrictedViaMySitesPG {
		var key_userScreenName = "${userScreenName}";

		Click(locator1 = "Button#INFO");

		AssertTextEquals.assertPartialText(
			locator1 = "SiteMemberships#MEMBERSHIP_TYPE",
			value1 = "Membership Type: Restricted");

		AssertElementPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		Click(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		Portlet.changeDisplayStyle(displayStyle = "List");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_COMMENTS",
			value1 = "Please allow me to join the Restricted Site.");

		Click(locator1 = "SiteMembershipsViewMembershipRequests#REPLY");

		Select(
			locator1 = "SiteMembershipsViewMembershipRequests#STATUS",
			value1 = "Approve");

		Type(
			locator1 = "SiteMembershipsViewMembershipRequests#REPLY_COMMENTS_AREA",
			value1 = "You may join the Restricted Site.");

		Button.clickSave();

		AssertElementPresent(locator1 = "Message#SUCCESS");

		Navigator.gotoNavItem(navItem = "Approved");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#REPLIER_NAME",
			value1 = "Test Test");
	}

	macro assertMemberListNotVisibleCP {
		for (var userEmail : list "${usersEmailsList}") {
			var userName = StringUtil.replace("${userEmail}", "@liferay.com", "");

			Site.assertMemberNotVisibleCP(
				userFirstName = "${userName}",
				userLastName = "${userName}",
				userScreenName = "${userName}");
		}
	}

	macro assertMemberNotVisibleCP {
		var key_userScreenName = "${userScreenName}";

		AssertTextNotPresent(
			locator1 = "SiteMemberships#USER_TABLE_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextNotPresent(
			locator1 = "SiteMemberships#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");
	}

	macro assignDefaultSiteTeam {
		Click(locator1 = "SitesEditSite#MENU_ADVANCED_TAB");

		AssertClick(
			locator1 = "SiteSettingsDefaultUserAssociations#TEAMS_SELECT",
			value1 = "Select");

		SelectFrame(locator1 = "SiteSettingsDefaultUserAssociations#TEAMS_SELECT_IFRAME");

		Portlet.changeDisplayStyle(displayStyle = "List");

		LexiconEntry.gotoEntry(rowEntry = "${teamName}");

		SelectFrame(value1 = "relative=top");

		PortletEntry.save();

		Click(locator1 = "SitesEditSite#MENU_ADVANCED_TAB");

		AssertTextEquals(
			key_teamName = "${teamName}",
			locator1 = "SiteSettingsDefaultUserAssociations#TEAMS_SELECTED",
			value1 = "${teamName}");
	}

	macro assignOrganizationAsMemberCP {
		var key_cardText = "${orgName}";

		Navigator.gotoNavItem(navItem = "Organizations");

		LexiconEntry.gotoAdd();

		IFrame.selectDialogFrame();

		AssertTextEquals(
			locator1 = "Card#SPECIFIC_LARGE_TEXT",
			value1 = "${orgName}");

		Check(locator1 = "Card#SPECIFIC_CHECKBOX");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignSiteRoleCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Site Roles",
			rowEntry = "${resourceName}");

		IFrame.selectDialogFrame();

		Check(
			key_cardText = "${roleName}",
			locator1 = "Card#SPECIFIC_CHECKBOX");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignSiteRoleToUserGroup {
		var key_userGroupName = "${userGroupName}";

		Navigator.gotoNavItem(navItem = "User Groups");

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${userGroupName}");

		Click(locator1 = "Button#SUBMIT");

		Click(locator1 = "Icon#DISPLAY_STYLE_LIST");

		AssertTextEquals(
			locator1 = "UserGroups#USER_GROUP_TABLE_NAME",
			value1 = "${userGroupName}");

		LexiconEntry.openEntryMenu(rowEntry = "${userGroupName}");

		MenuItem.click(menuItem = "Assign Site Roles");

		IFrame.selectDialogFrame();

		Portlet.changeDisplayStyle(displayStyle = "Icon");

		Check(
			key_cardText = "${roleName}",
			locator1 = "Card#SPECIFIC_CHECKBOX");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignUserAsMemberCP {
		var key_cardText = "${userScreenName}";

		LexiconEntry.gotoAdd();

		AssertVisible(locator1 = "Button#DONE");

		IFrame.selectDialogFrame();

		if (isSet(userMiddleName)) {
			AssertTextEquals(
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${userFirstName} ${userMiddleName} ${userLastName}");
		}
		else if (IsElementNotPresent(locator1 = "Card#SPECIFIC_LARGE_TEXT")) {
			var searchTerm = "${userFirstName}";

			Search.searchCP(searchTerm = "${searchTerm}");

			AssertTextEquals(
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${userFirstName} ${userLastName}");
		}
		else {
			AssertTextEquals(
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${userFirstName} ${userLastName}");
		}

		AssertTextEquals(
			locator1 = "Card#SPECIFIC_TEXT",
			value1 = "${userScreenName}");

		Check(locator1 = "Card#SPECIFIC_CHECKBOX");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignUserGroupAsMemberCP {
		var key_cardText = "${userGroupName}";

		Navigator.gotoNavItem(navItem = "User Groups");

		Pause(locator1 = "5000");

		LexiconEntry.gotoAdd();

		IFrame.selectDialogFrame();

		AssertTextEquals(
			locator1 = "Card#SPECIFIC_LARGE_TEXT",
			value1 = "${userGroupName}");

		Check(locator1 = "Card#SPECIFIC_CHECKBOX");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignUserSitesCP {
		var key_userScreenName = "${userScreenName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME_LINK",
			value1 = "${userScreenName}");

		AssertClick(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME_LINK",
			value1 = "${userScreenName}");

		Panel.expandPanel(panelHeading = "Sites");

		AssertClick(
			locator1 = "UsersAndOrganizationsEditUser#SITES_SELECT_LINK",
			value1 = "Select");

		SelectFrame(locator1 = "IFrame#SELECT_USER_SITES");

		var key_siteName = "${siteName}";

		Type(
			locator1 = "TextInput#SITE_SEARCH_KEYWORDS",
			value1 = "${siteName}");

		KeyPress(
			locator1 = "TextInput#SEARCH",
			value1 = "\RETURN");

		AssertTextEquals.assertPartialText(
			locator1 = "UsersAndOrganizationsEditUser#SITES_TABLE_NAME",
			value1 = "${siteName}");

		Click(locator1 = "SitesSelectSite#SITES_TABLE_CHOOSE_SITE_LINK");

		SelectFrame(value1 = "relative=top");

		PortletEntry.save();
	}

	macro configureCurrentLanguagesCP {
		var key_displaySettings = "${displaySettings}";

		if (IsNotVisible(locator1 = "SiteSettingsDisplaySettings#DEFINE_DEFAULT_AND_AVAILABLE_SITE_LANGUAGES_RADIO")) {
			if (isSet(localizedLanguageTab)) {
				Navigator.gotoNavTab(navTab = "${localizedLanguageTab}");
			}
			else {
				Navigator.gotoNavTab(navTab = "Languages");
			}
		}

		Check(locator1 = "SiteSettingsDisplaySettings#DEFINE_DEFAULT_AND_AVAILABLE_SITE_LANGUAGES_RADIO");

		if (isSet(currentSiteLanguages)) {
			while (IsElementPresent(locator1 = "LanguageConfiguration#LANGUAGES_CURRENT_SELECT")) {
				Click(locator1 = "LanguageConfiguration#LANGUAGES_CURRENT_SELECT");

				Click(locator1 = "SiteSettingsDisplaySettings#MOVE_CURRENT_TO_AVAILABLE_BUTTON");
			}

			for (var currentSiteLanguage : list "${currentSiteLanguages}") {
				var currentSiteLanguage = "${currentSiteLanguage}";

				Select(
					locator1 = "LanguageConfiguration#LANGUAGES_AVAILABLE",
					value1 = "${currentSiteLanguage}");

				Click(locator1 = "SiteSettingsDisplaySettings#MOVE_AVAILABLE_TO_CURRENT_BUTTON");

				AssertTextEquals.assertPartialText(
					locator1 = "LanguageConfiguration#LANGUAGES_CURRENT",
					value1 = "${currentSiteLanguage}");
			}
		}

		if (isSet(defaultSiteLanguage)) {
			var defaultSiteLanguage = "${defaultSiteLanguage}";

			Select(
				locator1 = "LanguageConfiguration#DEFAULT_LANGUAGE_SELECT",
				value1 = "${defaultSiteLanguage}");
		}

		if (isSet(localizedSaveButton)) {
			var key_saveButton = "${localizedSaveButton}";

			AssertClick(
				locator1 = "Button#SAVE_LOCALIZED",
				value1 = "${localizedSaveButton}");
		}
		else {
			Click(locator1 = "Button#SAVE");
		}

		if (isSet(localizedSuccessMessage)) {
			var localizedSuccessMessage = "${localizedSuccessMessage}";

			AssertElementPresent(locator1 = "Message#SUCCESS");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro deactivateCP {
		var key_siteName = "${siteName}";

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${siteName}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		AssertTextEquals.assertPartialText(
			locator1 = "Sites#SITE_TABLE_NAME",
			value1 = "${siteName}");

		LexiconEntry.openEntryMenu(rowEntry = "${siteName}");

		MenuItem.clickNoError(menuItem = "Deactivate");

		Confirm(value1 = "Are you sure you want to deactivate this?");

		if ("${successMessage}" == "true") {
			Alert.viewSuccessMessage();
		}
		else {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The site cannot be deleted or deactivated because you are accessing the site.");
		}
	}

	macro deleteAllCP {
		Click(locator1 = "Checkbox#SELECT_ALL");

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro deleteCP {
		var key_siteName = "${siteName}";

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "Name");

		Click(locator1 = "Icon#BASIC_SEARCH");

		AssertTextEquals.assertPartialText(
			locator1 = "Sites#SITE_TABLE_NAME",
			value1 = "${siteName}");

		LexiconEntry.openEntryMenu(rowEntry = "${siteName}");

		MenuItem.clickNoError(menuItem = "Delete");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${siteName}");
	}

	macro disableRecycleBinCP {
		AssertClick(
			locator1 = "SitesEditSite#MENU_ADVANCED_TAB",
			value1 = "Advanced");

		Panel.expandPanel(panelHeading = "Recycle Bin");

		Uncheck.uncheckToggleSwitchNoErrorNoAssert(locator1 = "Checkbox#ENABLE_RECYCLE_BIN");

		Confirm(value1 = "Disabling the Recycle Bin prevents the restoring of content that has been moved to the Recycle Bin.");

		AssertNotChecked.assertNotCheckedNotVisible(locator1 = "Checkbox#ENABLE_RECYCLE_BIN");

		PortletEntry.save();
	}

	macro editCP {
		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${siteName}");

		PortletEntry.inputName(name = "${siteNameEdit}");

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${siteNameEdit}");
	}

	macro editFriendlyURLCP {
		Panel.expandPanel(panelHeading = "Site URL");

		AssertTextEquals(
			locator1 = "SiteSettingsSiteURL#SITE_URL_FRIENDLY_URL_LABEL",
			value1 = "Friendly URL");

		Pause(locator1 = "5000");

		Type(
			locator1 = "TextInput#FRIENDLY_URL",
			value1 = "${siteFriendlyURLEdit}");

		Button.clickSave();

		if ("${friendlyURLInvalid}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The friendly URL has too many slashes.");

			AssertTextEquals(
				locator1 = "TextInput#FRIENDLY_URL",
				value1 = "${siteFriendlyURLEdit}");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro editGoogleAnalyticsIDCP {
		AssertClick(
			locator1 = "SitesEditSite#MENU_ADVANCED_TAB",
			value1 = "Advanced");

		Panel.expandPanel(panelHeading = "Analytics");

		Type(
			locator1 = "TextInput#GOOGLE_ANALYTICS_ID",
			value1 = "${googleAnalyticsID}");

		PortletEntry.save();
	}

	macro gotoSiteViaMySitesPG {
		AssertClick(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_NAME_LINK",
			value1 = "${siteName}");
	}

	macro inactivateCP {
		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			locator1 = "SitesEditSite#ACTIVE_LABEL",
			value1 = "Active");

		Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ACTIVE");

		PortletEntry.save();
	}

	macro joinViaMySitesPG {
		var key_siteName = "${siteName}";

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No sites were found.");

		Navigator.gotoNavItem(navItem = "Available Sites");

		Pause(locator1 = "3000");

		if ("${membershipType}" == "Private") {
			AssertElementNotPresent(
				key_itemName = "${siteName}",
				locator1 = "ListGroupItem#ITEM_NAME");
		}
		else if ("${membershipType}" == "Restricted") {
			AssertTextEquals(
				key_itemName = "${siteName}",
				locator1 = "ListGroupItem#ITEM_NAME",
				value1 = "${siteName}");

			AssertTextEquals(
				key_itemName = "${siteName}",
				locator1 = "ListGroupItem#ITEM_SUB_INFO",
				value1 = "Members: 1");

			LexiconEntry.gotoEntryMenuItemNoError(
				menuItem = "Request Membership",
				rowEntry = "${siteName}");

			Type(
				locator1 = "TextArea#COMMENTS",
				value1 = "Please allow me to join the Restricted Site.");

			Button.clickSave();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#SUCCESS_1",
				value1 = "Your request was sent. You will receive a reply by email.");

			Alert.viewSuccessMessage();

			MySites.viewMembershipRequested(siteName = "${siteName}");
		}
		else {
			AssertTextEquals(
				key_itemName = "${siteName}",
				locator1 = "ListGroupItem#ITEM_NAME",
				value1 = "${siteName}");

			AssertTextEquals(
				key_itemName = "${siteName}",
				locator1 = "ListGroupItem#ITEM_SUB_INFO",
				value1 = "Members: 1");

			LexiconEntry.gotoEntryMenuItemNoError(
				menuItem = "Join",
				rowEntry = "${siteName}");

			Alert.viewSuccessMessage();

			AssertTextEquals(
				key_itemName = "${siteName}",
				locator1 = "ListGroupItem#ITEM_NAME",
				value1 = "${siteName}");

			AssertTextEquals(
				key_itemName = "${siteName}",
				locator1 = "ListGroupItem#ITEM_SUB_INFO",
				value1 = "Members: 2");

			Click.waitForMenuToggleJSClick(
				key_rowEntry = "${siteName}",
				locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");

			AssertVisible(
				key_menuItem = "Leave",
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Leave");
		}
	}

	macro leaveViaMySitesPG {
		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_SUB_INFO",
			value1 = "Members: 2");

		Navigator.gotoNavItem(navItem = "Available Sites");

		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_SUB_INFO",
			value1 = "Members: 2");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Leave",
			rowEntry = "${siteName}");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_SUB_INFO",
			value1 = "Members: 1");

		Click.waitForMenuToggleJSClick(
			key_rowEntry = "${siteName}",
			locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");

		AssertVisible(
			key_menuItem = "Join",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Join");
	}

	macro limitMembership {
		if ("${siteLimitMembership}" == "Yes") {
			Check.checkToggleSwitch(locator1 = "SitesEditSite#PARENT_SITE_LIMIT_MEMBERSHIP_CHECKBOX");
		}
		else if ("${siteLimitMembership}" == "No") {
			Uncheck.uncheckToggleSwitch(locator1 = "SitesEditSite#PARENT_SITE_LIMIT_MEMBERSHIP_CHECKBOX");
		}

		PortletEntry.save();
	}

	macro manageMembershipOptions {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Site Settings");

		if ("${selectMembershipType}" == "true") {
			AssertTextEquals(
				locator1 = "SitesEditSite#MEMBERSHIP_OPTIONS_MEMBERSHIP_TYPE_LABEL",
				value1 = "Membership Type");

			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_OPTIONS_MEMBERSHIP_TYPE",
				value1 = "${membershipType}");
		}

		if ("${allowManualMembershipManagement}" == "true") {
			Check.checkToggleSwitch(locator1 = "Checkbox#ALLOW_MANUAL_MEMBERSHIP_MANAGEMENT");
		}
		else if ("${allowManualMembershipManagement}" == "false") {
			Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ALLOW_MANUAL_MEMBERSHIP_MANAGEMENT");
		}

		PortletEntry.save();
	}

	macro removeMemberCP {
		var key_cardText = "${userScreenName}";

		Type(
			locator1 = "TextInput#SEARCH",
			value1 = "${userScreenName}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		if (isSet(userMiddleName)) {
			AssertTextEquals.assertPartialText(
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${userFirstName} ${userMiddleName} ${userLastName}");
		}
		else {
			AssertTextEquals.assertPartialText(
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${userFirstName} ${userLastName}");
		}

		AssertTextEquals(
			locator1 = "Card#SPECIFIC_TEXT",
			value1 = "${userScreenName}");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Remove Membership",
			rowEntry = "${userScreenName}");

		Confirm.waitForConfirmation(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro removeSiteRolesSiteOwnerUserGroupsCP {
		var key_userGroupName = "${userGroupName}";

		Navigator.gotoNavItem(navItem = "User Groups");

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${userGroupName}");

		Click(locator1 = "Button#SUBMIT");

		Click(locator1 = "Icon#DISPLAY_STYLE_LIST");

		AssertTextEquals(
			locator1 = "UserGroups#USER_GROUP_TABLE_NAME",
			value1 = "${userGroupName}");

		LexiconEntry.openEntryMenu(rowEntry = "${userGroupName}");

		MenuItem.click(menuItem = "Assign Site Roles");

		IFrame.selectDialogFrame();

		Uncheck(locator1 = "Checkbox#SITE_OWNER");

		SelectFrameTop();

		AssertClick(
			locator1 = "Button#DONE",
			value1 = "Done");

		Alert.viewSuccessMessage();
	}

	macro searchCP {
		Site.searchInputCP(siteName = "Site Name");

		Site.viewSearchResultsCP(siteName = "Site Name");
	}

	macro searchInputCP {
		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${siteName}");

		Click(locator1 = "Icon#BASIC_SEARCH");
	}

	macro searchMemberCP {
		if (isSet(userScreenNameSearchValue)) {
			Type(
				locator1 = "TextInput#SEARCH",
				value1 = "${userScreenNameSearchValue}");
		}
		else if (isSet(userFirstNameSearchValue)) {
			Type(
				locator1 = "TextInput#SEARCH",
				value1 = "${userFirstNameSearchValue}");
		}
		else if (isSet(userLastNameSearchValue)) {
			Type(
				locator1 = "TextInput#SEARCH",
				value1 = "${userLastNameSearchValue}");
		}
		else {
			Type(
				locator1 = "TextInput#SEARCH",
				value1 = "${userEmailAddressSearchValue}");
		}

		Click(locator1 = "Icon#BASIC_SEARCH");

		Site.viewMemberCP(
			userFirstName = "${userFirstName}",
			userLastName = "${userLastName}",
			userScreenName = "${userScreenName}");
	}

	macro searchMembershipCP {
		Type(
			locator1 = "TextInput#SEARCH",
			value1 = "${searchValue}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		if ("${emptyResults}" == "true") {
			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}
		else {
			AssertElementPresent(locator1 = "Card#GENERIC_LARGE_TEXT");

			var cardText = StringUtil.unquote("${searchValue}");

			AssertTextEquals(
				key_cardText = "${cardText}",
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${cardText}");
		}
	}

	macro selectPageType {
		Panel.expandPanel(panelHeading = "Pages");

		if ("${selectPublicPages}" == "true") {
			Select(
				locator1 = "SitesEditSite#PAGES_PUBLIC_PAGES_SITE_TEMPLATE_SELECT",
				value1 = "${siteTemplate}");
		}
		else if ("${selectPrivatePages}" == "true") {
			Select(
				locator1 = "SitesEditSite#PAGES_PRIVATE_PAGES_SITE_TEMPLATE_SELECT",
				value1 = "${siteTemplate}");

			if ("${checkPropagation}" == "true") {
				Panel.expandPanel(panelHeading = "Pages");

				Check.checkToggleSwitch(locator1 = "Checkbox#PRIVATE_PAGES_ENABLE_PROPAGATION");
			}
		}

		PortletEntry.save();
	}

	macro selectParentSite {
		AssertClick(
			locator1 = "SitesEditSite#PARENT_SITE_SELECT",
			value1 = "Select");

		var key_parentSiteName = "${parentSiteName}";

		SelectFrame(locator1 = "SitesSelectSite#SELECT_SITE_IFRAME");

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${parentSiteName}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		if ("${chooseButtonAvailable}" == "false") {
			AssertTextNotPresent(
				locator1 = "SitesSelectSite#SITES_TABLE_NAME",
				value1 = "${parentSiteName}");

			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No sites were found.");
		}

		Close(locator1 = "null");

		SelectFrame(value1 = "relative=top");
	}

	macro tearDownCP {
		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "Name");

		Click(locator1 = "Icon#BASIC_SEARCH");

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
	}

	macro viewCannotAdd {
		LexiconEntry.gotoAddMenuItem(menuItem = "Blank Site");

		PortletEntry.inputName(name = "${siteName}");

		Click(locator1 = "Button#SAVE");

		if ("${siteNameInvalid}" == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "Please enter a unique name.");
		}
		else if ("${siteNameInvalid}" == "Null") {
			Alert.viewErrorMessage(errorMessage = "Your request failed to complete.");
			Alert.viewSecondErrorMessage(errorMessage = "The Site Name cannot be blank, numeric or a reserved word such as null. The Site Name cannot contain the following invalid characters: *.");

			AssertTextEquals(
				locator1 = "Message#REQUIRED_INFO",
				value1 = "This field is required.");
		}
		else {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The Site Name cannot be blank, numeric or a reserved word such as null.");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The Site Name cannot contain the following invalid characters: *.");
		}
	}

	macro viewChildCP {
		Site.viewCP(siteName = "${siteName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "View Child Sites",
			rowEntry = "${siteName}");

		Breadcrumb.viewActiveEntry(breadcrumbName = "${siteName}");

		Site.viewCP(siteName = "${childSiteName}");
	}

	macro viewCP {
		var key_siteName = "${siteName}";

		AssertElementPresent(locator1 = "Sites#SITE_TABLE_CHECKBOX");

		AssertTextEquals.assertPartialText(
			locator1 = "Sites#SITE_TABLE_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			locator1 = "Sites#SITE_TABLE_TYPE",
			value1 = "${siteType}");

		if ("${siteMember}" == "User") {
			AssertTextEquals(
				locator1 = "Sites#SITE_TABLE_MEMBERS_USERS",
				value1 = "1 ${siteMember}");
		}

		AssertTextEquals(
			locator1 = "Sites#SITE_TABLE_ACTIVE",
			value1 = "Yes");

		AssertElementPresent(locator1 = "Sites#SITE_TABLE_ACTIONS");
	}

	macro viewCurrentLanguagesCP {
		var key_displaySettings = "${displaySettings}";

		if (IsNotVisible(locator1 = "SiteSettingsDisplaySettings#DEFINE_DEFAULT_AND_AVAILABLE_SITE_LANGUAGES_RADIO")) {
			Navigator.gotoNavTab(navTab = "Languages");
		}

		Check(
			locator1 = "SiteSettingsDisplaySettings#DEFINE_DEFAULT_AND_AVAILABLE_SITE_LANGUAGES_RADIO",
			value1 = "Display Settings");

		for (var currentSiteLanguage : list "${currentSiteLanguages}") {
			var currentSiteLanguage = "${currentSiteLanguage}";

			AssertTextEquals.assertPartialText(
				locator1 = "LanguageConfiguration#LANGUAGES_CURRENT",
				value1 = "${currentSiteLanguage}");
		}

		for (var availableSiteLanguage : list "${availableSiteLanguages}") {
			var availableSiteLanguage = "${availableSiteLanguage}";

			AssertTextEquals.assertPartialText(
				locator1 = "LanguageConfiguration#LANGUAGES_AVAILABLE",
				value1 = "${availableSiteLanguage}");
		}

		if (isSet(defaultSiteLanguage)) {
			var defaultSiteLanguage = "${defaultSiteLanguage}";

			Select(
				locator1 = "LanguageConfiguration#DEFAULT_LANGUAGE_SELECT",
				value1 = "${defaultSiteLanguage}");
		}
	}

	macro viewGlobalLandingDefault {
		var key_portletName = "${portletName}";
		var key_siteName = "${siteName}";

		AssertElementPresent(locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME");

		AssertTextNotPresent(value1 = "You do not have the roles required to access this portlet");

		AssertTextEquals(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_MENU_ITEM",
			value1 = "${portletName}");
	}

	macro viewLimitMembershipParentSiteCP {
		var key_userScreenName = "${userScreenName}";

		Navigator.gotoNavItem(navItem = "Users");

		LexiconEntry.gotoAdd();

		IFrame.selectDialogFrame();

		AssertTextNotPresent(
			locator1 = "SiteMembershipsAddMembers#USER_TABLE_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextNotPresent(
			locator1 = "SiteMembershipsAddMembers#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");
	}

	macro viewMemberCP {
		var key_cardText = "${userScreenName}";

		AssertTextEquals.assertPartialText(
			locator1 = "Card#SPECIFIC_LARGE_TEXT",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextEquals(
			locator1 = "Card#SPECIFIC_TEXT",
			value1 = "${userScreenName}");

		var key_rowEntry = "${key_cardText}";

		AssertVisible(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
	}

	macro viewMemberListCP {
		for (var userEmail : list "${usersEmailsList}") {
			var userName = StringUtil.replace("${userEmail}", "@liferay.com", "");

			Site.viewMemberCP(
				userFirstName = "${userName}",
				userLastName = "${userName}",
				userScreenName = "${userName}");
		}
	}

	macro viewMemberOrganizationCP {
		var key_cardText = "${orgName}";

		Navigator.gotoNavItem(navItem = "Organizations");

		AssertTextEquals.assertPartialText(
			locator1 = "Card#SPECIFIC_LARGE_TEXT",
			value1 = "${orgName}");

		if ("${toolTip}" == "true") {
			MouseOver(locator1 = "Icon#TOOLTIP");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#TOOLTIP",
				value1 = "Users are not allowed to request membership. Only site administrators can add new members.");
		}
	}

	macro viewMemberUserGroupCP {
		Navigator.gotoNavItem(navItem = "User Groups");

		AssertTextEquals(
			key_cardText = "${userGroupName}",
			locator1 = "Card#SPECIFIC_LARGE_TEXT",
			value1 = "${userGroupName}");
	}

	macro viewMySitesViaMySitesPG {
		AssertTextEquals(
			key_navItem = "My Sites",
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "My Sites");

		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_SUB_INFO",
			value1 = "Members: ${memberCount}");
	}

	macro viewNoMySitesViaMySitesPG {
		AssertElementNotPresent(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_NAME");

		AssertElementNotPresent(
			key_itemName = "${siteName}",
			locator1 = "ListGroupItem#ITEM_SUB_INFO");

		AssertElementNotPresent(
			key_rowEntry = "${siteName}",
			locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
	}

	macro viewNoSiteMembershipsRequest {
		Click(locator1 = "Button#INFO");

		AssertElementNotPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		AssertTextEquals.assertPartialText(
			locator1 = "SiteMemberships#MEMBERSHIP_TYPE",
			value1 = "Membership Type: Restricted");

		AssertElementNotPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "View Membership Requests");

		AssertElementNotPresent(locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME");

		AssertTextNotPresent(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_COMMENTS",
			value1 = "Please allow me to join the Restricted Site.");
	}

	macro viewPrivateVirtualHostUrlUserLoginRedirect {
		AssertTextEquals(
			locator1 = "TextInput#EMAIL_ADDRESS",
			value1 = "${userEmailAddress}");

		AssertElementPresent(locator1 = "TextInput#PASSWORD");
	}

	macro viewSearchResultsCP {
		var key_siteName = "${siteName}";

		AssertElementPresent(locator1 = "Sites#SITE_TABLE_CHECKBOX");

		AssertTextEquals.assertPartialText(
			locator1 = "Sites#SITE_TABLE_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			locator1 = "Sites#SITE_TABLE_TYPE",
			value1 = "${siteType}");

		AssertTextEquals(
			locator1 = "Sites#SITE_TABLE_ACTIVE",
			value1 = "Yes");

		AssertVisible(locator1 = "Sites#SITE_TABLE_ACTIONS");
	}

	macro viewSiteAdminPermissionsCP {
		var key_siteName = "${siteName}";

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${siteName}");

		Click(locator1 = "Button#SUBMIT");

		LexiconEntry.openEntryMenu(rowEntry = "${siteName}");

		AssertTextEquals(
			key_menuItem = "Add Child Site",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Add Child Site");

		AssertTextEquals(
			key_menuItem = "Leave",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Leave");

		AssertTextEquals(
			key_menuItem = "Deactivate",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Deactivate");

		AssertTextEquals(
			key_menuItem = "Delete",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete");
	}

	macro viewSiteClonedFromSiteTemplateCP {
		var key_siteTemplateName = "${siteTemplateName}";

		Panel.expandPanel(panelHeading = "Site Template");

		if ("${clonedFromTemplate}" == "true") {
			AssertTextEquals.assertPartialText(
				locator1 = "SiteSettingsSiteTemplate#SITE_CLONED_FROM_TEMPLATE_MESSAGE",
				value1 = "This site was cloned from site template ${siteTemplateName}");
		}
		else {
			AssertTextEquals.assertPartialText(
				locator1 = "SiteSettingsSiteTemplate#SITE_NOT_CLONED_FROM_TEMPLATE_MESSAGE",
				value1 = "This site is not related to a site template.");
		}
	}

	macro viewSiteFriendlyURLPG {
		AssertTextEquals(
			locator1 = "Home#SITE_NAME",
			value1 = "${siteName}");

		AssertLocation(value1 = "${sitePageURL}");
	}

	macro viewSiteMembershipsRequest {
		Click(locator1 = "Button#INFO");

		AssertTextEquals.assertPartialText(
			locator1 = "SiteMemberships#MEMBERSHIP_TYPE",
			value1 = "Membership Type: Restricted");

		AssertElementPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		Click(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		Portlet.changeDisplayStyle(displayStyle = "List");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_COMMENTS",
			value1 = "Please allow me to join the Restricted Site.");
	}

	macro viewSiteRoleViaSiteMembershipsCP {
		AssertTextEquals(
			key_cardText = "${resourceName}",
			locator1 = "Card#SPECIFIC_LARGE_TEXT",
			value1 = "${resourceName}");

		AssertTextEquals(
			key_cardText = "${resourceName}",
			locator1 = "Card#SPECIFIC_SMALL_TEXT",
			value1 = "${roleName}");
	}

	macro viewVirtualHostsURLPG {
		var key_pageName = "${pageName}";

		AssertTextEquals(
			locator1 = "Home#SITE_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			locator1 = "Home#PAGE",
			value1 = "${pageName}");

		AssertLocation(value1 = "${sitePageURL}");
	}

}