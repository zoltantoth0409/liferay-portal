definition {
	macro _openDescriptiveMenulistCP {
		if (isSet(webContentTitleEscapeCharacter)) {
			var key_webContentTitle = "${webContentTitleEscapeCharacter}";
		}
		else {
			var key_webContentTitle = "${webContentTitle}";
		}

		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE",
			value1 = "${webContentTitle}"
		);

		Click(locator1 = "WC#ENTRY_DESCRIPTIVE_MENULIST_ICON");
	}

	macro gotoAddCP {
		AssertVisible(locator1 = "Button#PLUS");

		Pause(locator1 = "3000");

		LexiconEntry.gotoAddMenuItem(menuItem = "Basic Web Content");

		AssertVisible(locator1 = "TextInput#TITLE");
	}

	macro gotoAddFeed {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Feeds");

		LexiconEntry.gotoAdd();
	}

	macro gotoAddPGViaWCD {
		Click.clickNoWaitForVisible(locator1 = "WCD#ADD_WEB_CONTENT");

		if (!(contains("${structureName}", "{structureName}"))) {
			var structureName = "${structureName}";
		}
		else {
			var structureName = "Basic Web Content";
		}

		var key_structureName = "${structureName}";

		AssertVisible(
			locator1 = "WCD#ADD_STRUCTURED_WEB_CONTENT",
			value1 = "${structureName}"
		);

		AssertClick(
			locator1 = "WCD#ADD_STRUCTURED_WEB_CONTENT",
			value1 = "${structureName}"
		);

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		AssertVisible(locator1 = "TextInput#TITLE");
	}

	macro gotoAddWithStructureCP {
		var key_structureName = "${structureName}";

		LexiconEntry.gotoAdd();

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "${structureName}",
			key_menuItem = "${structureName}"
		);
	}

	macro gotoEditCP {
		WebContentNavigator._openDescriptiveMenulistCP(
			webContentTitle = "${webContentTitle}",
			webContentTitleEscapeCharacter = "${webContentTitleEscapeCharacter}"
		);

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		AssertElementPresent(locator1 = "Icon#BACK");
	}

	macro gotoEditFeed {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Feeds");

		LexiconEntry.gotoEntryMenuItem(menuItem = "Edit", rowEntry = "${feedName}");

		AssertElementPresent(locator1 = "Icon#BACK");
	}

	macro gotoEditPGViaWCD {
		MouseOver(locator1 = "WC#BODY");

		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Edit Web Content"
		);

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		AssertVisible(locator1 = "TextInput#TITLE");
	}

	macro gotoEditStructure {
		var key_ddlDataDefinitionName = "${structureName}";

		AssertClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = "${structureName}"
		);

		SelectFrame(value1 = "relative=top");
	}

	macro gotoLocalizedEditPGViaWCD {
		MouseOver(locator1 = "WC#BODY");

		Portlet.gotoPortletOptions(portletOption = "${localizedPortletOption}");
	}

	macro gotoManageStructures {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Structures");

		SiteAdmin.viewPortletTitle(portletTitle = "Structures");
	}

	macro gotoManageTemplates {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Templates");

		SiteAdmin.viewPortletTitle(portletTitle = "Templates");
	}

	macro gotoManageTemplatesViaStructures {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Manage Templates",
			rowEntry = "${structureName}"
		);

		SiteAdmin.viewPortletTitle(
			portletTitle = "Templates for Structure: ${structureName}"
		);
	}

	macro gotoPreviewEntryCP {
		WebContentNavigator._openDescriptiveMenulistCP(
			webContentTitle = "${webContentTitle}",
			webContentTitleEscapeCharacter = "${webContentTitleEscapeCharacter}"
		);

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Preview",
			key_menuItem = "Preview"
		);
	}

	macro gotoPreviewViaHeaderCP {
		SiteAdmin.viewPortletTitle(portletTitle = "${webContentTitle}");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Preview");
	}

	macro gotoViewStatus {
		var key_webContentTitle = "${webContentTitle}";

		Portlet.changeDisplayStyle(displayStyle = "Descriptive");

		AssertElementPresent.pauseAssertVisible(
			locator1 = "WC#ENTRY_DESCRIPTIVE_THUMBNAIL"
		);

		AssertTextEquals(
			locator1 = "WC#ENTRY_DESCRIPTIVE_WORKFLOW_STATUS",
			value1 = "${webContentStatus}"
		);

		AssertClick(
			locator1 = "WC#ENTRY_DESCRIPTIVE_TITLE",
			value1 = "${webContentTitle}"
		);
	}

	macro gotoViewTemplate {
		var key_ddlTemplateName = "${templateName}";

		AssertClick(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME_LINK",
			value1 = "${templateName}"
		);
	}
}