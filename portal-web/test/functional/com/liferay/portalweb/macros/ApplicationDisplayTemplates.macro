definition {
	macro addCP {
		var key_adtType = "${adtType}";

		Portlet.viewEmptyMessage();

		LexiconEntry.gotoAdd();

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "${adtType} Template",
			key_menuItem = "${adtType} Template"
		);

		SiteAdmin.viewPortletTitle(portletTitle = "New ${adtType} Template");

		PortletEntry.inputName(name = "${adtType} Test ADT");

		UploadCommonFile(
			locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
			value1 = "${adtFile}"
		);

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "ApplicationDisplayTemplates#ADT_TABLE_NAME_1",
			value1 = "${adtType} Test ADT"
		);
	}

	macro addManualViaRSS {
		IFrame.selectDialogFrame();

		Pause(locator1 = "5000");

		if (IsElementPresent(locator1 = "Configuration#SETUP_DISPLAY_SETTINGS")) {
			AssertClick(
				locator1 = "Configuration#SETUP_DISPLAY_SETTINGS",
				value1 = "Display Settings"
			);
		}

		AssertClick(
			locator1 = "ApplicationDisplayTemplatesConfiguration#MANAGE_DISPLAY_TEMPLATES_LINK",
			value1 = "Manage Display Templates for ${siteName}"
		);

		var key_adtType = "RSS";

		SelectFrameTop(value1 = "relative=top");

		SelectFrame(locator1 = "ApplicationDisplayTemplates#ADT_IFRAME");

		LexiconEntry.gotoAdd();

		AssertVisible(locator1 = "ApplicationDisplayTemplates#RSS_FEEDS");

		AssertElementPresent(locator1 = "ApplicationDisplayTemplates#RSS_FEEDS");

		PortletEntry.inputName(name = "RSS Test ADT");

		Click(locator1 = "ApplicationDisplayTemplates#RSS_FEEDS");

		PortletEntry.save();
	}

	macro addPG {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration"
		);

		IFrame.selectConfigurationFrame();

		Pause(locator1 = "5000");

		if (IsElementPresent(locator1 = "Configuration#SETUP_DISPLAY_SETTINGS")) {
			AssertClick(
				locator1 = "Configuration#SETUP_DISPLAY_SETTINGS",
				value1 = "Display Settings"
			);
		}
		else if (IsElementPresent(locator1 = "Configuration#SETUP_DISPLAY_SETTINGS_PANEL")) {
			Panel.expandPanel(panelHeading = "Display Settings");
		}

		AssertClick(
			locator1 = "ApplicationDisplayTemplatesConfiguration#MANAGE_DISPLAY_TEMPLATES_LINK",
			value1 = "Manage Display Templates for ${siteName}"
		);

		var key_adtType = "${adtType}";

		SelectFrameTop();

		SelectFrame(locator1 = "ApplicationDisplayTemplates#ADT_IFRAME");

		LexiconEntry.gotoAdd();

		var key_adtType = "${adtType}";

		AssertVisible(locator1 = "ApplicationDisplayTemplates#PORTLET_PREFERENCES");

		AssertElementPresent(
			locator1 = "ApplicationDisplayTemplates#PORTLET_PREFERENCES"
		);

		PortletEntry.inputName(name = "${adtType} Test ADT");

		UploadCommonFile(
			locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
			value1 = "${adtFile}"
		);

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "ApplicationDisplayTemplates#ADT_TABLE_NAME_1",
			value1 = "${adtType} Test ADT"
		);

		SelectFrameTop(value1 = "relative=top");
	}

	macro addPortletADTWithFieldEntry {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration"
		);

		IFrame.selectConfigurationFrame();

		Pause(locator1 = "3000");

		AssertClick.assertPartialTextClickAt(
			locator1 = "ApplicationDisplayTemplatesConfiguration#MANAGE_DISPLAY_TEMPLATES_LINK",
			value1 = "Manage Display Templates"
		);

		SelectFrameTop();

		SelectFrame(locator1 = "ApplicationDisplayTemplates#ADT_IFRAME");

		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${portletName} Test ADT");

		Click(locator1 = "DDMEditTemplate#SCRIPT_FIELD", key_fieldName = "${fieldName}");

		PortletEntry.save();

		SelectFrameTop(value1 = "relative=top");
	}

	macro selectPortletADT {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration"
		);

		IFrame.selectConfigurationFrame();

		var key_tab = "Display Settings";

		if (IsElementPresent(locator1 = "NavTab#TAB_LINK")) {
			Navigator.gotoNavTab(navTab = "Display Settings");
		}

		Select(locator1 = "Select#DISPLAY_TEMPLATE", value1 = "${templateName}");

		PortletEntry.save();

		SelectFrameTop();
	}
}