definition {
	macro addIdentityProviderConnection {
		AssertClick(
			locator1 = "CPSAMLAdminIdentityProviderConnection#ADD_IDENTITY_PROVIDER_BUTTON",
			value1 = "Add Identity Provider"
		);

		Type(locator1 = "TextInput#SAML_NAME_FIELD", value1 = "${idpNameID}");

		Type(
			locator1 = "CPSAMLAdminIdentityProviderConnection#IDENTITY_PROVIDER_CONNECTION_ENTITY_ID_FIELD",
			value1 = "${idpEntityID}"
		);

		Check(
			locator1 = "CPSAMLAdminIdentityProviderConnection#IDENTITY_PROVIDER_CONNECTION_ENABLED_CHECKBOX"
		);

		Type(
			locator1 = "TextInput#SAML_METADATA_URL_FIELD",
			value1 = "${idpMetadataURL}"
		);

		if (isSet(nameIdentifierFormat)) {
			Select(
				locator1 = "Select#SAML_NAME_IDENTIFIER_FORMAT_SELECT",
				value1 = "${nameIdentifierFormat}"
			);
		}
		else {
			Select(
				locator1 = "Select#SAML_NAME_IDENTIFIER_FORMAT_SELECT",
				value1 = "Email Address"
			);
		}

		PortletEntry.save();
	}

	macro addServiceProviderConnection {
		AssertClick(
			locator1 = "CPSAMLAdminServiceProviderConnections#ADD_SERVICE_PROVIDER_BUTTON",
			value1 = "Add Service Provider"
		);

		Type(locator1 = "TextInput#SAML_NAME_FIELD", value1 = "${spNameID}");

		Type(
			locator1 = "CPSAMLAdminAddServiceProvider#SERVICE_PROVIDER_ENTITY_ID_FIELD",
			value1 = "${spEntityID}"
		);

		Check(
			locator1 = "CPSAMLAdminAddServiceProvider#SERVICE_PROVIDER_ENABLED_CHECKBOX"
		);

		Type(
			locator1 = "TextInput#SAML_METADATA_URL_FIELD",
			value1 = "${spMetadataURL}"
		);

		if (!(isSet(nameIdentifierAttributeName))) {
			var nameIdentifierAttributeName = "emailAddress";
		}

		Type(
			locator1 = "CPSAMLAdminAddServiceProvider#SERVICE_PROVIDER_NAME_IDENTIFIER_ATTRIBUTE_NAME_FIELD",
			value1 = "${nameIdentifierAttributeName}"
		);

		if (isSet(nameIdentifierFormat)) {
			Select(
				locator1 = "Select#SAML_NAME_IDENTIFIER_FORMAT_SELECT",
				value1 = "${nameIdentifierFormat}"
			);
		}

		if (isSet(attributes)) {
			Check(
				locator1 = "CPSAMLAdminIdentityProviderConnection#IDENTITY_PROVIDER_CONNECTION_ATTRIBUTE_ENABLE"
			);

			Type(
				locator1 = "CPSAMLAdminIdentityProviderConnection#IDENTITY_PROVIDER_CONNECTION_ATTRIBUTE_TEXTAREA",
				value1 = "${attributes}"
			);
		}

		PortletEntry.save();
	}

	macro configureIdentityProvider {
		Check(
			locator1 = "CPSAMLAdminIdentityProvider#IDENTITY_PROVIDER_SIGN_METADATA_CHECKBOX"
		);

		Check(
			locator1 = "CPSAMLAdminIdentityProvider#IDENTITY_PROVIDER_AUTH_REQUEST_SIGNATURE_REQUIRED_CHECKBOX"
		);

		PortletEntry.save();
	}
}