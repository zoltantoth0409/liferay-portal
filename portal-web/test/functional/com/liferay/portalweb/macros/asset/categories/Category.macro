definition {

	macro addCategoryLocalizationCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Category",
			rowEntry = "${vocabularyName}");

		PortletEntry.inputTitle(title = "${categoryName}");

		PortletEntry.changeLocale(
			fieldName = "Name",
			locale = "${locale}");

		PortletEntry.inputTitle(title = "${localizationName}");

		if (isSet(locale2)) {
			PortletEntry.changeLocale(
				fieldName = "Name",
				locale = "${locale2}");

			PortletEntry.inputTitle(title = "${localization2Name}");
		}

		PortletEntry.save();
	}

	macro addCategoryViewableByCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Category",
			rowEntry = "${vocabularyName}");

		PortletEntry.inputTitle(title = "${categoryName}");

		Panel.expandPanel(panel = "Permissions");

		Select(
			locator1 = "Select#PERMISSIONS",
			value1 = "${viewableBy}");

		PortletEntry.save();
	}

	@summary = "Add a category named '${categoryName}' to the vocabulary named '${vocabularyName}'"
	macro addCP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		if (IsElementPresent(locator1 = "Categories#VOCABULARY_HEADER")) {
			AssertClick(
				locator1 = "Categories#ADD_CATEGORY_BUTTON",
				value1 = "Add Category");
		}
		else {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Add Category",
				rowEntry = "${vocabularyName}");
		}

		PortletEntry.inputTitle(title = "${categoryName}");

		if (isSet(categoryDescription)) {
			PortletEntry.inputDescription(description = "${categoryDescription}");
		}

		PortletEntry.save();
	}

	macro addPropertyCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		var i = "0";
		var pathNumber = "0";

		if ("${propertyNumber}" == "0") {
			var key_pathNumber = "${pathNumber}";

			Type(
				locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD",
				value1 = "${propertiesKeyField}");

			Type(
				locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD",
				value1 = "${propertiesValueField}");

			Click(locator1 = "Button#ADD_ROW_LAST");
		}
		else {
			while ("${i}" != "${propertyNumber}") {
				var i = ${i} + 1;
				var key_pathNumber = "${pathNumber}";
				var pathNumber = ${pathNumber} + 1;

				Type(
					locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD",
					value1 = "${propertiesKeyField} ${i}");

				Type(
					locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD",
					value1 = "${propertiesValueField} ${i}");

				Click(locator1 = "Button#ADD_ROW_LAST");
			}
		}

		PortletEntry.save();
	}

	macro addWithNullTitleCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Category",
			rowEntry = "${vocabularyName}");

		PortletEntry.inputTitle(title = " ");

		Button.clickSave();

		Alert.viewRequiredField();
	}

	macro deleteByIconCP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_CATEGORY",
			value1 = "${categoryName}");

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		PortletEntry.viewEmptyMessage(entity = "Categories");

		AssertTextNotPresent(value1 = "${categoryName}");
	}

	macro deleteCategoriesCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		PortletEntry.viewEmptyMessage(entity = "Categories");
	}

	macro deleteCategoryByMenuCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${categoryName}");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		PortletEntry.viewEmptyMessage(entity = "Categories");

		AssertTextNotPresent(value1 = "${categoryName}");
	}

	macro deleteCategoryNoPermissionCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.selectEntry(key_rowEntry = "${rowEntry}");

		AssertElementPresent(locator1 = "Icon#DELETE_NOT_FOCUSABLE");
	}

	macro deleteCategoryProperty2CP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		Click(
			key_rowIndexNumber = "2",
			locator1 = "Button#DELETE_ROW_N");

		AssertElementPresent(locator1 = "CategoriesEditCategory#PROPERTIES_UNDO_MESSAGE");

		PortletEntry.save();
	}

	macro editCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		PortletEntry.inputTitle(title = "${categoryNameEdit}");

		PortletEntry.save();
	}

	macro editPropertyCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD_1",
			value1 = "${propertiesKeyFieldEdit}");

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD_1",
			value1 = "${propertiesValueFieldEdit}");

		PortletEntry.save();
	}

	macro getAssetId {
		var assetId = selenium.getAttribute("//table[contains(@class,'table')]//tr[contains(.,'${assetName}')]/td[1]//input@value");

		return "${assetId}";
	}

	macro moveViaEditCP {
		AssertClick(
			key_rowEntry = "${vocabularyName}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move",
			rowEntry = "${categoryName}");

		AssetCategorization.selectCategory(categoryName = "${vocabularyName2}");
	}

	macro openCategoriesAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/categories_admin/");
	}

	macro searchCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		Search.searchCP(searchTerm = "${categorySearchEntry}");
	}

	macro viewCategoryPropertyCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		var i = "0";
		var pathNumber = "0";

		if ("${propertyNumber}" == "0") {
			var key_pathNumber = "${pathNumber}";

			AssertTextEquals(
				locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD",
				value1 = "${propertiesKeyField}");

			AssertTextEquals(
				locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD",
				value1 = "${propertiesValueField}");
		}
		else {
			while ("${i}" != "${propertyNumber}") {
				var key_pathNumber = "${i}";
				var i = ${i} + 1;

				AssertTextEquals(
					locator1 = "Categories#CATEGORY_DETAILS_PROPERTY_KEY_2",
					value1 = "${propertiesKeyField} ${i}");

				AssertTextEquals(
					locator1 = "Categories#CATEGORY_DETAILS_PROPERTY_VALUE_2",
					value1 = "${propertiesValueField} ${i}");
			}
		}
	}

	macro viewCategoryToVocabularyCP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		AssertClick(
			locator1 = "Categories#VOCABULARY_CATEGORY",
			value1 = "${categoryName}");
	}

	macro viewCategoryViewableByCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${categoryName}");

		SelectFrame(locator1 = "IFrame#DIALOG");

		if ("${viewableBy}" == "Anyone (Guest Role)") {
			AssertChecked(locator1 = "AssetPermissions#GUEST_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Owner") {
			AssertChecked(locator1 = "AssetPermissions#OWNER_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Site Members") {
			AssertChecked(locator1 = "AssetPermissions#SITE_MEMBER_VIEW_CHECKBOX");
		}

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Assert that the category named '${categoryName}' has been added to the vocabulary named '${vocabularyName}'"
	macro viewCP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_CATEGORY",
			value1 = "${categoryName}");
	}

	macro viewFilterViaCategoriesNavigationPG {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "CategoriesNavigation#VOCABULARY_NAME",
			value1 = "${vocabularyName}");

		AssertClick(
			locator1 = "CategoriesNavigation#CATEGORY_NAME",
			value1 = "${categoryName}");

		AssertTextEquals.assertPartialText(
			locator1 = "AP#CATEGORIES_FILTER_CATEGORY",
			value1 = "${categoryName}");
	}

	macro viewNavigationCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		var key_categoryName = "${categoryName}";

		Click(locator1 = "Categories#CATEGORY_ENTRY_LINK");

		SiteAdmin.viewPortletTitle(portletTitle = "Categories");

		AssertClick(
			key_breadcrumbName = "${vocabularyName}",
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY_LINK",
			value1 = "${vocabularyName}");

		SiteAdmin.viewPortletTitle(portletTitle = "Categories");
	}

	macro viewPath {
		AssertTextEquals(
			key_categoryName = "${categoryName}",
			locator1 = "Categories#CATEGORY_ENTRY_PATH",
			value1 = "${path}");
	}

	macro viewRemoveFilterViaAP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "CategoriesNavigation#VOCABULARY_NAME",
			value1 = "${vocabularyName}");

		Click(locator1 = "AP#CATEGORIES_FILTER_CATEGORY_REMOVE");

		AssertElementNotPresent(locator1 = "AP#CATEGORIES_FILTER_CATEGORY");
	}

	macro viewSearchResultsCP {
		if (isSet(categoryNameList)) {
			for (var categoryName : list "${categoryNameList}") {
				var key_categoryName = "${categoryName}";

				AssertTextEquals(
					locator1 = "Categories#CATEGORY_ENTRY",
					value1 = "${categoryName}");
			}
		}

		if (isSet(categoryDescriptionList)) {
			for (var categoryDescription : list "${categoryDescriptionList}") {
				var key_categoryDescription = "${categoryDescription}";

				AssertTextEquals(
					locator1 = "Categories#CATEGORY_ENTRY_DESCRIPTION",
					value1 = "${categoryDescription}");
			}
		}

		if ("${nullResult}" == "true") {
			PortletEntry.viewEmptyMessage(entity = "Categories");
		}
	}

}