definition {

	macro addAssociatedAssetType {
		Panel.expandPanel(panel = "Associated Asset Types");

		if (contains("${rowNumber}", "{rowNumber}")) {
			var rowNumber = "1";
		}

		Select(
			key_rowNumber = "${rowNumber}",
			locator1 = "CategoriesEditVocabulary#ASSOCIATED_ASSET_TYPES_SELECT_N",
			value1 = "${assetType}");

		if ("${required}" == "true") {
			var key_rowNumber = "${rowNumber}";

			Check.checkToggleSwitch(locator1 = "ToggleSwitch#REQUIRED");
		}
	}

	@summary = "Add a vocabulary named '${vocabularyName}'"
	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${vocabularyName}");

		if (isSet(vocabularyDescription)) {
			PortletEntry.inputDescription(description = "${vocabularyDescription}");
		}

		if ("${allowMultipleCategories}" == "false") {
			Uncheck.uncheckToggleSwitch(locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH");
		}

		if (isSet(locale)) {
			PortletEntry.changeLocale(
				fieldName = "Name",
				locale = "${locale}");

			PortletEntry.inputName(name = "${localizationName}");
		}

		if (isSet(locale2)) {
			PortletEntry.changeLocale(
				fieldName = "Name",
				locale = "${locale2}");

			PortletEntry.inputName(name = "${localization2Name}");
		}

		PortletEntry.save();
	}

	macro addViewableByCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${vocabularyName}");

		Panel.expandPanel(panel = "Permissions");

		Select(
			locator1 = "Select#PERMISSIONS",
			value1 = "${viewableBy}");

		PortletEntry.save();

		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");
	}

	macro addVocabularyWithNullTitle {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = " ");

		Button.clickSave();

		Alert.viewRequiredField();
	}

	macro addWithAssociatedAssetTypeCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${vocabularyName}");

		Vocabulary.addAssociatedAssetType(
			assetType = "${assetType}",
			required = "${required}",
			rowNumber = "${rowNumber}");

		if (isSet(assetSubtype)) {
			Select(
				locator1 = "Select#ASSET_SUBTYPE",
				value1 = "${assetSubtype}");
		}

		PortletEntry.save();

		AssertTextEquals(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");
	}

	macro deleteAssociatedAssetTypeViaVocabularyCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${vocabularyName}");

		Panel.expandPanel(panel = "Associated Asset Types");

		Click(locator1 = "CategoriesEditVocabulary#ASSOCIATED_ASSET_TYPES_DELETE_BUTTON_2");

		PortletEntry.save();
	}

	macro deleteByIconCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${vocabularyName}");
	}

	macro deleteByMenuCP {
		AssertTextEquals(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${vocabularyName}");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");
	}

	macro editCP {
		AssertTextEquals(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${vocabularyName}");

		if (isSet(vocabularyNameEdit)) {
			PortletEntry.inputTitle(title = "${vocabularyNameEdit}");
		}

		if ("${allowMultipleCategories}" == "false") {
			Uncheck.uncheckToggleSwitch(locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH");
		}
		else if ("${allowMultipleCategories}" == "true") {
			Check.checkToggleSwitch(locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH");
		}

		PortletEntry.save();

		if (isSet(vocabularyNameEdit)) {
			var key_vocabularyName = "${vocabularyNameEdit}";

			AssertTextEquals(
				locator1 = "Categories#VOCABULARY_ENTRY",
				value1 = "${vocabularyNameEdit}");
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Categorization",
			panel = "Site Administration",
			portlet = "Categories");

		if (IsElementPresent(locator1 = "Checkbox#SELECT_ALL")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");
		}
	}

	macro viewCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");

		if ("${allowMultipleCategories}" == "false") {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "${vocabularyName}");

			AssertNotChecked.assertNotCheckedNotVisible(locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH");
		}
	}

	macro viewSearchResultsCP {
		if (isSet(vocabularyNameList)) {
			for (var vocabularyName : list "${vocabularyNameList}") {
				var key_vocabularyName = "${vocabularyName}";

				AssertTextEquals(
					locator1 = "Categories#VOCABULARY_ENTRY",
					value1 = "${vocabularyName}");
			}
		}

		if (isSet(vocabularyDescriptionList)) {
			for (var vocabularyDescription : list "${vocabularyDescriptionList}") {
				var key_vocabularyDescription = "${vocabularyDescription}";

				AssertTextEquals(
					locator1 = "Categories#VOCABULARY_ENTRY_DESCRIPTION",
					value1 = "${vocabularyDescription}");
			}
		}

		if ("${nullResult}" == "true") {
			PortletEntry.viewEmptyMessage(entity = "Vocabularies");
		}
	}

	macro viewVocabularyPermissionsCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${vocabularyName}");

		SelectFrame(locator1 = "IFrame#DIALOG");

		if ("${viewableBy}" == "Anyone (Guest Role)") {
			AssertChecked(locator1 = "AssetPermissions#GUEST_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Owner") {
			AssertChecked(locator1 = "AssetPermissions#OWNER_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Site Members") {
			AssertChecked(locator1 = "AssetPermissions#SITE_MEMBER_VIEW_CHECKBOX");
		}

		SelectFrameTop(value1 = "relative=top");
	}

	macro viewWithAssociatedAssetTypeCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${vocabularyName}");

		Panel.expandPanel(panel = "Associated Asset Types");

		AssertSelectedLabel(
			locator1 = "CategoriesEditVocabulary#ASSOCIATED_ASSET_TYPES_SELECT",
			value1 = "${vocabularyAssociatedAssetType}");
	}

}