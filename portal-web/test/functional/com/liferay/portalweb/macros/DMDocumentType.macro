definition {
	macro add {
		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Document Types");

		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${dmDocumentTypeName}");

		PortletEntry.inputDescription(description = "${dmDocumentTypeDescription}");

		Panel.expandPanel(panelHeading = "Main Metadata Fields");

		for (var dmDocumentTypeFieldName : list "${dmDocumentTypeFieldNames}") {
			DynamicDataMapping.addField(
				field = "${dmDocumentTypeFieldName}",
				fieldFieldLabel = "${dmDocumentTypeFieldName}",
				fieldName = StringUtil.replace("${dmDocumentTypeFieldName}", " ", "")
			);
		}

		if (isSet(metadataSetName)) {
			Panel.expandPanel(panelHeading = "Additional Metadata Fields");

			DMDocumentType.selectPGMetadataSet(key_metadataSetName = "${metadataSetName}");
		}

		DDLDataDefinition.save();
	}

	macro deletePG {
		DMDocumentType.selectDocumentTypeFrame();

		var key_dmDocumentTypeName = "${dmDocumentTypeName}";

		Click(locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_ACTIONS");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		AssertTextNotPresent(
			locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_NAME",
			value1 = "${dmDocumentTypeName}"
		);
	}

	macro edit {
		DMDocumentType.selectDocumentTypeFrame();

		var key_dmDocumentTypeName = "${dmDocumentTypeName}";

		Click(locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_ACTIONS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		if (isSet(dmDocumentTypeNameEdit)) {
			Type.clickAtType(
				locator1 = "TextInput#NAME",
				value1 = "${dmDocumentTypeNameEdit}"
			);
		}

		if (isSet(dmDocumentTypeDescriptionEdit)) {
			Type(
				locator1 = "DocumentsAndMediaEditDocumentType#DETAILS_DESCRIPTION_FIELD",
				value1 = "${dmDocumentTypeDescriptionEdit}"
			);
		}

		if (isSet(dmDocumentTypeFieldRequiredEdit)) {
			Panel.expandPanel(panelHeading = "Main Metadata Fields");

			DynamicDataMapping.editFieldSettings(
				fieldFieldLabel = "${dmDocumentTypeFieldRequiredEdit}"
			);

			DynamicDataMapping.makeFieldRequired();
		}

		if (isSet(dmDocumentTypeFieldNames)) {
			Panel.expandPanel(panelHeading = "Main Metadata Fields");

			for (var dmDocumentTypeFieldName : list "${dmDocumentTypeFieldNames}") {
				if (!(isSet(dmDocumentTypeFieldLabel))) {
					var dmDocumentTypeFieldLabel = "${dmDocumentTypeFieldName}";
				}

				DynamicDataMapping.addField(
					field = "${dmDocumentTypeFieldName}",
					fieldFieldLabel = "${dmDocumentTypeFieldLabel}",
					fieldName = StringUtil.replace("${dmDocumentTypeFieldLabel}", " ", "")
				);

				if (isSet(fieldPredefinedValue)) {
					DynamicDataMapping.editFieldPredefinedValue(
						field = "${dmDocumentTypeFieldName}",
						fieldFieldLabel = "${dmDocumentTypeFieldLabel}",
						fieldName = "${dmDocumentTypeFieldLabel}",
						fieldPredefinedValue = "${fieldPredefinedValue}"
					);
				}
			}
		}

		DMDocumentType.selectDocumentTypeFrame();

		if ("${removeMetadataSet}" == "true") {
			Panel.expandPanel(panelHeading = "Additional Metadata Fields");

			AssertClick(
				locator1 = "DocumentsAndMediaEditDocumentType#REMOVE_METADATA_SET_BUTTON",
				value1 = "Remove",
				key_metadataSetName = "${metadataSetName}"
			);
		}

		DDLDataDefinition.save();

		if (isSet(dmDocumentTypeNameEdit)) {
			AssertTextEquals(
				locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_NAME",
				value1 = "${dmDocumentTypeNameEdit}"
			);
		}
		else {
			AssertTextEquals(
				locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_NAME",
				value1 = "${dmDocumentTypeName}",
				key_dmDocumentTypeName = "${dmDocumentTypeName}"
			);
		}
	}

	macro selectDocumentTypeFrame {
		if (IsElementPresent(locator1 = "IFrame#DOCUMENT_TYPES")) {
			SelectFrame(locator1 = "IFrame#DOCUMENT_TYPES");
		}
	}

	macro selectPGMetadataSet {
		AssertClick(
			locator1 = "DocumentsAndMediaEditDocumentType#SELECT_METADATA_SET_BUTTON",
			value1 = "Select Metadata Set"
		);

		SelectFrameTop(value1 = "relative=top");

		DMMetadataSet.selectMetadataSetsFrame();

		Click(
			locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_NAME",
			key_metadataSetName = "${metadataSetName}"
		);

		SelectFrameTop(value1 = "relative=top");
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Documents and Media"
		);

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Document Types");

		DMDocumentType.selectDocumentTypeFrame();

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "DM Document Type");

		AssertClick(locator1 = "NavBar#BASIC_SEARCH_BUTTON", value1 = "Search");

		while (IsElementPresent(locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_ACTIONS_1")) {
			Click(
				locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_ACTIONS_1"
			);

			AssertClickNoError(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Delete",
				key_menuItem = "Delete"
			);

			Confirm.waitForConfirmation(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		var key_dmDocumentTypeName = "${dmDocumentTypeName}";

		AssertTextEquals(
			locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_NAME",
			value1 = "${dmDocumentTypeName}"
		);
	}

	macro viewMenulistOption {
		LexiconEntry.gotoAdd();

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "${dmDocumentTypeName}",
			key_menuItem = "${dmDocumentTypeName}"
		);
	}

	macro viewNoAvailableActionsViaDocumentTypesPG {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Document Types");

		AssertElementNotPresent(
			locator1 = "DocumentsAndMediaDocumentTypes#DOCUMENT_TYPES_TABLE_ACTIONS_1"
		);
	}
}