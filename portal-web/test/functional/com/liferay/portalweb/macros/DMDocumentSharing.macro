definition {
	macro _manageCollaborators {
		Click(locator1 = "Button#INFO");

		var key_text = "Manage Collaborators";

		AssertClick(locator1 = "Button#ANY", value1 = "Manage Collaborators");
	}

	macro changePermission {
		DMDocument.viewPG(
			dmDocumentDescription = "${dmDocumentDescription}",
			dmDocumentTitle = "${dmDocumentTitle}"
		);

		DMDocumentSharing._manageCollaborators();

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		var key_userName = "${userName}";

		AssertTextEquals(
			locator1 = "DocumentsAndMediaShare#MANAGE_COLLABORATORS_NAME",
			value1 = "${userName}"
		);

		Select(
			locator1 = "DocumentsAndMediaShare#MANAGE_COLLABORATORS_ACCESS_DROPDOWN",
			value1 = "${selectPermission}"
		);

		AssertSelectedLabel(
			locator1 = "DocumentsAndMediaShare#MANAGE_COLLABORATORS_ACCESS_DROPDOWN",
			value1 = "${selectPermission}"
		);

		Button.clickSave();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "Permissions Changed");
	}

	macro removeCollaborator {
		DMDocumentSharing._manageCollaborators();

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		for (var collaborator : list "${collaboratorList}") {
			var key_userName = "${collaborator}";

			Click(locator1 = "DocumentsAndMediaShare#MANAGE_COLLABORATORS_REMOVE_BUTTON");
		}

		if (isSet(emptyStateMessage)) {
			AssertElementPresent(
				locator1 = "DocumentsAndMediaShare#MANAGE_COLLABORATORS_EMPTY_STATE_MESSAGE"
			);
		}

		Button.clickSave();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "Permissions Changed");
	}

	macro viewCollaborators {
		Click(locator1 = "Button#INFO");

		for (var collaborator : list "${collaboratorList}") {
			var key_collaborator = "${collaborator}";

			AssertElementPresent(
				locator1 = "DocumentsAndMediaShare#COLLABORATOR_TOOLTIP",
				value1 = "${collaborator}"
			);
		}
	}

	macro viewDocumentViaSharedWithMe {
		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Shared With Me"
		);

		if ("${assetSharingPermission}" == "View") {
			AssertElementNotPresent(locator1 = "Comments#COMMENT_FIELD");
		}

		else {
			AssertElementPresent(locator1 = "Comments#COMMENT_FIELD");
		}

		DMDocument.viewCP(dmDocumentTitle = "${dmDocumentTitle}");
	}
}