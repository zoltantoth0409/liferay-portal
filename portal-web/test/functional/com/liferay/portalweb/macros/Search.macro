definition {
	macro addTagFacet {
		AssertClick.assertPartialTextClickAt(
			locator1 = "SearchResults#USER_FACET_TAGS",
			value1 = "${tagName}",
			key_tagName = "${tagName}"
		);
	}

	macro configureSearchScopePG {
		var key_searchScope = "${searchScope}";

		if (("${searchScope}" == "Everything") || ("${searchScope}" == "Let the User Choose") || ("${searchScope}" == "This Site")) {
			IFrame.selectConfigurationFrame();

			Select(locator1 = "Select#SCOPE", value1 = "${searchScope}");
		}
		else {
			fail("Invalid search scope. Must be (Everything|Let the User Choose|This Site).");
		}

		if (isSet(uncheckDisplayFacet)) {
			var key_facetName = "${uncheckDisplayFacet}";

			AssertClick(locator1 = "Search#FACET_CONFIGURE", value1 = "Configure");

			Uncheck(locator1 = "Checkbox#DISPLAY_FACET");
		}

		PortletEntry.save();
	}

	macro gotoModifiedRangeFacetPG {
		var key_searchModifiedRange = "${searchModifiedRange}";

		AssertClick.assertPartialTextClickAt(
			locator1 = "SearchResults#MODIFIED_RANGE_FACET_TIME_RANGE",
			value1 = "${searchModifiedRange}"
		);
	}

	macro gotoUserSearchResultPG {
		var key_searchAssetTitle = "${searchAssetTitle}";

		AssertClick(
			locator1 = "SearchResults#ASSET_ENTRY_TABLE_TITLE",
			value1 = "${searchAssetTitle}"
		);
	}

	macro removeUserFacetDisplayPG {
		ProductMenuHelper.closeProductMenu();

		Click(locator1 = "Search#SEARCH_FIELD");

		Click.makeVisibleClickAt(locator1 = "Search#SEARCH_PORTLET_OPTIONS_WRENCH");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Configuration",
			key_menuItem = "Configuration"
		);

		IFrame.selectConfigurationFrame();

		Uncheck(locator1 = "SearchConfiguration#BASIC_DISPLAY_USER_FACET_CHECKBOX");

		Button.clickSave();

		SelectFrame(value1 = "relative=top");
	}

	macro searchCP {
		AssertElementPresent(locator1 = "NavBar#BASIC_SEARCH_FIELD");

		AssertElementPresent(locator1 = "Button#SEARCH");

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "${searchTerm}");

		KeyPress(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "\RETURN");
	}

	macro searchPG {
		AssertElementPresent(locator1 = "Search#SEARCH_FIELD");

		AssertElementPresent(locator1 = "Search#SEARCH_PORTLET_SEARCH_BUTTON");

		Type(locator1 = "Search#SEARCH_FIELD", value1 = "${searchTerm}");

		if (isSet(localizationIDName)) {
			var key_searchIDName = "${localizationIDName}";
		}
		else {
			var key_searchIDName = "Search";
		}

		var key_searchScope = "${searchScope}";

		if ("${searchScope}" == "Everything") {
			Select(locator1 = "Select#SCOPE", value1 = "${searchScope}");
		}
		else if ("${searchScope}" == "This Site") {
			Select(locator1 = "Select#SCOPE", value1 = "${searchScope}");
		}

		KeyPress(locator1 = "Search#SEARCH_FIELD", value1 = "\RETURN");
	}

	macro unscopeAssetFacetPG {
		Click(locator1 = "SearchResults#ASSET_TYPE_FACET_ANY_ASSET");
	}

	macro viewAssetTypeFacetPG {
		var key_searchAssetType = "${searchAssetType}";

		AssertTextEquals(
			locator1 = "SearchResults#ASSET_TYPE_FACET_ANY_ASSET",
			value1 = "Any Asset"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "SearchResults#ASSET_TYPE_FACET_FREQUENCY",
			value1 = "${facetFrequency}"
		);

		AssertClick.assertPartialTextClickAt(
			locator1 = "SearchResults#ASSET_TYPE_FACET_ASSET",
			value1 = "${searchAssetType}"
		);

		Search.viewSearchResultPG(
			searchAssetSummary = "${searchAssetSummary}",
			searchAssetTitle = "${searchAssetTitle}",
			searchAssetType = "${searchAssetType}"
		);
	}

	macro viewSearchAssetTitle {
		var key_searchAssetTitle = "${searchAssetTitle}";

		AssertTextEquals(
			locator1 = "SearchResults#ASSET_ENTRY_TABLE_TITLE",
			value1 = "${searchAssetTitle}"
		);
	}

	macro viewSearchNoPG {
		var key_searchAssetTitle = "${searchAssetTitle}";

		AssertElementNotPresent(locator1 = "SearchResults#ASSET_ENTRY_TABLE_TITLE");

		AssertElementNotPresent(locator1 = "SearchResults#ASSET_ENTRY_TABLE_TYPE");

		if (isSet(searchAssetSummary)) {
			AssertElementNotPresent(locator1 = "SearchResults#ASSET_ENTRY_TABLE_SUMMARY");
		}

		if ("${searchNoResults}" == "true") {
			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No results were found that matched the keywords: ${searchTerm}."
			);
		}
	}

	macro viewSearchResultPG {
		var key_searchAssetTitle = "${searchAssetTitle}";

		AssertTextEquals.assertPartialText(
			locator1 = "SearchResults#ASSET_ENTRY_TABLE_TITLE",
			value1 = "${searchAssetTitle}"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "SearchResults#ASSET_ENTRY_TABLE_TYPE",
			value1 = "${searchAssetType}"
		);

		if (isSet(searchAssetSummary)) {
			AssertTextEquals.assertPartialText(
				locator1 = "SearchResults#ASSET_ENTRY_TABLE_SUMMARY",
				value1 = "${searchAssetSummary}"
			);
		}
	}

	macro viewSearchResultsPG {
		var key_searchAssetTitle = "${searchAssetTitle}";
		var key_searchAssetType = "${searchAssetType}";

		AssertTextEquals.assertPartialText(
			locator1 = "SearchResults#ASSET_ENTRY_TABLE_TITLE_SPECIFIC",
			value1 = "${searchAssetTitle}"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "SearchResults#ASSET_ENTRY_TABLE_TYPE_SPECIFIC",
			value1 = "${searchAssetType}"
		);

		if (isSet(searchAssetSummary)) {
			AssertTextEquals.assertPartialText(
				locator1 = "SearchResults#ASSET_ENTRY_TABLE_SUMMARY_SPECIFIC",
				value1 = "${searchAssetSummary}"
			);
		}
	}

	macro viewSearchResultWithNoPermissions {
		AssertTextEquals(
			locator1 = "Message#INFO_NO_RESULTS_FOUND",
			value1 = "No results were found that matched the keywords: ${searchTerm}."
		);
	}

	macro viewUserFacetPG {
		var key_facetFrequency = "${facetFrequency}";

		AssertTextEquals(
			locator1 = "SearchResults#USER_FACET_ANY_USER",
			value1 = "Any User"
		);

		var userLoginFullName = StringUtil.lowerCase("${userLoginFullName}");
		var key_userLoginFullName = "${userLoginFullName}";

		AssertTextEquals.assertPartialText(
			locator1 = "SearchResults#USER_FACET_FREQUENCY",
			value1 = "${facetFrequency}"
		);

		AssertClick.assertPartialTextClickAt(
			locator1 = "SearchResults#USER_FACET_USER_NAME",
			value1 = "${userLoginFullName}"
		);

		Search.viewSearchResultPG(
			searchAssetSummary = "${searchAssetSummary}",
			searchAssetTitle = "${searchAssetTitle}",
			searchAssetType = "${searchAssetType}"
		);
	}

	macro viewUserSearchResultPG {
		AssertTextEquals(locator1 = "Home#USER_NAME_HEADER", value1 = "${userName}");

		AssertTextEquals(locator1 = "Home#PAGE_1", value1 = "${pageName}");
	}
}