definition {
	macro _editCP {
		PortletEntry.inputName(name = "${teamName}");

		PortletEntry.inputDescription(description = "${teamDescription}");
	}

	macro _searchCP {
		Type(locator1 = "TextInput#SEARCH", value1 = "${searchTerm}");

		KeyPress(locator1 = "TextInput#SEARCH", value1 = "\RETURN");
	}

	macro addCP {
		LexiconEntry.gotoAdd();

		Team._editCP(teamDescription = "${teamDescription}", teamName = "${teamName}");

		PortletEntry.save();
	}

	macro assignMembersCP {
		var key_teamName = "${teamName}";
		var key_userScreenName = "${userScreenName}";

		Team._searchCP(searchTerm = "${teamName}");

		Portlet.changeDisplayStyle(displayStyle = "List");

		AssertTextEquals(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		AssertClick(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		Navigator.gotoNavItem(navItem = "Users");

		LexiconEntry.gotoAdd();

		IFrame.selectDialogFrame();

		Team._searchCP(searchTerm = "${userScreenName}");

		if ("${userNameIsNotFound}" == "true") {
			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No users were found."
			);
		}
		else {
			Portlet.changeDisplayStyle(displayStyle = "Icon");

			var key_cardText = "${userScreenName}";

			AssertTextEquals(locator1 = "Card#SPECIFIC_TEXT", value1 = "${userScreenName}");

			Check(locator1 = "Card#SPECIFIC_CHECKBOX");

			Pause(locator1 = "5000");

			SelectFrameTop();

			AssertClick(locator1 = "Button#ADD", value1 = "Add");

			Alert.viewSuccessMessage();

			AssertElementPresent(locator1 = "Card#SPECIFIC_LARGE_TEXT");
		}

		SelectFrameTop();
	}

	macro assignUserGroups {
		var key_teamName = "${teamName}";

		Team._searchCP(searchTerm = "${teamName}");

		Portlet.changeDisplayStyle(displayStyle = "List");

		AssertTextEquals(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		AssertClick(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		Navigator.gotoNavItem(navItem = "User Groups");

		LexiconEntry.gotoAdd();

		IFrame.selectDialogFrame();

		var key_cardText = "${userGroupName}";

		Team._searchCP(searchTerm = "${userGroupName}");

		if ("${userGroupsNameIsNotAvailable}" == "true") {
			AssertTextNotPresent(
				locator1 = "SiteTeamsAssignMembers#USER_GROUPS_TABLE_NAME",
				value1 = "${userGroupName}"
			);
		}
		else {
			Portlet.changeDisplayStyle(displayStyle = "Icon");

			AssertTextEquals(
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				value1 = "${userGroupName}"
			);

			Check(locator1 = "Card#SPECIFIC_CHECKBOX");

			SelectFrameTop();

			AssertClick(locator1 = "Button#ADD", value1 = "Add");

			Alert.viewSuccessMessage();
		}

		SelectFrameTop();
	}

	macro definePermissionPG {
		Check(
			locator1 = "Checkbox#TEAM_PERMISSIONS",
			key_permissionDefinitionKey = "${permissionDefinitionKey}",
			key_teamName = "${teamName}"
		);
	}

	macro deleteCP {
		var key_teamName = "${teamName}";

		Portlet.changeDisplayStyle(displayStyle = "List");

		AssertTextEquals(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		Click(locator1 = "SiteTeams#TEAM_TABLE_ACTIONS");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();
	}

	macro editCP {
		Portlet.changeDisplayStyle(displayStyle = "List");

		LexiconEntry.gotoEntryMenuItem(menuItem = "Edit", rowEntry = "${teamName}");

		Team._editCP(
			teamDescription = "${teamDescriptionEdit}",
			teamName = "${teamNameEdit}"
		);

		PortletEntry.save();
	}

	macro removeMemberCP {
		LexiconEntry.gotoEntry(rowEntry = "${teamName}");

		Navigator.gotoNavItem(navItem = "${membershipType}s");

		if ("${membershipType}" == "User") {
			Check(
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX",
				key_rowEntry = "${userFirstName} ${userLastName}"
			);
		}
		else if ("${membershipType}" == "User Group") {
			Check(
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX",
				key_rowEntry = "${userGroupName}"
			);
		}

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();
	}

	macro removePermissionPG {
		Uncheck(
			locator1 = "//tr[contains(.,'${teamName}')]/td/input[contains(@id,'${permissionDefinitionKey}')]"
		);

		PortletEntry.save();

		AssertNotChecked(
			locator1 = "//tr[contains(.,'${teamName}')]/td/input[contains(@id,'${permissionDefinitionKey}')]"
		);
	}

	macro searchCP {
		Navigator.gotoNavItem(navItem = "${membershipType}s");

		Team._searchCP(searchTerm = "${searchTerm}");

		if ("${isEmpty}" == "true") {
			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}
		else {
			AssertElementPresent(
				locator1 = "Card#SPECIFIC_LARGE_TEXT",
				key_cardText = StringUtil.unquote("${searchTerm}")
			);
		}
	}

	macro tearDownCP {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		ProductMenu.gotoSite(site = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Members",
			panel = "Site Administration",
			portlet = "Site Teams"
		);

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		var key_teamName = "${teamName}";

		Portlet.changeDisplayStyle(displayStyle = "List");

		AssertTextEquals(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		AssertTextEquals(
			locator1 = "SiteTeams#TEAM_TABLE_DESCRIPTION",
			value1 = "${teamDescription}"
		);

		AssertElementPresent(locator1 = "SiteTeams#TEAM_TABLE_ACTIONS");
	}

	macro viewDefaultCP {
		var key_teamName = "${teamName}";

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no site teams. You can add a site team by clicking the plus button on the bottom right corner."
		);

		AssertTextNotPresent(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = "${teamName}"
		);
	}

	macro viewMemberCP {
		LexiconEntry.gotoEntry(rowEntry = "${teamName}");

		Navigator.gotoNavItem(navItem = "${membershipType}s");

		if ("${membershipType}" == "User") {
			AssertTextEquals.assertPartialText(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = "${userFirstName} ${userLastName}",
				key_rowEntry = "${userFirstName} ${userLastName}"
			);
		}
		else if ("${membershipType}" == "User Group") {
			AssertTextEquals.assertPartialText(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = "${userGroupName}",
				key_rowEntry = "${userGroupName}"
			);
		}
	}

	macro viewNoMembersCP {
		LexiconEntry.gotoEntry(rowEntry = "${teamName}");

		for (var navItem : list "Users,User Groups") {
			Navigator.gotoNavItem(navItem = "${navItem}");

			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "There are no members. You can add a member by clicking the plus button on the bottom right corner."
			);
		}
	}

	macro viewPermissionPG {
		AssertChecked(
			locator1 = "Checkbox#TEAM_PERMISSIONS",
			key_permissionDefinitionKey = "${permissionDefinitionKey}",
			key_teamName = "${teamName}"
		);
	}
}