definition {

	macro gotoAddEntry {
		if (IsElementPresent(locator1 = "Blogs#ADD_BLOGS_ENTRY")) {
			Click(locator1 = "Blogs#ADD_BLOGS_ENTRY");
		}
		else {
			var key_text = "New Entry";

			AssertClick(
				locator1 = "Button#ANY",
				value1 = "New Entry");
		}

		AssertVisible(locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE");
	}

	macro gotoBlogsEntriesPermissionsCP {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Entries Permissions");

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoBlogsEntryPermissionsCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${entryTitle}");

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoBlogsEntryPermissionsPG {
		Portlet.mouseOverPortletTitle(portletTitleName = "Blogs");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Permissions");

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoBrowseImage {
		Click(locator1 = "Button#BROWSE_IMAGE");
	}

	macro gotoConfigurationScopePG {
		Navigator.gotoNavItem(navItem = "Scope");
	}

	macro gotoConfigurationSetupPG {
		Portlet.gotoPortletOptions(portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoEditCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(
			locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_TITLE",
			value1 = "${entryTitle}");

		Click(locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_OPTIONS_DROPDOWN");

		MenuItem.click(menuItem = "Edit");

		AssertElementPresent(locator1 = "Icon#BACK");

		AssertVisible(
			key_editor = "title",
			locator1 = "AlloyEditor#EDITOR");
	}

	macro gotoEditPG {
		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = "${entryTitle}");

		if (IsElementPresent(locator1 = "Button#EDIT_PENCIL")) {
			Click(locator1 = "Button#EDIT_PENCIL");
		}
		else {
			Portlet.mouseOverPortletTitle(portletTitleName = "Blogs");

			PortletEntry.clickEditFromEllipsis();
		}

		AssertVisible(locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE");
	}

	macro gotoEntryCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = "${entryTitle}";

		if (isSet(entryStatus)) {
			AssertTextEquals(
				locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_STATUS",
				value1 = "${entryStatus}");
		}

		AssertClick(
			locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_TITLE",
			value1 = "${entryTitle}");
	}

	macro gotoEntryPG {
		var key_entryContent = "${entryContent}";
		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = "${entryTitle}");

		AssertTextEquals(
			locator1 = "BlogsEntry#CONTENT",
			value1 = "${entryContent}");

		AssertClick.assertTextClickAt(
			locator1 = "BlogsEntry#TITLE_LINK",
			value1 = "${entryTitle}");
	}

	macro gotoEntryWithAbstractPG {
		var key_entryAbstractDescription = "${entryAbstractDescription}";
		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = "${entryTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "BlogsEntry#CONTENT_ABSTRACT",
			value1 = "${entryAbstractDescription}");

		AssertClick(
			locator1 = "BlogsEntry#TITLE_LINK",
			value1 = "${entryTitle}");
	}

	macro gotoPG {
		var key_entryTitle = "${entryTitle}";

		if (isSet(blogEntryURL)) {
			AssertLocation(value1 = "${blogEntryURL}");
		}
		else {
			Click(
				locator1 = "BlogsEntry#TITLE_LINK",
				value1 = "${entryTitle}");
		}

		AssertTextEquals(
			locator1 = "BlogsEntry#TITLE",
			value1 = "${entryTitle}");
	}

	macro gotoSelectFile {
		AssertClick(
			locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE",
			value1 = "Select File");
	}

	macro moveToRecycleBinCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(
			locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_TITLE",
			value1 = "${entryTitle}");

		Click(locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_OPTIONS_DROPDOWN");

		PortletEntry.clickMoveToRecycleBin();

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}

}