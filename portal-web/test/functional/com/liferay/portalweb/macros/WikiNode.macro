definition {

	var assetType = "Wiki Node";

	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${wikiNodeName}");

		Button.clickSave();

		var key_wikiNodeName = "${wikiNodeName}";

		Alert.viewSuccessMessage();
	}

	macro addMainCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Wiki"
		);

		if (IsElementNotPresent(locator1 = "Wiki#NODE_TABLE")) {
			WikiNode.addCP(wikiNodeName = "Main");
		}
	}

	macro deleteCP {
		Click(locator1 = "Icon#DISPLAY_STYLE_LIST");

		var key_wikiNodeName = "${wikiNodeName}";

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}"
		);

		LexiconEntry.openEntryMenu(rowEntry = "${wikiNodeName}");

		PortletEntry.clickMoveToRecycleBin();

		AssertElementPresent(locator1 = "Message#SUCCESS");

		RecycleBin.emptyCP();
	}

	macro editCP {
		var key_wikiNodeName = "${wikiNodeName}";

		Click(locator1 = "Icon#DISPLAY_STYLE_LIST");

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}"
		);

		LexiconEntry.openEntryMenu(rowEntry = "${wikiNodeName}");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.inputName(name = "${wikiNodeNameEdit}");

		PortletEntry.inputDescription(description = "${wikiNodeDescriptionEdit}");

		Button.clickSave();

		var key_wikiNodeName = "${wikiNodeNameEdit}";

		Alert.viewSuccessMessage();
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Wiki"
		);

		while (IsElementPresent(locator1 = "Wiki#NODE_TABLE_ACTIONS_GENERIC_DROPDOWN_2")) {
			Click(locator1 = "Wiki#NODE_TABLE_ACTIONS_GENERIC_DROPDOWN_2");

			PortletEntry.clickMoveToRecycleBin();

			AssertElementPresent(locator1 = "Message#SUCCESS");

			AssertElementPresent(locator1 = "Message#SUCCESS");
		}

		RecycleBin.emptyCP();
	}

	macro viewCP {
		var key_wikiNodeName = "${wikiNodeName}";

		Click(locator1 = "Icon#DISPLAY_STYLE_LIST");

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}"
		);

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_NUMBER_OF_PAGES",
			value1 = "${numberOfWikiPages}"
		);

		AssertClick(locator1 = "Wiki#NODE_TABLE_WIKI_NAME", value1 = "${wikiNodeName}");

		SiteAdmin.viewPortletTitle(portletTitle = "${wikiNodeName}");
	}

	macro viewMainNode {
		var key_wikiNodeName = "${wikiNodeName}";

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}"
		);

		AssertElementNotPresent(locator1 = "Wiki#NODE_TABLE_WIKI_GENERIC_2");
	}

}