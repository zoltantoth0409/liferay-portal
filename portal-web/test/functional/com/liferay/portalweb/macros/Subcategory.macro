definition {

	macro _addCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Subcategory",
			rowEntry = "${categoryName}");

		PortletEntry.inputTitle(title = "${subcategoryName}");

		if (!(contains("${subcategoryDescription}", "{subcategoryDescription}"))) {
			PortletEntry.inputDescription(description = "${subcategoryDescription}");
		}
	}

	macro addCP {
		Subcategory._addCP(
			categoryName = "${categoryName}",
			subcategoryDescription = "${subcategoryDescription}",
			subcategoryName = "${subcategoryName}",
			vocabularyName = "${vocabularyName}");

		PortletEntry.save();
	}

	macro addDuplicateCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Subcategory",
			rowEntry = "${categoryName}");

		PortletEntry.inputTitle(title = "${subcategoryName}");

		Button.clickSave();

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "Please enter a unique name.");
	}

	macro addPropertyCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${subcategoryName}");

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD_1",
			value1 = "${propertiesKeyField}");

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD_1",
			value1 = "${propertiesValueField}");

		PortletEntry.save();
	}

	macro deleteViaIconCP {
		for (var rowEntry : list "${vocabularyName},${categoryName}") {
			LexiconEntry.gotoEntry(rowEntry = "${rowEntry}");
		}

		var key_subcategoryName = "${subcategoryName}";

		Check(locator1 = "Categories#SUBCATEGORY_CHECKBOX");

		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${subcategoryName}");
	}

	macro deleteViaMenuCP {
		for (var rowEntry : list "${vocabularyName},${categoryName}") {
			LexiconEntry.gotoEntry(rowEntry = "${rowEntry}");
		}

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${subcategoryName}");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${subcategoryName}");
	}

	macro editCP {
		for (var rowEntry : list "${vocabularyName},${categoryName}") {
			LexiconEntry.gotoEntry(rowEntry = "${rowEntry}");
		}

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${subcategoryName}");

		PortletEntry.inputTitle(title = "${subcategoryNameEdit}");

		PortletEntry.save();
	}

	macro moveCP {
		AssertClick(
			key_rowEntry = "${vocabularyName}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${vocabularyName}");

		AssertClick(
			key_rowEntry = "${categoryName}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${categoryName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move",
			rowEntry = "${subcategoryName}");

		AssetCategorization.selectCategory(categoryName = "${categoryNameMove}");
	}

	macro searchCP {
		for (var rowEntry : list "${vocabularyName},${categoryName}") {
			LexiconEntry.gotoEntry(rowEntry = "${rowEntry}");
		}

		Search.searchCP(searchTerm = "${subcategorySearchEntry}");
	}

	macro viewCategoryPropertyCP {
		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		var key_categoryName = "${categoryName}";

		Click(locator1 = "Categories#CATEGORY_ENTRY_LINK");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${subcategoryName}");

		Navigator.gotoNavUnderline(navUnderline = "Properties");

		AssertTextEquals(
			locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD_1",
			value1 = "${propertiesKeyField}");

		AssertTextEquals(
			locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD_1",
			value1 = "${propertiesValueField}");
	}

	macro viewCP {
		var key_categoryName = "${categoryName}";

		LexiconEntry.gotoEntry(rowEntry = "${vocabularyName}");

		AssertClick(
			locator1 = "Categories#CATEGORY_ENTRY_LINK",
			value1 = "${categoryName}");

		var key_subcategoryName = "${subcategoryName}";

		AssertTextEquals(
			locator1 = "Categories#SUBCATEGORY_ENTRY",
			value1 = "${subcategoryName}");
	}

	macro viewSearchResultsCP {
		if (isSet(subCategoryNameList)) {
			for (var subCategoryName : list "${subCategoryNameList}") {
				var key_subCategoryName = "${subCategoryName}";

				AssertTextEquals(
					locator1 = "Categories#SUBCATEGORY_ENTRY",
					value1 = "${subcategoryName}");
			}
		}

		if (isSet(subcategoryDescriptionList)) {
			for (var categoryDescription : list "${subcategoryDescriptionList}") {
				var key_subcategoryDescription = "${subcategoryDescription}";

				AssertTextEquals(
					locator1 = "Categories#SUBCATEGORY_ENTRY_DESCRIPTION",
					value1 = "${subcategoryDescription}");
			}
		}

		if ("${nullResult}" == "true") {
			PortletEntry.viewEmptyMessage(entity = "Categories");
		}
	}

}