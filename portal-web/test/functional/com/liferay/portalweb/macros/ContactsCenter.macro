definition {

	macro addPG {
		Button.click(button = "Add Contact");

		AssertElementPresent(locator1 = "TextInput#NAME");

		PortletEntry.inputName(name = "${contactName}");

		PortletEntry.inputText(
			fieldLabel = "Email Address",
			text = "${emailAddress}");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "ContactsCenter#SUCCESS_MESSAGE",
			value1 = "You have successfully added a new contact.");
	}

	macro editPG {
		AssertClick(
			key_contact = "${contactName}",
			locator1 = "ContactsCenter#CONTACT_LIST",
			value1 = "${contactName}");

		AssertClick(
			locator1 = "Button#EDIT",
			value1 = "Edit");

		AssertElementPresent(locator1 = "TextInput#NAME");

		if (isSet(contactNameEdit)) {
			PortletEntry.inputName(name = "${contactNameEdit}");
		}

		if (isSet(emailAddressEdit)) {
			PortletEntry.inputText(
				fieldLabel = "Email Address",
				text = "${emailAddressEdit}");
		}

		Button.clickSave();

		AssertTextEquals(
			locator1 = "ContactsCenter#SUCCESS_MESSAGE",
			value1 = "You have successfully updated the contact.");
	}

	macro viewContactCenterContainerPG {
		if ("${contactConnection}" != "1") {
			AssertTextEquals(
				locator1 = "ContactsCenter#CONTACT_CONNECTIONS_COUNT",
				value1 = "You have ${contactConnection} connections.");
		}
		else {
			AssertTextEquals(
				locator1 = "ContactsCenter#CONTACT_CONNECTIONS_COUNT",
				value1 = "You have one connection.");
		}

		AssertTextEquals(
			locator1 = "ContactsCenter#CONTACT_FOLLOWING_COUNT",
			value1 = "You are following ${contactFollowing} people.");

		if ("${contactFollower}" != "1") {
			AssertTextEquals(
				locator1 = "ContactsCenter#CONTACT_FOLLOWER_COUNT",
				value1 = "You have ${contactFollower} followers.");
		}
		else {
			AssertTextEquals(
				locator1 = "ContactsCenter#CONTACT_FOLLOWER_COUNT",
				value1 = "You have one follower.");
		}

		if ("${myContact}" != "1") {
			AssertTextEquals(
				locator1 = "ContactsCenter#MY_CONTACTS_COUNT",
				value1 = "View my ${myContact} contacts.");
		}
		else {
			AssertTextEquals(
				locator1 = "ContactsCenter#MY_CONTACTS_COUNT",
				value1 = "View one contact.");
		}

		if ("${allUser}" != "1") {
			AssertTextEquals(
				locator1 = "ContactsCenter#All_USERS_COUNT",
				value1 = "View all ${allUser} users.");
		}
		else {
			AssertTextEquals(
				locator1 = "ContactsCenter#All_USERS_COUNT",
				value1 = "View one user.");
		}
	}

	macro viewContactPG {
		AssertClick.assertPartialTextClickAt(
			key_contact = "${contactName}",
			locator1 = "ContactsCenter#CONTACT_LIST",
			value1 = "${contactName}");

		AssertTextEquals.assertPartialText(
			key_contactText = "${contactName}",
			locator1 = "ContactsCenter#CONTACT_CONTAINER_TEXT",
			value1 = "${contactName}");

		AssertTextEquals(
			key_contactText = "${emailAddress}",
			locator1 = "ContactsCenter#CONTACT_CONTAINER_TEXT",
			value1 = "${emailAddress}");

		if (isSet(interactButton)) {
			for (var button : list "${interactButton}") {
				Button.viewPresent(button = "${button}");
			}
		}
	}

}