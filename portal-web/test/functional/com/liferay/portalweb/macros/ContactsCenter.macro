definition {

	macro addPG {
		Button.click(button = "Add Contact");

		AssertElementPresent(locator1 = "TextInput#NAME");

		PortletEntry.inputName(name = "${contactName}");

		Type(
			locator1 = "ContactsCenter#CONTACT_EMAIL_ADDRESS",
			value1 = "${emailAddress}");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "ContactsCenter#SUCCESS_MESSAGE",
			value1 = "You have successfully added a new contact.");
	}

	macro editPG {
		AssertClick(
			key_contact = "${contactName}",
			locator1 = "ContactsCenter#CONTACT_LIST",
			value1 = "${contactName}");

		AssertClick(
			locator1 = "Button#EDIT",
			value1 = "Edit");

		AssertElementPresent(locator1 = "TextInput#NAME");

		if (isSet(contactNameEdit)) {
			PortletEntry.inputName(name = "${contactNameEdit}");
		}

		if (isSet(emailAddressEdit)) {
			Type(
				locator1 = "ContactsCenter#CONTACT_EMAIL_ADDRESS",
				value1 = "${emailAddressEdit}");
		}

		Button.clickSave();

		AssertTextEquals(
			locator1 = "ContactsCenter#SUCCESS_MESSAGE",
			value1 = "You have successfully updated the contact.");
	}

	macro viewContactPG {
		AssertClick.assertPartialTextClickAt(
			key_contact = "${contactName}",
			locator1 = "ContactsCenter#CONTACT_LIST",
			value1 = "${contactName}");

		AssertTextEquals.assertPartialText(
			key_contactText = "${contactName}",
			locator1 = "ContactsCenter#CONTACT_CONTAINER_TEXT",
			value1 = "${contactName}");

		AssertTextEquals(
			key_contactText = "${emailAddress}",
			locator1 = "ContactsCenter#CONTACT_CONTAINER_TEXT",
			value1 = "${emailAddress}");

		if (isSet(interactButton)) {
			for (var button : list "${interactButton}") {
				Button.viewPresent(button = "${button}");
			}
		}
	}

}