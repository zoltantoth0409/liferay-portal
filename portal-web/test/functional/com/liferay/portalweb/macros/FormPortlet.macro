definition {

	var validationErrorMessage = "This field is required.";

	macro changeLocale {
		Click(locator1 = "FormPortlet#LOCALIZATION_BUTTON");

		Click(
			key_locale = "${locale}",
			locator1 = "FormPortlet#LOCALIZATION_OPTION");
	}

	macro clickNextPage {
		AssertClick(
			locator1 = "Button#NEXT",
			value1 = "Next");
	}

	macro clickSubmit {
		AssertClick(
			locator1 = "Button#MAIN_CONTENT_SUBMIT",
			value1 = "Submit");
	}

	macro nextPage {
		AssertClick(
			locator1 = "Button#NEXT",
			value1 = "Next");

		var key_formPageName = "${formPageCurrent}";

		AssertTextEquals(
			locator1 = "FormPortlet#FORM_PAGE_INACTIVE",
			value1 = "${formPageCurrent}");

		var key_formPageName = "${formPageNext}";

		AssertTextEquals(
			locator1 = "FormPortlet#FORM_PAGE_ACTIVE",
			value1 = "${formPageNext}");
	}

	macro previousPage {
		Click(locator1 = "Button#PREVIOUS");
	}

	macro submit {
		if (IsElementPresent(locator1 = "FormPortlet#PORTLET_CONTENT")) {
			Click(locator1 = "FormPortlet#PORTLET_CONTENT");
		}
		else {
			Click(locator1 = "Form#PUBLISH_URL_FORM_PAGE");

			Click(locator1 = "Form#PUBLISH_URL_FORM_PAGE");
		}

		FormPortlet.clickSubmit();
	}

	macro submitLocalizedSuccessfully {
		AssertClick(
			locator1 = "Button#MAIN_CONTENT_SUBMIT",
			value1 = "${buttonName}");

		Alert.viewSuccessMessage();
	}

	macro submitSuccessfully {
		FormPortlet.submit();

		Alert.viewSuccessMessage();
	}

	macro submitWithError {
		FormPortlet.submit();

		Alert.viewErrorMessage(errorMessage = "${errorMessage}");
	}

	macro submitWithValidationError {
		FormPortlet.submit();

		AssertTextEquals(
			locator1 = "Message#ERROR_FORM_FIELD_REQUIRED",
			value1 = "${validationErrorMessage}");
	}

	macro viewSuccessPage {
		var key_successPageTitle = "${successPageTitle}";

		AssertTextEquals(
			locator1 = "FormPortlet#SUCCESS_PAGE_TITLE",
			value1 = "${successPageTitle}");

		var key_successPageDescription = "${successPageDescription}";

		AssertTextEquals(
			locator1 = "FormPortlet#SUCCESS_PAGE_DESCRIPTION",
			value1 = "${successPageDescription}");
	}

}