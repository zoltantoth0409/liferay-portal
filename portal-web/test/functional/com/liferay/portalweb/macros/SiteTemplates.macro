definition {
	macro addChildPageViaSiteTemplateCP {
		var key_siteTemplateName = "${siteTemplateName}";

		Click(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIONS");

		Click(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "Manage");

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "Navigation");

		Page.addChild(
			page = "Private Pages",
			pageName = "${pageName}",
			siteChildPageName = "${siteChildPageName}",
			siteScopeSelected = "true"
		);
	}

	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${siteTemplateName}");

		PortletEntry.inputDescription(
			description = "This is a site template description"
		);

		if ("${siteTemplateActive}" == "No") {
			Uncheck.uncheckHiddenToggleSwitch(locator1 = "ToggleSwitch#ACTIVE");
		}

		if ("${disableChanges}" == "true") {
			Uncheck.uncheckHiddenToggleSwitch(
				locator1 = "ToggleSwitch#ALLOW_SITE_ADMIN_MODIFY_PAGES"
			);
		}

		PortletEntry.save();
	}

	macro addSitePageViaSiteTemplateCP {
		var key_siteTemplateName = "${siteTemplateName}";

		Click(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIONS");

		Click(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "Manage");

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "Navigation");

		var key_page = "${pageName}";

		Click(locator1 = "SitePages#PRIVATE_PAGE_ELLIPSIS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Add Page",
			key_menuItem = "Add Page"
		);

		if (isSet(pageType)) {
			Select(locator1 = "SitePagesEditPage#PAGE_TYPE_SELECT", value1 = "${pageType}");

			if ("${pageTemplateAutoApplyChanges}" == "true") {
				var key_pageType = "${pageType}";

				MouseOver(locator1 = "Icon#INHERIT_CHANGES_TOOLTIP");

				AssertTextEquals(
					locator1 = "Message#TOOLTIP",
					value1 = "Automatically apply changes done to the page template."
				);

				Check.checkToggleSwitch(
					locator1 = "SitePagesEditPage#PAGE_TYPE_TEMPLATE_AUTOMATICALLY_APPLY_CHANGES_CHECKBOX",
					value1 = "${pageType}"
				);
			}

			if ("${pageType}" == "Link to URL") {
				Page._addPageTypeLinkToURL(
					pageTypeDescription = "${pageTypeDescription}",
					pageTypeURL = "${pageTypeURL}"
				);
			}
		}

		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${pageName}");

		AssertClick(locator1 = "Button#ADD_PAGE_BTN", value1 = "Add Page");
	}

	macro deleteCP {
		var key_siteTemplateName = "${siteTemplateName}";

		Click(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIONS");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		if ("${associatedToSite}" == "true") {
			Alert.viewRequestFailedToComplete();

			Alert.viewFailedSiteTemplateDeletionMessage();
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro deleteViaDeleteIconCP {
		Click(
			locator1 = "Checkbox#SITE_TEMPLATE",
			key_siteTemplateName = "${siteTemplateName}"
		);

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();
	}

	macro editCP {
		SiteTemplates.gotoSettingsCP(siteTemplateName = "${siteTemplateName}");

		PortletEntry.inputName(name = "${siteTemplateName} Edit");

		if (isSet(siteTemplateDescription)) {
			PortletEntry.inputDescription(description = "${siteTemplateDescription}");
		}

		Button.clickSave();
	}

	macro editTemplateStatusCP {
		SiteTemplates.gotoSettingsCP(siteTemplateName = "${siteTemplateName}");

		if (isSet(siteTemplateActiveEdit)) {
			if ("${siteTemplateActiveEdit}" == "No") {
				Uncheck.uncheckHiddenToggleSwitch(locator1 = "ToggleSwitch#ACTIVE");
			}
		}

		Button.clickSave();
	}

	macro gotoSettingsCP {
		var key_siteTemplateName = "${siteTemplateName}";

		AssertTextEquals.assertPartialText(
			locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME",
			value1 = "${siteTemplateName}"
		);

		Click(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIONS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Manage",
			key_menuItem = "Manage"
		);

		ProductMenuHelper.openProductMenu();

		AssertTextEquals(
			locator1 = "SiteTemplates#SITE_TEMPLATE_NAME",
			value1 = "${siteTemplateName}"
		);

		ProductMenuHelper.expandCategory(category = "Configuration");

		AssertClick(
			locator1 = "SiteTemplates#CONFIGURATION_SITE_TEMPLATE_SETTINGS",
			value1 = "Site Template Settings"
		);

		SiteAdmin.viewPortletTitle(portletTitle = "Site Template Settings");
	}

	macro gotoViewPages {
		var key_siteTemplateName = "${siteTemplateName}";

		AssertClick.assertPartialTextClickAt(
			locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME",
			value1 = "${siteTemplateName}"
		);

		SelectWindow(locator1 = "home - ${siteTemplateName} - ${siteName}");
	}

	macro resetPageChanges {
		Click(locator1 = "ControlMenu#INFO");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#CUSTOMIZED_PAGE_INFO",
			value1 = "This page has been changed since the last update from the site template."
		);

		AssertTextEquals(locator1 = "Button#RESET_CHANGES", value1 = "Reset Changes");

		var key_portletTitleName = "${portletName}";

		AssertTextEquals(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberTo}_PORTLET_TITLE",
			value1 = "${portletName}"
		);

		AssertClick(locator1 = "Button#RESET_CHANGES", value1 = "Reset Changes");

		AssertElementNotPresent(locator1 = "ControlMenu#INFO");
	}

	macro tearDownCP {
		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates"
		);

		var key_siteTemplateName = "Site Template Name";

		while (IsElementPresent(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIONS")) {
			Click(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIONS");

			AssertClickNoError(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Delete",
				key_menuItem = "Delete"
			);

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		AssertTextEquals.assertPartialText(
			locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME",
			value1 = "${siteTemplateName}",
			key_siteTemplateName = "${siteTemplateName}"
		);

		if (isSet(siteTemplateDescription)) {
			AssertTextEquals(
				locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_DESCRIPTION",
				value1 = "${siteTemplateDescription}",
				key_siteTemplateDescription = "${siteTemplateDescription}"
			);
		}
	}

	macro viewDefaultCP {
		for (var siteTemplateName : list "${siteTemplateNameList}") {
			var key_siteTemplateName = "${siteTemplateName}";

			AssertTextEquals.assertPartialText(
				locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME",
				value1 = "${siteTemplateName}"
			);
		}

		AssertElementNotPresent(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME_3");
	}

	macro viewDefaultDataLocalizedCP {
		for (var siteTemplateNameTranslation : list "${siteTemplateNameList}") {
			var key_siteTemplateName = "${siteTemplateNameTranslation}";

			AssertTextEquals.assertPartialText(
				locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME",
				value1 = "${siteTemplateNameTranslation}"
			);
		}

		for (var DescriptionTranslation : list "${siteTemplateDescriptionList}") {
			var key_siteTemplateDescription = "${DescriptionTranslation}";

			AssertTextEquals(
				locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_DESCRIPTION",
				value1 = "${DescriptionTranslation}"
			);
		}
	}

	macro viewNoInfoMessage {
		AssertElementNotPresent(locator1 = "ControlMenu#INFO");

		AssertTextNotPresent(
			locator1 = "Message#CUSTOMIZED_PAGE_INFO",
			value1 = "This page has been changed since the last update from the site template."
		);
	}

	macro viewNoSiteOrWorkflowLinksCP {
		var key_portletCategoryName = "Configuration";
		var key_siteTemplateName = "Site Template Name";

		AssertTextEquals.assertPartialText(
			locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME",
			value1 = "Site Template Name"
		);

		Click(locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIONS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Manage",
			key_menuItem = "Manage"
		);

		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.expandCategory(category = "Configuration");

		for (var portletName : list "Site Settings,Social Activities,Workflow Configuration") {
			var key_portletName = "${portletName}";

			AssertTextNotPresent(
				locator1 = "Home#SITE_ADMINISTRATION_NAVIGATION",
				value1 = "${portletName}"
			);
		}

		var key_portletCategoryName = "${portletCategoryName}";

		AssertElementNotPresent(
			locator1 = "Home#SITE_ADMINISTRATION_CATEGORY_COLLAPSED"
		);
	}

	macro viewNoTemplatesDisplay {
		LexiconEntry.gotoAdd();

		var key_siteTemplateName = "${siteTemplateName}";

		AssertElementNotPresent(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			key_menuItem = "${siteTemplateName}"
		);
	}

	macro viewResetPageChanges {
		AssertTextNotPresent(
			locator1 = "Message#CUSTOMIZED_PAGE_INFO",
			value1 = "This page has been changed since the last update from the site template."
		);

		AssertTextNotPresent(
			locator1 = "Button#RESET_CHANGES",
			value1 = "Reset Changes"
		);

		var key_portletTitleName = "${portletName}";

		AssertTextEquals(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberFrom}_PORTLET_TITLE",
			value1 = "${portletName}"
		);
	}

	macro viewTemplateStatusCP {
		var key_siteTemplateName = "${siteTemplateName}";

		AssertTextEquals.assertPartialText(
			locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_NAME",
			value1 = "${siteTemplateName}"
		);

		AssertTextEquals(
			locator1 = "SiteTemplates#SITE_TEMPLATE_TABLE_ACTIVE",
			value1 = "${siteTemplateActiveEdit}"
		);
	}
}