definition {

	macro assertEngineVendorAndVersions {
		AssertTextEquals.assertPartialText(
			locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
			position = "1",
			searchInformation = "${engineVendor}",
			value1 = "${engineVendor}");

		AssertTextEquals.assertPartialText(
			locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
			position = "2",
			searchInformation = "${clientVersion}",
			value1 = "${clientVersion}");

		AssertTextEquals.assertPartialText(
			locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
			position = "3",
			searchInformation = "${serverVersion}",
			value1 = "${serverVersion}");
	}

	macro executeSearchActions {
		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Search");

		var searchClientVersion = selenium.getText("//div[contains(@class,'alert alert-info')]//strong[2]");
		var searchEngineVendor = selenium.getText("//div[contains(@class,'alert alert-info')]//strong[1]");

		if (contains("${searchEngineVendor}", "Elasticsearch")) {
			if (contains("${searchClientVersion}", "7.") && contains("${searchEngineVendor}", "Embedded")) {
				var curl = "http://localhost:9201/_tasks";
			}
			else {
				var curl = "http://localhost:9200/_tasks";
			}
		}

		Panel.expandPanel(panel = "Index Actions");

		AssertClick.assertTextClickAtAndPause(
			key_actionsDescription = "${actionsDescription}",
			locator1 = "Button#EXECUTE_ACTION",
			value1 = "Execute");

		var reindexElapsedTime = "0";
		var reindexStartTime = "0";

		if (contains("${searchEngineVendor}", "Elasticsearch")) {
			echo("Current Elasticsearch Tasks:");

			var elasticsearchTasks = JSONCurlUtil.get("${curl}");

			while ((!(contains("${elasticsearchTasks}", "indices:data/write/bulk"))) && ("${reindexStartTime}" != "10")) {
				Pause(locator1 = "1000");

				var reindexStartTime = ${reindexStartTime} + 1;

				echo("Current Elasticsearch Tasks:");

				elasticsearchTasks = JSONCurlUtil.get("${curl}");
			}

			echo("Started reindexing ${reindexStartTime} second(s) after clicking 'Execute'.");

			while (contains("${elasticsearchTasks}", "indices:data/write/bulk") && ("${reindexElapsedTime}" != "120")) {
				Pause(locator1 = "5000");

				var reindexElapsedTime = ${reindexElapsedTime} + 5;

				echo("Current Elasticsearch Tasks:");

				elasticsearchTasks = JSONCurlUtil.get("${curl}");
			}
		}
		else if (contains("${searchEngineVendor}", "Solr")) {
			while (IsElementPresent(locator1 = "ControlMenu#RELOAD_TOOLTIP")&& ("${reindexElapsedTime}" != "120")) {
				Pause(locator1 = "5000");

				var reindexElapsedTime = ${reindexElapsedTime} + 5;
			}
		}

		if ("${reindexElapsedTime}" == "120") {
			fail("Reindex timed out after 120 seconds.");
		}
		else {
			echo("Finished reindexing after ${reindexElapsedTime} seconds.");
		}
	}

}