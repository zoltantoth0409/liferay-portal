definition {

	macro assertEngineVendorAndVersions {
		if (IsElementNotPresent(locator1 = "//div[contains(@class,'alert alert-info')]//strong[2]")) {
			AssertTextEquals.assertPartialText(
				locator1 = "SearchAdmin#SEARCH_ENGINE_VENDOR",
				value1 = "${engineVendor}");

			AssertTextEquals.assertPartialText(
				locator1 = "SearchAdmin#CLIENT_VERSION",
				value1 = "${clientVersion}");
		}
		else {
			AssertTextEquals.assertPartialText(
				locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
				position = "1",
				searchInformation = "${engineVendor}",
				value1 = "${engineVendor}");

			AssertTextEquals.assertPartialText(
				locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
				position = "2",
				searchInformation = "${clientVersion}",
				value1 = "${clientVersion}");

			AssertTextEquals.assertPartialText(
				locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
				position = "3",
				searchInformation = "${serverVersion}",
				value1 = "${serverVersion}");
		}
	}

	macro executeSearchActions {
		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Search");

		if (IsElementPresent(locator1 = "//div[contains(@class,'alert alert-info')]//strong[2]")) {
			var searchClientVersion = selenium.getText("//div[contains(@class,'alert alert-info')]//strong[2]");
			var searchEngineVendor = selenium.getText("//div[contains(@class,'alert alert-info')]//strong[1]");
		}
		else {
			var searchClientVersion = selenium.getText("//div[contains(@class,'value')]//preceding-sibling::div[contains(.,'Client Version')]");
			var searchEngineVendor = selenium.getText("//div[contains(@class,'value')]//preceding-sibling::div[contains(.,'Search Engine Vendor')]");
		}

		if (contains("${searchEngineVendor}", "Elasticsearch")) {
			if (contains("${searchClientVersion}", "7.") && contains("${searchEngineVendor}", "Embedded")) {
				var curl = "http://localhost:9201/_tasks";
			}
			else {
				var curl = "http://localhost:9200/_tasks";
			}
		}

		Navigator.gotoNavItem(navItem = "Index Actions");

		AssertClick.assertTextClickAt(
			key_actionsDescription = "${actionsDescription}",
			locator1 = "Button#EXECUTE_ACTION",
			value1 = "Execute");

		var reindexElapsedTime = "0";
		var reindexStartTime = "0";

		if (contains("${searchEngineVendor}", "Elasticsearch")) {
			echo("Current Elasticsearch Tasks:");

			var elasticsearchTasks = JSONCurlUtil.get("${curl}");

			while ((!(contains("${elasticsearchTasks}", "indices:data/write/bulk"))) && ("${reindexStartTime}" != "10")) {
				Pause.pauseNoSPARefresh(locator1 = "1000");

				var reindexStartTime = ${reindexStartTime} + 1;

				echo("Current Elasticsearch Tasks:");

				elasticsearchTasks = JSONCurlUtil.get("${curl}");
			}

			if ("${reindexStartTime}" == "10") {
				echo("Elasticsearch failed to receive reindex request after 10 seconds.");
			}
			else {
				echo("Started reindexing ${reindexStartTime} second(s) after clicking 'Execute'.");
			}

			while (contains("${elasticsearchTasks}", "indices:data/write/bulk") && ("${reindexElapsedTime}" != "120")) {
				Pause.pauseNoSPARefresh(locator1 = "5000");

				var reindexElapsedTime = ${reindexElapsedTime} + 5;

				echo("Current Elasticsearch Tasks:");

				elasticsearchTasks = JSONCurlUtil.get("${curl}");
			}
		}
		else if (contains("${searchEngineVendor}", "Solr")) {
			while (IsElementPresent(locator1 = "ControlMenu#RELOAD_TOOLTIP")&& ("${reindexElapsedTime}" != "120")) {
				Pause.pauseNoSPARefresh(locator1 = "5000");

				var reindexElapsedTime = ${reindexElapsedTime} + 5;
			}
		}

		while (IsElementNotPresent(locator1 = "Button#EXECUTE_ACTION", key_actionsDescription = "${actionsDescription}")) {
			Pause(locator1 = "3000");
		}

		if ("${reindexElapsedTime}" == "120") {
			fail("Reindex timed out after 120 seconds.");
		}
		else {
			echo("Finished reindexing after ${reindexElapsedTime} seconds.");
		}
	}

}