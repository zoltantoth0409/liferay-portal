definition {
	macro deactivateAppCP {
		Click(locator1 = "AppManager#APP_ACTIONS", key_appName = "${appName}");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Deactivate",
			key_menuItem = "Deactivate"
		);

		Confirm(value1 = "Are you sure you want to deactivate this?");
	}

	macro selectAppCP {
		var key_appName = "${appName}";

		while (IsElementNotPresent(locator1 = "AppManager#APP_NAME")) {
			Click(locator1 = "Pagination#NEXT_LINK");
		}

		AssertClick(locator1 = "AppManager#APP_NAME", value1 = "${appName}");
	}

	macro uninstallAppCP {
		Search.searchCP(searchTerm = "${appName}");

		Click(locator1 = "AppManager#APP_ACTIONS", key_appName = "${appName}");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Uninstall",
			key_menuItem = "Uninstall"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Pause(locator1 = "15000");
	}

	macro uploadAppCP {
		Click(locator1 = "Portlet#OPTIONS_ICON");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Upload",
			key_menuItem = "Upload"
		);

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertElementPresent(locator1 = "Button#BROWSE");

		Click(locator1 = "Button#BROWSE");

		SikuliClick(locator1 = "OS#FILE_DIALOG_BOX_TYPE_FILE_NAME_ICON_PNG");

		SikuliUploadCommonFile(
			locator1 = "OS#FILE_DIALOG_BOX_FILE_NAME_FIELD_PNG",
			value1 = "${warFileName}"
		);

		Button.clickInstall();

		AssertTextEquals(
			locator1 = "Message#SUCCESS_3",
			value1 = "The plugin was uploaded successfully and is now being installed."
		);

		SelectFrame.selectFrameTop();

		Click(locator1 = "Modal#CLOSE_BUTTON");

		Pause(locator1 = "15000");

		AssertConsoleTextPresent(value1 = "STARTED ${appName}");

		Pause(locator1 = "3000");

		AssertConsoleTextNotPresent(
			value1 = "HotDeployException: Error initializing Spring for ${appName}"
		);

		AssertConsoleTextNotPresent(
			value1 = "HotDeployException: Error registering hook for ${appName}"
		);

		AssertConsoleTextNotPresent(
			value1 = "HotDeployException: Error registering portlets for ${appName}"
		);
	}

	macro viewAppCP {
		Search.searchCP(searchTerm = "${appName}");

		AssertTextEquals.assertPartialText(
			locator1 = "AppManager#APP_NAME",
			value1 = "${appName}",
			key_appName = "${appName}"
		);

		if (isSet(appStatus)) {
			AppManager.viewStatus(appName = "${appName}", appStatus = "${appStatus}");
		}
	}

	macro viewNoAppCP {
		var key_appName = "${appName}";

		AssertElementNotPresent(locator1 = "AppManager#APP_NAME");

		while (IsElementPresent(locator1 = "Pagination#NEXT_LINK")) {
			Click(locator1 = "Pagination#NEXT_LINK");

			AssertElementNotPresent(locator1 = "AppManager#APP_NAME");
		}
	}

	macro viewStatus {
		AssertTextEquals.assertPartialText(
			locator1 = "AppManager#APP_STATUS",
			value1 = "${appStatus}",
			key_appName = "${appName}",
			key_appStatus = "${appStatus}"
		);
	}

	macro viewUninstallAppCP {
		Search.searchCP(searchTerm = "${appName}");

		AssertElementNotPresent(
			locator1 = "AppManager#APP_NAME",
			key_appName = "${appName}"
		);
	}
}