definition {

	@summary = "Directly calls the JSON WS API to add a fragment collection"
	macro _addFragmentCollection {
		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/fragment.fragmentcollection/add-fragment-collection \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d name=${name} \
			-d description=
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to add a fragment (default type is section)"
	macro _addSection {
		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(html))) {
			var html = "test";
		}

		if (!(isSet(configuration))) {
			var configuration = "";
		}

		if (!(isSet(type))) {
			var type = "0";
		}

		if (!(isSet(status))) {
			var status = "0";
		}

		var curl = '''
			${portalURL}/api/jsonws/fragment.fragmententry/add-fragment-entry \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d fragmentCollectionId=${collectionId} \
			-d fragmentEntryKey= \
			-d name=${name} \
			-d css= \
			-d html=${html} \
			-d js= \
			-d configuration=${configuration} \
			-d previewFileEntryId=0 \
			-d type=${type} \
			-d status=${status}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Get the collectionId from a collection by using collectionName and groupId"
	macro _getCollectionIdByName {
		if (!(isSet(collectionName))) {
			fail("Please set 'collectionName'.");
		}

		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		echo("## Searching for a 'Collection' with the following info using JSONWS:");

		echo("## * Collection Name: ${collectionName}");

		echo("## * Group Name: ${groupName}");

		echo("## * Group Id: ${groupId}");

		var curl = '''
			${portalURL}/api/jsonws/fragment.fragmentcollection/get-fragment-collections \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d name=${collectionName} \
			-d <CURL_DATA[start=-1]CURL_DATA> \
			-d <CURL_DATA[end=-1]CURL_DATA> \
			-d -orderByComparator=
		''';
		var collectionId = JSONCurlUtil.post("${curl}", "$..fragmentCollectionId");
		var collectionId = StringUtil.replace("${collectionId}", "[&quot;", "");
		var collectionId = StringUtil.replace("${collectionId}", "&quot;]", "");

		if ("${collectionId}" == "[]") {
			fail("FAIL. Cannot find collection.");
		}
		else {
			echo("FOUND. Returning collectionId: ${collectionId}");
		}

		return "${collectionId}";
	}

	@summary = "Get the collectionName by using collectionName"
	macro _getCollectionName {
		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/fragment.fragmentcollection/get-fragment-collections \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d name=${collectionName} \
			-d <CURL_DATA[start=-1]CURL_DATA> \
			-d <CURL_DATA[end=-1]CURL_DATA> \
			-d -orderByComparator=
		''';
		var collectionNameResult = JSONCurlUtil.post("${curl}", "$..[?(@['name'] == '${collectionName}')]");

		return "${collectionNameResult}";
	}

	@summary = "Get the fragmentName from a fragmentCollection by using fragmentName "
	macro _getFragmentName {
		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/fragment.fragmententry/get-fragment-entries \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d fragmentCollectionId=${fragmentCollectionId} \
			-d <CURL_DATA[start=-1]CURL_DATA> \
			-d <CURL_DATA[end=-1]CURL_DATA>
		''';
		var fragmentNameResult = JSONCurlUtil.post("${curl}", "$..[?(@['name'] == '${fragmentName}')]");

		return "${fragmentNameResult}";
	}

	@summary = "Add a basic fragment collection."
	macro addFragmentCollection {
		if (isSet(groupName)) {
			var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");
		}
		else if (isSet(groupKey)) {
			var groupId = JSONGroup._getGroupIdByGroupKey(groupKey = "${groupKey}", site = "${site}");
		}
		else {
			fail("Please set 'groupKey or groupName to get groupId'.");
		}

		if (!(isSet(name))) {
			fail("Please set 'name'.");
		}

		echo("## Adding a 'Fragment Collection' with the following info using JSONWS:");

		echo("## * Collection Name: ${name}");

		if (isSet(groupName)) {
			echo("## * Group Name: ${groupName}");
		}
		else {
			echo("## * Group Key: ${groupKey}");
		}

		echo("## * Group Id: ${groupId}");

		JSONFragment._addFragmentCollection(
			groupId = "${groupId}",
			name = "${name}");
	}

	@summary = "Add a section to a fragment collection."
	macro addSection {
		if (isSet(groupName)) {
			var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");
		}
		else if (isSet(groupKey)) {
			var groupId = JSONGroup._getGroupIdByGroupKey(groupKey = "${groupKey}", site = "${site}");
		}
		else {
			fail("Please set 'groupKey or groupName to get groupId'.");
		}

		if (!(isSet(collectionName))) {
			fail("Please set 'collectionName'.");
		}
		else {
			var collectionId = JSONFragment._getCollectionIdByName(
				collectionName = "${collectionName}",
				groupId = "${groupId}"
			);
		}

		if (!(isSet(collectionId))) {
			fail("Please set 'collectionId'.");
		}

		if (!(isSet(name))) {
			fail("Please set 'name'.");
		}

		echo("## Adding a 'Fragment' with the following info using JSONWS:");

		echo("## * Collection Name: ${collectionName}");

		echo("## * Collection Id: ${collectionId}");

		echo("## * Fragment Name: ${name}");

		if (isSet(groupName)) {
			echo("## * Group Name: ${groupName}");
		}
		else {
			echo("## * Group Key: ${groupKey}");
		}

		echo("## * Group Id: ${groupId}");

		JSONFragment._addSection(
			collectionId = "${collectionId}",
			configuration = "${configuration}",
			groupId = "${groupId}",
			html = "${html}",
			name = "${name}",
			status = "${status}",
			type = "${type}");
	}

	@summary = "Assert that a fragmentCollection does not exist in a group given its collectionName and groupName"
	macro assertFragmentCollectionInGroup {
		if (!(isSet(collectionName))) {
			fail("Please set 'collectionName'.");
		}

		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		echo("## Asserts a 'Fragment Collection' doesn't exist in a group using JSONWS:");

		echo("## * Collection Name: ${collectionName}");

		echo("## * Group Name: ${groupName}");

		var collectionNameResult = JSONFragment._getCollectionName(
			collectionName = "${collectionName}",
			groupId = "${groupId}"
		);

		if ("${collectionNameResult}" == "[]") {
			fail("The collection ${collectionName} was not found.");
		}
		else {
			echo("The collection ${collectionName} was found.");
		}
	}

	@summary = "Assert that a fragmentCollection does not exist in a group given its collectionName and groupName"
	macro assertFragmentCollectionNotInGroup {
		if (!(isSet(collectionName))) {
			fail("Please set 'collectionName'.");
		}

		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		echo("## Asserts a 'Fragment Collection' doesn't exist in a group using JSONWS:");

		echo("## * Collection Name: ${collectionName}");

		echo("## * Group Name: ${groupName}");

		var collectionNameResult = JSONFragment._getCollectionName(
			collectionName = "${collectionName}",
			groupId = "${groupId}"
		);

		if ("${collectionNameResult}" == "[]") {
			echo("The collection ${collectionName} was not found.");
		}
		else {
			fail("The collection ${collectionName} was found.");
		}
	}

	@summary = "Assert that a fragment exists in a fragmentCollection given its collectionName, fragmentName, and groupName"
	macro assertFragmentInGroup {
		if (!(isSet(collectionName))) {
			fail("Please set 'collectionName'.");
		}

		if (!(isSet(fragmentName))) {
			fail("Please set 'fragmentName'.");
		}

		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var fragmentCollectionId = JSONFragment._getCollectionIdByName(
			collectionName = "${collectionName}",
			groupId = "${groupId}"
		);

		echo("## Asserts a 'Fragment' exists in a group using JSONWS:");

		echo("## * Collection Name: ${collectionName}");

		echo("## * Fragment Name: ${fragmentName}");

		echo("## * Group Name: ${groupName}");

		var fragmentNameResult = JSONFragment._getFragmentName(
			fragmentCollectionId = "${fragmentCollectionId}",
			fragmentName = "${fragmentName}",
			groupId = "${groupId}"
		);

		if ("${fragmentNameResult}" == "[]") {
			fail("The fragment ${fragmentName} was not found.");
		}
		else {
			echo("The fragment ${fragmentName} was found.");
		}
	}

	@summary = "Assert that a fragment does not exist in a group given its fragmentName"
	macro assertFragmentNotInGroup {
		if (!(isSet(collectionName))) {
			fail("Please set 'collectionName'.");
		}

		if (!(isSet(fragmentName))) {
			fail("Please set 'fragmentName'.");
		}

		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var fragmentCollectionId = JSONFragment._getCollectionIdByName(
			collectionName = "${collectionName}",
			groupId = "${groupId}"
		);

		echo("## Asserts a 'Fragment' doesn't exist in a group using JSONWS:");

		echo("## * Collection Name: ${collectionName}");

		echo("## * Fragment Name: ${fragmentName}");

		echo("## * Group Name: ${groupName}");

		var fragmentNameResult = JSONFragment._getFragmentName(
			fragmentCollectionId = "${fragmentCollectionId}",
			fragmentName = "${fragmentName}",
			groupId = "${groupId}"
		);

		if ("${fragmentNameResult}" == "[]") {
			echo("The fragment ${fragmentName} was not found.");
		}
		else {
			fail("The fragment ${fragmentName} was found.");
		}
	}

}