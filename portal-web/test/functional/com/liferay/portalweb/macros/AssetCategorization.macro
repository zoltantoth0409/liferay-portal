definition {
	macro _addTag {
		Type(locator1 = "AssetCategorization#TAGS_FIELD", value1 = "${tagName}");

		if (IsElementPresent(locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_CONTAINER")) {
			Click(
				locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_SPECIFIC",
				key_tagName = "${tagName}"
			);
		}

		AssertClick(locator1 = "Button#ADD_TAGS", value1 = "Add");
	}

	macro addCategory {
		AssertClick(
			locator1 = "AssetCategorization#CATEGORY_SELECT",
			value1 = "Select",
			key_vocabularyName = "${vocabularyName}"
		);

		AssetCategorization.selectCategory(categoryName = "${categoryName}");

		AssertTextEquals(
			locator1 = "AssetCategorization#CATEGORY",
			value1 = "${categoryName}",
			key_categoryName = "${categoryName}"
		);
	}

	macro addDuplicateTag {
		AssetCategorization._addTag(tagName = "${tagName}");

		AssertTextEquals(
			locator1 = "//div[contains(@class,'help-block')]",
			value1 = "Duplicate Tag: ${tagName}"
		);
	}

	macro addMultipleCategories {
		AssertClick(
			locator1 = "AssetCategorization#CATEGORY_SELECT",
			value1 = "Select",
			key_vocabularyName = "${vocabularyName}"
		);

		AssetCategorization.gotoSelectCategory();

		for (var categoryName : list "${categoryNameList}") {
			Portlet.selectTreeNode(nodeName = "${categoryName}");
		}

		SelectFrame(value1 = "relative=top");

		AssertClick(locator1 = "Button#DONE", value1 = "Done");
	}

	macro addTag {
		AssetCategorization._addTag(tagName = "${tagName}");

		AssertTextEquals(
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}",
			key_tagName = "${tagName}"
		);
	}

	macro gotoSelectCategory {
		SelectFrameTop();

		if (IsElementPresent(locator1 = "IFrame#SELECT_CATEGORY")) {
			IFrame.selectCategoryFrame();
		}

		Portlet.expandTree();
	}

	macro removeTag {
		var key_tagName = "${tagName}";

		AssertTextEquals(locator1 = "AssetCategorization#TAG", value1 = "${tagName}");

		Click(locator1 = "AssetCategorization#TAG_REMOVE");

		AssertElementNotPresent(locator1 = "AssetCategorization#TAG");
	}

	macro selectCategory {
		AssetCategorization.gotoSelectCategory();

		Portlet.selectTreeNode(nodeName = "${categoryName}");

		SelectFrameTop();

		if (IsElementPresent(locator1 = "Button#ADD")) {
			AssertClick(locator1 = "Button#ADD", value1 = "Add");
		}
		else {
			Click(locator1 = "Icon#CLOSE");
		}

		Pause(locator1 = "3000");

		if (IsElementPresent(locator1 = "IFrame#MODAL_IFRAME")) {
			IFrame.selectModalFrame();
		}
	}

	macro selectTag {
		Click(locator1 = "AssetCategorization#TAGS_SELECT_BUTTON");

		SelectFrameTop();

		if (IsElementPresent(locator1 = "IFrame#SELECT_TAG")) {
			SelectFrame(locator1 = "IFrame#SELECT_TAG");
		}

		for (var tagName : list "${tagNameList}") {
			Check(
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX",
				key_rowEntry = "${tagName}"
			);
		}

		SelectFrameTop();

		if (IsElementPresent(locator1 = "Button#DONE")) {
			AssertClick(locator1 = "Button#DONE", value1 = "Done");
		}
		else {
			Click(locator1 = "Icon#CLOSE");
		}

		if (IsElementPresent(locator1 = "IFrame#MODAL_IFRAME")) {
			IFrame.selectModalFrame();
		}

		AssertTextEquals(
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}",
			key_tagName = "${tagName}"
		);
	}
}