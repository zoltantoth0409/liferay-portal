definition {
	macro addCP {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}"
		);

		LexiconEntry.gotoAdd();

		var customFieldKey = StringUtil.lowerCase("${customFieldName}");

		PortletEntry.inputName(name = "${customFieldKey}");

		Select(locator1 = "Select#TYPE", value1 = "${customFieldType}");

		Button.clickSave();

		var key_customFieldName = "${customFieldName}";

		AssertTextEquals(
			locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME",
			value1 = "${customFieldName}"
		);
	}

	macro editCP {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}"
		);

		var key_customFieldName = "${customFieldName}";

		AssertVisible(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME");

		Pause(locator1 = "3000");

		Click(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_ACTIONS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		if (isSet(defaultValue)) {
			if ("${customFieldType}" == "True/False") {
				Select(locator1 = "Select#DEFAULT_VALUE_SELECT", value1 = "${defaultValue}");
			}
		}

		PortletEntry.save();
	}

	macro tearDownCP {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Custom Fields"
		);

		while (IsElementPresent(locator1 = "CustomFields#RESOURCE_TABLE_CUSTOM_FIELDS_LINK_GENERIC")) {
			Click(locator1 = "CustomFields#RESOURCE_TABLE_CUSTOM_FIELDS_LINK_GENERIC");

			while (IsElementPresent(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_ACTIONS_GENERIC")) {
				AssertClick(
					locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_ACTIONS_GENERIC",
					value1 = "Actions"
				);

				AssertClickNoError(
					locator1 = "MenuItem#ANY_MENU_ITEM",
					value1 = "Delete",
					key_menuItem = "Delete"
				);

				Confirm(
					value1 = "Are you sure you want to delete this? It will be deleted immediately."
				);

				Alert.viewSuccessMessage();
			}

			ProductMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Custom Fields"
			);
		}
	}
}