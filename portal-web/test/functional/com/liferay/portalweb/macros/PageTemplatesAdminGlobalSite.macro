definition {
	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${pageTemplateName}");

		PortletEntry.inputDescriptionTextInput(
			description = "This is a page template description"
		);

		if ("${pageTemplateActive}" == "Not Active") {
			Click(locator1 = "ToggleSwitch#ACTIVE");
		}

		PortletEntry.save();

		LexiconEntry.viewEntryName(rowEntry = "${pageTemplateName}");

		AssertTextEquals(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_ACTIVE",
			value1 = "${pageTemplateActive}",
			key_pageTemplateName = "${pageTemplateName}"
		);
	}

	macro deactivateAndActivate {
		LexiconEntry.viewEntryName(rowEntry = "${pageTemplateName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Configure",
			rowEntry = "${pageTemplateName}"
		);

		if (isSet(pageTemplateStatus)) {
			Click(locator1 = "ToggleSwitch#ACTIVE");
		}

		PortletEntry.save();
	}

	macro deleteCP {
		var key_pageTemplateName = "${pageTemplateName}";

		LexiconEntry.openEntryMenu(rowEntry = "${pageTemplateName}");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();
	}

	macro editCP {
		LexiconEntry.viewEntryName(rowEntry = "${pageTemplateName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Configure",
			rowEntry = "${pageTemplateName}"
		);

		PortletEntry.inputName(name = "${pageTemplateName} Edit");

		if ("${pageTemplateActive}" == "Not Active") {
			Click(locator1 = "ToggleSwitch#ACTIVE");
		}

		Button.clickSave();

		var key_pageTemplateName = "${pageTemplateName}";

		Alert.viewSuccessMessage();

		AssertTextEquals.assertPartialText(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
			value1 = "${pageTemplateName} Edit"
		);

		AssertTextEquals(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_ACTIVE",
			value1 = "${pageTemplateActive}"
		);
	}

	macro editDescription {
		PageTemplatesAdminGlobalSite.viewDescription(
			pageTemplateDescription = "${pageTemplateDescription}",
			pageTemplateName = "${pageTemplateName}"
		);

		PortletEntry.inputDescriptionTextInput(
			description = "This is a page template description edit"
		);

		PortletEntry.save();
	}

	macro gotoPageTemplateViaEdit {
		LexiconEntry.viewEntryName(rowEntry = "${pageTemplateName}");

		AssertClick.assertPartialTextClickAt(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
			value1 = "${pageTemplateName}",
			key_pageTemplateName = "${pageTemplateName}"
		);
	}

	macro tearDownCP {
		ProductMenu.gotoSite(site = "Global");

		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Pages"
		);

		var pageTemplateName = "Page Template Name";

		while (IsElementPresent(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS", rowEntry = "${pageTemplateName}")) {
			LexiconEntry.openEntryMenu(rowEntry = "${pageTemplateName}");

			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		AssertTextEquals.assertPartialText(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
			value1 = "${pageTemplateName}",
			key_pageTemplateName = "${pageTemplateName}"
		);
	}

	macro viewDefaultCP {
		for (var pageTemplateName : list "Search,Blog,Wiki") {
			LexiconEntry.viewEntryName(rowEntry = "${pageTemplateName}");

			AssertElementNotPresent(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER",
				key_rowNumber = "4"
			);
		}

		AssertElementNotPresent(locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME_4");
	}

	macro viewDefaultLocalizedCP {
		for (var pageTemplateNameTranslation : list "${pageTemplateNameList}") {
			var key_pageTemplateName = "${pageTemplateNameTranslation}";

			AssertTextEquals.assertPartialText(
				locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
				value1 = "${pageTemplateNameTranslation}"
			);
		}

		SiteAdmin.viewPortletTitle(portletTitle = "${portletTitle}");
	}

	macro viewDefaultPortletPG {
		if ("${pageTemplateName}" == "Blog") {
			AssertElementPresent(locator1 = "Home#PORTLET_1");

			AssertElementPresent(locator1 = "Home#PORTLET_2");

			AssertElementPresent(locator1 = "Home#PORTLET_3");

			for (var portletName : list "${portletTitleList}") {
				var key_portletName = "${portletName}";

				AssertTextEquals(
					locator1 = "Portlet#TITLE",
					value1 = "${portletName}"
				);
			}
		}
	}

	macro viewDescription {
		LexiconEntry.viewEntryName(rowEntry = "${pageTemplateName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Configure",
			rowEntry = "${pageTemplateName}"
		);

		AssertTextEquals.assertValue(
			locator1 = "TextInput#DESCRIPTION",
			value1 = "${pageTemplateDescription}"
		);
	}

	macro viewPG {
		var key_pageTemplateName = "${pageTemplateName}";

		AssertClick.assertPartialTextClickAt(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
			value1 = "${pageTemplateName}"
		);

		SelectWindow(locator1 = "title=${pageTemplateName} - ${siteName}");

		AssertTextEquals(locator1 = "Home#SITE_NAME", value1 = "${pageTemplateName}");
	}

	macro viewStatus {
		LexiconEntry.viewEntryName(rowEntry = "${pageTemplateName}");

		AssertTextEquals(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_ACTIVE",
			value1 = "${pageTemplateStatus}",
			key_pageTemplateName = "${pageTemplateName}"
		);
	}
}