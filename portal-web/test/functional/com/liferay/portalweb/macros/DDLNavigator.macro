definition {
	var ddlDataDefinitionName = "Data Definition Name";
	var ddlListName = "List Name";

	macro gotoAddListRecord {
		LexiconEntry.gotoAdd();

		SiteAdmin.viewPortletTitle(portletTitle = "New ${ddlDataDefinitionName}");
	}

	macro gotoDataDefinitionPermissions {
		DDLNavigator.gotoManageDataDefinitions();

		Type(
			locator1 = "NavBar#BASIC_SEARCH_FIELD",
			value1 = "${ddlDataDefinitionName}"
		);

		Click(locator1 = "Icon#BASIC_SEARCH");

		Click.waitForMenuToggleJSClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1"
		);

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Permissions",
			key_menuItem = "Permissions"
		);
	}

	macro gotoDDLRecordEditViaWorkflow {
		Click(locator1 = "WorkflowSubmissionsTask#PREVIEW_EDIT");

		SelectFrame(value1 = "relative=top");

		IFrame.selectDialogFrame();

		WorkflowAsset.viewDetails(
			userName = "${userName}",
			workflowStatus = "${workflowStatus}"
		);
	}

	macro gotoEdit {
		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);
	}

	macro gotoListRecords {
		var key_ddlListName = "${ddlListName}";

		AssertTextEquals(locator1 = "DDL#LIST_TABLE_NAME", value1 = "${ddlListName}");

		AssertClick(locator1 = "DDL#LIST_TABLE_NAME", value1 = "${ddlListName}");

		SiteAdmin.viewPortletTitle(portletTitle = "${ddlListName}");
	}

	macro gotoManageDataDefinitions {
		Pause(locator1 = "5000");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Manage Data Definitions");

		if (IsTextEqual(locator1 = "Portlet#HEADER", value1 = "Data Definitions")) {
			AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Data Definitions");
		}
		else {
			AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Structures");
		}
	}

	macro gotoManageTemplates {
		DDLNavigator.gotoManageDataDefinitions();

		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		Click.waitForMenuToggleJSClick(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS"
		);

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Manage Templates",
			key_menuItem = "Manage Templates"
		);
	}

	macro gotoOptionalConfiguration {
		SelectFrame(locator1 = "DDLConfiguration#CONFIGURATION_IFRAME");

		AssertClick(
			locator1 = "DDLConfiguration#NAVIGATION_OPTIONAL_CONFIGURATION",
			value1 = "Optional Configuration"
		);
	}

	macro gotoSpreadsheetView {
		AssertTextEquals(
			locator1 = "DDL#LIST_TABLE_NAME",
			value1 = "${ddlListName}",
			key_ddlListName = "${ddlListName}"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Spreadsheet View",
			rowEntry = "${ddlListName}"
		);
	}

	macro gotoView {
		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "View",
			key_menuItem = "View"
		);
	}
}