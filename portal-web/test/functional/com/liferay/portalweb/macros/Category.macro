definition {

	macro addCategoryLocalizationCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Category",
			rowEntry = "${vocabularyName}");

		PortletEntry.inputTitle(title = "${categoryName}");

		Click(
			key_localizationName = "${localizationNameKey}",
			locator1 = "Translation#LOCALIZATION_NAME");

		PortletEntry.inputTitle(title = "${localizationName}");

		if (isSet(localization2NameKey)) {
			Click(
				key_localizationName = "${localization2NameKey}",
				locator1 = "Translation#LOCALIZATION_NAME");

			PortletEntry.inputTitle(title = "${localization2Name}");
		}

		PortletEntry.save();
	}

	macro addCategoryPropertyCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Category",
			rowEntry = "${vocabularyName}");

		PortletEntry.inputTitle(title = "${categoryName}");

		Panel.expandPanel(panelHeading = "Properties");

		var i = "0";
		var pathNumber = "0";

		if ("${propertyNumber}" == "0") {
			var key_pathNumber = "${pathNumber}";

			Type(
				locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD",
				value1 = "${propertiesKeyField}");

			Type(
				locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD",
				value1 = "${propertiesValueField}");

			Click(locator1 = "Button#ADD_ROW_LAST");
		}
		else {
			while ("${i}" != "${propertyNumber}") {
				var i = MathUtil.sum("${i}", "1");
				var key_pathNumber = "${pathNumber}";
				var pathNumber = MathUtil.sum("${pathNumber}", "2");

				Type(
					locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD",
					value1 = "${propertiesKeyField} ${i}");

				Type(
					locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD",
					value1 = "${propertiesValueField} ${i}");

				Click(locator1 = "Button#ADD_ROW_LAST");
			}
		}

		PortletEntry.save();
	}

	macro addCategoryViewableByCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Category",
			rowEntry = "${vocabularyName}");

		PortletEntry.inputTitle(title = "${categoryName}");

		Panel.expandPanel(panelHeading = "Permissions");

		Select(
			locator1 = "Select#PERMISSIONS",
			value1 = "${viewableBy}");

		PortletEntry.save();
	}

	@summary = "Add a category named '${categoryName}' to the vocabulary named '${vocabularyName}'"
	macro addCP {
		var key_vocabularyName = "${vocabularyName}";

		if (IsElementPresent(locator1 = "Categories#VOCABULARY_HEADER")) {
			AssertClick(
				locator1 = "Categories#ADD_CATEGORY_BUTTON",
				value1 = "Add Category");
		}
		else {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Add Category",
				rowEntry = "${vocabularyName}");
		}

		PortletEntry.inputTitle(title = "${categoryName}");

		Click(locator1 = "TextArea#DESCRIPTION");

		if (isSet(categoryDescription)) {
			PortletEntry.inputDescription(description = "${categoryDescription}");
		}

		PortletEntry.save();
	}

	macro addWithNullTitleCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Add Category",
			rowEntry = "${vocabularyName}");

		PortletEntry.inputTitle(title = " ");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "Message#ERROR_FIELD_REQUIRED",
			value1 = "This field is required.");
	}

	macro deleteByIconCP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_CATEGORY",
			value1 = "${categoryName}");

		Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no categories.");

		AssertTextNotPresent(value1 = "${categoryName}");
	}

	macro deleteCategoriesCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no categories.");
	}

	macro deleteCategoryByMenuCP {
		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${categoryName}");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no categories.");

		AssertTextNotPresent(value1 = "${categoryName}");
	}

	macro deleteCategoryProperty2CP {
		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		Panel.expandPanel(panelHeading = "Properties");

		Click(
			key_rowIndexNumber = "2",
			locator1 = "Button#DELETE_ROW_N");

		AssertElementPresent(locator1 = "CategoriesEditCategory#PROPERTIES_UNDO_MESSAGE");

		PortletEntry.save();
	}

	macro deletePartialCategoryCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_2",
			value1 = "You do not have the required permissions.");
	}

	macro editCategoryPropertyCP {
		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		PortletEntry.inputTitle(title = "${categoryNameEdit}");

		Panel.expandPanel(panelHeading = "Properties");

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD_1",
			value1 = "${propertiesKeyFieldEdit}");

		Type(
			locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD_1",
			value1 = "${propertiesValueFieldEdit}");

		PortletEntry.save();
	}

	macro editCP {
		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		PortletEntry.inputTitle(title = "${categoryNameEdit}");

		PortletEntry.save();
	}

	macro moveViaEditCP {
		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Move",
			rowEntry = "${categoryName}");

		Select(
			locator1 = "CategoriesEditCategory#VOCABULARY_SELECT",
			value1 = "${vocabularyName2}");

		AssertClick(
			locator1 = "Button#MOVE",
			value1 = "Move");

		Alert.viewSuccessMessage();
	}

	macro searchCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		Type(
			locator1 = "TextInput#SEARCH",
			value1 = "${categorySearchEntry}");

		Click(locator1 = "Icon#BASIC_SEARCH");
	}

	macro viewCategoryPropertyCP {
		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${categoryName}");

		Panel.expandPanel(panelHeading = "Properties");

		var i = "0";
		var pathNumber = "0";

		if ("${propertyNumber}" == "0") {
			var key_pathNumber = "${pathNumber}";

			AssertTextEquals(
				locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD",
				value1 = "${propertiesKeyField}");

			AssertTextEquals(
				locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD",
				value1 = "${propertiesValueField}");
		}
		else {
			while ("${i}" != "${propertyNumber}") {
				var key_pathNumber = "${i}";
				var i = MathUtil.sum("${i}", "1");

				AssertTextEquals(
					locator1 = "CategoriesEditCategory#PROPERTIES_KEY_FIELD",
					value1 = "${propertiesKeyField} ${i}");

				AssertTextEquals(
					locator1 = "CategoriesEditCategory#PROPERTIES_VALUE_FIELD",
					value1 = "${propertiesValueField} ${i}");
			}
		}
	}

	macro viewCategoryToVocabularyCP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		AssertClick(
			locator1 = "Categories#VOCABULARY_CATEGORY",
			value1 = "${categoryName}");
	}

	macro viewCategoryViewableByCP {
		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${categoryName}");

		IFrame.selectDialogFrame();

		if ("${viewableBy}" == "Anyone (Guest Role)") {
			AssertChecked(locator1 = "AssetPermissions#GUEST_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Owner") {
			AssertChecked(locator1 = "AssetPermissions#OWNER_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Site Members") {
			AssertChecked(locator1 = "AssetPermissions#SITE_MEMBER_VIEW_CHECKBOX");
		}

		SelectFrameTop(value1 = "relative=top");
	}

	@summary = "Assert that the category named '${categoryName}' has been added to the vocabulary named '${vocabularyName}'"
	macro viewCP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_CATEGORY",
			value1 = "${categoryName}");
	}

	macro viewFilterViaCategoriesNavigationPG {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "CategoriesNavigation#VOCABULARY_NAME",
			value1 = "${vocabularyName}");

		AssertClick(
			locator1 = "CategoriesNavigation#CATEGORY_NAME",
			value1 = "${categoryName}");

		AssertTextEquals.assertPartialText(
			locator1 = "AP#CATEGORIES_FILTER_CATEGORY",
			value1 = "${categoryName}");
	}

	macro viewNavigationCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(
			locator1 = "Categories#VOCABULARY_ENTRY_LINK",
			value1 = "${vocabularyName}");

		var key_categoryName = "${categoryName}";

		Click(locator1 = "Categories#CATEGORY_ENTRY_LINK");

		SiteAdmin.viewPortletTitle(portletTitle = "${categoryName}");

		Click(locator1 = "Icon#BACK");

		SiteAdmin.viewPortletTitle(portletTitle = "${vocabularyName}");
	}

	macro viewRemoveFilterViaAP {
		var key_categoryName = "${categoryName}";
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "CategoriesNavigation#VOCABULARY_NAME",
			value1 = "${vocabularyName}");

		Click(locator1 = "AP#CATEGORIES_FILTER_CATEGORY_REMOVE");

		AssertElementNotPresent(locator1 = "AP#CATEGORIES_FILTER_CATEGORY");
	}

	macro viewSearchResultsCP {
		if (isSet(categoryNameList)) {
			for (var categoryName : list "${categoryNameList}") {
				var key_categoryName = "${categoryName}";

				AssertTextEquals(
					locator1 = "Categories#CATEGORY_ENTRY",
					value1 = "${categoryName}");
			}
		}

		if (isSet(categoryDescriptionList)) {
			for (var categoryDescription : list "${categoryDescriptionList}") {
				var key_categoryDescription = "${categoryDescription}";

				AssertTextEquals(
					locator1 = "Categories#CATEGORY_ENTRY_DESCRIPTION",
					value1 = "${categoryDescription}");
			}
		}

		if ("${nullResult}" == "true") {
			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "There are no categories.");
		}
	}

}