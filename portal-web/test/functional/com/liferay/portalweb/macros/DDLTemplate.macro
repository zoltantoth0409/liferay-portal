definition {

	var ddlDataDefinitionDescription = "Data Definition Description";
	var ddlDataDefinitionName = "Data Definition Name";
	var ddlListName = "List Name";

	macro _showDetails {
		if (IsElementNotPresent.isNotVisible(locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT")) {
			Click(locator1 = "DDMEditTemplate#DETAILS_TOGGLE");

			if (IsElementNotPresent.isNotVisible(locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT")) {
				Click(locator1 = "DDMEditTemplate#DETAILS_TOGGLE");
			}
		}

		AssertVisible(locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT");
	}

	macro addDisplayTemplateCP {
		DDLNavigator.gotoManageDataDefinitions();

		Click(locator1 = "TextInput#SEARCH");

		Search.searchCP(searchTerm = "${ddlDataDefinitionName}");

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Manage Templates");

		AssertElementPresent.assertVisible(
			locator1 = "Portlet#HEADER",
			value1 = "Templates for Structure: ${ddlDataDefinitionName}");

		var key_assetTitle = "Templates";

		AssertElementPresent.assertVisible(
			locator1 = "NavBar#ASSET_TITLE",
			value1 = "Templates");

		LexiconEntry.gotoAddMenuItem(menuItem = "Add Display Template");

		Click(locator1 = "TextInput#NAME");

		PortletEntry.inputName(name = "${ddlDisplayTemplateName}");

		DDLTemplate._showDetails();

		Select(
			locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
			value1 = "${ddlDisplayTemplateLanguage}");

		Pause(locator1 = "5000");

		if (isSet(ddlDisplayTemplateTextFieldScript)) {
			AssertClick(
				locator1 = "DDMEditTemplate#DATA_LIST_RECORD_FIELDS_TEXT",
				value1 = "${ddlDisplayTemplateTextFieldScript}");
		}

		if (isSet(ddlDisplayTemplateScript)) {
			Type.typeAceEditor(
				locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD_TEXT_AREA",
				value1 = "${ddlDisplayTemplateScript}");
		}

		if (isSet(ddlDisplayTemplateScriptFile)) {
			UploadCommonFile(
				locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
				value1 = "${ddlDisplayTemplateScriptFile}");
		}

		Pause(locator1 = "3000");

		SelectFrameTop();

		ScrollBy(value1 = "0, 400");

		PortletEntry.save();
	}

	macro addDisplayTemplateTemplateViaDDLDisplayPG {
		Click(locator1 = "DDL#PORTLET_ACTIONS_ADD_DISPLAY_TEMPLATE");

		SelectFrame(locator1 = "IFrame#DIALOG");

		PortletEntry.inputName(name = "${ddlDisplayTemplateName}");

		DDLTemplate._showDetails();

		Select(
			locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
			value1 = "${ddlDisplayTemplateLanguage}");

		Pause(locator1 = "5000");

		Type.typeAceEditor(
			locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD_TEXT_AREA",
			value1 = "${ddlDisplayTemplateScript}");

		Button.clickSave();

		SelectFrame(value1 = "relative=top");
	}

	macro addFormTemplateCP {
		DDLNavigator.gotoManageDataDefinitions();

		Search.searchCP(searchTerm = "${ddlDataDefinitionName}");

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Manage Templates");

		AssertElementPresent.assertVisible(
			locator1 = "Portlet#HEADER",
			value1 = "Templates for Structure: Data Definition");

		var key_assetTitle = "Templates";

		AssertElementPresent.assertVisible(
			locator1 = "NavBar#ASSET_TITLE",
			value1 = "Templates");

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "Add Form Template");

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "${ddlFormTemplateName}");
	}

	macro addFormTemplateViaBackButton {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		Click(locator1 = "Icon#ANGLE_LEFT_BACK");

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "Add Form Template");

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "${ddlFormTemplateName}");
	}

	macro addFormTemplateViaDDLDisplayPG {
		Click(locator1 = "DDL#PORTLET_ACTIONS_ADD_FORM_TEMPLATE");

		SelectFrame(locator1 = "IFrame#DIALOG");

		PortletEntry.inputName(name = "${ddlFormTemplateName}");
	}

	macro deleteTemplateCP {
		DDLNavigator.gotoManageDataDefinitions();

		Search.searchCP(searchTerm = "${ddlDataDefinitionName}");

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Manage Templates");

		Search.searchCP(searchTerm = "${ddlTemplateName}");

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#TIMES");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists");

		DDLNavigator.gotoManageDataDefinitions();

		Search.searchCP(searchTerm = "${ddlDataDefinitionName}");

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Manage Templates");

		Portlet.viewEmptyMessage(message = "There are no templates.");
	}

	macro editDisplayTemplateCP {
		DDLNavigator.gotoManageDataDefinitions();

		Search.searchCP(searchTerm = "${ddlDataDefinitionName}");

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Manage Templates");

		var key_ddlTemplateName = "${ddlDisplayTemplateName}";

		Click(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		Pause(locator1 = "1000");

		PortletEntry.inputName(name = "${ddlDisplayTemplateNameEdit}");

		DDLTemplate._showDetails();

		Select(
			locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
			value1 = "${ddlDisplayTemplateLanguage}");

		AssertClick(
			locator1 = "DDMEditTemplate#DATA_LIST_RECORD_FIELDS_TEXT",
			value1 = "${ddlDisplayTemplateTextFieldScriptEdit}");

		PortletEntry.save();
	}

	macro editFormTemplateCP {
		DDLNavigator.gotoManageDataDefinitions();

		Search.searchCP(searchTerm = "${ddlDataDefinitionName}");

		Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Manage Templates");

		Search.searchCP(searchTerm = "${ddlFormTemplateName}");

		Click(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS_1");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.inputName(name = "${ddlFormTemplateNameEdit}");
	}

	macro editFormTemplateModeCP {
		if (IsElementNotPresent.isNotVisible(locator1 = "DDMEditStructure#DETAILS_MODE_SELECT")) {
			Click(locator1 = "DDMEditTemplate#DETAILS_TOGGLE");
		}

		Pause(locator1 = "3000");

		Select(
			locator1 = "DDMEditStructure#DETAILS_MODE_SELECT",
			value1 = "${ddlFormTemplateMode}");

		Pause(locator1 = "3000");
	}

	macro saveAndContinueFormTemplateCP {
		AssertClick(
			locator1 = "Button#SAVE_AND_CONTINUE",
			value1 = "Save and Continue");

		Alert.viewSuccessMessage();
	}

	macro saveFormTemplateViaDDLDisplayPG {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		Button.clickSave();

		SelectFrame(value1 = "relative=top");
	}

	macro selectDisplayTemplateViaDDLDisplayPG {
		DDLNavigator.gotoOptionalConfiguration();

		Select(
			locator1 = "DDLConfiguration#TEMPLATES_DISPLAY_TEMPLATE_SELECT",
			value1 = "${ddlDisplayTemplateName}");

		PortletEntry.save();

		AssertClick(
			locator1 = "DDLConfiguration#NAVIGATION_OPTIONAL_CONFIGURATION",
			value1 = "Optional Configuration");

		AssertSelectedLabel(
			locator1 = "DDLConfiguration#TEMPLATES_DISPLAY_TEMPLATE_SELECT",
			value1 = "${ddlDisplayTemplateName}");

		SelectFrameTop();
	}

	macro selectFormTemplateViaDDLDisplayPG {
		DDLNavigator.gotoOptionalConfiguration();

		Select(
			locator1 = "DDLConfiguration#TEMPLATES_FORM_TEMPLATE_SELECT",
			value1 = "${ddlFormTemplateName}");

		PortletEntry.save();

		AssertClick(
			locator1 = "DDLConfiguration#NAVIGATION_OPTIONAL_CONFIGURATION",
			value1 = "Optional Configuration");

		AssertSelectedLabel(
			locator1 = "DDLConfiguration#TEMPLATES_FORM_TEMPLATE_SELECT",
			value1 = "${ddlFormTemplateName}");

		SelectFrameTop();
	}

	macro tearDownTemplateCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists");

		DDLNavigator.gotoManageDataDefinitions();

		if (!(isSet(ddlDataDefinitionName))) {
			var ddlDataDefinitionName = "Data Definition";
		}

		var key_ddlDataDefinitionName = "${ddlDataDefinitionName}";

		if (IsElementPresent(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS")) {
			Click.waitForMenuToggleJSClick(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");

			MenuItem.click(menuItem = "Manage Templates");

			if (IsElementPresent(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS_1")) {
				Check.checkAll(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ALL_CHECKBOX");

				ClickNoError(locator1 = "Icon#TIMES");

				AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

				Alert.viewSuccessMessage();
			}
		}
	}

	macro viewDisplayTemplateCP {
		DDLNavigator.gotoManageTemplates(ddlDataDefinitionName = "${ddlDataDefinitionName}");

		var key_ddlTemplateName = "${ddlTemplateName}";

		ScrollWebElementIntoView(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS");

		Click(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "${ddlTemplateName} (${ddlDataDefinitionName})");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${ddlTemplateName}");
	}

	macro viewFormTemplateCP {
		DDLNavigator.gotoManageTemplates(ddlDataDefinitionName = "${ddlDataDefinitionName}");

		var key_ddlTemplateName = "${ddlTemplateName}";

		AssertClick(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME_LINK",
			value1 = "${ddlTemplateName}");

		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#HEADER",
			value1 = "${ddlTemplateName} (${ddlDataDefinitionName})");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${ddlTemplateName}");
	}

	macro viewNoDuplicateTemplateCP {
		DDLNavigator.gotoManageTemplates(ddlDataDefinitionName = "${ddlDataDefinitionName}");

		var key_ddlTemplateName = "${ddlTemplateName}";

		AssertTextPresent(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME_LINK",
			value1 = "${ddlTemplateName}");

		AssertElementNotPresent(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DUPLICATE_TEMPLATE_NAME");
	}

	macro viewNoTemplateCP {
		DDLNavigator.gotoManageTemplates(ddlDataDefinitionName = "${ddlDataDefinitionName}");

		var key_ddlTemplateName = "${ddlTemplateName}";

		AssertElementNotPresent(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME_LINK");
	}

}