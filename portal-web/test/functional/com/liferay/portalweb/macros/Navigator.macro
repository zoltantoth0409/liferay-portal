definition {

	macro _gotoPage {
		var key_pageName = "${pageName}";
		var count = "0";

		while (("${count}" != "3") && (IsElementNotPresent(locator1 = "Home#PAGE_ACTIVE"))) {
			AssertClick(
				locator1 = "Home#PAGE",
				value1 = "${pageName}");

			var count = MathUtil.sum("${count}", "1");
		}

		AssertVisible(locator1 = "Home#PAGE_ACTIVE");
	}

	macro _isLocalizedURL {
		var currentURL = Navigator.getCurrentURL();

		var portalURL = PropsUtil.get("portal.url");
		var isLocalizedURL = "false";

		if (contains("${currentURL}", "${portalURL}/${locale}")) {
			var isLocalizedURL = "true";
		}

		return "${isLocalizedURL}";
	}

	macro closeSpecificWindow {
		Navigator.selectSpecificWindow(windowTitle = "${closeWindowTitle}");

		Close.closeAndSelectWindow(locator1 = "title=${selectWindowTitle}");
	}

	macro closeWindow {
		Close.closeWindow();
	}

	macro ensureLocaleURL {
		var isLocalizedURL = Navigator._isLocalizedURL(locale = "${locale}");

		if ("${isLocalizedURL}" == "false") {
			var currentURL = Navigator.getCurrentURL();

			var portalURL = PropsUtil.get("portal.url");

			Navigator.openSpecificURL(url = StringUtil.replace("${currentURL}", "${portalURL}", "${portalURL}/${locale}"));
		}
	}

	macro getCurrentURL {
		var currentURL = selenium.getLocation();

		return "${currentURL}";
	}

	macro gotoBack {
		Click(locator1 = "Icon#BACK");
	}

	macro gotoEditPageViaControlMenu {
		Click(locator1 = "ControlMenu#EDIT_COG");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "${pageName}");
	}

	macro gotoNavItem {
		var key_navItem = "${navItem}";

		AssertClick(
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "${navItem}");

		AssertElementPresent(locator1 = "NavBar#ACTIVE_NAV_ITEM_LINK");
	}

	macro gotoNavTab {
		var key_tab = "${navTab}";

		AssertClick(
			locator1 = "NavTab#TAB_LINK",
			value1 = "${navTab}");

		AssertElementPresent(locator1 = "NavTab#ACTIVE_TAB_LINK");
	}

	@summary = "Navigate to the page named '${pageName}'"
	macro gotoPage {
		Navigator.openURL();

		if (isSet(sitePagePrivacy)) {
			ProductMenu.gotoPrivatePages();
		}

		Pause(locator1 = "3000");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoPrivateSitePage {
		Navigator.openPrivateSiteURL(siteName = "${siteName}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	@summary = "Navigate to the page named '${pageName}'"
	macro gotoPrivateStagedSitePage {
		Navigator.openPrivateStagedSiteURL(siteNameURL = "${siteName}");

		ProductMenu.gotoPrivatePages();

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoSEOPanel {
		Navigator.gotoNavTab(navTab = "SEO");

		Panel.expandPanel(panelHeading = "SEO");
	}

	macro gotoSitePage {
		Navigator.openSiteURL(
			baseURL = "${baseURL}",
			siteName = "${siteName}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoSpecificURLPage {
		Navigator.openSpecificURL(url = "${url}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	@summary = "Navigate to the page named '${pageName}'"
	macro gotoStagedSitePage {
		Navigator.openStagingSiteURL(siteName = "${siteName}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoStagedView {
		ProductMenuHelper.openProductMenu();

		Click(locator1 = "Link#STAGING");
	}

	macro openNodePort {
		Open(locator1 = "http://localhost:${nodePort}");
	}

	macro openPrivateSitePageURL {
		var siteNameURL = StringUtil.replace("${siteName}", " ", "-");
		var siteNameURL = StringUtil.lowerCase("${siteNameURL}");
		var pageNameURL = StringUtil.replace("${pageName}", " ", "-");
		var pageNameURL = StringUtil.lowerCase("${pageNameURL}");

		Open(locator1 = "/group/${siteNameURL}/${pageNameURL}");
	}

	macro openPrivateSiteURL {
		var siteNameURL = StringUtil.replace("${siteName}", " ", "-");
		var siteNameURL = StringUtil.lowerCase("${siteNameURL}");

		Open(locator1 = "/group/${siteNameURL}");
	}

	macro openPrivateStagedSiteURL {
		var siteNameURL = StringUtil.replace("${siteNameURL}", " ", "-");
		var siteNameURL = StringUtil.lowerCase("${siteNameURL}");

		Open(locator1 = "/group/${siteNameURL}-staging");
	}

	macro openPrivateURL {
		Open(locator1 = "/group/guest");
	}

	macro openSiteLanding {
		var siteNameURL = StringUtil.replace("${siteURLKey}", " ", "-");
		var siteURLKey = StringUtil.lowerCase("${siteNameURL}");

		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_journal_web_portlet_JournalPortlet");
	}

	macro openSiteURL {
		var siteNameURL = StringUtil.replace("${siteName}", " ", "-");
		var siteNameURL = StringUtil.lowerCase("${siteNameURL}");
		var baseURL = "${baseURL}";

		if (contains("${baseURL}", "{baseURL}")) {
			var baseURL = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseURL}/web/${siteNameURL}");
	}

	macro openSpecificURL {
		Open(locator1 = "${url}");
	}

	macro openStagingSiteURL {
		var siteNameURL = StringUtil.replace("${siteName}", " ", "-");
		var siteNameURL = StringUtil.lowerCase("${siteNameURL}");

		Open(locator1 = "/web/${siteNameURL}-staging");
	}

	macro openURL {
		Open(locator1 = "/web/guest/home");
	}

	macro openWithAppendToBaseURL {
		var baseURL = "${baseURL}";

		if (contains("${baseURL}", "{baseURL}")) {
			var baseURL = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseURL}/${urlAppend}");
	}

	macro selectSpecificWindow {
		SelectWindow(locator1 = "title=${windowTitle}");
	}

}