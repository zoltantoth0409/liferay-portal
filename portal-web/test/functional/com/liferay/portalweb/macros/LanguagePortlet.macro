definition {

	macro configureViewCurrentLanguages {
		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		Panel.expandPanel(panel = "Languages");

		for (var currentLanguage : list "${currentLanguages}") {
			AssertTextEquals.assertPartialText(
				locator1 = "LanguageConfiguration#LANGUAGES_CURRENT",
				value1 = "${currentLanguage}"
			);
		}

		SelectFrame(value1 = "relative=top");
	}

	macro moveLanguagesToAvailable {
		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		Panel.expandPanel(panel = "Languages");

		while (IsElementPresent(locator1 = "LanguageConfiguration#LANGUAGES_CURRENT_SELECT")) {
			Click(locator1 = "LanguageConfiguration#LANGUAGES_CURRENT_SELECT");

			Click(locator1 = "Button#MOVE_CURRENT_TO_AVAILABLE");
		}
	}

	macro moveLanguagesToCurrent {
		for (var currentLanguage : list "${currentLanguages}") {
			var key_currentLanguage = "${currentLanguage}";

			Select(
				locator1 = "LanguageConfiguration#LANGUAGES_AVAILABLE",
				value1 = "${currentLanguage}"
			);

			Click(locator1 = "Button#MOVE_AVAILABLE_TO_CURRENT");

			AssertTextEquals.assertPartialText(
				locator1 = "LanguageConfiguration#LANGUAGES_CURRENT",
				value1 = "${currentLanguage}"
			);
		}

		PortletEntry.save();

		SelectFrame(value1 = "relative=top");
	}

	macro selectLanguagePG {
		var portletName = "${portletName}";

		if (contains("${portletName}", "{portletName}")) {
			var portletName = "LanguagePortlet";
		}

		if (IsTextNotEqual(locator1 = "Language#DROPDOWN_LANGUAGE", value1 = "${locale}", key_portletName = "${portletName}")) {
			Click(locator1 = "Language#DROPDOWN", key_portletName = "${portletName}");

			MenuItem.click(menuItem = "${textIcon}");
		}
	}

	macro tearDownLanguagePG {
		LanguagePortlet.selectLanguagePG(
			locale = "en_US",
			textIcon = "English-United States"
		);
	}

	macro toggleDisplayTemplate {
		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		Panel.expandPanel(panel = "Languages");

		Select(locator1 = "Select#DISPLAY_TEMPLATE", value1 = "${displayStyle}");

		Button.clickSave();

		SelectFrame(value1 = "relative=top");
	}

	macro viewDisplayTemplateLongText {
		var key_language = "${language}";

		AssertTextEquals.assertPartialText(
			locator1 = "Language#LONG_TEXT_LINK",
			value1 = "${language}"
		);
	}

	macro viewDisplayTemplateSelectBox {
		AssertTextEquals.assertPartialText(
			locator1 = "Language#LANGUAGE_MENU_SELECT",
			value1 = "${language}"
		);
	}

	macro viewDisplayTemplateShortText {
		var key_languageKey = "${languageKey}";

		AssertTextEquals.assertPartialText(
			locator1 = "Language#SHORT_TEXT_LINK",
			value1 = "${languageKey}"
		);
	}

	macro viewLanguageCurrent {
		AssertElementPresent(
			locator1 = "Language#DROPDOWN_CURRENT_LANGUAGE",
			key_locale = "${locale}"
		);
	}

	macro viewLanguagesAbsent {
		Click(locator1 = "Language#DROPDOWN", key_portletName = "LanguagePortlet");

		for (var missingLocale : list "${missingLocales}") {
			MenuItem.viewNotPresent(menuItem = "${missingLocale}");
		}

		Click(locator1 = "Language#DROPDOWN", key_portletName = "LanguagePortlet");
	}

	macro viewLanguagesPresent {
		Click(locator1 = "Language#DROPDOWN", key_portletName = "LanguagePortlet");

		for (var currentLocale : list "${currentLocales}") {
			MenuItem.viewPresent(menuItem = "${currentLocale}");
		}

		Click(locator1 = "Language#DROPDOWN", key_portletName = "LanguagePortlet");
	}

}