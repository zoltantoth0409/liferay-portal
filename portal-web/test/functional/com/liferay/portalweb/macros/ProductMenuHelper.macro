definition {
	macro addFirstPage {
		ClickNoError.waitForMenuToggleJSClick(locator1 = "SitePages#PAGES_ELLIPSIS");

		if ("${sitePagePrivacy}" == "Private") {
			AssertClick(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Add Private Page",
				key_menuItem = "Add Private Page"
			);
		}
		else {
			AssertClick(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Add Public Page",
				key_menuItem = "Add Public Page"
			);
		}
	}

	macro closeProductMenu {
		if (IsElementPresent(locator1 = "ProductMenu#PRODUCT_MENU_OPENED")) {
			Click(locator1 = "ProductMenu#TOGGLE");
		}

		AssertElementPresent(locator1 = "ProductMenu#PRODUCT_MENU_CLOSED");
	}

	macro expandCategory {
		var key_category = "${category}";

		if (IsElementPresent(locator1 = "ProductMenu#CATEGORY_COLLAPSED")) {
			Click(locator1 = "ProductMenu#CATEGORY_COLLAPSED");
		}

		AssertElementPresent(locator1 = "ProductMenu#CATEGORY_EXPANDED");
	}

	macro expandPageTree {
		while ((IsElementPresent(locator1 = "ProductMenu#PAGE_PLUS_ICON_COLLAPSED")) && (IsVisible(locator1 = "ProductMenu#PAGE_PLUS_ICON_COLLAPSED"))) {
			Click(locator1 = "ProductMenu#PAGE_PLUS_ICON_COLLAPSED");
		}

		AssertElementNotPresent(locator1 = "ProductMenu#PAGE_PLUS_ICON_COLLAPSED");
	}

	macro expandPanel {
		ProductMenuHelper.openProductMenu();

		var key_panel = StringUtil.toLowerCase("${panel}");
		var key_panel = StringUtil.replace("${key_panel}", " ", "_");

		if (IsElementPresent(locator1 = "ProductMenu#PANEL_COLLAPSED", key_panel = "${key_panel}")) {
			Click(locator1 = "ProductMenu#PANEL_COLLAPSED", key_panel = "${key_panel}");
		}

		AssertElementPresent(
			locator1 = "ProductMenu#PANEL_EXPANDED",
			key_panel = "${key_panel}"
		);
	}

	macro gotoPortlet {
		AssertClick.assertPartialTextClickAt(
			locator1 = "ProductMenu#PORTLET",
			value1 = "${portlet}",
			key_portlet = "${portlet}"
		);
	}

	macro openProductMenu {
		if (IsElementNotPresent(locator1 = "ProductMenu#PRODUCT_MENU_OPENED")) {
			Click(locator1 = "ProductMenu#TOGGLE");
		}

		AssertElementPresent(locator1 = "ProductMenu#PRODUCT_MENU_OPENED");

		AssertVisible(locator1 = "ProductMenu#PRODUCT_MENU_BODY");
	}

	macro openSitesPopup {
		if (IsElementNotPresent(locator1 = "Portlet#H3_MODAL_TITLE")) {
			Click(locator1 = "ProductMenu#SITES_COMPASS");

			AssertVisible(locator1 = "Portlet#H3_MODAL_TITLE");
		}
	}

	macro viewNoSite {
		var key_site = "${site}";

		AssertElementNotPresent(locator1 = "ProductMenu#SITE");
	}
}