definition {

	macro rateStar {
		Click(locator1 = "Ratings#YOUR_RATING_STAR");

		Click.clickNoMouseOver(
			key_ratingStarCount = "${ratingStarCount}",
			locator1 = "Ratings#YOUR_RATING_STAR_COUNT");

		AssertElementPresent(
			key_ratingStarCount = "${ratingStarCount}",
			locator1 = "Ratings#YOUR_RATING_STAR_RESULT");
	}

	macro rateThumb {
		if (isSet(commentRating)) {
			Click.clickNoMouseOver(
				key_ratingResult = "${ratingResult}",
				locator1 = "Ratings#COMMENT_RATING_THUMB");
		}
		else {
			Click(
				key_ratingResult = "${ratingResult}",
				locator1 = "Ratings#THUMB");
		}
	}

	macro viewRateStar {
		AssertElementPresent(
			key_ratingStarCount = "${ratingStarCount}",
			locator1 = "Ratings#YOUR_RATING_STAR_RESULT");

		AssertTextEquals(
			key_ratingStarCount = "${ratingStarCount}",
			locator1 = "Ratings#YOUR_RATING_STAR_RESULT_COUNT",
			value1 = "${ratingStarCount}");

		AssertTextEquals.assertPartialText(
			locator1 = "Ratings#AVERAGE_STAR_RESULT",
			value1 = "${averageRatingStarCount}");
	}

	macro viewRateStarReadOnly {
		AssertElementNotPresent(locator1 = "Ratings#YOUR_RATING_STAR_EMPTY");

		AssertElementPresent(locator1 = "Ratings#YOUR_RATING_STAR_DISABLE");
		AssertElementPresent(locator1 = "Ratings#AVERAGE_STAR");
	}

	macro viewRateThumb {
		if (isSet(ratingResult)) {
			if (isSet(commentRating)) {
				AssertTextEquals(
					key_ratingResult = "${ratingResult}",
					locator1 = "Ratings#COMMENT_RATING_THUMB",
					value1 = "${voteCount}");
			}
			else {
				AssertTextEquals(
					key_ratingResult = "${ratingResult}",
					locator1 = "Ratings#THUMB",
					value1 = "${voteCount}");
			}
		}
	}

	macro viewRateThumbReadOnly {
		AssertElementNotPresent(
			key_ratingResult = "Rate this as good",
			locator1 = "Ratings#THUMB");

		AssertElementNotPresent(
			key_ratingResult = "Rate this as bad",
			locator1 = "Ratings#THUMB");

		AssertElementPresent(locator1 = "Ratings#THUMB_UP_OFF");
		AssertElementPresent(locator1 = "Ratings#THUMB_DOWN_OFF");
	}

}