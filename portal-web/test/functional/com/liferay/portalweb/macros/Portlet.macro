definition {

	macro _clickAddPortlet {
		AssertClick.pauseMakeVisibleAssertTextEqualsClickAt(
			locator1 = "ControlMenuAddPanel#ADD_PORTLET_LINK",
			value1 = "Add");
	}

	macro addDuplicatePG {
		var portletTypeKey = "PORTLET";

		if ("${portletName}" == "Map") {
			var portletName = "Where are the Liferay members?";
		}
		else if ("${portletName}" == "Requests") {
			var portletTypeKey = "PORTLET_BORDERLESS";
		}

		if (!(isSet(portletCount))) {
			var portletCount = "1";
		}

		var i = "0";

		while ("${i}" != "${portletCount}") {
			var i = ${i} + 1;

			Refresh();

			Portlet.addPGCmd(portletName = "${portletName}");
		}

		AssertElementPresent(
			key_portletName = "${portletName}",
			locator1 = "Home#${portletTypeKey}");
	}

	@summary = "Add a '${portletName}' portlet"
	macro addPG {
		if ("${portletName}" == "Map") {
			var portletName = "Where are the Liferay members?";
		}

		Portlet.addPGCmd(portletName = "${portletName}");

		var i = "1";
		var key_portletName = "${portletName}";

		while (!((IsElementPresent(locator1 = "Home#PORTLET")) || ("${i}" == "3"))) {
			var i = ${i} + 1;

			Pause(locator1 = "30000");

			echo("The PORTLET could not be found. Refreshing the page. Attempt number ${i}...");

			Refresh();

			Portlet.addPGCmd(portletName = "${portletName}");
		}

		AssertElementPresent(locator1 = "Home#PORTLET");
	}

	macro addPGCmd {
		if ("${portletName}" == "Where are the Liferay members?") {
			var portletName = "Map";
		}

		if (IsNotVisible(locator1 = "ControlMenuAddPanel#CLOSE_ADD_PANEL")) {
			var browserType = PropsUtil.get("browser.type");

			if ("${browserType}" == "safari") {
				Pause(locator1 = "10000");

				Click.clickAt(locator1 = "ControlMenu#ADD");
			}
			else {
				Click.clickAt(locator1 = "ControlMenu#ADD");
			}
		}

		AssertElementPresent(locator1 = "ControlMenuAddPanel#APPLICATIONS_ACCORDION_PANEL_TITLE");

		AssertVisible(
			locator1 = "ControlMenuAddPanel#APPLICATIONS_ACCORDION_PANEL_TITLE",
			value1 = "Applications");

		Pause(locator1 = "5000");

		Panel.expandSidebarApplicationsPanel();

		Type.sendKeysApplicationSearch(
			locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
			value1 = "${portletName}");

		Portlet._clickAddPortlet(portletName = "${portletName}");

		AssertElementPresent(
			key_portletName = "${portletName}",
			locator1 = "Portlet#LOADED");
	}

	macro assertServerShutdown {
		var shutdownPauseCount = "0";

		while ((IsElementPresent(locator1 = "//body")) && ("${shutdownPauseCount}" != "12")) {
			Refresh();

			Pause(locator1 = "10000");

			var shutdownPauseCount = ${shutdownPauseCount} + 1;

			echo("Shutdown pause count: ${shutdownPauseCount}");
		}
	}

	macro assertServerStartup {
		var startupPauseCount = "0";

		while ((!(IsElementPresent(locator1 = "UserBar#USER_SIGN_IN"))) && ("${startupPauseCount}" != "12")) {
			Refresh();

			Pause(locator1 = "10000");

			var startupPauseCount = ${startupPauseCount} + 1;

			echo("Startup pause count: ${startupPauseCount}");
		}
	}

	macro changeDisplayStyle {
		var displayStyle = StringUtil.upperCase("${displayStyle}");

		if (IsElementNotPresent(locator1 = "Icon#DISPLAY_STYLE_${displayStyle}_ACTIVE")) {
			Click(locator1 = "Icon#DISPLAY_STYLE_${displayStyle}");
		}

		AssertElementPresent(locator1 = "Icon#DISPLAY_STYLE_${displayStyle}_ACTIVE");
	}

	macro clickPortletPlusIconPG {
		Click.makeVisibleClickAt(
			key_portletName = "${portletName}",
			locator1 = "Portlet#ICON_PLUS_SIGN");
	}

	macro configureCustomStylesViaLookandfeelPG {
		Navigator.gotoNavTab(navTab = "Text Styles");

		Select(
			locator1 = "Portlet#TEXT_FONT_SELECT",
			value1 = "Verdana");

		Select(
			locator1 = "Portlet#TEXT_FONT_SIZE_SELECT",
			value1 = "2em");

		Navigator.gotoNavTab(navTab = "Border Styles");

		Type(
			locator1 = "Portlet#BORDER_WIDTH_TOP_INPUT",
			value1 = "20");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");
	}

	macro configureCustomTitleViaLookandfeelPG {
		Check.checkToggleSwitch(locator1 = "ToggleSwitch#USE_CUSTOM_TITLE");

		if (isSet(customPortletTitle)) {
			Type(
				locator1 = "Portlet#CONFIGURATION_CUSTOM_TITLE_FIELD",
				value1 = "${customPortletTitle}");
		}
		else {
			Type(
				locator1 = "Portlet#CONFIGURATION_CUSTOM_TITLE_FIELD",
				value1 = "${portletName} Custom");
		}

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#SUCCESS",
			value1 = "Your request processed successfully.");
	}

	macro deletePG {
		Click.makeVisibleClickAt(
			key_portletName = "${portletName}",
			locator1 = "Portlet#SPECIFIC_ELLIPSIS_ICON");

		MenuItem.clickNoError(menuItem = "Remove");

		Confirm(value1 = "Are you sure you want to remove this component?");
	}

	@summary = "Drag and drop the '${portletName}' portlet from column ${columnNumberFrom} to column ${columnNumberTo}"
	macro dragAndDropPortletToColumnPG {
		var key_portletTitleName = "${portletName}";

		AssertTextEquals(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberFrom}_PORTLET_TITLE",
			value1 = "${portletName}");

		DragAndDrop.dragAndDropPortletToObject(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberFrom}_PORTLET_HEADER",
			locator2 = "Home#PAGE_COLUMN_${columnNumberTo}",
			value1 = "${portletName}");

		AssertTextEquals(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberTo}_PORTLET_TITLE",
			value1 = "${portletName}");
	}

	macro expandTree {
		while (IsElementPresent(locator1 = "Treeview#GLYPHICON_PLUS")) {
			Click(locator1 = "Treeview#GLYPHICON_PLUS");
		}

		AssertElementNotPresent(locator1 = "Treeview#GLYPHICON_PLUS");
	}

	macro gotoPortletOptions {
		if (isSet(portletName)) {
			var key_portletName = "${portletName}";

			Click.makeVisibleClickAt(locator1 = "Portlet#SPECIFIC_BORDERLESS_ELLIPSIS_ICON");
		}
		else {
			Click.makeVisibleClickAt(locator1 = "Portlet#OPTIONS_ICON");
		}

		if (isSet(portletOption)) {
			if (IsElementPresent(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "${portletOption}")) {
				AssertClick(
					key_menuItem = "${portletOption}",
					locator1 = "MenuItem#ANY_MENU_ITEM",
					value1 = "${portletOption}");
			}
			else {
				LexiconEntry.gotoEllipsisMenuItem(menuItem = "${portletOption}");
			}
		}
	}

	macro gotoSpecificPortletOptions {
		if (("${portletName}" == "Web Content Display") && (isSet(order))) {
			var key_order = "${order}";

			Click.makeVisibleClickAt(locator1 = "WCD#SPECIFIC_BORDERLESS_ELLIPSIS_ICON");
		}
		else if (("${portletName}" == "Asset Publisher") && (isSet(order))) {
			var key_order = "${order}";

			Click.makeVisibleClickAt(locator1 = "AP#SPECIFIC_BORDERLESS_ELLIPSIS_ICON");
		}

		if (isSet(portletOption)) {
			MenuItem.click(menuItem = "${portletOption}");
		}
	}

	macro selectApplicationDecorator {
		Select(
			locator1 = "Select#APPLICATION_DECORATORS",
			value1 = "${decorator}");

		PortletEntry.save();
	}

	macro selectTreeNode {
		if (IsElementPresent(locator1 = "Treeview#NODE_UNSELECTED", key_nodeName = "${nodeName}")) {
			AssertClick.assertPartialTextClickAt(
				key_nodeName = "${nodeName}",
				locator1 = "Treeview#NODE_UNSELECTED",
				value1 = "${nodeName}");
		}

		AssertElementPresent(
			key_nodeName = "${nodeName}",
			locator1 = "Treeview#NODE_SELECTED");
	}

	@summary = "Shutdown the current Liferay Portal instance."
	macro shutdownServer {
		AntCommand(
			locator1 = "build-test.xml",
			value1 = "stop-app-server");
	}

	@description = "This will shutdown the server at a specific bundle at ${base_branch}-bundles-${bundle_index}\tomcat."
	macro shutdownSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "stop-app-server -Dapp.server.bundle.index=${bundleIndex}");
	}

	@summary = "Start the current Liferay Portal instance."
	macro startServer {
		if ("${deleteLiferayHome}" == "false") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Ddelete.liferay.home=false");
		}
		else if ("${keepCachedAppServerData}" == "true") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dkeep.cached.app.server.data=true");
		}
		else {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server");
		}
	}

	@description = "This will start the server at a specific bundle at ${base_branch}-bundles-${bundle_index}\tomcat."
	macro startSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		if ("${deleteLiferayHome}" == "false") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex} -Ddelete.liferay.home=false");
		}
		else if ("${keepCachedAppServerData}" == "true") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex} -Dkeep.cached.app.server.data=true");
		}
		else {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex}");
		}
	}

	macro viewApplicationDecorator {
		var decorator = StringUtil.lowerCase("${decorator}");

		AssertElementPresent(
			key_decorator = "${decorator}",
			locator1 = "Portlet#PORTLET_BORDER_DECORATOR");
	}

	@summary = "Assert that the ${portletName} portlet body has the text '${portletBody}'"
	macro viewBody {
		var key_portletName = "${portletName}";

		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#BODY",
			value1 = "${portletBody}");
	}

	@description = "View Cannot Add ${portletName} portlet."
	macro viewCannotAddPG {
		if ("${portletName}" == "Map") {
			var portletName = "Where are the Liferay members?";
		}
		else if ("${portletName}" == "Requests") {
			var portletTypeKey = "PORTLET_BORDERLESS";
		}

		Click.clickAtSidebarClickAtWaitForScript(locator1 = "ControlMenu#ADD");

		Panel.expandSidebarApplicationsPanel();

		Type.sendKeysApplicationSearch(
			locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
			value1 = "${portletName}");

		var portletLocator = "//span[@data-title=&quot;${portletName}&quot; and contains(@class,&apos;add-content-item&apos;)]";

		AssertElementNotPresent(locator1 = "${portletLocator}");
	}

	macro viewCannotEdit {
		AssertElementNotPresent(locator1 = "Button#EDIT");
	}

	macro viewCannotPublish {
		AssertElementNotPresent(locator1 = "Button#PUBLISH");
	}

	macro viewCannotSaveAsDraft {
		AssertElementNotPresent(locator1 = "Button#SAVE_AS_DRAFT");
	}

	macro viewCustomStylesViaLookandfeelPG {
		SikuliClick(locator1 = "Portlet#SIGN_IN_CUSTOM_LOOK_AND_FEEL");
	}

	macro viewDefaultDisplayStyle {
		var displayStyleUppercase = StringUtil.upperCase("${displayStyle}");

		AssertElementPresent(locator1 = "Icon#DISPLAY_STYLE_${displayStyleUppercase}_ACTIVE");
	}

	macro viewDuplicatePG {
		var i = "0";

		while ("${i}" != "${totalPortletCount}") {
			var i = ${i} + 1;

			AssertElementPresent(locator1 = "xpath=(//h2[contains(.,'${portletName}')])[${i}]");
		}
	}

	macro viewEmptyMessage {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no results.");
	}

	macro viewNoPermissionPG {
		AssertTextEquals(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");
	}

	macro viewNoPortletsPresentPG {
		AssertElementNotPresent(locator1 = "Portlet#PORTLET_CONTENT");
	}

	macro viewNoSpecificPG {
		AssertElementNotPresent(
			key_portletTitleName = "${portletName}",
			locator1 = "Portlet#SPECIFIC_TITLE");
	}

	macro viewPG {
		Portlet.viewTitle(portletName = "${portletName}");

		if (isSet(portletBody)) {
			Portlet.viewBody(
				portletBody = "${portletBody}",
				portletName = "${portletName}");
		}
	}

	macro viewPortletOptions {
		Click.makeVisibleClickAt(locator1 = "Portlet#OPTIONS_ICON");

		AssertTextEquals(
			key_menuItem = "${OptionsName}",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "${OptionsName}");
	}

	macro viewPortletOptionsMenulist {
		Click.makeVisibleClickAt(locator1 = "Portlet#OPTIONS_ICON");

		AssertTextEquals(
			key_menuItem = "Look and Feel Configuration",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Look and Feel Configuration");

		AssertTextEquals(
			key_menuItem = "Configuration",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Configuration");

		AssertTextEquals(
			key_menuItem = "Configuration Templates",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Configuration Templates");

		AssertTextEquals(
			key_menuItem = "Export / Import",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Export / Import");

		AssertTextEquals(
			key_menuItem = "Permissions",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Permissions");

		AssertTextEquals(
			key_menuItem = "Remove",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Remove");
	}

	macro viewSpecificPG {
		AssertTextEquals(
			key_portletTitleName = "${portletName}",
			locator1 = "Portlet#SPECIFIC_TITLE",
			value1 = "${portletName}");
	}

	@summary = "Assert that the ${portletName} portlet title has the text '${portletName}'"
	macro viewTitle {
		AssertTextEquals(
			key_portletName = "${portletName}",
			locator1 = "Portlet#TITLE",
			value1 = "${portletName}");
	}

	macro viewUndeployMessage {
		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#PORTLET_CONTENT",
			value1 = "This portlet has been undeployed.");
	}

}