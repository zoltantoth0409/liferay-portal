definition {
	macro _clickAddPortlet {
		var browserType = PropsUtil.get("browser.type");

		if ("${browserType}" == "safari") {
			Click(locator1 = "ControlMenuAddPanel#ADD_PORTLET_DRAG_LINK");
		}
		else {
			MouseOver(locator1 = "ControlMenuAddPanel#ADD_PORTLET_LINK");
			Click(locator1 = "ControlMenuAddPanel#ADD_PORTLET_LINK", value1 = "Add");
		}
	}

	macro addDuplicatePG {
		var portletTypeKey = "PORTLET";

		if ("${portletName}" == "Map") {
			var portletName = "Where are the Liferay members?";
		}
		else if ("${portletName}" == "Requests") {
			var portletTypeKey = "PORTLET_BORDERLESS";
		}

		if (!(isSet(portletCount))) {
			var portletCount = "1";
		}

		var i = "0";

		while (!("${i}" == "${portletCount}")) {
			var i = MathUtil.sum("${i}", "1");

			Refresh();

			Portlet.addPGCmd(portletName = "${portletName}");
		}

		var key_portletName = "${portletName}";

		AssertElementPresent(locator1 = "Home#${portletTypeKey}");
	}

	@summary = "Add a '${portletName}' portlet"
	macro addPG {
		if ("${portletName}" == "Map") {
			var portletName = "Where are the Liferay members?";
		}

		Portlet.addPGCmd(portletName = "${portletName}");

		var i = "1";
		var key_portletName = "${portletName}";

		while (!((IsElementPresent(locator1 = "Home#PORTLET")) || ("${i}" == "3"))) {
			var i = MathUtil.sum("${i}", "1");

			Pause(locator1 = "30000");

			echo("The PORTLET could not be found. Refreshing the page. Attempt number ${i}...");

			Refresh();

			Portlet.addPGCmd(portletName = "${portletName}");
		}

		AssertElementPresent(locator1 = "Home#PORTLET");
	}

	macro addPGCmd {
		if ("${portletName}" == "Where are the Liferay members?") {
			var portletName = "Map";
		}

		if (IsNotVisible(locator1 = "ControlMenuAddPanel#CLOSE_ADD_PANEL")) {
			Click.clickAt(locator1 = "ControlMenu#ADD");
		}

		AssertElementPresent(
			locator1 = "ControlMenuAddPanel#APPLICATIONS_ACCORDION_PANEL_TITLE"
		);

		AssertVisible(
			locator1 = "ControlMenuAddPanel#APPLICATIONS_ACCORDION_PANEL_TITLE",
			value1 = "Widgets"
		);

		Pause(locator1 = "5000");

		Panel.expandSidebarApplicationsPanel();

		Type.sendKeysApplicationSearch(
			locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
			value1 = "${portletName}"
		);

		Portlet._clickAddPortlet(portletName = "${portletName}");

		var key_portletName = "${portletName}";

		AssertElementPresent(locator1 = "Portlet#LOADED");
	}

	macro assertServerShutdown {
		var shutdownPauseCount = "0";

		while ((IsElementPresent(locator1 = "//body")) && (!("${shutdownPauseCount}" == "12"))) {
			Refresh();

			Pause(locator1 = "10000");

			var shutdownPauseCount = MathUtil.sum("${shutdownPauseCount}", "1");

			echo("Shutdown pause count: ${shutdownPauseCount}");
		}
	}

	macro assertServerStartup {
		var startupPauseCount = "0";

		while ((!(IsElementPresent(locator1 = "UserBar#USER_SIGN_IN"))) && (!("${startupPauseCount}" == "12"))) {
			Refresh();

			Pause(locator1 = "10000");

			var startupPauseCount = MathUtil.sum("${startupPauseCount}", "1");

			echo("Startup pause count: ${startupPauseCount}");
		}
	}

	macro changeDisplayStyle {
		if (IsElementNotPresent(locator1 = "Icon#DISPLAY_STYLE_ACTIVE", key_displayStyle = StringUtil.toLowerCase("${displayStyle}"))) {
			Click(
				locator1 = "Icon#DISPLAY_STYLE",
				key_displayStyle = StringUtil.toLowerCase("${displayStyle}")
			);
		}

		AssertElementPresent(
			locator1 = "Icon#DISPLAY_STYLE_ACTIVE",
			key_displayStyle = StringUtil.toLowerCase("${displayStyle}")
		);
	}

	macro clickPortletPlusIconPG {
		MouseOver.mouseOverNotVisible(
			locator1 = "Portlet#ICON_PLUS_SIGN",
			key_portletName = "${portletName}"
		);

		Click(locator1 = "Portlet#ICON_PLUS_SIGN", key_portletName = "${portletName}");
	}

	macro configureApplicationDecoratorViaLookandfeelPG {
		Navigator.gotoNavTab(navTab = "General");

		Select(
			locator1 = "Portlet#APPLICATION_DECORATOR_SELECT",
			value1 = "${applicationDecorator}"
		);
	}

	macro configureCustomStylesViaLookandfeelPG {
		Navigator.gotoNavTab(navTab = "Text Styles");

		Select(locator1 = "Portlet#TEXT_FONT_SELECT", value1 = "Verdana");

		Select(locator1 = "Portlet#TEXT_FONT_SIZE_SELECT", value1 = "2em");

		Navigator.gotoNavTab(navTab = "Border Styles");

		Type(locator1 = "Portlet#BORDER_WIDTH_TOP_INPUT", value1 = "20");
	}

	macro configureCustomTitleViaLookandfeelPG {
		IFrame.selectConfigurationFrame();

		Navigator.gotoNavTab(navTab = "General");

		Check.checkToggleSwitch(locator1 = "ToggleSwitch#USE_CUSTOM_TITLE");

		Type(locator1 = "TextInput#CUSTOM_TITLE", value1 = "${portletName} Custom");
	}

	macro deletePG {
		var key_portletName = "${portletName}";

		MouseOver.mouseOverNotVisible(locator1 = "Portlet#SPECIFIC_ELLIPSIS_ICON");

		Click(locator1 = "Portlet#SPECIFIC_ELLIPSIS_ICON");

		MenuItem.clickNoError(menuItem = "Remove");

		AssertConfirm(value1 = "Are you sure you want to remove this component?");
	}

	@summary = "Drag and drop the '${portletName}' portlet from column ${columnNumberFrom} to column ${columnNumberTo}"
	macro dragAndDropPortletToColumnPG {
		var key_portletTitleName = "${portletName}";

		AssertTextEquals.assertTextCaseInsensitive(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberFrom}_PORTLET_TITLE",
			value1 = "${portletName}"
		);

		DragAndDrop.dragAndDropPortletToObject(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberFrom}_PORTLET_HEADER",
			locator2 = "Home#PAGE_COLUMN_${columnNumberTo}",
			value1 = "${portletName}"
		);

		AssertTextEquals.assertTextCaseInsensitive(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberTo}_PORTLET_TITLE",
			value1 = "${portletName}"
		);

		AssertTextEquals.assertTextCaseInsensitive(
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_${columnNumberTo}_PORTLET_TITLE",
			value1 = "${portletName}"
		);
	}

	macro expandTree {
		while (IsElementPresent(locator1 = "Treeview#NODE_COLLAPSED")) {
			Click(locator1 = "Treeview#NODE_COLLAPSED");
		}

		AssertElementNotPresent(locator1 = "Treeview#NODE_COLLAPSED");
	}

	macro gotoPortletOptions {
		if (isSet(portletName)) {
			var key_portletName = "${portletName}";

			MouseOver.mouseOverNotVisible(
				locator1 = "Portlet#SPECIFIC_BORDERLESS_ELLIPSIS_ICON"
			);

			Click(locator1 = "Portlet#SPECIFIC_BORDERLESS_ELLIPSIS_ICON");
		}
		else {
			MouseOver.mouseOverNotVisible(locator1 = "Portlet#OPTIONS_ICON");

			Click(locator1 = "Portlet#OPTIONS_ICON");
		}

		if (isSet(portletOption)) {
			MenuItem.click(menuItem = "${portletOption}");
		}
	}

	macro mouseOverPortletTitle {
		var key_portletTitleName = "${portletTitleName}";

		MouseOver(locator1 = "Portlet#SPECIFIC_TITLE");
	}

	macro saveAndCloseConfig {
		Button.clickSave();

		SelectFrameTop();

		Click(locator1 = "Icon#CLOSE");

		WaitForElementNotPresent(locator1 = "IFrame#DIALOG");
	}

	macro selectApplicationDecorator {
		Select(locator1 = "Select#APPLICATION_DECORATORS", value1 = "${decorator}");
	}

	macro selectTreeNode {
		if (IsElementPresent(locator1 = "Treeview#NODE_UNSELECTED", key_nodeName = "${nodeName}")) {
			AssertClick.assertPartialTextClickAt(
				locator1 = "Treeview#NODE_UNSELECTED",
				value1 = "${nodeName}",
				key_nodeName = "${nodeName}"
			);
		}

		AssertElementPresent(
			locator1 = "Treeview#NODE_SELECTED",
			key_nodeName = "${nodeName}"
		);
	}

	@summary = "Shutdown the current Liferay Portal instance."
	macro shutdownServer {
		AntCommand(locator1 = "build-test.xml", value1 = "stop-app-server");
	}

	@description = "This will shutdown the server at a specific bundle at ${base_branch}-bundles-${bundle_index}\tomcat."
	macro shutdownSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = MathUtil.difference("${leadingPortNumber}", "8");

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "stop-app-server -Dapp.server.bundle.index=${bundleIndex}"
		);
	}

	@summary = "Start the current Liferay Portal instance."
	macro startServer {
		if ("${deleteLiferayHome}" == "false") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Ddelete.liferay.home=false"
			);
		}
		else if ("${keepCachedAppServerData}" == "true") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dkeep.cached.app.server.data=true"
			);
		}
		else {
			AntCommand(locator1 = "build-test.xml", value1 = "start-app-server");
		}
	}

	@description = "This will start the server at a specific bundle at ${base_branch}-bundles-${bundle_index}\tomcat."
	macro startSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = MathUtil.difference("${leadingPortNumber}", "8");

		if ("${deleteLiferayHome}" == "false") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex} -Ddelete.liferay.home=false"
			);
		}
		else if ("${keepCachedAppServerData}" == "true") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex} -Dkeep.cached.app.server.data=true"
			);
		}
		else {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex}"
			);
		}
	}

	macro viewApplicationDecorator {
		AssertElementPresent(
			locator1 = "Portlet#PORTLET_BORDER_DECORATOR",
			key_decorator = StringUtil.lowerCase("${decorator}")
		);
	}

	@summary = "Assert that the ${portletName} portlet body has the text '${portletBody}'"
	macro viewBody {
		var key_portletName = "${portletName}";

		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#BODY",
			value1 = "${portletBody}"
		);
	}

	@description = "View Cannot Add ${portletName} portlet."
	macro viewCannotAddPG {
		if ("${portletName}" == "Map") {
			var portletName = "Where are the Liferay members?";
		}
		else if ("${portletName}" == "Requests") {
			var portletTypeKey = "PORTLET_BORDERLESS";
		}

		Click.clickAtSidebarClickAtWaitForScript(
			locator1 = "ControlMenu#ADD",
			value1 = "Add"
		);

		Panel.expandSidebarApplicationsPanel();

		Type.sendKeysApplicationSearch(
			locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
			value1 = "${portletName}"
		);

		var portletLocator = "//span[@data-title=&quot;${portletName}&quot; and contains(@class,&apos;add-content-item&apos;)]";

		AssertElementNotPresent(locator1 = "${portletLocator}");
	}

	macro viewCannotEdit {
		AssertElementNotPresent(locator1 = "Button#EDIT");
	}

	macro viewCannotPublish {
		AssertElementNotPresent(locator1 = "Button#PUBLISH");
	}

	macro viewCannotSaveAsDraft {
		AssertElementNotPresent(locator1 = "Button#SAVE_AS_DRAFT");
	}

	macro viewCustomStylesViaLookandfeelPG {
		SikuliClick(locator1 = "Portlet#SIGN_IN_CUSTOM_LOOK_AND_FEEL");
	}

	macro viewDuplicatePG {
		var i = "0";

		while (!("${i}" == "${totalPortletCount}")) {
			var i = MathUtil.sum("${i}", "1");

			AssertElementPresent(
				locator1 = "xpath=(//h2[contains(.,'${portletName}')])[${i}]"
			);
		}
	}

	macro viewEmptyMessage {
		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "${message}"
		);
	}

	macro viewNoPermissionPG {
		AssertTextEquals(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet."
		);
	}

	macro viewNoPortletsPresentPG {
		AssertElementNotPresent(locator1 = "Portlet#PORTLET_CONTENT");
	}

	macro viewNoSpecificPG {
		AssertElementNotPresent(
			locator1 = "Portlet#SPECIFIC_TITLE",
			key_portletTitleName = "${portletName}"
		);
	}

	macro viewPG {
		Portlet.viewTitle(portletName = "${portletName}");

		if (isSet(portletBody)) {
			Portlet.viewBody(portletBody = "${portletBody}", portletName = "${portletName}");
		}
	}

	macro viewPortletOptions {
		MouseOver.mouseOverNotVisible(locator1 = "Portlet#OPTIONS_ICON");

		Click(locator1 = "Portlet#OPTIONS_ICON");

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "${OptionsName}",
			key_menuItem = "${OptionsName}"
		);
	}

	macro viewPortletOptionsMenulist {
		MouseOver.mouseOverNotVisible(locator1 = "Portlet#OPTIONS_ICON");

		Click(locator1 = "Portlet#OPTIONS_ICON");

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Look and Feel Configuration",
			key_menuItem = "Look and Feel Configuration"
		);

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Configuration",
			key_menuItem = "Configuration"
		);

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Configuration Templates",
			key_menuItem = "Configuration Templates"
		);

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Export / Import",
			key_menuItem = "Export / Import"
		);

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Permissions",
			key_menuItem = "Permissions"
		);

		AssertTextEquals(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Remove",
			key_menuItem = "Remove"
		);
	}

	macro viewSpecificPG {
		var key_portletTitleName = "${portletName}";

		AssertTextEquals.assertTextCaseInsensitive(
			locator1 = "Portlet#SPECIFIC_TITLE",
			value1 = "${portletName}"
		);
	}

	@summary = "Assert that the ${portletName} portlet title has the text '${portletName}'"
	macro viewTitle {
		var key_portletName = "${portletName}";

		AssertTextEquals.assertTextCaseInsensitive(
			locator1 = "Portlet#TITLE",
			value1 = "${portletName}"
		);
	}

	macro viewUndeployMessage {
		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#PORTLET_CONTENT",
			value1 = "This portlet has been undeployed."
		);
	}
}