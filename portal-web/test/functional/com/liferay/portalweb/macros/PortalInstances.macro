definition {
	macro addCP {
		LexiconEntry.gotoAdd();

		Type(locator1 = "TextInput#WEB_ID", value1 = "${webId}");

		Type(locator1 = "TextInput#VIRTUAL_HOST", value1 = "${virtualHost}");

		Type(locator1 = "TextInput#MAIL_DOMAIN", value1 = "${mailDomain}");

		if ("${maxUsers}" == "true") {
			Type(locator1 = "TextInput#MAX_USERS", value1 = "${maxUserNumber}");
		}

		if ("${portalInstanceActive}" == "false") {
			Uncheck(locator1 = "Checkbox#ACTIVE");
		}

		PortletEntry.save();
	}

	macro deactivatePortalInstance {
		var key_virtualHost = "${virtualHost}";

		Click(locator1 = "PortalInstances#INSTANCE_TABLE_ACTIONS");

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);

		Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ACTIVE");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_ACTIVE",
			value1 = "No"
		);
	}

	macro deleteCP {
		var key_virtualHost = "${virtualHost}";

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_VIRTUAL_HOST",
			value1 = "${virtualHost}"
		);

		Click(locator1 = "PortalInstances#INSTANCE_TABLE_ACTIONS");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		AssertTextNotPresent(
			locator1 = "PortalInstances#INSTANCE_TABLE_VIRTUAL_HOST",
			value1 = "${virtualHost}"
		);
	}

	macro editCP {
		var key_virtualHost = "${virtualHost}";

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_VIRTUAL_HOST",
			value1 = "${virtualHost}"
		);

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Edit");

		Type(locator1 = "TextInput#MAIL_DOMAIN", value1 = "${mailDomain}");

		AssertClick(locator1 = "Button#SAVE", value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro tearDownCP {
		SignOut.signOutOfSpecificURL(url = "http://localhost:8080/");

		SignIn.signInSpecificURL(url = "http://localhost:8080/");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances"
		);

		while (IsElementPresent(locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_ELLIPSIS", key_rowNumber = "2")) {
			Click.waitForMenuToggleJSClick(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_ELLIPSIS",
				key_rowNumber = "2"
			);

			AssertClickNoError(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Delete",
				key_menuItem = "Delete"
			);

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.waitForSuccessMessageHide();
		}
	}

	macro viewCP {
		var key_virtualHost = "${virtualHost}";

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_WEB_ID",
			value1 = "${virtualHost}"
		);

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_VIRTUAL_HOST",
			value1 = "${virtualHost}"
		);

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_MAIL_DOMAIN",
			value1 = "${virtualHost}"
		);
	}
}