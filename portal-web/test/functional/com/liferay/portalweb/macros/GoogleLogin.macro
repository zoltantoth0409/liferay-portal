definition {

	macro acceptConditions {
		if (IsElementPresent.pauseIsElementPresent(locator1 = "GoogleLogin#ALLOW")) {
			Click(locator1 = "GoogleLogin#ALLOW");
		}

		if (IsElementPresent.pauseIsElementPresent(locator1 = "Button#I_AGREE")) {
			Click(locator1 = "Button#I_AGREE");
		}

		if (IsElementPresent.pauseIsElementPresent(locator1 = "TextInput#ANSWER")) {
			Type(
				locator1 = "TextInput#ANSWER",
				value1 = "test");

			Button.clickSave();
		}

		SelectWindow.selectWindowTopNoSPARefresh();
	}

	macro accountSignIn {
		Navigator.openURL();

		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		Click(locator1 = "Link#GOOGLE");

		SelectWindow(locator1 = "GoogleLogin#PAGE_TITLE");

		Type(
			locator1 = "GoogleLogin#LOGIN_EMAIL_FIELD",
			value1 = "${userEmailAddress}");

		Click(locator1 = "GoogleLogin#LOGIN_NEXT_BUTTON");

		Type(
			locator1 = "GoogleLogin#LOGIN_PASSWORD_FIELD",
			value1 = "${userPassword}");

		if (IsElementPresent(locator1 = "GoogleLogin#LOGIN_SIGN_IN_BUTTON")) {
			Click(locator1 = "GoogleLogin#LOGIN_SIGN_IN_BUTTON");
		}
		else {
			Click(locator1 = "GoogleLogin#LOGIN_NEXT_BUTTON");
		}
	}

}