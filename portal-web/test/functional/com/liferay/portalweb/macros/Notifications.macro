definition {

	macro acceptRequest {
		var key_requestsTitle = "${requestsTitle}";

		AssertTextEquals(
			locator1 = "Notifications#NOTIFICATIONS_TITLE",
			value1 = "${requestsTitle}");

		AssertClick(
			locator1 = "Button#CONFIRM",
			value1 = "Confirm");
	}

	macro deleteNotification {
		var key_contentBody = "${contentBody}";

		Click(locator1 = "Notifications#NOTIFICATIONS_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertElementNotPresent(locator1 = "Notifications#NOTIFICATIONS_CONTENT");
	}

	macro goToFilterValues {
		var key_filterValue = "${filterValue}";

		Click(locator1 = "Dropdown#FILTER");

		AssertClick(
			locator1 = "Dropdown#FILTER_VALUES",
			value1 = "${filterValue}");
	}

	macro gotoNotifications {
		if (IsElementPresent(locator1 = "Notifications#USER_BAR_BADGE_COUNT")) {
			Click(locator1 = "Notifications#USER_BAR_BADGE_COUNT");
		}

		if (IsElementPresent(locator1 = "Notifications#MY_ACCOUNT_BADGE_COUNT")) {
			Click(locator1 = "Notifications#MY_ACCOUNT_BADGE_COUNT");
		}

		if (IsElementPresent(locator1 = "Notifications#NOTIFICATIONS_BADGE_COUNT")) {
			Click(locator1 = "Notifications#NOTIFICATIONS_BADGE_COUNT");
		}

		if (IsElementPresent(locator1 = "Notifications#USER_NAVIGATION_BAR_BADGE_COUNT")) {
			Click(locator1 = "Notifications#USER_NAVIGATION_BAR_BADGE_COUNT");
		}
	}

	macro gotoNotificationsEntry {
		AssertClick(
			key_notificationsWorkflowTitle = "${notificationsWorkflowTitle}",
			locator1 = "Notifications#NOTIFICATIONS_WORKFLOW_TITLE",
			value1 = "${notificationsWorkflowTitle}");
	}

	macro navigateToContent {
		AssertClick.assertPartialTextClickAt(
			key_contentBody = "${contentBody}",
			locator1 = "Notifications#NOTIFICATIONS_CONTENT",
			value1 = "${contentBody}");
	}

	macro reviewViaNotificationsEntry {
		Notifications.gotoNotificationsEntry(notificationsWorkflowTitle = "${notificationsWorkflowTitle}");

		Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "${menuItem}");

		IFrame.selectDialogFrame();

		Button.clickDone();

		SelectFrameTop();

		Alert.viewSuccessMessage();
	}

	macro tearDownUnreadNotifications {
		ProductMenu.gotoPortlet(
			category = "My Account",
			panel = "User",
			portlet = "Notifications");

		var key_contentBody = "${contentBody}";

		if (IsElementPresent(locator1 = "Notifications#NOTIFICATIONS_CONTENT")) {
			AssertClick(
				locator1 = "Notifications#MARK_AS_READ",
				value1 = "Mark as Read");
		}
	}

	macro viewBadgeCount {
		if (IsElementPresent(locator1 = "Notifications#USER_BAR_BADGE_COUNT")) {
			AssertTextEquals(
				locator1 = "Notifications#USER_BAR_BADGE_COUNT",
				value1 = "${notificationCount}");
		}

		if (IsElementPresent(locator1 = "Notifications#MY_ACCOUNT_BADGE_COUNT")) {
			AssertTextEquals(
				locator1 = "Notifications#MY_ACCOUNT_BADGE_COUNT",
				value1 = "${notificationCount}");
		}

		if (IsElementPresent(locator1 = "Notifications#NOTIFICATIONS_BADGE_COUNT")) {
			if (IsElementPresent.isVisible(locator1 = "Notifications#NOTIFICATIONS_BADGE_COUNT")) {
				AssertTextEquals(
					locator1 = "Notifications#NOTIFICATIONS_BADGE_COUNT",
					value1 = "${notificationCount}");
			}
		}

		if (IsElementPresent(locator1 = "Notifications#USER_NAVIGATION_BAR_BADGE_COUNT")) {
			AssertTextEquals(
				locator1 = "Notifications#USER_NAVIGATION_BAR_BADGE_COUNT",
				value1 = "${notificationCount}");
		}
	}

	macro viewDefaultContent {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "You do not have any notifications.");
	}

	macro viewNewComment {
		var assetType = StringUtil.lowerCase("${assetType}");
		var key_contentBody = "${contentBody}";

		if (isSet(mentioned)) {
			AssertTextEquals.assertPartialText(
				locator1 = "Notifications#NOTIFICATIONS_TITLE",
				value1 = "${user} mentioned you in a comment in a ${assetType}.");
		}

		else {
			AssertTextEquals(
				locator1 = "Notifications#NOTIFICATIONS_TITLE",
				value1 = "${user} added a new ${assetType} to ${content}.");
		}

		AssertTextEquals.assertPartialText(
			locator1 = "Notifications#NOTIFICATIONS_CONTENT",
			value1 = "${contentBody}");
	}

	macro viewNewContent {
		var assetType = StringUtil.lowerCase("${assetType}");
		var key_contentBody = "${contentBody}";

		AssertTextEquals(
			locator1 = "Notifications#NOTIFICATIONS_TITLE",
			value1 = "${user} added a new ${assetType}.");

		AssertTextEquals(
			locator1 = "Notifications#NOTIFICATIONS_CONTENT",
			value1 = "${contentBody}");
	}

	macro viewNoNotificationEntry {
		AssertElementNotPresent(locator1 = "Notifications#USER_BAR_BADGE_COUNT");

		AssertElementNotPresent(locator1 = "Notifications#NOTIFICATIONS_CONTENT_GENERIC");

		AssertElementNotPresent(locator1 = "Notifications#NOTIFICATIONS_TITLE");
	}

	macro viewNoNotifications {
		if ("${activePanel}" == "Profile") {
			AssertElementNotPresent(locator1 = "Notifications#USER_NAVIGATION_BAR_BADGE_COUNT");
		}

		if ("${activePanel}" == "My Account") {
			AssertElementNotPresent(locator1 = "Notifications#MY_ACCOUNT_BADGE_COUNT");
		}

		if ("${activePanel}" == "Notifications") {
			AssertElementNotPresent(locator1 = "Notifications#NOTIFICATIONS_BADGE_COUNT");
		}
	}

	macro viewNoRequest {
		AssertElementNotPresent(locator1 = "Button#CONFIRM");

		AssertElementNotPresent(locator1 = "Button#IGNORE");
	}

	macro viewRequest {
		Click(locator1 = "Notifications#REQUESTS_LIST_TAB");

		AssertTextEquals(
			locator1 = "Notifications#NOTIFICATIONS_TITLE",
			value1 = "${requestsTitle}");
	}

	macro viewUpdatedContent {
		var assetType = StringUtil.lowerCase("${assetType}");
		var key_contentBody = "${contentBody}";

		AssertTextEquals(
			locator1 = "Notifications#NOTIFICATIONS_TITLE",
			value1 = "${user} updated a ${assetType}.");

		AssertTextEquals(
			locator1 = "Notifications#NOTIFICATIONS_CONTENT",
			value1 = "${contentBody}");
	}

}