definition {
	macro deleteStatus {
		var key_actionType = "Delete";
		var key_entryContent = "${entryContent}";

		MakeVisible(locator1 = "Microblogs#FOOTER");

		AssertClickNoError(
			locator1 = "Microblogs#TIMELINE_ENTRY_ACTION",
			value1 = "Delete"
		);

		Confirm(value1 = "Are you sure you want to delete this post?");

		AssertElementNotPresent(locator1 = "Microblogs#TIMELINE_ENTRY_CONTENT");
	}

	macro editStatus {
		var key_actionType = "Edit";
		var key_entryContent = "${entryContent}";

		MakeVisible(locator1 = "Microblogs#FOOTER");

		AssertClick(locator1 = "Microblogs#TIMELINE_ENTRY_ACTION", value1 = "Edit");

		Type.clickAtType(
			locator1 = "TextArea#FIELD_INPUT",
			value1 = "${entryContentEdit}"
		);

		AssertClick(locator1 = "Button#POST", value1 = "Post");
	}

	macro gotoTimelineTab {
		AssertElementPresent.assertVisible(
			locator1 = "NavTab#TIMELINE",
			value1 = "Timeline"
		);

		AssertClick(locator1 = "NavTab#TIMELINE", value1 = "Timeline");

		AssertTextEquals(locator1 = "Microblogs#ACTIVE_TAB", value1 = "Timeline");
	}

	macro updateStatus {
		AssertTextEquals(locator1 = "Microblogs#COUNTDOWN_HOLDER", value1 = "150");

		var key_status = "Update your status";

		AssertClick(
			locator1 = "Microblogs#STATUS_FIELD",
			value1 = "Update your status..."
		);

		var key_entryContent = "${entryContent}";

		Type.clickAtType(locator1 = "TextArea#FIELD_INPUT", value1 = "${entryContent}");

		AssertClick(locator1 = "Button#POST", value1 = "Post");

		AssertElementNotPresent(locator1 = "Button#POST");
	}

	macro viewEntry {
		var key_entryContent = "${entryContent}";

		AssertTextEquals(
			locator1 = "Microblogs#TIMELINE_ENTRY_CONTENT",
			value1 = "${entryContent}"
		);

		AssertTextEquals(
			locator1 = "Microblogs#TIMELINE_ENTRY_USER_NAME",
			value1 = "${userName}"
		);
	}
}