definition {

	@summary = "Navigate to Applications Menu > ${site}"
	macro gotoLocalizedSite {
		ApplicationsMenuHelper.openApplicationsMenu();

		Click(
			key_site = "${site}",
			locator1 = "ApplicationsMenu#SITE_LINK");
	}

	macro gotoPanel {
		var key_panel = "${panel}";

		Click(locator1 = "ApplicationsMenu#PANEL");

		AssertElementPresent(locator1 = "ApplicationsMenu#ACTIVE_PANEL");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	@summary = "Navigate to Applications Menu > ${panel} > ${category} > ${portlet}"
	macro gotoPortlet {
		ApplicationsMenuHelper.openApplicationsMenu();

		ApplicationsMenu.gotoPanel(panel = "${panel}");

		ApplicationsMenuHelper.gotoPortlet(
			category = "${category}",
			portlet = "${portlet}");
	}

	@summary = "Navigate to Applications Menu > ${site}"
	macro gotoSite {
		ApplicationsMenuHelper.openApplicationsMenu();

		Click(
			key_site = "${site}",
			locator1 = "ApplicationsMenu#SITE_LINK");
	}

	@summary = "Navigate to Applications Menu > Sites > Sites Modal > ${site}"
	macro gotoSiteViaViewAll {
		ApplicationsMenuHelper.openApplicationsMenu();

		ApplicationsMenuHelper.openWorkspace();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Pause(locator1 = "3000");

		if (isSet(depotName)) {
			Navigator.gotoNavItem(navItem = "Asset Libraries");

			LexiconCard.clickCard(card = "${depotName}");
		}
		else {
			Navigator.gotoNavItem(navItem = "My Sites");

			SiteSelector.selectSite(site = "${site}");

			ProductMenuHelper.openProductMenu();

			AssertTextEquals.assertPartialText(
				locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
				value1 = "${site}");
		}
	}

}