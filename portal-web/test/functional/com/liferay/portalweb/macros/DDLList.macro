definition {
	var ddlDataDefinitionDescription = "Data Definition Description";
	var ddlDataDefinitionName = "Data Definition Name";
	var ddlListName = "List Name";

	macro addCP {
		LexiconEntry.gotoAdd();

		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${ddlListName}");

		if (isSet(ddlListDescription)) {
			Type.clickAtType(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${ddlListDescription}"
			);
		}

		if (isSet(workflowDefinition)) {
			Select(
				locator1 = "DDLEditList#WORKFLOW_SELECT",
				value1 = "${workflowDefinition}"
			);
		}

		AssertClick(locator1 = "DDLEditList#DATA_DEFINITION_SELECT", value1 = "Select");
	}

	macro addViaDDLDisplayPG {
		Click(locator1 = "DDL#PORTLET_ACTIONS_ADD_LIST");

		AssertTextEquals(locator1 = "Portlet#H3_MODAL_TITLE", value1 = "Add List");

		IFrame.selectDialogFrame();

		PortletEntry.inputName(name = "${ddlListName}");

		if (isSet(ddlListDescription)) {
			PortletEntry.inputDescription(description = "${ddlListDescription}");
		}

		if (isSet(workflowDefinition)) {
			Select(
				locator1 = "DDLEditList#WORKFLOW_SELECT",
				value1 = "${workflowDefinition}"
			);
		}

		AssertClick(locator1 = "DDLEditList#DATA_DEFINITION_SELECT", value1 = "Select");

		SelectFrameTop();
	}

	macro assertCannotDeleteRecordViaDDLDisplayPG {
		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		AssertElementNotPresent(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			key_menuItem = "Delete"
		);

		AssertElementPresent(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "View");
	}

	macro deleteCP {
		var key_ddlListName = "${ddlListName}";

		AssertTextEquals(locator1 = "DDL#LIST_TABLE_NAME", value1 = "${ddlListName}");

		LexiconEntry.gotoEllipsisMenuItemNoError(menuItem = "Delete");

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${ddlListName}");
	}

	macro editCP {
		var key_ddlListName = "${ddlListName}";

		AssertTextEquals(locator1 = "DDL#LIST_TABLE_NAME", value1 = "${ddlListName}");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Edit");

		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${ddlListNameEdit}");

		if (isSet(ddlListDescriptionEdit)) {
			Type.clickAtType(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${ddlListDescriptionEdit}"
			);
		}

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro editPermissions {
		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "${ddlListName}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Permissions");
	}

	macro editViaDDLDisplayPG {
		Click(locator1 = "DDL#PORTLET_ACTIONS_EDIT_LIST");

		IFrame.selectDialogFrame();

		PortletEntry.inputName(name = "${ddlListNameEdit}");

		if (isSet(ddlListDescription)) {
			PortletEntry.inputDescription(description = "${ddlListDescriptionEdit}");
		}

		AssertClick(locator1 = "Button#SAVE", value1 = "Save");
	}

	macro saveViaDDLDisplayPG {
		DDLDataDefinition.save();

		Pause(locator1 = "5000");
	}

	macro selectViaDDLDisplayPG {
		Click(locator1 = "DDL#PORTLET_ACTIONS_SELECT_LIST");

		SelectFrame(locator1 = "DDLConfiguration#CONFIGURATION_IFRAME");

		var key_ddlListName = "${ddlListName}";

		AssertClick(
			locator1 = "DDLConfiguration#LIST_TABLE_NAME",
			value1 = "${ddlListName}"
		);

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "Displaying List: ${ddlListName} (Modified)"
		);

		AssertClick(locator1 = "Button#SAVE", value1 = "Save");

		Alert.viewSuccessMessage();

		SelectFrameTop();
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);

		while (IsElementPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			LexiconEntry.gotoEllipsisMenuItemNoError(menuItem = "Delete");

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		var key_ddlListName = "${ddlListName}";

		AssertTextEquals(locator1 = "DDL#LIST_TABLE_NAME", value1 = "${ddlListName}");

		if (isSet(ddlListDescription)) {
			var key_ddlListDescription = "${ddlListDescription}";

			AssertTextEquals(
				locator1 = "DDL#LIST_TABLE_DESCRIPTION",
				value1 = "${ddlListDescription}"
			);
		}

		AssertClick(locator1 = "DDL#LIST_TABLE_NAME", value1 = "${ddlListName}");

		SiteAdmin.viewPortletTitle(portletTitle = "${ddlListName}");
	}

	macro viewDeleteRecordOptionViaDDLDisplayPG {
		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1");

		AssertElementPresent(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			key_menuItem = "Delete"
		);
	}

	macro viewViaDDLDisplayPG {
		AssertTextEquals(locator1 = "Portlet#H2_HEADER", value1 = "${ddlListName}");
	}
}