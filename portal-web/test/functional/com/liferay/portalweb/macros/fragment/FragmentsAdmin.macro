definition {

	macro _gotoAddCollection {
		if (IsElementPresent(locator1 = "NavNested#NAV_NESTED_PLUS")) {
			Click(locator1 = "NavNested#NAV_NESTED_PLUS");
		}
		else {
			Button.click(button = "New");

			MenuItem.click(menuItem = "Collection");
		}
	}

	macro addCollection {
		FragmentsAdmin._gotoAddCollection();

		Collection.editCollection(
			collectionDescription = "${collectionDescription}",
			collectionName = "${collectionName}");

		Button.clickSave();
	}

	macro addResource {
		LexiconEntry.gotoAdd();

		IFrame.selectConfigurationFrame();

		UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${resourceFile}");

		IFrame.selectTopFrame();

		AssertClick(
			locator1 = "Button#OK",
			value1 = "OK");
	}

	macro addSection {
		LexiconEntry.gotoAddMenuItem(menuItem = "Section");

		AssertTextEquals(
			locator1 = "Modal#HEADER",
			value1 = "Add Section");

		PortletEntry.inputName(name = "${fragmentName}");

		Button.clickSave();
	}

	@summary = "Type HTML into the fragment HTML editor using the auto complete feature."
	macro autocompleteFragmentHTML {

		var htmlSnippet = '''<lfr-''';

		Type.typeAceEditorNoError(
			locator1 = "FragmentEditor#HTML_EDITOR",
			value1 = "${htmlSnippet}");

		Click(
			autocompleteField = "${autocompleteField}",
			locator1 = "FragmentEditor#AUTOCOMPLETE_FIELD");

		AssertTextEquals.assertPartialText(
			locator1 = "FragmentEditor#HTML_EDITOR_CONTENT",
			value1 = "${fragmentHTML}");
	}

	macro deleteEntry {
		LexiconCard.delete(card = "${entry}");
	}

	macro exportCollection {
		Navigator.gotoNavNested(navNested = "${collectionName}");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Export");
	}

	macro gotoCollection {
		AssertClick.assertPartialTextClickAt(
			locator1 = "FragmentAdmin#FRAGMENT_COLLECTION",
			value1 = "${collectionName}");
	}

	macro gotoEntry {
		LexiconCard.clickCardTitle(card = "${entry}");
	}

	macro gotoEntryEllipsisItem {
		LexiconCard.clickEllipsisItem(
			card = "${entry}",
			item = "${item}");
	}

	macro gotoImportCollection {
		if (IsElementPresent(locator1 = "NavNested#NAV_NESTED_ELLIPSIS")) {
			Click(locator1 = "NavNested#NAV_NESTED_ELLIPSIS");
		}
		else {
			Button.click(button = "New");
		}

		Button.click(button = "Import");

		IFrame.selectConfigurationFrame();
	}

	macro importCollectionFromDependenciesFolder {
		FragmentsAdmin.gotoImportCollection();

		UploadCommonFile(
			locator1 = "TextInput#FILE",
			value1 = "${collectionZipFile}");

		Button.click(button = "Import");

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	macro importCollectionFromTempFolder {
		FragmentsAdmin.gotoImportCollection();

		var collectionsZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "collections-*.zip");

		UploadTempFile(
			locator1 = "TextInput#FILE",
			value1 = "${collectionsZipFileName}");

		Button.click(button = "Import");

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	macro openFragmentsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/fragments");
	}

	macro viewEntryNotPresent {
		LexiconCard.viewCardNotPresent(card = "${entry}");
	}

	macro viewEntryPresent {
		LexiconCard.viewCardPresent(card = "${entry}");
	}

	macro viewEntryWorkflowStatus {
		LexiconCard.viewCardLabel(
			card = "${entry}",
			cardLabel = "${workflowStatus}");
	}

}