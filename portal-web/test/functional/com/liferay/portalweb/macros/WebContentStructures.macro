definition {
	macro addCP {
		LexiconEntry.gotoAdd();

		WebContentStructures.addName(structureName = "${structureName}");

		Panel.expandPanel(panelHeading = "Details");

		if (isSet(structureDescription)) {
			PortletEntry.inputDescriptionTextInput(description = "${structureDescription}");
		}

		SelectFrame(value1 = "relative=top");
	}

	macro addLocalization {
		var key_localizationName = "${localization1NameKey}";

		Click(locator1 = "Translation#LOCALIZATION_NAME");

		WebContentStructures.addName(structureName = "${localization1Name}");

		if (isSet(localization2NameKey)) {
			var key_localizationName = "${localization2NameKey}";

			Click(locator1 = "Translation#LOCALIZATION_NAME");

			WebContentStructures.addName(structureName = "${localization2Name}");
		}
	}

	macro addName {
		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${structureName}");
	}

	macro addNullCP {
		LexiconEntry.gotoAdd();

		AssertElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "Message#ERROR_FIELD_REQUIRED",
			value1 = "This field is required."
		);

		SelectFrame(value1 = "relative=top");
	}

	macro addSubstructure {
		AssertClick(locator1 = "Button#SELECT", value1 = "Select");

		SelectFrame(value1 = "relative=top");

		IFrame.selectDialogFrame();

		var key_ddlDataDefinitionName = "${parentStructureName}";

		AssertClickNoError(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = "${parentStructureName}"
		);

		Confirm.assertConfirmationNoWait(
			value1 = "Selecting a new structure deletes all unsaved content. Do you want to proceed?"
		);

		SelectFrame(value1 = "relative=top");
	}

	macro advancedSearchCP {
		var key_ddlDataDefinitionName = "${structureName}";

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
			value1 = "${structureName}"
		);

		Click(locator1 = "Icon#ADVANCED_SEARCH");

		if (isSet(structureName)) {
			PortletEntry.inputName(name = "${structureName}");
		}

		if (isSet(structureDescription)) {
			PortletEntry.inputDescriptionTextInput(description = "${structureDescription}");
		}

		Click(locator1 = "Icon#BASIC_SEARCH");

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
			value1 = "${structureName}"
		);

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_DESCRIPTION",
			value1 = "${structureDescription}"
		);
	}

	macro deleteCP {
		WebContentNavigator.gotoManageStructures();

		var key_ddlDataDefinitionName = "${structureName}";

		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		if ("${webContentHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The structure cannot be deleted because it is required by one or more templates."
			);
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro editSubstructureDefaultValuesCP {
		var key_ddlDataDefinitionName = "${substructureName}";

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "${substructureName}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Default Values",
			rowEntry = "${substructureName}"
		);

		for (var structureFieldName : list "${editSubstructureFieldNames}") {
			var key_fieldDefaultValue = "${structureFieldName}";
			var structureFieldName = StringUtil.replace("${structureFieldName}", " ", "");

			Type(
				locator1 = "DDMField#DDM_INPUT",
				value1 = "Edited ${structureFieldName} Field"
			);
		}

		Button.clickSave();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageStructures();

		var key_ddlDataDefinitionName = "${substructureName}";

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "${substructureName}");

		Click(locator1 = "Icon#BASIC_SEARCH");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Default Values",
			rowEntry = "${substructureName}"
		);

		for (var structureFieldName : list "${editSubstructureFieldNames}") {
			var key_fieldDefaultValue = "${structureFieldName}";

			AssertTextEquals(
				locator1 = "DDMField#DDM_INPUT",
				value1 = "Edited ${structureFieldName} Field"
			);
		}

		SelectFrameTop();
	}

	macro saveCP {
		AssertClick(locator1 = "DDMEditStructure#SAVE_BUTTON", value1 = "Save");

		Alert.viewSuccessMessage();

		if (isSet(addRoleViewPermissions)) {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Permissions",
				rowEntry = "${structureName}"
			);

			IFrame.selectDialogFrame();

			PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
				addOrRemoveViewPermission = "Add",
				roleList = "${addRoleViewPermissions}"
			);

			IFrame.selectTopFrame();
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageStructures();

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "2");

		Click(locator1 = "Icon#BASIC_SEARCH");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "Structure");

		Click(locator1 = "Icon#BASIC_SEARCH");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm.assertConfirmationNoWait(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Portlet.viewEmptyMessage();
	}

	macro viewCP {
		var key_ddlDataDefinitionName = "${structureName}";

		if (isSet(structureName)) {
			AssertTextEquals(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
				value1 = "${structureName}"
			);
		}

		if (isSet(structureDescription)) {
			AssertTextEquals(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_DESCRIPTION",
				value1 = "${structureDescription}"
			);
		}

		SelectFrame(value1 = "relative=top");
	}

	macro viewTableEntryCP {
		var key_ddlDataDefinitionName = "${structureName}";

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
			value1 = "${structureName}"
		);

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_DESCRIPTION",
			value1 = "${structureDescription}"
		);
	}

	macro viewTemplateValidation {
		Confirm(value1 = "Please add a template to render this structure.");
	}
}