definition {
	macro addCP {
		LexiconEntry.gotoAdd();

		WebContentStructures.addName(structureName = "${structureName}");

		Panel.expandPanel(panel = "Details");

		if (isSet(structureDescription)) {
			PortletEntry.inputDescriptionTextInput(description = "${structureDescription}");
		}

		SelectFrame(value1 = "relative=top");
	}

	macro addFavoriteStructure {
		AssertTextEquals.assertPartialText(
			locator1 = "Message#INFO",
			value1 = "You can add as many as 7 favorites in your quick menu."
		);

		Click(locator1 = "Icon#ADD_FAVORITE", key_rowEntry = "${structureName}");

		Alert.viewSuccessMessage();
	}

	macro addLocalization {
		PortletEntry.changeLocale(fieldName = "Name", locale = "${locale}");

		WebContentStructures.addName(structureName = "${localization1Name}");

		if (isSet(locale2)) {
			PortletEntry.changeLocale(fieldName = "Name", locale = "${locale2}");

			WebContentStructures.addName(structureName = "${localization2Name}");
		}
	}

	macro addName {
		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${structureName}");
	}

	macro addNullCP {
		LexiconEntry.gotoAdd();

		AssertElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Button.clickSave();

		Alert.viewRequiredField();

		SelectFrame(value1 = "relative=top");
	}

	macro addSubstructure {
		AssertClick(locator1 = "Button#SELECT", value1 = "Select");

		SelectFrame(value1 = "relative=top");

		SelectFrame(locator1 = "IFrame#DIALOG");

		var key_ddlDataDefinitionName = "${parentStructureName}";

		AssertClickNoError(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = "${parentStructureName}"
		);

		AssertConfirm.assertConfirmationNoWait(
			value1 = "Selecting a new structure deletes all unsaved content. Do you want to proceed?"
		);

		SelectFrame(value1 = "relative=top");
	}

	macro deleteCP {
		Navigator.gotoNavItem(navItem = "Structures");

		var key_ddlDataDefinitionName = "${structureName}";

		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		if ("${webContentHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The structure cannot be deleted because it is required by one or more templates."
			);
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro editSubstructureDefaultValuesCP {
		var key_ddlDataDefinitionName = "${substructureName}";

		Search.searchCP(searchTerm = "${substructureName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Default Values",
			rowEntry = "${substructureName}"
		);

		for (var structureFieldName : list "${editSubstructureFieldNames}") {
			var key_fieldDefaultValue = "${structureFieldName}";
			var structureFieldName = StringUtil.replace("${structureFieldName}", " ", "");

			Type(
				locator1 = "DDMField#DDM_INPUT",
				value1 = "Edited ${structureFieldName} Field"
			);
		}

		Button.clickSave();

		Alert.viewSuccessMessage();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		Navigator.gotoNavItem(navItem = "Structures");

		Search.searchCP(searchTerm = "${substructureName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Default Values",
			rowEntry = "${substructureName}"
		);

		for (var structureFieldName : list "${editSubstructureFieldNames}") {
			var key_fieldDefaultValue = "${structureFieldName}";

			AssertTextEquals(
				locator1 = "DDMField#DDM_INPUT",
				value1 = "Edited ${structureFieldName} Field"
			);
		}

		SelectFrameTop();
	}

	macro saveCP {
		DDLDataDefinition.save();

		if (isSet(addRoleViewPermissions)) {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Permissions",
				rowEntry = "${structureName}"
			);

			SelectFrame(locator1 = "IFrame#DIALOG");

			PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
				addOrRemoveViewPermission = "Add",
				roleList = "${addRoleViewPermissions}"
			);

			IFrame.selectTopFrame();
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		Navigator.gotoNavItem(navItem = "Structures");

		Search.searchCP(searchTerm = "2");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Search.searchCP(searchTerm = "Structure");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm.assertConfirmationNoWait(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Portlet.viewEmptyMessage(message = "There are no results.");
	}

	macro viewCP {
		var key_ddlDataDefinitionName = "${structureName}";

		if (isSet(structureName)) {
			AssertTextEquals(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
				value1 = "${structureName}"
			);
		}

		if (isSet(structureDescription)) {
			AssertTextEquals(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_DESCRIPTION",
				value1 = "${structureDescription}"
			);
		}

		SelectFrame(value1 = "relative=top");
	}

	macro viewFavoriteStructureViaIFrame {
		AssertTextEquals(
			locator1 = "WC#ADD_TO_FAVORITES",
			value1 = "Add to Favorites (${indexNumber})"
		);

		ScrollBy(value1 = "0, 50");

		AssertElementPresent(
			locator1 = "Icon#REMOVE_FAVORITE",
			key_rowEntry = "${structureName}"
		);
	}

	macro viewFavoriteStructureViaPlusButton {
		LexiconEntry.gotoAdd();

		MenuItem.viewPresent(menuItem = "More");

		MenuItem.viewPresent(menuItem = "favorites");

		MenuItem.viewPresent(menuItem = "${structureName}");
	}

	macro viewNoFavoriteStructure {
		AssertTextEquals(
			locator1 = "WC#ADD_TO_FAVORITES",
			value1 = "Add to Favorites (0)"
		);
	}

	macro viewStructuresViaMoreIFrame {
		AssertTextEquals(
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${structureName}",
			key_rowEntry = "${structureName}"
		);
	}

	macro viewTableEntryCP {
		var key_ddlDataDefinitionName = "${structureName}";

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
			value1 = "${structureName}"
		);

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_DESCRIPTION",
			value1 = "${structureDescription}"
		);
	}

	macro viewTemplateValidation {
		AssertConfirm(value1 = "Please add a template to render this structure.");
	}
}