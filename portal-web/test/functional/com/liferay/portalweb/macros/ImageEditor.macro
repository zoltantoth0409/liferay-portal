definition {

	macro _applyChanges {
		AssertClick(
			locator1 = "Button#APPLY_EFFECTS",
			value1 = "Apply");
	}

	macro _saveFile {
		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");
	}

	macro applyChangesAndSaveFile {
		ImageEditor._applyChanges();

		Pause(locator1 = "3000");

		ImageEditor._saveFile();
	}

	macro gotoImageEditorViaDM {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		Click(locator1 = "DocumentsAndMedia#ICON_DOCUMENT_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit With Image Editor");

		Pause(locator1 = "5000");

		SelectFrame(locator1 = "ImageEditor#IMAGE_EDITOR_IFRAME");
	}

	macro gotoImageEditorViaItemSelector {
		ItemSelector.gotoItemSelectorViaAlloyEditor();

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		var key_navTab = "${navTab}";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "${navTab}");

		var key_imageFileName = StringUtil.replace("${imageFileName}", " ", "+");

		Pause(locator1 = "1000");

		Click(locator1 = "ItemSelector#SELECT_FILE_IMAGE_PREVIEW");

		Click(locator1 = "Icon#EDIT_PENCIL");

		SelectFrameTop(value1 = "relative=top");

		SelectFrame(locator1 = "ImageEditor#IMAGE_EDITOR_IFRAME");
	}

	macro gotoRotateImage {
		Click(locator1 = "Button#TRANSFORM");

		MenuItem.click(menuItem = "Rotate");
	}

	macro rotateImage {
		ImageEditor.gotoRotateImage();

		if ("${rotateDirection}" == "left") {
			Click(locator1 = "Button#ROTATE_LEFT");
		}
		else if ("${rotateDirection}" == "right") {
			Click(locator1 = "Button#ROTATE_RIGHT");
		}
	}

	macro viewBaseControls {
		AssertVisible(locator1 = "Button#TRANSFORM");

		AssertVisible(locator1 = "Button#FILTER_EFFECT");

		AssertVisible(locator1 = "Button#COLOR_EFFECT");
	}

}