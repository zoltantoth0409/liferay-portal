definition {

	macro _addPage {
		SitePages._selectPageType(
			collectionName = "${collectionName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		SitePages._addPageToNavigationMenu(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			navigationMenus = "${navigationMenus}");

		PortletEntry.inputName(name = "${pageName}");

		if (isSet(categoryName)) {
			AssetCategorization.addCategory(
				categoryName = "${categoryName}",
				vocabularyName = "${vocabularyName}");
		}

		Button.clickAdd();

		SelectFrame.selectFrameTop();

		if ("${sitePageType}" == "Embedded") {
			Type(
				locator1 = "SitePagesEditPage#PAGE_TYPE_EMBEDDED_URL_FIELD",
				value1 = "${pageTypeURL}");
		}

		if ("${sitePageType}" == "Link to URL") {
			Type(
				locator1 = "TextInput#URL",
				value1 = "${pageTypeURL}");
		}

		if (isSet(inheritChanges)) {
			Page.togglePageTemplateInheritChanges(toggleValue = "${inheritChanges}");
		}

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		AssertVisible(locator1 = "TextInput#NAME");

		if (isSet(openGraph)) {
			SitePages._configureOpenGraph(
				customDescription = "${customDescription}",
				customTitle = "${customTitle}");
		}

		Button.click(button = "Save");

		WaitForSPARefresh();
	}

	macro addPrivatePage {
		PagesFinder.selectPageType(optionValue = "Private Pages");

		Click(locator1 = "PagesFinder#PAGE_TYPE_SELECTOR_ELLIPSIS_ICON");

		MenuItem.click(menuItem = "Add Page");

		PagesFinder._addPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			categoryName = "${categoryName}",
			collectionName = "${collectionName}",
			customDescription = "${customDescription}",
			customTitle = "${customTitle}",
			inheritChanges = "${inheritChanges}",
			navigationMenus = "${navigationMenus}",
			openGraph = "${openGraph}",
			pageName = "${pageName}",
			pageTemplateName = "${pageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			sitePageType = "${sitePageType}",
			vocabularyName = "${vocabularyName}");
	}

	macro addPublicChildPage {
		PagesFinder.selectPageType(optionValue = "Public Pages");

		Click(
			key_pageName = "${parentPageName}",
			locator1 = "PagesFinder#PAGE_ELLIPSIS_ICON");

		MenuItem.click(menuItem = "Add Child Page");

		PagesFinder._addPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			navigationMenus = "${navigationMenus}",
			pageName = "${childPageName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}");
	}

	macro addPublicCollectionPage {
		PagesFinder.selectPageType(optionValue = "Public Pages");

		Click(locator1 = "PagesFinder#PAGE_TYPE_SELECTOR_ELLIPSIS_ICON");

		MenuItem.click(menuItem = "Add Collection Page");

		Navigator.gotoNavTab(navTab = "${tabName}");

		if (isSet(menuItem)) {
			ContentPages.addSelection(
				collectionName = "${collectionName}",
				menuItem = "${menuItem}",
				selectedAsset = "${selectedAsset}");

			Click(locator1 = "Icon#BACK");
		}
		
		LexiconCard.clickCardTitle(card = "${collectionName}");

		if (isSet(masterLayoutName)) {
			LexiconCard.clickCard(card = "${masterLayoutName}");
		}
		else {
			LexiconCard.clickCard(card = "Blank");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${pageName}");

		if (isSet(categoryName)) {
			AssetCategorization.addCategory(
				categoryName = "${categoryName}",
				vocabularyName = "${vocabularyName}");
		}

		if (isSet(categoryNameList)) {
			AssetCategorization.addMultipleCategories(
				categoryNameList = "${categoryNameList}",
				vocabularyName = "${vocabularyName}");
		}

		Button.clickAdd();

		SelectFrame.selectFrameTop();

		PageEditor.assertFragmentSidebarLoaded();
	}

	macro addPublicContentPage {
		PagesFinder.selectPageType(optionValue = "Public Pages");

		Click(locator1 = "PagesFinder#PAGE_TYPE_SELECTOR_ELLIPSIS_ICON");

		MenuItem.click(menuItem = "Add Page");

		ContentPages._selectCard(
			collectionName = "${collectionName}",
			pageTemplateName = "${pageTemplateName}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${pageName}");

		if (isSet(categoryName)) {
			AssetCategorization.addCategory(
				categoryName = "${categoryName}",
				vocabularyName = "${vocabularyName}");
		}

		Button.clickAdd();

		SelectFrame.selectFrameTop();

		PageEditor.assertFragmentSidebarLoaded();
	}

	macro addPublicPage {
		PagesFinder.selectPageType(optionValue = "Public Pages");

		Click(locator1 = "PagesFinder#PAGE_TYPE_SELECTOR_ELLIPSIS_ICON");

		MenuItem.click(menuItem = "Add Page");

		PagesFinder._addPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			categoryName = "${categoryName}",
			collectionName = "${collectionName}",
			customDescription = "${customDescription}",
			customTitle = "${customTitle}",
			inheritChanges = "${inheritChanges}",
			navigationMenus = "${navigationMenus}",
			openGraph = "${openGraph}",
			pageName = "${pageName}",
			pageTemplateName = "${pageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			sitePageType = "${sitePageType}",
			vocabularyName = "${vocabularyName}");
	}

	macro goToFindAPage {
		ProductMenuHelper.openProductMenu();

		if (IsElementNotPresent(locator1 = "PagesFinder#INPUT_FIELD")) {
			Click(locator1 = "PagesFinder#OUTLINE_ICON");
		}
	}

	macro gotoPageEllipsisItem {
		Click.mouseOverClick(
			key_pageName = "${pageName}",
			locator1 = "PagesFinder#PAGE_ELLIPSIS_ICON");

		MenuItem.clickNoError(menuItem = "${item}");
	}

	macro inputPageSearch {
		Type(
			locator1 = "PagesFinder#INPUT_FIELD",
			value1 = "${keyword}");
	}

	macro navigateToChildPage {
		AssertClick(
			key_childPageName = "${childPageName}",
			key_parentPageName = "${parentPageName}",
			locator1 = "PagesFinder#CHILD_PAGE_NAME",
			value1 = "${childPageName}");

		AssertTextEquals(
			locator1 = "ControlMenu#HEADING",
			value1 = "${childPageName}");
	}

	macro navigateToPage {
		Click(
			locator1 = "PagesFinder#PAGE_NAME",
			value1 = "${pageName}");

		AssertTextEquals(
			locator1 = "ControlMenu#HEADING",
			value1 = "${pageName}");
	}

	macro selectPageType {
		Select(
			locator1 = "PagesFinder#PAGE_TYPE_SELECTOR",
			value1 = "${optionValue}");
	}

	macro viewInPagesAdministration {
		Click(locator1 = "PagesFinder#VIEW_IN_PAGE_ADMINISTRATION");
	}

	macro viewPageNameInPagesFinder {
		AssertElementPresent(locator1 = "PagesFinder#PAGE_NAME");
	}

}