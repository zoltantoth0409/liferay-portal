definition {

	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${pageTemplateName}");

		PortletEntry.inputDescriptionTextInput(description = "This is a page template description");

		if ("${pageTemplateActive}" == "Not Active") {
			Click(locator1 = "ToggleSwitch#ACTIVE");
		}

		PortletEntry.save();

		PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName}");

		LexiconCard.viewCardLabel(
			card = "${pageTemplateName}",
			cardLabel = "${pageTemplateActive}");
	}

	macro deactivateAndActivate {
		PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName}");

		PageTemplatesAdminGlobalSite.gotoPageTemplateEllipsisItem(
			item = "Configure",
			pageTemplateName = "${pageTemplateName}");

		if (isSet(pageTemplateStatus)) {
			Click(locator1 = "ToggleSwitch#ACTIVE");
		}

		PortletEntry.save();
	}

	macro deleteCP {
		LexiconCard.delete(card = "${pageTemplateName}");

		Alert.viewSuccessMessage();
	}

	macro editCP {
		PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName}");

		PageTemplatesAdminGlobalSite.gotoPageTemplateEllipsisItem(
			item = "Configure",
			pageTemplateName = "${pageTemplateName}");

		PortletEntry.inputName(name = "${pageTemplateName} Edit");

		if ("${pageTemplateActive}" == "Not Active") {
			Click(locator1 = "ToggleSwitch#ACTIVE");
		}

		Button.clickSave();

		var key_pageTemplateName = "${pageTemplateName}";

		Alert.viewSuccessMessage();

		PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName} Edit");

		PageTemplatesAdminGlobalSite.viewStatus(
			pageTemplateName = "${pageTemplateName} Edit",
			pageTemplateStatus = "${pageTemplateActive}");
	}

	macro editDescription {
		PageTemplatesAdminGlobalSite.viewDescription(
			pageTemplateDescription = "${pageTemplateDescription}",
			pageTemplateName = "${pageTemplateName}");

		PortletEntry.inputDescriptionTextInput(description = "This is a page template description edit");

		PortletEntry.save();
	}

	macro gotoPageTemplateEllipsisItem {
		LexiconCard.clickEllipsisItem(
			card = "${pageTemplateName}",
			item = "${item}");
	}

	macro gotoPageTemplateViaEdit {
		PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName}");

		LexiconCard.clickCardTitle(card = "${pageTemplateName}");
	}

	macro openGlobalPageTemplatesAdmin() {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/global/~/control_panel/manage?p_p_id=com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet");
	}

	macro tearDownCP {
		PageTemplatesAdminGlobalSite.openGlobalPageTemplatesAdmin();

		var pageTemplateName = "Page Template Name";

		while (IsElementPresent(locator1 = "Card#CARD_ELLIPSIS", key_card = "${pageTemplateName}")) {
			PageTemplatesAdminGlobalSite.deleteCP(pageTemplateName = "${pageTemplateName}");
		}
	}

	macro viewCP {
		AssertTextEquals.assertPartialText(
			key_pageTemplateName = "${pageTemplateName}",
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
			value1 = "${pageTemplateName}");
	}

	macro viewDefaultCP {
		for (var pageTemplateName : list "Search,Blog,Wiki") {
			PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName}");

			AssertElementNotPresent(
				key_rowNumber = "4",
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER");
		}

		AssertElementNotPresent(locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME_4");
	}

	macro viewDefaultLocalizedCP {
		for (var pageTemplateNameTranslation : list "${pageTemplateNameList}") {
			var key_pageTemplateName = "${pageTemplateNameTranslation}";

			AssertTextEquals.assertPartialText(
				locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
				value1 = "${pageTemplateNameTranslation}");
		}

		SiteAdmin.viewPortletTitle(portletTitle = "${portletTitle}");
	}

	macro viewDefaultPortletPG {
		if ("${pageTemplateName}" == "Blog") {
			AssertElementPresent(locator1 = "Home#PORTLET_1");

			AssertElementPresent(locator1 = "Home#PORTLET_2");

			AssertElementPresent(locator1 = "Home#PORTLET_3");

			for (var portletName : list "${portletTitleList}") {
				var key_portletName = "${portletName}";

				AssertTextEquals(
					locator1 = "Portlet#TITLE",
					value1 = "${portletName}");
			}
		}
	}

	macro viewDescription {
		PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName}");

		PageTemplatesAdminGlobalSite.gotoPageTemplateEllipsisItem(
			item = "Configure",
			pageTemplateName = "${pageTemplateName}");

		AssertTextEquals.assertValue(
			locator1 = "TextInput#DESCRIPTION",
			value1 = "${pageTemplateDescription}");
	}

	macro viewPageTemplatePresent {
		LexiconCard.viewCardTitle(card = "${pageTemplateName}");
	}

	macro viewPG {
		var key_pageTemplateName = "${pageTemplateName}";

		AssertClick.assertPartialTextClickAt(
			locator1 = "PageTemplates#PAGE_TEMPLATE_TABLE_NAME",
			value1 = "${pageTemplateName}");

		SelectWindow(locator1 = "title=${pageTemplateName} - ${siteName}");

		AssertTextEquals(
			locator1 = "Home#SITE_NAME",
			value1 = "${pageTemplateName}");
	}

	macro viewStatus {
		PageTemplatesAdminGlobalSite.viewPageTemplatePresent(pageTemplateName = "${pageTemplateName}");

		LexiconCard.viewCardLabel(
			card = "${pageTemplateName}",
			cardLabel = "${pageTemplateStatus}");
	}

}