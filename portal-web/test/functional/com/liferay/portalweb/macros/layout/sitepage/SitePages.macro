definition {

	macro _addPage {
		SitePages._selectPageType(
			collectionName = "${collectionName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		SitePages._addPageToNavigationMenu(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			navigationMenus = "${navigationMenus}");

		PortletEntry.inputName(name = "${pageName}");

		if (isSet(categoryName)) {
			AssetCategorization.addCategory(
				categoryName = "${categoryName}",
				vocabularyName = "${vocabularyName}");
		}

		Button.clickAdd();

		SelectFrame.selectFrameTop();

		if ("${sitePageType}" == "Embedded") {
			Type(
				locator1 = "SitePagesEditPage#PAGE_TYPE_EMBEDDED_URL_FIELD",
				value1 = "${pageTypeURL}");
		}

		if (isSet(inheritChanges)) {
			Page.togglePageTemplateInheritChanges(toggleValue = "${inheritChanges}");
		}

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		AssertVisible(locator1 = "TextInput#NAME");

		if (isSet(openGraph)) {
			SitePages._configureOpenGraph(
				customDescription = "${customDescription}",
				customTitle = "${customTitle}");
		}

		Button.click(button = "Save");
	}

	@summary = "Add newly created pages to specified navigation menus or assert that the option to add the pages to navigation menus is missing."
	macro _addPageToNavigationMenu {
		if (isSet(navigationMenus)) {
			Pause(locator1 = "5000");

			for (var navigationMenu : list "${navigationMenus}") {
				if ("${assertCheckboxMissing}" == "true") {
					AssertElementNotPresent(
						checkboxName = "${navigationMenu}",
						locator1 = "Checkbox#ANY_CHECKBOX");
				}
				else {
					Check(
						checkboxName = "${navigationMenu}",
						locator1 = "Checkbox#ANY_CHECKBOX");

					AssertChecked(
						checkboxName = "${navigationMenu}",
						locator1 = "Checkbox#ANY_CHECKBOX");
				}
			}
		}
	}

	@summary = "Configures openGraph for a site page. This should be moved outside of _addPage for stability."
	macro _configureOpenGraph {
		Navigator.gotoNavTab(navTab = "Open Graph");

		var checkboxName = "Use Custom Title";

		if (IsNotChecked(locator1 = "Checkbox#ANY_CHECKBOX")) {
			Click(locator1 = "Checkbox#ANY_CHECKBOX");
		}

		Type(
			locator1 = "TextInput#OPEN_GRAPH_CUSTOM_TITLE",
			value1 = "${customTitle}");

		var checkboxName = "Use Custom Description";

		if (IsNotChecked(locator1 = "Checkbox#ANY_CHECKBOX")) {
			Click(locator1 = "Checkbox#ANY_CHECKBOX");
		}

		Type(
			key_text = "openGraphDescription",
			locator1 = "TextArea#ANY",
			value1 = "${customDescription}");
	}

	macro _configurePage {
		if ("${sitePageType}" == "Embedded") {
			Page._addPageTypeEmbedded(pageTypeURL = "${pageTypeURL}");
		}
		else if ("${sitePageType}" == "Panel") {
			Page._addPageTypePanel(componentName = "${componentName}");
		}
		else if ("${sitePageType}" == "Widget Page") {
			var key_columnName = "${columnName}";

			Click(locator1 = "SitePagesEditPage#LAYOUT_CARD");
		}
		else if ("${sitePageType}" == "Content Page") {
			Select(
				locator1 = "SitePagesEditPage#LAYOUT_PAGE_TEMPLATE_SELECT",
				value1 = "${layoutPageTemplateName}");
		}
		else if ("${sitePageType}" == "Full Page Application") {
			Page._addPageTypeFullPageApplication(portlet = "${portlet}");
		}
	}

	macro _selectPageType {
		if (!(contains("${pageTemplateName}", "{pageTemplateName}"))) {
			var sitePageType = "${pageTemplateName}";

			if (isSet(collectionName)) {
				Navigator.gotoNavNested(navNested = "${collectionName}");
			}
			else {
				Navigator.gotoNavNested(navNested = "Global Templates");
			}
		}
		else if (isSet(sitePageType)) {
			var sitePageType = "${sitePageType}";
		}
		else {
			var sitePageType = "Widget Page";
		}

		LexiconCard.viewCardPresent(card = "${sitePageType}");

		Pause(locator1 = "3000");

		LexiconCard.clickCard(card = "${sitePageType}");
	}

	macro addChildPage {
		MouseOver(
			key_itemName = "${parentPageName}",
			locator1 = "SitePages#LIST_GROUP_ITEM");

		Click(
			key_pageName = "${parentPageName}",
			locator1 = "SitePages#LIST_GROUP_ITEM_PLUS_ICON");

		DropdownMenuItem.click(menuItem = "Add Page");

		SitePages._addPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			navigationMenus = "${navigationMenus}",
			pageName = "${childPageName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}");
	}

	macro addPrivateChildPage {
		if (IsTextNotEqual(locator1 = "ControlMenu#HEADING", value1 = "Pages")) {
			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");
		}

		SitePages.togglePrivacy(privacy = "Private Pages");

		SitePages.addChildPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			childPageName = "${childPageName}",
			navigationMenus = "${navigationMenus}",
			pageTemplateName = "${pageTemplateName}",
			parentPageName = "${parentPageName}",
			sitePageType = "${sitePageType}");
	}

	macro addPrivatePage {
		if (IsTextNotEqual(locator1 = "ControlMenu#HEADING", value1 = "Pages")) {
			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");
		}

		Pause(locator1 = "3000");

		var key_breadcrumbName="Pages";

		if ("${siteTemplate}" == "true") {
			LexiconEntry.gotoAddMenuItem(menuItem = "Add Site Template Page");
		}
		else if (IsElementPresent(locator1 = "Message#EMPTY_INFO_TAGLIB")) {
			SitePages.gotoAddFirstPage(pageType = "Private Page");
		}
		else {
			AssertClick(
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Pages");

			LexiconEntry.gotoAddMenuItem(menuItem = "Private Page");
		}

		SitePages._addPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			navigationMenus = "${navigationMenus}",
			pageName = "${pageName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}");
	}

	macro addPublicChildPage {
		if (IsTextNotEqual(locator1 = "ControlMenu#HEADING", value1 = "Pages")) {
			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");
		}

		SitePages.togglePrivacy(privacy = "Public Pages");

		SitePages.addChildPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			childPageName = "${childPageName}",
			navigationMenus = "${navigationMenus}",
			pageTemplateName = "${pageTemplateName}",
			parentPageName = "${parentPageName}",
			sitePageType = "${sitePageType}");
	}

	macro addPublicPage {
		if (IsTextNotEqual(locator1 = "ControlMenu#HEADING", value1 = "Pages")) {
			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");
		}

		Pause(locator1 = "3000");

		var key_breadcrumbName="Pages";

		if (IsElementPresent(locator1 = "Message#EMPTY_INFO_TAGLIB")) {
			SitePages.gotoAddFirstPage(pageType = "Public Page");
		}
		else {
			AssertClick(
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY_EXACT_MATCH",
				value1 = "Pages");

			LexiconEntry.gotoAddMenuItem(menuItem = "Public Page");
		}

		SitePages._addPage(
			assertCheckboxMissing = "${assertCheckboxMissing}",
			categoryName = "${categoryName}",
			collectionName = "${collectionName}",
			customDescription = "${customDescription}",
			customTitle = "${customTitle}",
			inheritChanges = "${inheritChanges}",
			navigationMenus = "${navigationMenus}",
			openGraph = "${openGraph}",
			pageName = "${pageName}",
			pageTemplateName = "${pageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			sitePageType = "${sitePageType}",
			vocabularyName = "${vocabularyName}");
	}

	macro addPublicPageUnderSitePagesVariation {
		LexiconEntry.gotoAddMenuItem(menuItem = "Public Page");

		SitePages._selectPageType(
			collectionName = "${collectionName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${pageName}");

		Button.clickAdd();

		SelectFrame.selectFrameTop();

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		AssertVisible(locator1 = "TextInput#NAME");

		Button.click(button = "Save");

		WaitForSPARefresh();
	}

	macro checkPage {
		var key_pageName = "${pageName}";

		Check.checkNotVisible(locator1 = "SitePages#LIST_GROUP_ITEM_CHECKBOX");
	}

	macro configureChildPage {
		if (IsTextNotEqual(locator1 = "ControlMenu#HEADING", value1 = "Pages")) {
			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");
		}

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "${pageName}",
			parentPagesHierarchy = "${parentPagesHierarchy}");

		SitePages._configurePage(
			columnName = "${columnName}",
			componentName = "${componentName}",
			layoutPageTemplateName = "${layoutPageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			portlet = "${portlet}",
			sitePageType = "${sitePageType}");

		PortletEntry.save();
	}

	macro configurePage {
		if (IsTextNotEqual(locator1 = "ControlMenu#HEADING", value1 = "Pages")) {
			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");
		}

		SitePages.togglePrivacy(privacy = "${privacy}");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "${pageName}");

		SitePages._configurePage(
			columnName = "${columnName}",
			componentName = "${componentName}",
			layoutPageTemplateName = "${layoutPageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			portlet = "${portlet}",
			sitePageType = "${sitePageType}");

		PortletEntry.save();
	}

	macro deletePage {
		var browserType = PropsUtil.get("browser.type");

		if ("${browserType}" == "chrome") {
			MetalComponent.waitForLayout();
		}

		Click(
			key_pageName = "${pageName}",
			locator1 = "SitePages#LIST_GROUP_ITEM_ELLIPSIS_ICON");

		MenuItem.clickNoError(menuItem = "Delete");

		if (isSet(hasChildren)) {
			AssertConfirm(value1 = "This page has child pages that will also be removed. Are you sure you want to delete this page?");
		}
		else {
			AssertConfirm(value1 = "Are you sure you want to delete this page?");
		}

		if (isSet(failure)) {
			if (IsElementPresent(locator1 = "Message#ERROR_2")) {
				Alert.viewRequestFailedToComplete();

				AssertTextEquals(
					locator1 = "Message#ERROR_2",
					value1 = "${failure}");
			}
			else {
				AssertTextEquals(
					locator1 = "Message#ERROR",
					value1 = "${failure}");
			}
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro expandPageTree {
		if (!(isSet(depth))) {
			var depth = "1";
		}

		var count = "0";

		while ("${count}" != "${depth}") {
			var index = ${count} + 1;

			Click(
				key_depth = "${index}",
				locator1 = "SitePages#LIST_GROUP_ITEM_DEPTH");

			var count = ${count} + 1;
		}
	}

	@summary = "Go to the page creation form through the empty-result-message taglib"
	macro gotoAddFirstPage {
		var key_menuItem = "${pageType}";

		Refresh();

		Click(locator1 = "Message#EMPTY_INFO_TAGLIB");

		Click.waitForMenuToggleJSClick(locator1 = "Dropdown#EMPTY_MESSAGE_DROPDOWN");

		Click(locator1 = "MenuItem#DROPDOWN_MENU_ITEM");
	}

	@summary = "Go to the pageMenuItem of a Site Page. Parent pages hierarchy is required to navigate to child pages."
	macro gotoPageMenuItem {
		var browserType = PropsUtil.get("browser.type");

		if ("${browserType}" == "chrome") {
			MetalComponent.waitForLayout();
		}

		if (isSet(parentPagesHierarchy)) {
			for (var parentPage : list "${parentPagesHierarchy}") {
				Click(
					key_itemName = "${parentPage}",
					locator1 = "SitePages#LIST_GROUP_ITEM");
			}
		}

		if ("${browserType}" == "internetexplorer") {
			Click(
				key_pageName = "${pageName}",
				locator1 = "SitePages#LIST_GROUP_ITEM_ELLIPSIS_ICON_IE");
		}
		else {
			Click(
				key_pageName = "${pageName}",
				locator1 = "SitePages#LIST_GROUP_ITEM_ELLIPSIS_ICON");
		}

		MenuItem.click(menuItem = "${menuItem}");
	}

	macro movePageToPageUp {
		var key_pageName = "${pageName}";
		var key_pageNameTo = "${pageNameTo}";

		DragAndDrop.javaScriptDragAndDropToUp(
			locator1 = "SitePages#LIST_GROUP_ITEM_DRAG_HANDLER",
			locator2 = "SitePages#LIST_GROUP_ITEM_DROP_TARGET",
			value1 = "");
	}

	macro openPagesAdmin {
		var siteURLKey = StringUtil.replace("${siteURLKey}", " ", "-");
		var siteURLKey = StringUtil.lowerCase("${siteURLKey}");

		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_layout_admin_web_portlet_GroupPagesPortlet");
	}

	macro togglePrivacy {
		var browserType = PropsUtil.get("browser.type");

		if ("${browserType}" == "chrome") {
			MetalComponent.waitForLayout();
		}

		if (IsElementNotPresent(locator1 = "Breadcrumb#BREADCRUMB_ENTRY_EXACT_MATCH", key_breadcrumbName = "${privacy}")) {
			Click(
				key_itemName = "${privacy}",
				locator1 = "SitePages#LIST_GROUP_ITEM");
		}
	}

	macro viewCheckboxChecked {
		AssertChecked.assertCheckedNotVisible(
			key_pageName = "${pageName}",
			locator1 = "SitePages#LIST_GROUP_ITEM_CHECKBOX");
	}

	macro viewPageItems {
		AssertVisible(
			key_pageName = "${pageName}",
			locator1 = "SitePages#LIST_GROUP_ITEM_ENTRY_TITLE");
	}

}