definition {

	macro _addPageTemplate {
		if (isSet(pageTemplateType)) {
			var menuItem = "${pageTemplateType}";
		}
		else {
			var menuItem = "Content Page Template";
		}

		if ("${siteTemplate}" == "true") {
			LexiconEntry.gotoAdd();
		}
		else {
			LexiconEntry.gotoAddMenuItem(menuItem = "${menuItem}");
		}

		PortletEntry.inputName(name = "${pageTemplateName}");

		Button.clickSave();
	}

	macro _gotoAddCollection {
		if (IsElementPresent(locator1 = "NavNested#NAV_NESTED_PLUS")) {
			Click(locator1 = "NavNested#NAV_NESTED_PLUS");
		}
		else {
			Button.click(button = "New");
		}
	}

	macro addCollection {
		PageTemplatesAdmin._gotoAddCollection();

		Collection.editCollection(
			collectionDescription = "${collectionDescription}",
			collectionName = "${collectionName}");

		Button.clickSave();
	}

	macro addDuplicatePageTemplate {
		PageTemplatesAdmin._addPageTemplate(
			menuItem = "${menuItem}",
			pageTemplateName = "${pageTemplateName}",
			pageTemplateType = "${pageTemplateType}",
			siteTemplate = "${siteTemplate}");

		AssertTextEquals(
			locator1 = "Message#ERROR_MODAL_CONTENT",
			value1 = "A page template entry with that name already exists.");
	}

	macro addMastersPageTemplate {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${pageTemplateName}");

		Button.clickSave();

		PageEditor.addFragment(
			collectionName = "Drop Zone",
			fragmentName = "Drop Zone Component");

		PageEditor.clickPublish();
	}

	macro addPageTemplate {
		PageTemplatesAdmin._addPageTemplate(
			menuItem = "${menuItem}",
			pageTemplateName = "${pageTemplateName}",
			pageTemplateType = "${pageTemplateType}",
			siteTemplate = "${siteTemplate}");

		if ("${pageTemplateType}" == "Content Page Template") {
			PageEditor.assertFragmentSidebarLoaded();
		}
	}

	macro configureWidgetPageTemplateLayout {
		Click.pauseClickAt(locator1 = "ControlMenu#EDIT_COG");

		Click(
			key_columnName = "${columnName}",
			locator1 = "SitePagesEditPage#LAYOUT_CARD");

		PortletEntry.save();
	}

	macro deletePageTemplate {
		LexiconCard.delete(card = "${pageTemplateName}");
	}

	macro gotoPageTemplate {
		LexiconCard.clickCardTitle(card = "${pageTemplateName}");
	}

	macro gotoPageTemplateEllipsisItem {
		LexiconCard.clickEllipsisItem(
			card = "${pageTemplateName}",
			item = "${item}");
	}

	macro openPageTemplatesAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet&_com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet_tabs1=page-templates");
	}

	macro openPageTemplatesMenuAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet");
	}

	macro renamePageTemplate {
		PageTemplatesAdmin.gotoPageTemplateEllipsisItem(
			item = "Rename",
			pageTemplateName = "${pageTemplateName}");

		PortletEntry.inputName(name = "${newPageTemplateName}");

		Modal.save();
	}

	macro viewPageTemplateNotPresent {
		LexiconCard.viewCardNotPresent(card = "${pageTemplateName}");
	}

	macro viewPageTemplatePresent {
		LexiconCard.viewCardPresent(card = "${pageTemplateName}");
	}

}