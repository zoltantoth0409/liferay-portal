definition {
	@summary = "Helper function to add fields '${templateFieldNames}' to the template"
	macro _addFields {
		if (isSet(templateFieldNames)) {
			for (var fieldName : list "${templateFieldNames}") {
				var key_fieldName = "${fieldName}";

				AssertClick(locator1 = "DDMEditTemplate#SCRIPT_FIELD", value1 = "${fieldName}");
			}
		}
	}

	@summary = "Helper function to add description '${description}' to the template"
	macro _inputDescription {
		if (isSet(templateDescription)) {
			PortletEntry.inputDescription(description = "${templateDescription}");
		}
	}

	@summary = "Helper function to add a script to the template"
	macro _inputScript {
		if (isSet(templateScript)) {
			Type.typeAceEditor(
				locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD_TEXT_AREA",
				value1 = "${templateScript}"
			);
		}
	}

	@summary = "Helper function to link structure '${structureName}' to the template"
	macro _inputStructure {
		if (isSet(structureName)) {
			AssertClick(
				locator1 = "DDMEditTemplate#DETAILS_SELECT_STRUCTURE",
				value1 = "Select"
			);

			SelectFrameTop();

			SelectFrame(locator1 = "IFrame#DIALOG");

			Click(locator1 = "Icon#BASIC_SEARCH");

			var key_ddlDataDefinitionName = "${structureName}";

			AssertClickNoError(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
				value1 = "${structureName}"
			);

			SelectFrameTop();
		}
	}

	@summary = "Helper function to add a script to a template via file"
	macro _inputTemplateFile {
		if (isSet(templateFile)) {
			UploadCommonFile(
				locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
				value1 = "${templateFile}"
			);
		}
	}

	@summary = "Helper function to select template language 'templateLanguage' to read the template in"
	macro _inputTemplateLanguage {
		if ("${templateLanguage}" == "vm") {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Velocity (.vm)"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Extensible Stylesheet Language (.xsl)"
			);
		}
		else {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "FreeMarker (.ftl)"
			);
		}

		if ("${templateLanguage}" == "vm") {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Velocity (.vm)"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Extensible Stylesheet Language (.xsl)"
			);
		}
		else {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "FreeMarker (.ftl)"
			);
		}
	}

	@summary = "Click on the add button to add a template named '${templateName}' for structure '${structureName}', input most basic fields and then save the template"
	macro addCP {
		LexiconEntry.gotoAdd();

		WebContentTemplates.inputTemplateAddScreenFields(
			structureName = "${structureName}",
			templateDescription = "${templateDescription}",
			templateFieldNames = "${templateFieldNames}",
			templateFile = "${templateFile}",
			templateLanguage = "${templateLanguage}",
			templateName = "${templateName}",
			templateScript = "${templateScript}",
			templateSmallImageURL = "${templateSmallImageURL}"
		);

		DDLDataDefinition.save();
	}

	@summary = "Attempt to add a blank template"
	macro addNullCP {
		LexiconEntry.gotoAdd();

		AssertElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Button.clickSave();

		Alert.viewRequiredField();

		SelectFrame(value1 = "relative=top");
	}

	@summary = "Give the roles '${roles}' permissions to view the template '${templateName}'"
	macro addRoleViewPermissions {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${templateName}"
		);

		SelectFrame(locator1 = "IFrame#DIALOG");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Add",
			roleList = "${roles}"
		);

		SelectFrameTop();
	}

	@summary = "Attempt to delete the template '${templateName}'"
	macro deleteCP {
		var key_ddlTemplateName = "${templateName}";

		Click(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		if ("${webContentHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The template cannot be deleted because it is required by one or more template links."
			);
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	@summary = "Click on '${templateName}' ellipsis > '${item}'"
	macro gotoTemplateEllipsisItem {
		LexiconCard.clickEllipsisItem(
			card = "${templateName}",
			item = "${item}"
		);
	}

	@summary = "Input a small image with the url '${templateSmallImageURL}'"
	macro inputSmallImage {
		if (isSet(templateSmallImageURL)) {
			if (IsElementNotPresent(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL")) {
				Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");
			}
			else {
				Click(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");

				Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");
			}
		}

		Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL");

		Type(
			locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL_INPUT",
			value1 = "${templateSmallImageURL}"
		);
	}

	@summary = "Input most basic fields inside a template"
	macro inputTemplateAddScreenFields {
		if (isSet(templateName)) {
			PortletEntry.inputName(name = "${templateName}");
		}

		if (isSet(structureName)) {
			WebContentTemplates._inputStructure(structureName = "${structureName}");
		}

		WebContentTemplates._inputDescription(
			templateDescription = "${templateDescription}"
		);

		WebContentTemplates._inputTemplateLanguage(
			templateLanguage = "${templateLanguage}"
		);

		if (isSet(templateSmallImageURL)) {
			WebContentTemplates.inputSmallImage(
				templateSmallImageURL = "${templateSmallImageURL}"
			);
		}

		WebContentTemplates._inputScript(templateScript = "${templateScript}");

		if (("${templateLanguage}" == "ftl") || ("${templateLanguage}" =="vm")) {
			WebContentTemplates._addFields(templateFieldNames = "${templateFieldNames}");
		}

		WebContentTemplates._inputTemplateFile(templateFile = "${templateFile}");
	}

	@summary = "Add localization for the template name"
	macro localizeTemplateName {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		var key_ddlTemplateName = "${templateName}";

		WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

		AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${templateName}");

		PortletEntry.changeLocale(fieldName = "Name", locale = "${locale}");

		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${localization1Name}");

		if (isSet(locale2)) {
			PortletEntry.changeLocale(fieldName = "Name", locale = "${locale2}");

			Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${localization2Name}");
		}
	}

	@summary = "Modify template"
	macro modifyTemplate {
		LexiconCard.clickEllipsisItem(
			card = "${templatePreviousName}",
			item = "Edit"
		);

		WebContentTemplates.inputTemplateAddScreenFields(
			templateDescription = "${templateDescription}",
			templateFieldNames = "${templateFieldNames}",
			templateFile = "${templateFile}",
			templateLanguage = "${templateLanguage}",
			templateName = "${templateName}",
			templateScript = "${templateScript}",
			templateSmallImageURL = "${templateSmallImageURL}"
		);

		DDLDataDefinition.save();
	}

	@summary = "Teardown templates in the guest site"
	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		Navigator.gotoNavItem(navItem = "Templates");

		Search.searchCP(searchTerm = "2");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Search.searchCP(searchTerm = "Template");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Portlet.viewEmptyMessage(message = "There are no results.");
	}

	@summary = "Assert that certain template attributes are present for the template '${templateName}'"
	macro viewCP {
		var key_ddlStructureName = "${structureName}";
		var key_ddlTemplateDescription = "${templateDescription}";
		var key_ddlTemplateName = "${templateName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
			value1 = "${templateDescription}"
		);

		if (isSet(structureName)) {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_MODE",
				value1 = "${structureName}"
			);
		}

		if ("${templateLanguage}" == "vm") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Velocity"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Extensible Stylesheet Language"
			);
		}
		else {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "FreeMarker"
			);
		}

		if (!("${userEditPermission}" == "noEditPermission")) {
			WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

			AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${templateName}");

			AssertTextEquals(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${templateDescription}"
			);

			if (isSet(structureName)) {
				AssertTextEquals.assertValue(
					locator1 = "DDMEditTemplate#DETAILS_STRUCTURE",
					value1 = "${structureName}"
				);
			}

			if ("${templateLanguage}" == "vm") {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "Velocity (.vm)"
				);
			}
			else if ("${templateLanguage}" == "xsl") {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "Extensible Stylesheet Language (.xsl)"
				);
			}
			else {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "FreeMarker (.ftl)"
				);
			}

			if (isSet(templateScript)) {
				AssertTextEquals.assertPartialTextAceEditor(
					locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD",
					value1 = "${templateScript}"
				);
			}
		}
	}

	@summary = "Assert that the table entry for template '${templateName}' is present"
	macro viewTableEntryCP {
		var key_ddlTemplateName = "${templateName}";

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		if (isSet(templateSmallImage)) {
			AssertVisible(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION_SMALL_IMAGE",
				value1 = "${templateSmallImage}"
			);
		}
		else {
			var key_ddlTemplateDescription = "${templateDescription}";

			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
				value1 = "${templateDescription}"
			);
		}
	}
}