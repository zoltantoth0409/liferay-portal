definition {
	macro addCP {
		LexiconEntry.gotoAdd();

		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${templateName}");

		Panel.expandPanel(panelHeading = "Details");

		if (isSet(templateDescription)) {
			Panel.expandPanel(panelHeading = "Details");

			PortletEntry.inputDescription(description = "${templateDescription}");
		}

		if ("${templateLanguage}" == "ftl") {
			Panel.expandPanel(panelHeading = "Details");

			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "FreeMarker (.ftl)"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Extensible Stylesheet Language (.xsl)"
			);
		}
		else {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Velocity (.vm)"
			);
		}

		Panel.expandPanel(panelHeading = "Details");

		if ("${templateLanguage}" == "ftl") {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "FreeMarker (.ftl)"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Extensible Stylesheet Language (.xsl)"
			);
		}
		else {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Velocity (.vm)"
			);
		}

		if (isSet(templateFile)) {
			UploadCommonFile(
				locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
				value1 = "${templateFile}"
			);
		}

		if (isSet(templateScript)) {
			Type.typeAceEditor(
				locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD_TEXT_AREA",
				value1 = "${templateScript}"
			);
		}

		if (isSet(templateSmallImageURL)) {
			WebContentTemplates.inputSmallImage(
				templateSmallImageURL = "${templateSmallImageURL}"
			);
		}
	}

	macro addField {
		var fieldNameLabel = StringUtil.replace("${fieldName}", " ", "");
		var fieldNameLabel = StringUtil.lowerCase("${fieldNameLabel}");
		var key_fieldName = "${fieldName}";

		AssertClick(locator1 = "DDMEditTemplate#SCRIPT_FIELD", value1 = "${fieldName}");
	}

	macro addNullCP {
		WebContentNavigator.gotoManageTemplates();

		LexiconEntry.gotoAdd();

		AssertElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "Message#ERROR_FIELD_REQUIRED",
			value1 = "This field is required."
		);

		SelectFrame(value1 = "relative=top");
	}

	macro addRoleViewPermissions {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${templateName}"
		);

		IFrame.selectDialogFrame();

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Add",
			roleList = "${roles}"
		);

		SelectFrameTop();
	}

	macro addStructure {
		Panel.expandPanel(panelHeading = "Details");

		AssertClick(
			locator1 = "DDMEditTemplate#DETAILS_SELECT_STRUCTURE",
			value1 = "Select"
		);

		SelectFrameTop();

		IFrame.selectDialogFrame();

		Click(locator1 = "Icon#BASIC_SEARCH");

		var key_ddlDataDefinitionName = "${structureName}";

		AssertClickNoError(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
			value1 = "${structureName}"
		);

		Confirm(
			value1 = "Selecting a new structure deletes all unsaved content. Do you want to proceed?"
		);

		SelectFrameTop();

		if ("${templateLanguage}" == "ftl") {
			for (var templateFieldName : list "${templateFieldNames}") {
				WebContentTemplates.addField(
					fieldName = "${templateFieldName}",
					fieldNameLabel = "${fieldNameLabel}"
				);
			}
		}
	}

	macro advancedSearchCP {
		WebContentNavigator.gotoManageTemplates();

		var key_ddlTemplateName = "${templateName}";

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		Click(locator1 = "Icon#ADVANCED_SEARCH");

		if (isSet(templateName)) {
			PortletEntry.inputName(name = "${templateName}");
		}

		if (isSet(templateDescription)) {
			PortletEntry.inputDescriptionTextInput(description = "${templateDescription}");
		}

		Click(locator1 = "Icon#BASIC_SEARCH");

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
			value1 = "${templateDescription}"
		);
	}

	macro deleteCP {
		WebContentNavigator.gotoManageTemplates();

		var key_ddlTemplateName = "${templateName}";

		Click(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS");

		AssertClickNoError(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete",
			key_menuItem = "Delete"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		if ("${webContentHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The template cannot be deleted because it is required by one or more template links."
			);
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro editTemplateFile {
		WebContentNavigator.gotoManageTemplates();

		AssertClick(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}",
			key_ddlTemplateName = "${templateName}"
		);

		Panel.expandPanel(panelHeading = "Script");

		UploadCommonFile(
			locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
			value1 = "${templateFile}"
		);

		PortletEntry.save();
	}

	@summary = "Input a small image with the url '${templateSmallImageURL}'"
	macro inputSmallImage {
		Panel.expandPanel(panelHeading = "Details");

		if (IsElementNotPresent(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL")) {
			Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");
			}
		else {
			Click(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");

			Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");
			}

		Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL");

		Type(
			locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL_INPUT",
			value1 = "${templateSmallImageURL}"
		);
	}

	macro localizeTemplateName {
		var key_ddlTemplateName = "${templateName}";
		var key_localizationName = "${localization1NameKey}";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageTemplates();

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

		AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${templateName}");

		Click(locator1 = "Translation#LOCALIZATION_NAME");

		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${localization1Name}");

		if (isSet(localization2NameKey)) {
			var key_localizationName = "${localization2NameKey}";

			Click(locator1 = "Translation#LOCALIZATION_NAME");

			Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${localization2Name}");
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageTemplates();

		Type(locator1 = "DDMSelectTemplate#TOOLBAR_SEARCH_FIELD", value1 = "2");

		Click(locator1 = "Icon#BASIC_SEARCH");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Type(locator1 = "DDMSelectTemplate#TOOLBAR_SEARCH_FIELD", value1 = "Template");

		Click(locator1 = "Icon#BASIC_SEARCH");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Portlet.viewEmptyMessage();
	}

	macro viewCP {
		WebContentNavigator.gotoManageTemplates();

		var key_ddlTemplateName = "${templateName}";

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
			value1 = "${templateDescription}"
		);

		if (isSet(structureName)) {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_MODE",
				value1 = "${structureName}"
			);
		}

		if ("${templateLanguage}" == "ftl") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "FreeMarker"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Extensible Stylesheet Language"
			);
		}
		else {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Velocity"
			);
		}

		if (!("${userEditPermission}" == "noEditPermission")) {
			WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

			AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${templateName}");

			Panel.expandPanel(panelHeading = "Details");

			AssertTextEquals(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${templateDescription}"
			);

			if (isSet(structureName)) {
				AssertTextEquals.assertValue(
					locator1 = "DDMEditTemplate#DETAILS_STRUCTURE",
					value1 = "${structureName}"
				);
			}

			if ("${templateLanguage}" == "ftl") {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "FreeMarker (.ftl)"
				);
			}
			else if ("${templateLanguage}" == "xsl") {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "Extensible Stylesheet Language (.xsl)"
				);
			}
			else {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "Velocity (.vm)"
				);
			}

			if (isSet(templateScript)) {
				AssertTextEquals.assertPartialTextAceEditor(
					locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD",
					value1 = "${templateScript}"
				);
			}
		}
	}

	macro viewTableEntryCP {
		var key_ddlTemplateName = "${templateName}";

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		if (isSet(templateSmallImage)) {
			AssertVisible(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION_SMALL_IMAGE",
				value1 = "${templateSmallImage}"
			);
		}
		else {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
				value1 = "${templateDescription}"
			);
		}
	}
}