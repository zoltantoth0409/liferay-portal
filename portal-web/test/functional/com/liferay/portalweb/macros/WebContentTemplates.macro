definition {
	macro addCP {
		LexiconEntry.gotoAdd();

		WebContentTemplates.inputTemplateAddScreenFields(
			structureName = "${structureName}",
			templateDescription = "${templateDescription}",
			templateFieldNames = "${templateFieldNames}",
			templateFile = "${templateFile}",
			templateLanguage = "${templateLanguage}",
			templateName = "${templateName}",
			templateScript = "${templateScript}",
			templateSmallImageURL = "${templateSmallImageURL}"
		);

		DDLDataDefinition.save();
	}

	macro addFields {
		if (isSet(templateFieldNames)) {
			for (var fieldName : list "${templateFieldNames}") {
				var key_fieldName = "${fieldName}";

				AssertClick(locator1 = "DDMEditTemplate#SCRIPT_FIELD", value1 = "${fieldName}");
			}
		}
	}

	macro addNullCP {
		Navigator.gotoNavItem(navItem = "Templates");

		LexiconEntry.gotoAdd();

		AssertElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Button.clickSave();

		Alert.viewRequiredField();

		SelectFrame(value1 = "relative=top");
	}

	macro addRoleViewPermissions {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${templateName}"
		);

		SelectFrame(locator1 = "IFrame#DIALOG");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Add",
			roleList = "${roles}"
		);

		SelectFrameTop();
	}

	macro deleteCP {
		Navigator.gotoNavItem(navItem = "Templates");

		var key_ddlTemplateName = "${templateName}";

		Click(locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		if ("${webContentHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The template cannot be deleted because it is required by one or more template links."
			);
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro inputDescription {
		if (isSet(templateDescription)) {
			Panel.expandPanel(panel = "Details");

			PortletEntry.inputDescription(description = "${templateDescription}");
		}
	}

	macro inputScript {
		if (isSet(templateScript)) {
			Type.typeAceEditor(
				locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD_TEXT_AREA",
				value1 = "${templateScript}"
			);
		}
	}

	macro inputSmallImage {
		Panel.expandPanel(panel = "Details");

		if (isSet(templateSmallImageURL)) {
			if (IsElementNotPresent(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL")) {
				Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");
			}
			else {
				Click(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");

				Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE");
			}
		}

		Check(locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL");

		Type(
			locator1 = "DDMEditTemplate#DETAILS_SMALL_IMAGE_URL_INPUT",
			value1 = "${templateSmallImageURL}"
		);
	}

	macro inputStructure {
		if (isSet(structureName)) {
			Panel.expandPanel(panel = "Details");

			AssertClick(
				locator1 = "DDMEditTemplate#DETAILS_SELECT_STRUCTURE",
				value1 = "Select"
			);

			SelectFrameTop();

			SelectFrame(locator1 = "IFrame#DIALOG");

			Click(locator1 = "Icon#BASIC_SEARCH");

			var key_ddlDataDefinitionName = "${structureName}";

			AssertClickNoError(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
				value1 = "${structureName}"
			);

			AssertConfirm(
				value1 = "Selecting a new structure deletes all unsaved content. Do you want to proceed?"
			);

			SelectFrameTop();
		}
	}

	macro inputTemplateAddScreenFields {
		PortletEntry.inputName(name = "${templateName}");

		WebContentTemplates.inputStructure(structureName = "${structureName}");

		WebContentTemplates.inputDescription(
			templateDescription = "${templateDescription}"
		);

		WebContentTemplates.inputTemplateLanguage(
			templateLanguage = "${templateLanguage}"
		);

		if (isSet(templateSmallImageURL)) {
			WebContentTemplates.inputSmallImage(
				templateSmallImageURL = "${templateSmallImageURL}"
			);
		}

		WebContentTemplates.inputScript(templateScript = "${templateScript}");

		if (("${templateLanguage}" == "ftl") || ("${templateLanguage}" =="vm")) {
			WebContentTemplates.addFields(templateFieldNames = "${templateFieldNames}");
		}

		WebContentTemplates.inputTemplateFile(templateFile = "${templateFile}");
	}

	macro inputTemplateFile {
		if (isSet(templateFile)) {
			UploadCommonFile(
				locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
				value1 = "${templateFile}"
			);
		}
	}

	macro inputTemplateLanguage {
		Panel.expandPanel(panel = "Details");

		if ("${templateLanguage}" == "vm") {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Velocity (.vm)"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Extensible Stylesheet Language (.xsl)"
			);
		}
		else {
			Select(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "FreeMarker (.ftl)"
			);
		}

		if ("${templateLanguage}" == "vm") {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Velocity (.vm)"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "Extensible Stylesheet Language (.xsl)"
			);
		}
		else {
			AssertSelectedLabel(
				locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
				value1 = "FreeMarker (.ftl)"
			);
		}
	}

	macro localizeTemplateName {
		var key_ddlTemplateName = "${templateName}";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		Navigator.gotoNavItem(navItem = "Templates");

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

		AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${templateName}");

		PortletEntry.changeLocale(fieldName = "Name", locale = "${locale}");

		Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${localization1Name}");

		if (isSet(locale2)) {
			PortletEntry.changeLocale(fieldName = "Name", locale = "${locale2}");

			Type.clickAtType(locator1 = "TextInput#NAME", value1 = "${localization2Name}");
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		Navigator.gotoNavItem(navItem = "Templates");

		Search.searchCP(searchTerm = "2");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Search.searchCP(searchTerm = "Template");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}

		Portlet.viewEmptyMessage(message = "There are no results.");
	}

	macro viewCP {
		Navigator.gotoNavItem(navItem = "Templates");

		var key_ddlTemplateName = "${templateName}";

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
			value1 = "${templateDescription}"
		);

		if (isSet(structureName)) {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_MODE",
				value1 = "${structureName}"
			);
		}

		if ("${templateLanguage}" == "vm") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Velocity"
			);
		}
		else if ("${templateLanguage}" == "xsl") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Extensible Stylesheet Language"
			);
		}
		else {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "FreeMarker"
			);
		}

		if (!("${userEditPermission}" == "noEditPermission")) {
			WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

			AssertTextEquals(locator1 = "TextInput#NAME", value1 = "${templateName}");

			Panel.expandPanel(panel = "Details");

			AssertTextEquals(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${templateDescription}"
			);

			if (isSet(structureName)) {
				AssertTextEquals.assertValue(
					locator1 = "DDMEditTemplate#DETAILS_STRUCTURE",
					value1 = "${structureName}"
				);
			}

			if ("${templateLanguage}" == "vm") {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "Velocity (.vm)"
				);
			}
			else if ("${templateLanguage}" == "xsl") {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "Extensible Stylesheet Language (.xsl)"
				);
			}
			else {
				AssertSelectedLabel(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE_SELECT",
					value1 = "FreeMarker (.ftl)"
				);
			}

			if (isSet(templateScript)) {
				AssertTextEquals.assertPartialTextAceEditor(
					locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD",
					value1 = "${templateScript}"
				);
			}
		}
	}

	macro viewTableEntryCP {
		var key_ddlTemplateName = "${templateName}";

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}"
		);

		if (isSet(templateSmallImage)) {
			AssertVisible(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION_SMALL_IMAGE",
				value1 = "${templateSmallImage}"
			);
		}
		else {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
				value1 = "${templateDescription}"
			);
		}
	}
}