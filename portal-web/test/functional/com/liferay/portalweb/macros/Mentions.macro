definition {

	macro addMentionViaBlogsEntry {
		var key_entryContent = "${entryContent}";
		var key_mentionsUserScreenName = "${mentionsUserScreenName}";

		BlogsNavigator.gotoAddEntry();

		BlogsEntry.addTitle(entryTitle = "${entryTitle}");

		Type.sendKeys(
			key_editor = "content",
			locator1 = "AlloyEditor#EDITOR",
			value1 = "${entryContent} @${mentionsUserScreenName}");

		Click.clickNoMouseOver(locator1 = "Mentions#MENTIONS_USER_DISPLAY_OPTION");

		PortletEntry.publish();
	}

	macro addMentionViaComments {
		var key_entryComment = "${entryComment}";
		var key_mentionsUserScreenName = "${mentionsUserScreenName}";

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		Type.sendKeys(
			locator1 = "CKEditor#BODY",
			value1 = "${entryComment} @${mentionsUserScreenName}");

		SelectFrame(value1 = "relative=top");

		Click.clickNoMouseOver(locator1 = "Mentions#MENTIONS_USER_DISPLAY_OPTION");

		AssertClick(
			locator1 = "Button#REPLY",
			value1 = "Reply");

		Alert.viewSuccessMessage();
	}

	macro addMentionViaMessageBoardsReply {
		var key_mentionsUserScreenName = "${mentionsUserScreenName}";
		var key_threadSubject = "${threadSubject}";

		AssertClick(
			locator1 = "MessageBoards#THREAD_LIST_THREAD",
			value1 = "${threadSubject}");

		AssertElementPresent(locator1 = "MessageBoardsThread#THREAD_REPLY_BUTTON");

		AssertClick(
			locator1 = "MessageBoardsThread#THREAD_REPLY_BUTTON",
			value1 = "Reply");

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		Type.sendKeys(
			locator1 = "CKEditor#BODY",
			value1 = "${threadBody} @${mentionsUserScreenName}");

		SelectFrame.selectFrameTop();

		Click.clickNoMouseOver(locator1 = "Mentions#MENTIONS_USER_DISPLAY_OPTION");

		PortletEntry.publish();
	}

	macro addMentionViaMessageBoardThread {
		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "Thread");

		var key_mentionsUserScreenName = "${mentionsUserScreenName}";

		Type.typeWaitForCKEditorScript(
			locator1 = "TextInput#SUBJECT",
			value1 = "${threadSubject}");

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		Type.sendKeys(
			locator1 = "CKEditor#BODY",
			value1 = "@${mentionsUserScreenName}");

		SelectFrame.selectFrameTop();

		Click.clickNoMouseOver(locator1 = "Mentions#MENTIONS_USER_DISPLAY_OPTION");

		PortletEntry.publish();
	}

	macro addMentionViaMessageBoardThreadEdit {
		Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		Type(
			locator1 = "CKEditor#BODY",
			value1 = "${threadBody} @${mentionsUserScreenName}");

		SelectFrame(value1 = "relative=top");

		var key_mentionsUserScreenName = "${mentionsUserScreenName}";

		Click.clickNoMouseOver(locator1 = "Mentions#MENTIONS_USER_DISPLAY_OPTION");

		PortletEntry.publish();
	}

	macro viewCommentMentionTagLink {
		var key_mentionsUserFullName = "${mentionsUserFullName}";
		var key_mentionsUserScreenName = "${mentionsUserScreenName}";
		var key_siteName = "${mentionsUserFullName}";

		AssertClick(
			locator1 = "Mentions#MENTIONS_COMMENT_TAG_LINK",
			value1 = "@${mentionsUserScreenName}");

		ProductMenuHelper.openProductMenu();

		// Disabling assertion for Test Test until LPS-108776 is fixed.

		if ("${mentionsUserFullName}" != "Test Test") {
			AssertTextEquals.assertPartialText(
				locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
				value1 = "${mentionsUserFullName}");
		}
	}

	macro viewMentionTagLink {
		var key_mentionsUserScreenName = "${mentionsUserScreenName}";
		var key_siteName = "${mentionsUserFullName}";

		AssertClick(
			locator1 = "Mentions#MENTIONS_TAG_LINK",
			value1 = "@${mentionsUserScreenName}");

		ProductMenuHelper.openProductMenu();

		AssertTextEquals.assertPartialText(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
			value1 = "${mentionsUserFullName}");
	}

}