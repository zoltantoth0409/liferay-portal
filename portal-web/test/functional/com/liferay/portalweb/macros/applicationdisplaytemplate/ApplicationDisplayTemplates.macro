definition {

	macro addADTWithFieldEntryCP {
		AssertVisible(locator1 = "Button#PLUS");

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "${portletName} Template");

		SiteAdmin.viewPortletTitle(portletTitle = "New ${portletName} Template");

		PortletEntry.inputName(name = "${portletName} Test ADT");

		Click(
			key_fieldName = "${fieldName}",
			locator1 = "DDMEditTemplate#SCRIPT_FIELD");

		PortletEntry.save();

		Refresh();
	}

	macro addCP {
		var key_adtType = "${adtType}";

		Portlet.viewEmptyMessage(message = "There are no templates.");

		LexiconEntry.gotoAdd();

		if (IsElementPresent(key_text = "More", locator1 = "Button#ANY")) {
			Button.clickMore();

			ScrollWebElementIntoView(
				key_menuItem = "${adtType} Template",
				locator1 = "MenuItem#ANY_MENU_ITEM");

			MenuItem.click(menuItem = "${adtType} Template");
		}
		else {
			MenuItem.click(menuItem = "${adtType} Template");
		}

		SiteAdmin.viewPortletTitle(portletTitle = "New ${adtType} Template");

		PortletEntry.inputName(name = "${adtType} Test ADT");

		UploadCommonFile(
			locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
			value1 = "${adtFile}");

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "ApplicationDisplayTemplates#ADT_TABLE_NAME_1",
			value1 = "${adtType} Test ADT");
	}

	macro addManualViaRSS {
		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Pause(locator1 = "5000");

		if (IsElementPresent(locator1 = "Configuration#SETUP_DISPLAY_SETTINGS")) {
			AssertClick(
				locator1 = "Configuration#SETUP_DISPLAY_SETTINGS",
				value1 = "Display Settings");
		}

		AssertClick(
			locator1 = "ApplicationDisplayTemplatesConfiguration#MANAGE_DISPLAY_TEMPLATES_LINK",
			value1 = "Manage Templates");

		var key_adtType = "RSS";

		SelectFrameTop(value1 = "relative=top");

		SelectFrame(locator1 = "ApplicationDisplayTemplates#ADT_IFRAME");

		LexiconEntry.gotoAdd();

		AssertVisible(locator1 = "ApplicationDisplayTemplates#RSS_FEEDS");

		AssertElementPresent(locator1 = "ApplicationDisplayTemplates#RSS_FEEDS");

		PortletEntry.inputName(name = "RSS Test ADT");

		Click(locator1 = "ApplicationDisplayTemplates#RSS_FEEDS");

		PortletEntry.save();
	}

	macro addPG {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		Pause(locator1 = "5000");

		if (IsElementPresent(locator1 = "NavTab#TAB_LINK", key_tab = "Display Settings")) {
			Navigator.gotoNavTab(navTab = "Display Settings");
		}
		else if (IsElementPresent(locator1 = "Panel#PANEL", key_panel = "Display Settings")) {
			Panel.expandPanel(panel = "Display Settings");
		}

		AssertClick(
			locator1 = "ApplicationDisplayTemplatesConfiguration#MANAGE_DISPLAY_TEMPLATES_LINK",
			value1 = "Manage Templates");

		var key_adtType = "${adtType}";

		SelectFrameTop();

		SelectFrame(locator1 = "ApplicationDisplayTemplates#ADT_IFRAME");

		LexiconEntry.gotoAdd();

		var key_adtType = "${adtType}";

		AssertVisible(locator1 = "ApplicationDisplayTemplates#PORTLET_PREFERENCES");

		AssertElementPresent(locator1 = "ApplicationDisplayTemplates#PORTLET_PREFERENCES");

		PortletEntry.inputName(name = "${adtType} Test ADT");

		UploadCommonFile(
			locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
			value1 = "${adtFile}");

		PortletEntry.save();

		SelectFrameTop(value1 = "relative=top");
	}

	macro addPortletADTWithFieldEntry {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		Pause(locator1 = "3000");

		AssertClick.assertPartialTextClickAt(
			locator1 = "ApplicationDisplayTemplatesConfiguration#MANAGE_DISPLAY_TEMPLATES_LINK",
			value1 = "Manage Templates");

		SelectFrameTop();

		SelectFrame(locator1 = "ApplicationDisplayTemplates#ADT_IFRAME");

		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${portletName} Test ADT");

		Click(
			key_fieldName = "${fieldName}",
			locator1 = "DDMEditTemplate#SCRIPT_FIELD");

		PortletEntry.save();

		SelectFrameTop(value1 = "relative=top");
	}

	macro openWidgetTemplatesAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_dynamic_data_mapping_web_portlet_PortletDisplayTemplatePortlet");
	}

	macro selectPortletADT {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		var key_tab = "Display Settings";

		if (IsElementPresent(locator1 = "NavTab#TAB_LINK")) {
			Navigator.gotoNavTab(navTab = "Display Settings");
		}

		Select(
			locator1 = "Select#DISPLAY_TEMPLATE",
			value1 = "${templateName}");

		PortletEntry.save();

		SelectFrameTop();
	}

	macro viewTemplate {
		AssertTextEquals(
			locator1 = "ApplicationDisplayTemplates#ADT_TABLE_NAME_1",
			value1 = "${adtType} Test ADT");
	}

}