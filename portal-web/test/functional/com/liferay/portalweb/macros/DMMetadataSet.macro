definition {
	macro add {
		DMMetadataSet.selectMetadataSetsFrame();

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "Metadata Sets");

		LexiconEntry.gotoAdd();

		AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "New Metadata Set");

		PortletEntry.inputName(name = "${metadataSetName}");

		for (var dmMetadataSetFieldName : list "${dmMetadataSetFieldNames}") {
			DMMetadataSet.addField(
				field = "${dmMetadataSetFieldName}",
				fieldFieldLabel = "${dmMetadataSetFieldName}",
				fieldName = StringUtil.replace("${dmMetadataSetFieldName}", " ", "")
			);
		}
	}

	macro addField {
		var key_field = "${field}";

		DragAndDrop(
			locator1 = "DDMEditStructure#FIELDS_FIELD",
			locator2 = "DDMEditStructure#FORM_BUILDER",
			value1 = "${field}"
		);

		var key_fieldFieldLabel = "${field}";

		Click(locator1 = "DDMEditStructure#FORM_FIELD");

		DoubleClick(locator1 = "DDMEditStructure#SETTINGS_FIELD_LABEL");

		Type(
			locator1 = "DDMEditStructure#SETTINGS_CELL_EDITOR_FIELD",
			value1 = "${fieldFieldLabel}"
		);

		AssertClick(
			locator1 = "DDMEditStructure#SETTINGS_CELL_EDITOR_SAVE_BUTTON",
			value1 = "Save"
		);

		AssertTextEquals(
			locator1 = "DDMEditStructure#SETTINGS_FIELD_LABEL",
			value1 = "${fieldFieldLabel}"
		);

		DoubleClick(locator1 = "DDMEditStructure#SETTINGS_NAME");

		Type(
			locator1 = "DDMEditStructure#SETTINGS_CELL_EDITOR_FIELD",
			value1 = "${fieldName}"
		);

		AssertClick(
			locator1 = "DDMEditStructure#SETTINGS_CELL_EDITOR_SAVE_BUTTON",
			value1 = "Save"
		);

		AssertTextEquals(
			locator1 = "DDMEditStructure#SETTINGS_NAME",
			value1 = "${fieldName}"
		);

		AssertClick(locator1 = "DDMEditStructure#FIELDS_LINK", value1 = "Fields");

		Click(locator1 = "TextInput#NAME");
	}

	macro selectMetadataSetsFrame {
		if (IsElementPresent(locator1 = "IFrame#DIALOG")) {
			SelectFrameTop();

			SelectFrame(locator1 = "IFrame#DIALOG");
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Documents and Media"
		);

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Metadata Sets");

		DMMetadataSet.selectMetadataSetsFrame();

		Type(locator1 = "NavBar#BASIC_SEARCH_FIELD", value1 = "DM");

		AssertClick(locator1 = "NavBar#BASIC_SEARCH_BUTTON", value1 = "Search");

		while (IsElementPresent(locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_TABLE_ACTIONS_1")) {
			AssertClick(
				locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_TABLE_ACTIONS_1",
				value1 = "Actions"
			);

			AssertClickNoError(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Delete",
				key_menuItem = "Delete"
			);

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}
	}
}