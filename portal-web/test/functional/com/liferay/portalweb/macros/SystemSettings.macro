definition {
	macro editConfiguration {
		var key_rowEntry = "${configurationName}";

		while (IsElementNotPresent(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS")) {
			Click(locator1 = "Pagination#NEXT_LINK");
		}

		if ("${configurationName}" == "Dynamic Data Lists") {
			Click.waitForMenuToggleJSClick(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS_2");
		}
		else {
			Click.waitForMenuToggleJSClick(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
		}

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Edit",
			key_menuItem = "Edit"
		);
	}

	macro editSecurityCredential {
		Type(
			locator1 = "SystemSettings#SECURITY_CREDENTIAL_FIELD",
			value1 = "${securityCredential}"
		);

		var key_fieldName = "securityPrincipal";

		Type(locator1 = "FormFields#TEXT_MULTILINE_FIELD", value1 = "test");

		AssertElementPresent(locator1 = "SystemSettings#SECURITY_CREDENTIAL_FIELD");

		SikuliAssertElementPresent(
			locator1 = "SystemSettings#SECURITY_CREDENTIAL_HIDDEN_IMAGE"
		);
	}

	macro editTextAreaSetting {
		var key_fieldName = "${settingName}";

		Type(locator1 = "DDMField#TEXT_BOX", value1 = "${settingValue}");
	}

	macro enableSetting {
		Check(locator1 = "SystemSettings#${checkboxSetting}");
	}

	macro exitEditConfiguration {
		Click(locator1 = "Icon#ANGLE_LEFT_BACK");
	}

	macro gotoCategory {
		var key_category = "${category}";

		AssertClick(
			locator1 = "SystemSettings#MENUBAR_CATEGORY",
			value1 = "${category}"
		);
	}

	macro resetConfiguration {
		SystemSettings.gotoCategory(category = "${category}");

		var key_rowEntry = "${configurationName}";

		if ("${configurationName}" == "Dynamic Data Lists") {
			Click.waitForMenuToggleJSClick(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS_2");
		}
		else {
			Click.waitForMenuToggleJSClick(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
		}

		AssertClick(
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Reset Default Values",
			key_menuItem = "Reset Default Values"
		);

		Alert.viewSuccessMessage();
	}

	macro saveConfiguration {
		if (IsElementPresent(locator1 = "Button#SAVE")) {
			PortletEntry.save();
		}
		else {
			Button.clickUpdate();

			Alert.viewSuccessMessage();
		}
	}

	macro viewConfiguration {
		var key_rowEntry = "${configurationName}";

		while (IsElementNotPresent(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS")) {
			Click(locator1 = "Pagination#NEXT_LINK");
		}

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${configurationName}"
		);

		if ("${configurationName}" == "LDAP Auth") {
			if ("${enableLDAP}" == "true") {
				FormFields.viewCheckboxChecked(fieldName = "enabled");
			}
			else {
				FormFields.viewCheckboxNotChecked(fieldName = "enabled");
			}
		}
	}
}