definition {
	macro configureDisplayStyle {
		Click(locator1 = "Dropdown#TRIGGER");

		AssertClick(
			locator1 = "Dropdown#RESULT",
			value1 = "${displayStyle}",
			key_option = "${displayStyle}"
		);

		AssertTextEquals(
			locator1 = "Dropdown#SELECTED_VALUE",
			value1 = "${displayStyle}"
		);

		SystemSettings.saveConfiguration();
	}

	macro configureAssetDynamicExportSetting {
		if ("${enableDynamicExport}" == "true") {
			Check.toggleSwitch(locator1 = "SystemSettings#ASSETS_DYNAMIC_EXPORT_CHECKBOX");

			SystemSettings.saveConfiguration();

			AssertChecked.assertCheckedNotVisible(
				locator1 = "SystemSettings#ASSETS_DYNAMIC_EXPORT_CHECKBOX"
			);

			if ("${exportLimit}" == "true") {
				Type(locator1 = "SystemSettings#ASSETS_DYNAMIC_EXPORT_LIMIT", value1 = "${exportLimitNumber}");

				SystemSettings.saveConfiguration();
			}
		}
		else if ("${enableDynamicExport}" == "false") {
			Uncheck.uncheckToggleSwitch(locator1 = "SystemSettings#ASSETS_DYNAMIC_EXPORT_CHECKBOX");

			SystemSettings.saveConfiguration();

			AssertNotChecked.assertNotCheckedNotVisible(
				locator1 = "SystemSettings#ASSETS_DYNAMIC_EXPORT_CHECKBOX"
			);
		}
	}

	macro configureAssetManualExportSetting {
		if ("${enableManualExport}" == "true") {
			Check.toggleSwitch(locator1 = "SystemSettings#ASSETS_MANUAL_EXPORT_CHECKBOX");

			SystemSettings.saveConfiguration();

			AssertChecked.assertCheckedNotVisible(
				locator1 = "SystemSettings#ASSETS_MANUAL_EXPORT_CHECKBOX"
			);
		}
		else if ("${enableManualExport}" == "false") {
			Uncheck.uncheckToggleSwitch(locator1 = "SystemSettings#ASSETS_MANUAL_EXPORT_CHECKBOX");

			SystemSettings.saveConfiguration();

			AssertNotChecked.assertNotCheckedNotVisible(
				locator1 = "SystemSettings#ASSETS_MANUAL_EXPORT_CHECKBOX"
			);
		}
	}

	macro deleteConfiguration {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Delete");

		Alert.viewSuccessMessage();
	}

	macro editSecurityCredential {
		Type(
			locator1 = "SystemSettings#SECURITY_CREDENTIAL_FIELD",
			value1 = "${securityCredential}"
		);

		var key_fieldName = "securityPrincipal";

		Type(locator1 = "FormFields#TEXT_MULTILINE_FIELD", value1 = "test");

		AssertElementPresent(locator1 = "SystemSettings#SECURITY_CREDENTIAL_FIELD");

		SikuliAssertElementPresent(
			locator1 = "SystemSettings#SECURITY_CREDENTIAL_HIDDEN_IMAGE"
		);
	}

	macro editTextAreaSetting {
		Type(
			locator1 = "TextArea#ANY_LABELED",
			value1 = "${settingValue}",
			key_label = "${settingName}"
		);
	}

	macro editTextSetting {
		Type(
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "${settingValue}",
			key_fieldLabel = "${settingName}"
		);
	}

	macro exitEditConfiguration {
		Click(locator1 = "Icon#ANGLE_LEFT_BACK");
	}

	macro gotoConfiguration {
		Click(
			locator1 = "ListGroupItem#ITEM_TEXT",
			key_itemName = "${configurationCategory}"
		);

		var browserType = PropsUtil.get("browser.type");

		if ("${browserType}" == "chrome") {
			Pause(locator1 = "5000");
		}

		Click(
			locator1 = "SystemSettings#SCOPED_CONFIGURATION_NAME",
			key_configurationName = "${configurationName}",
			key_configurationScope = "${configurationScope}"
		);

		if (("${configurationCategory}" == "Instance Configuration") && ("${configurationName}" == "General") && ("${configurationScope}" == "Virtual Instance Scope")) {
			AssertElementPresent(
				locator1 = "Header#H3_TITLE",
				key_title = "${configurationName}"
			);
		}
		else {
			AssertElementPresent(
				locator1 = "Header#H2_TITLE",
				key_title = "${configurationName}"
			);
		}
	}

	macro gotoConfigurationEntry {
		Click(
			locator1 = "SystemSettings#CONFIGURATION_ENTRY",
			key_configurationEntry = "${configurationEntry}"
		);
	}

	macro openSystemSettingsAdmin {
		Navigator.openWithAppendToBaseURL(
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_configuration_admin_web_portlet_SystemSettingsPortlet"
		);
	}

	macro resetConfiguration {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Reset Default Values");

		Alert.viewSuccessMessage();
	}

	macro saveConfiguration {
		if (IsElementPresent(locator1 = "Button#SAVE")) {
			PortletEntry.save();
		}
		else {
			Button.clickUpdate();

			Alert.viewSuccessMessage();
		}
	}

	macro selectOptionSetting {
		Click(
			locator1 = "SystemSettings#DROPDOWN_MENU",
			key_fieldLabel = "${fieldName}"
		);

		Click(
			locator1 = "SystemSettings#DROPDOWN_MENU_OPTION",
			key_selectOption = "${selectOption}"
		);
	}
}