@component-name = "portal-user-management-and-portal-configuration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Portal Instances";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			PortalInstances.tearDownCP();

			Page.tearDownPG();
		}
	}

	@priority = "5"
	test AddPortalInstance {
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#AddPortalInstance";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.viewCP(virtualHost = "www.able.com");
	}

	@priority = "5"
	test DefaultPortalLandingPage {
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#DefaultPortalLandingPage";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		PortalSettings.editDefaultLandingPageCP(defaultLandingPage = "/web/guest/landing");

		Page.add(pageName = "landing");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		var portalURL = PropsUtil.get("portal.url");

		AssertLocation(value1 = "${portalURL}/web/guest/landing");
	}

	@description = "This is a use case for LPS-39744."
	@priority = "5"
	test DefaultPortalLandingPageSetByProperty {
		property custom.properties = "default.landing.page.path=/web/guest/landing";
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#DefaultPortalLandingPageSetByProperty";

		Page.add(pageName = "landing");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		var portalURL = PropsUtil.get("portal.url");

		AssertLocation(value1 = "${portalURL}/web/guest/landing");
	}

	@priority = "5"
	test DeletePortalInstance {
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#DeletePortalInstance";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.deleteCP(virtualHost = "www.able.com");
	}

	@priority = "4"
	test PermissionCheckPortalInstances {
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#PermissionCheckPortalInstances";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.addCP(
			mailDomain = "www.baker.com",
			virtualHost = "www.baker.com",
			webId = "www.baker.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		Navigator.openSpecificURL(url = "http://www.able.com:8080");

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Documents and Media");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document Title");

		DMDocument.viewCP(dmDocumentTitle = "DM Document Title");

		var documentURL = selenium.getLocation();
		var otherInstanceAccessAttempt = StringUtil.replace("${documentURL}", "able", "baker");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.baker.com",
			virtualHostsURL = "http://www.baker.com:8080");

		Navigator.openSpecificURL(url = "${otherInstanceAccessAttempt}");

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_2",
			value1 = "You do not have the required permissions.");
	}

	@priority = "5"
	test ViewDeactivatedPortalInstance {
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#ViewDeactivatedPortalInstance";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		Navigator.openURL();

		Page.add(pageName = "Blogs Page");

		Navigator.gotoPage(pageName = "Blogs Page");

		Portlet.addPG(portletName = "Blogs");

		Page.add(pageName = "Wiki Page");

		Navigator.gotoPage(pageName = "Wiki Page");

		Portlet.addPG(portletName = "Wiki");

		Page.add(pageName = "Language Page");

		Navigator.gotoPage(pageName = "Language Page");

		Portlet.addPG(portletName = "Language Selector");

		User.logoutPG();

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		Page.viewNoPG(pageName = "Blogs Page");

		Page.viewNoPG(pageName = "Wiki Page");

		Page.viewNoPG(pageName = "Language Page");

		Navigator.openSpecificURL(url = "http://www.able.com:8080");

		Page.add(pageName = "Web Content Page");

		Navigator.gotoSpecificURLPage(
			pageName = "Web Content Page",
			url = "http://www.able.com:8080");

		Portlet.addPG(portletName = "Web Content Display");

		Navigator.gotoSpecificURLPage(
			pageName = "Web Content Page",
			url = "http://www.able.com:8080");

		Portlet.addPG(portletName = "Hello World");

		User.logoutPG();

		User.loginPG();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.deactivatePortalInstance(virtualHost = "www.able.com");

		Navigator.openSpecificURL(url = "http://www.able.com:8080");

		AssertTextEquals(
			locator1 = "Home#STATUS_MESSAGE",
			value1 = "This instance is inactive. Please contact the administrator.");
	}

	@priority = "4"
	test ViewVirtualHostPrivateSitePage {
		property custom.properties = "virtual.hosts.valid.hosts=localhost,127.0.0.1,www.able.com,www.baker.com,www.charlie.com";
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#ViewVirtualHostPrivateSitePage";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		PortalSettings.editVirtualHostCP(virtualHostName = "www.able.com");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Site Settings");

		Site.addVirtualHostsURLCP(
			publicPageVirtualHostURL = "www.baker.com",
			publicVirtualHosts = "${publicPageVirtualHostURL}");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Site Settings");

		Site.addVirtualHostsURLCP(
			privatePageVirtualHostURL = "www.charlie.com",
			privateVirtualHosts = "${privatePageVirtualHostURL}");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Page.add(pageName = "Public Page");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Page.add(
			pageName = "Private Page",
			sitePagePrivacy = "Private");

		Navigator.openSpecificURL(url = "http://www.charlie.com:8080");

		var key_pageName = "Welcome";

		AssertElementPresent(locator1 = "Home#PAGE");

		User.loginPG(
			password = "test",
			specificURL = "http://www.charlie.com:8080/group/guest",
			userEmailAddress = "test@liferay.com");

		var key_pageName = "Private Page";

		AssertTextEquals(
			locator1 = "Home#PAGE",
			value1 = "Private Page");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@priority = "4"
	test ViewVirtualInstanceInheritsSystemSettings {
		property test.name.skip.portal.instance = "PortalconfigurationUsecase#ViewVirtualInstanceInheritsSystemSettings";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoCategory(category = "Foundation");

		SystemSettings.editConfiguration(configurationName = "LDAP Auth");

		SystemSettings.enableSetting(checkboxSetting = "ENABLED_CHECKBOX");

		SystemSettings.saveConfiguration();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		PortalSettingsNavigator.gotoLDAP();

		PortalSettings.viewEnabledLDAPSetting(enableCheckboxKey = "ENABLED");
	}

}