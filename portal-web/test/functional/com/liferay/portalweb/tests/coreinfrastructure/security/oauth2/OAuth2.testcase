@component-name = "portal-security"
definition {
	property portal.upstream = "true";
	property testray.main.component.name = "OAuth 2";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Page.tearDownCP();
		}
	}

	test AddAndEditApplication {
		property app.server.types = "jboss,resin,tcserver,tomcat,weblogic,websphere,wildfly";
		property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property environment.acceptance = "true";
		property portal.acceptance = "true";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration"
		);

		OAuth2.addApplication(
			applicationName = "OAuth Application",
			callbackURI = "http://www.liferay.com",
			clientProfile = "Web Application",
			websiteURL = "http://www.liferay.com"
		);

		var generatedClientId = selenium.getElementValue("OAuth2#ADMIN_INPUT_FORM_CLIENT_ID");
		var generatedClientSecret = selenium.getElementValue("OAuth2#ADMIN_INPUT_FORM_CLIENT_SECRET");

		OAuth2.inputApplicationValues(
			description = "Registering a new application",
			privacyPolicyURL = "http://www.liferay.com"
		);

		Navigator.gotoBack();

		OAuth2.viewApplicationInTable(
			applicationName = "OAuth Application",
			clientId = "${generatedClientId}",
			description = "Registering a new application"
		);

		OAuth2.viewApplication(
			applicationName = "OAuth Application",
			callbackURI = "http://www.liferay.com",
			checkboxCheckList = "Authorization Code,Client Credentials,Refresh Token,Resource Owner Password Credentials",
			clientId = "${generatedClientId}",
			clientProfile = "Web Application",
			clientSecret = "${generatedClientSecret}",
			description = "Registering a new application",
			privacyPolicyURL = "http://www.liferay.com",
			websiteURL = "http://www.liferay.com"
		);

		OAuth2.editApplication(
			applicationName = "OAuth Application",
			callbackURI = "http://www.liferay.com/callback",
			checkboxCheckList = "Token Introspection",
			checkboxUncheckList = "Refresh Token,Resource Owner Password Credentials",
			clientId = "abcd1234",
			clientProfile = "Native Application",
			clientSecret = "abcd1234",
			description = "Editing a new application",
			newApplicationName = "OAuth Application Edited",
			privacyPolicyURL = "http://www.google.com",
			websiteURL = "http://www.google.com"
		);

		Navigator.gotoBack();

		OAuth2.viewApplicationInTable(
			applicationName = "OAuth Application Edited",
			clientId = "abcd1234",
			description = "Editing a new application"
		);

		OAuth2.viewApplication(
			applicationName = "OAuth Application Edited",
			callbackURI = "http://www.liferay.com/callback",
			checkboxCheckList = "Token Introspection",
			clientId = "abcd1234",
			clientProfile = "Native Application",
			clientSecret = "abcd1234",
			description = "Editing a new application",
			privacyPolicyURL = "http://www.google.com",
			websiteURL = "http://www.google.com"
		);

		var newGeneratedClientSecret = OAuth2.editApplication(
			applicationName = "OAuth Application Edited",
			generateNewSecret = "true"
		);

		Navigator.gotoBack();

		OAuth2.viewApplication(
			applicationName = "OAuth Application Edited",
			clientSecret = "${newGeneratedClientSecret}"
		);

		OAuth2.deleteApplication(applicationName = "OAuth Application Edited");
	}

	@description = "This is a use case for OAUTH2-242."
	@priority = "5"
	test AssertAuthorizationCodeWorksInCluster {
		property app.server.bundles.size = "1";
		property cluster.enabled = "true";
		property custom.properties = "virtual.hosts.default.site.name=Guest";
		property remote.elasticsearch.enabled = "true";
		property test.name.skip.portal.instance = "OAuth2#AssertAuthorizationCodeWorksInCluster";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration"
		);

		OAuth2.addApplication(
			applicationName = "OAuth Application",
			callbackURI = "http://www.liferay.com"
		);

		Navigator.gotoNavItem(navItem = "Scopes");

		for (var resourcePanel : list "Analytics,Portal Services") {
			var key_panel = "${resourcePanel}";

			AssertElementPresent(locator1 = "OAuth2#ADMIN_RESOURCE_PANEL_COLLAPSED");
		}

		User.logoutPG();

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "OAuth Application");

		Navigator.gotoNavItem(navItem = "Scopes");

		for (var resourcePanel : list "Analytics,Portal Services") {
			var key_panel = "${resourcePanel}";

			AssertElementPresent(locator1 = "OAuth2#ADMIN_RESOURCE_PANEL_COLLAPSED");
		}

		User.logoutPG(nodePort = "9080");

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "OAuth Application");

		OAuth2.inputApplicationValues(
			clientId = "abc123",
			clientSecret = "abc123"
		);

		var authorizationCode = OAuth2.getAuthorizationCode();

		var curl = '''
			http://localhost:9080/o/oauth2/token \
				 --data client_id=abc123&client_secret=abc123&grant_type=authorization_code&code=${authorizationCode}
		''';

		var reponse = JSONCurlUtil.post("${curl}");

		if (contains("${reponse}", "access_token")) {
			echo("OAuth2 Authorization Code is working in cluster.");
		}
		else {
			fail("OAuth2 Authorization Code is not working in cluster.");
		}
	}

	test AuthorizeExternalApplication {
		property custom.properties = "virtual.hosts.default.site.name=Guest";

 		var clientId = "abc123";
		var clientSecret = "abc123";
		var portalURL = PropsUtil.get("portal.url");

 		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration"
		);

 		OAuth2.addApplication(
			applicationName = "Third-party Application",
			callbackURI = '''http://martamedio.com/oauth2-tester/?url=${portalURL}&client_id=${clientId}''',
			clientProfile = "Web Application"
		);

 		OAuth2.inputApplicationValues(
			clientId = "${clientId}",
			clientSecret = "${clientSecret}"
		);

 		Navigator.gotoBack();

 		OAuth2.editScopes(
			applicationName = "Third-party Application",
			resourceCheckList = "liferay-json-web-services.everything.read.userprofile",
			resourcePanels = "Portal Services"
		);

 		Navigator.openSpecificURL(url = "http://martamedio.com/oauth2-tester/");

 		Type(
			locator1 = "OAuth2#OAUTH2_TESTER_CLIENT_ID",
			value1 = "${clientId}"
		);

 		Button.click(button = "Authorize My Application");

 		Button.click(button = "Authorize");

 		Type(
			locator1 = "OAuth2#OAUTH2_TESTER_CLIENT_SECRET",
			value1 = "${clientSecret}"
		);

 		Button.click(button = "Obtain OAuth2 Access");

		Pause(locator1 = "3000");

 		Button.click(button = "Launch Request");

 		AssertTextEquals.assertPartialText(
			locator1 = "OAuth2#OAUTH2_TESTER_REQUEST_RESULTS",
			value1 = "test@liferay.com"
		);
	}

	test EditApplicationScopes {
		property liferay.dependency.jars = "com.liferay.oauth2.provider.scope.sample.qa.jar";
		property osgi.module.configuration.file.names = "com.liferay.oauth2.provider.scope.internal.configuration.ConfigurableScopeMapperConfiguration-default.config";
		property osgi.module.configurations = "mapping=[&quot;example-scope-1,example-scope-2\=everything&quot;]${line.separator}osgi.jaxrs.name=&quot;liferay-oauth2-scope-sample-qa-app&quot;${line.separator}passthrough=&quot;true&quot;";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings"
		);

		SystemSettings.gotoConfiguration(
			configurationCategory = "OAuth 2",
			configurationName = "Bundle PrefixHandlerFactory",
			configurationScope = "System Scope"
		);

		SystemSettings.gotoConfigurationEntry(
			configurationEntry = "Default"
		);

		SystemSettings.deleteConfiguration();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration"
		);

		OAuth2.addApplication(
			applicationName = "OAuth Application",
			checkboxUncheckList = "Authorization Code"
		);

		Navigator.gotoBack();

		OAuth2.editScopes(
			applicationName = "OAuth Application",
			resourceCheckList = "everything.read.userprofile",
			resourcePanels = "Portal Services"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "OAuth2#ADMIN_TABLE_APP_SCOPES_COUNT",
			value1 = "1",
			key_tableName = "OAuth Application"
		);

		OAuth2.viewScopes(
			applicationName = "OAuth Application",
			resourceCheckedList = "everything.read.userprofile",
			resourcePanels = "Portal Services"
		);

		OAuth2.editScopes(
			applicationName = "OAuth Application",
			checkGlobalScope = "Everything"
		);

		/*

		Workaround for OAUTH2-243

		AssertTextEquals.assertPartialText(
			locator1 = "OAuth2#ADMIN_TABLE_APP_SCOPES_COUNT",
			value1 = "7",
			key_tableName = "OAuth Application"
		);

		OAuth2.viewScopes(
			applicationName = "OAuth Application",
			checkedGlobalScope = "Everything",
			resourceCheckedList = "example-scope-1,example-scope-2,everything.read,everything.write,everything.read.userprofile",
			resourcePanels = "Portal Services,Sample QA app"
		);

		*/

		OAuth2.editScopes(
			applicationName = "OAuth Application",
			resourcePanels = "Portal Services,Sample QA app",
			uncheckAllResourceScopes = "true",
			uncheckGlobalScope = "Everything"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "OAuth2#ADMIN_TABLE_APP_SCOPES_COUNT",
			value1 = "0",
			key_tableName = "OAuth Application"
		);

		OAuth2.viewScopes(
			allResourceScopesUnchecked = "true",
			applicationName = "OAuth Application",
			resourcePanels = "Portal Services,Sample QA app",
			uncheckedGlobalScope = "Everything"
		);
	}

	test ViewAndRevokeGrantedAuthorizations {
		property app.server.types = "jboss,resin,tcserver,tomcat,weblogic,websphere,wildfly";
		property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property environment.acceptance = "true";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "OAuth2#ViewAndRevokeGrantedAuthorizations";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Search"
		);

		SearchAdministration.executeSearchActions(actionsDescription = "Reindex all search indexes.");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration"
		);

		OAuth2.addApplication(
			applicationName = "OAuth Application",
			checkboxUncheckList = "Authorization Code"
		);

		var generatedClientId = selenium.getElementValue("OAuth2#ADMIN_INPUT_FORM_CLIENT_ID");
		var generatedClientSecret = selenium.getElementValue("OAuth2#ADMIN_INPUT_FORM_CLIENT_SECRET");
		var portalURL = PropsUtil.get("portal.url");

		var curl = '''
			${portalURL}/o/oauth2/token \
				--data grant_type=client_credentials&client_id=${generatedClientId}&client_secret=${generatedClientSecret}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");

		Navigator.gotoBack();

		OAuth2.viewGrantedAuthViaAdmin(
			applicationName = "OAuth Application",
			userName = "Test Test"
		);

		OAuth2.revokeGrantedAuthViaAdmin(
			applicationName = "OAuth Application",
			userName = "Test Test"
		);

		OAuth2.viewGrantedAuthViaAdmin(
			applicationName = "OAuth Application",
			noAuthorizations = "true"
		);

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");

		OAuth2.viewGrantedAuthViaUser(applicationName = "OAuth Application");

		OAuth2.revokeGrantedAuthViaUser(applicationName = "OAuth Application");

		OAuth2.viewGrantedAuthViaUser(noAuthorizations = "true");
	}

	test ViewAuthPromptRequestedScopes {
		property custom.properties = "virtual.hosts.default.site.name=Guest";
		property osgi.module.configuration.file.names = "com.liferay.oauth2.provider.scope.internal.configuration.BundlePrefixHandlerFactoryConfiguration-default.config";
		property osgi.module.configurations = "include.bundle.symbolic.name=&quot;false&quot;${line.separator}osgi.jaxrs.name=&quot;Default&quot;${line.separator}separator=&quot;&quot;${line.separator}service.properties=&quot;&quot;";

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Analytics Cloud");

		WaitForSPARefresh();

		var generatedClientId = selenium.getElementValue("OAuth2#ADMIN_INPUT_FORM_CLIENT_ID");
		var portalURL = PropsUtil.get("portal.url");

		Navigator.gotoBack();

		OAuth2.editScopes(
			applicationName = "Analytics Cloud",
			checkGlobalScope = "Everything");

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "deploy-dependency-jars -Dliferay.dependency.jars=com.liferay.oauth2.provider.sample.jar");

		Pause(locator1 = "7000");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Analytics Cloud");

		AssertClick(
			key_navItem = "Scopes",
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "Scopes");

		OAuth2.expandResourcePanel(resourcePanel = "sample-app");

		AssertChecked(
			key_panel = "sample-app",
			key_scopeName = "everything.read",
			locator1 = "OAuth2#ADMIN_GLOBAL_RESOURCE_SCOPE_CHECKBOX");

		Navigator.openSpecificURL(url = "${portalURL}/o/oauth2/authorize?client_id=${generatedClientId}&response_type=code");

		AssertElementPresent(
			key_applicationName = "Analytics Cloud",
			locator1 = "OAuth2#AUTHORIZATION_REQUEST_APPLICATION_NAME");

		AssertElementPresent(
			key_resourceName = "Portal Services",
			locator1 = "OAuth2#AUTHORIZATION_REQUEST_RESOURCE_NAME");

		AssertElementNotPresent(
			key_resourceName = "sample-app",
			locator1 = "OAuth2#AUTHORIZATION_REQUEST_RESOURCE_NAME");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "OAuth 2 Administration");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Analytics Cloud");

		AssertClick(
			key_navItem = "Scopes",
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "Scopes");

		PortletEntry.save();

		Navigator.openSpecificURL(url = "${portalURL}/o/oauth2/authorize?client_id=${generatedClientId}&response_type=code");

		AssertElementPresent(
			key_resourceName = "Portal Services",
			locator1 = "OAuth2#AUTHORIZATION_REQUEST_RESOURCE_NAME");

		AssertElementPresent(
			key_resourceName = "sample-app",
			locator1 = "OAuth2#AUTHORIZATION_REQUEST_RESOURCE_NAME");
	}
}