@component-name = "portal-locale"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Locale";

	setUp {
		TestCase.setUpPortalInstance();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONUser.tearDownNonAdminUsers();

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "4"
	test ChangesToInstanceTimeZoneDoNotPropagateToExistingUsers {
		property custom.properties = "company.default.time.zone=America/Los_Angeles";

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		PortalSettings.gotoConfiguration(
			configurationCategory = "Localization",
			configurationName = "Time Zone",
			configurationScope = "Virtual Instance Scope");

		task ("Assert current time displays in user's selected time zone by asserting time zone specific label") {
			AssertSelectedLabel(
				locator1 = "Select#TIME_ZONE",
				value1 = "(UTC -08:00) Pacific Standard Time");
		}

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.gotoEditCP(userScreenName = "usersn");

		UserNavigator.gotoDisplaySettings();

		task ("Assert current time displays in user's selected time zone by asserting time zone specific label") {
			AssertSelectedLabel(
				locator1 = "Select#TIME_ZONE",
				value1 = "(UTC -08:00) Pacific Standard Time");
		}

		PortalSettings.gotoConfiguration(
			configurationCategory = "Localization",
			configurationName = "Time Zone",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureTimeZone(timeZone = "(UTC) Coordinated Universal Time");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.gotoEditCP(userScreenName = "usersn");

		UserNavigator.gotoDisplaySettings();

		task ("Assert current time displays in user's selected time zone by asserting time zone specific label") {
			AssertSelectedLabel(
				locator1 = "Select#TIME_ZONE",
				value1 = "(UTC -08:00) Pacific Standard Time");
		}
	}

	@priority = "4"
	test GuestUserViewSiteChangedLocalePersistsFriendlyURL {
		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site Name");

		Portlet.addPG(portletName = "Hello World");

		Site.openSiteSettingsAdmin(siteURLKey = "test-site-name");

		Site.configureCurrentLanguagesCP(
			currentSiteLanguages = "English (United States),Spanish (Spain)",
			defaultSiteLanguage = "English (United States)");

		User.logoutPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name");

		Portlet.viewTitle(portletName = "Hello World");

		Navigator.openWithAppendToBaseURL(urlAppend = "es/web/test-site-name");

		Portlet.viewTitle(portletName = "Hola Mundo");

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name");

		Portlet.viewTitle(portletName = "Hello World");
	}

	@priority = "4"
	test RestrictDefaultLocale {
		property custom.properties = "company.default.locale=es_ES";

		User.firstLoginPG(
			localization = "ES",
			localizedAgree = "Acepto",
			localizedAnswer = "Respuesta",
			localizedSaveButton = "Guardar");

		ProductMenu.gotoPortlet(
			category = "Configuración",
			panel = "Control Panel",
			portlet = "Configuración de instancia");

		PortalSettings.viewCurrentDefaultPortalLanguageCP(
			defaultPortalLanguage = "español (España)",
			localizationTranslation = "Localización");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoAddMenuItem(menuItem = "Contenido web básico");

		PortletEntry.viewLocale(locale = "es-ES");
	}

	@priority = "4"
	test ViewSiteLocaleWithoutUserPreferredLocale {
		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		Site.openSiteSettingsAdmin(siteURLKey = "test-site-name");

		Site.configureCurrentLanguagesCP(
			currentSiteLanguages = "Spanish (Spain)",
			defaultSiteLanguage = "Spanish (Spain)");

		User.logoutPG();

		Navigator.openSiteURL(siteName = "Test Site Name");

		task ("Assert site displays in default locale for guest by asserting sign in text") {
			AssertTextEquals(
				locator1 = "UserBar#USER_SIGN_IN",
				value1 = "Acceder");
		}
	}

	@priority = "4"
	test ViewUserPreferredLocale {
		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Settings");

		Site.configureCurrentLanguagesCP(defaultSiteLanguage = "Spanish (Spain)");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Test Page");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editUserSiteCP(
			siteName = "Site Name",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editDisplaySettingsCP(
			languageName = "中文 (中国)",
			userScreenName = "usersn");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openSiteURL(siteName = "Site Name");

		task ("Assert site displays in user's locale by asserting user dropdown menu item") {
			UserBar.gotoDropdown();

			var dropdownItem = "我的个人资料";
			var key_dropdownItem = "${dropdownItem}";

			AssertTextEquals(
				locator1 = "UserBar#USER_AVATAR_DROPDOWN_ITEM",
				value1 = "${dropdownItem}");
		}
	}

	@description = "This is a use case for LPS-103369."
	@ignore = "true"
	@priority = "4"
	test ViewUserPreferredLocalePersistsFriendlyURL {
		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page",
			widgetName = "Asset Publisher");

		Site.openSiteSettingsAdmin(siteURLKey = "test-site-name");

		Site.configureCurrentLanguagesCP(
			currentSiteLanguages = "Chinese (China),English (United States),Spanish (Spain)",
			defaultSiteLanguage = "English (United States)");

		JSONUser.addUser(
			preferredLocale = "zh_CN",
			userEmailAddress = "test2@liferay.com",
			userFirstName = "test2",
			userLastName = "test2",
			userScreenName = "test2");

		JSONUser.addUserToSite(
			groupName = "Test Site Name",
			userEmailAddress = "test2@liferay.com");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test2@liferay.com",
			userLoginFullName = "test2 test2");

		Navigator.openWithAppendToBaseURL(urlAppend = "es/web/test-site-name");

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name");

		task ("Assert preferred locale persists by asserting Widget name.") {
			AssertElementPresent(
				key_portletName = "资产发布器",
				locator1 = "Portlet#TITLE");
		}
	}

}