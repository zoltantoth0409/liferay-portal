@component-name = "portal-locale"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Locale";

	setUp {
		TestCase.setUpPortalInstance();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "4"
	test RestrictDefaultLanguage {
		property custom.properties = "company.default.locale=es_ES";

		User.firstLoginPG(
			localization = "ES",
			localizedAgree = "Acepto",
			localizedAnswer = "Respuesta",
			localizedSaveButton = "Guardar");

		ProductMenu.gotoPortlet(
			category = "Configuración",
			panel = "Control Panel",
			portlet = "Configuración de instancia");

		PortalSettings.viewCurrentDefaultPortalLanguageCP(
			defaultPortalLanguage = "español (España)",
			localizationTranslation = "Localización");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoAddMenuItem(menuItem = "Contenido web básico");

		PortletEntry.viewLocale(locale = "es-ES");
	}

	@priority = "4"
	test UserPreferredLanguagesConfiguration {
		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Settings");

		Site.configureCurrentLanguagesCP(defaultSiteLanguage = "Spanish (Spain)");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Test Page");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editUserSiteCP(
			siteName = "Site Name",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editDisplaySettingsCP(
			languageName = "中文 (中国)",
			userScreenName = "usersn");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openSiteURL(siteName = "Site Name");

		User.viewUserLocale(dropdownItem = "我的个人资料");
	}

	@priority = "4"
	test ViewSiteLanguageWithoutUserPreferredLanguages {
		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		Site.openSiteSettingsAdmin(siteURLKey = "test-site-name");

		Site.configureCurrentLanguagesCP(
			currentSiteLanguages = "Spanish (Spain)",
			defaultSiteLanguage = "Spanish (Spain)");

		User.logoutPG();

		Navigator.openSiteURL(siteName = "Test Site Name");

		User.viewUserLocale(localizedSignIn = "Acceder");
	}

}