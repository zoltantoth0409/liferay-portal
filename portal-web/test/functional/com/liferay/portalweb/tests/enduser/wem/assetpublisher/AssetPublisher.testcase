@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Asset Publisher";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Asset Publisher Page"
		);

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name"
		);

		Portlet.addPG(portletName = "Asset Publisher");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-86192. The default asset link behavior should be 'View in Context'."
	@priority = "4"
	test ViewDefaultAssetLinkBehavior {
		echo("## Navigate to Asset Publisher widget display settings.");

		AssetPublisherPortlet.gotoConfigurationPG();

		Navigator.gotoNavTab(navTab = "Display Settings");

		echo("## Assert the default asset link behavior is 'View in Context'.");

		AssertSelectedLabel(
			locator1 = "APConfigurationDisplaysettings#ASSET_LINK_BEHAVIOR_SELECT",
			value1 = "View in Context"
		);
	}

	@description = "This is a test for LPS-93357. The subscribe button can be enabled/disabled."
	@priority = "3"
	test enableDisableSubscribeButton {
		task ("Assert the subscribe button is disabled by default") {
			AssertElementNotPresent(locator1 = "Button#SUBSCRIBE");
		}

		task("Enable the subscribe button") {
			AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Subscribe");
		}

		task ("Assert the subscribe button displays") {
			Navigator.gotoSitePage(
				pageName = "Asset Publisher Page",
				siteName = "Test Site Name"
			);

			AssertElementPresent(locator1 = "Button#SUBSCRIBE");
		}
	}
}