@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Bulk Editing";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		LexiconEntry.changeDisplayStyle(displayStyle = "list");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This test adds a tag for a single document via management toolbar"
	@priority = "4"
	test AddTagToSingleAsset {
		DMDocument.addMultiplePG(dmDocumentFileList = "Document_1.jpeg,Document_2.docx");

		var key_dmDocumentTitle = "Document_1.jpeg";

		Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(tagName = "newtag");

		DMNavigator.gotoDocumentPG(dmDocumentTitle = "Document_1.jpeg");

		DMDocument.viewTag(tagName = "newtag");

		SearchPortlets.searchEmbedded(searchTerm = "newtag");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Document_1.jpeg",
			searchAssetType = "Document");
	}

	@description = "This test edits tags by bulk using Append action."
	@priority = "4"
	test EditAndAppendTags {
		DMDocument.addMultiplePG(dmDocumentFileList = "Document_1.doc,Document_2.jpg,Document_3.xls,Document_4.txt");

		for (var key_dmDocumentTitle : list "Document_2.jpg,Document_4.txt") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "tagone");

		for (var key_dmDocumentTitle : list "Document_2.jpg,Document_4.txt") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewTag(tagName = "tagone");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}

		for (var key_dmDocumentTitle : list "Document_2.jpg,Document_4.txt") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "tagtwo");

		for (var key_dmDocumentTitle : list "Document_2.jpg,Document_4.txt") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewTag(tagNameList = "tagone,tagtwo");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}

		for (var key_searchTerm : list "tagone,tagtwo") {
			SearchPortlets.searchEmbedded(searchTerm = "${key_searchTerm}");

			SearchResultPortlet.viewSearchResults(
				searchAssetTitle = "Document_2.jpg",
				searchAssetType = "Document");

			SearchResultPortlet.viewSearchResults(
				searchAssetTitle = "Document_4.txt",
				searchAssetType = "Document");
		}
	}

	@description = "This test edits tags by bulk using Replace action, also covers LPS-91586."
	@priority = "4"
	test EditAndReplaceTags {
		DMDocument.addMultiplePG(dmDocumentFileList = "Document_1.doc,Document_2.jpg,Document_3.xls,Document_4.txt");

		var key_dmDocumentTitle = "Document_1.doc";

		Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(tagName = "one");

		Uncheck(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");

		var key_dmDocumentTitle = "Document_2.jpg";

		Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(tagName = "two");

		Uncheck(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");

		var key_dmDocumentTitle = "Document_3.xls";

		Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(tagName = "three");

		for (var key_dmDocumentTitle : list "Document_1.doc,Document_3.xls") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(
			editAction = "Replace",
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "four");

		for (var key_dmDocumentTitle : list "Document_1.doc,Document_3.xls") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewTag(tagNameList = "four");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}

		SearchPortlets.searchEmbedded(searchTerm = "four");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Document_1.doc",
			searchAssetType = "Document");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Document_3.xls",
			searchAssetType = "Document");

		for (var key_searchTerm : list "one,three") {
			SearchPortlets.searchEmbedded(searchTerm = "${key_searchTerm}");

			SearchResultPortlet.viewNoSearchResults(searchTerm = "${key_searchTerm}");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		for (var key_dmDocumentTitle : list "Document_1.doc,Document_2.jpg") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		Click(locator1 = "Icon#EDIT_TAGS");

		DMDocument.editTags(
			editAction = "Replace",
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "");

		for (var key_dmDocumentTitle : list "Document_1.doc,Document_2.jpg") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewNoTag();

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}
	}

	@description = "This test selects multiple documents and moves them into one folder."
	@priority = "5"
	test MoveDocumentsToFolder {
		DMFolder.addPG(
			dmFolderDescription = "Folder 1",
			dmFolderName = "Folder 1");

		DMDocument.addMultiplePG(dmDocumentFileList = "Document_1.doc,Document_2.doc");

		for (var key_dmDocumentTitle : list "Document_1.doc,Document_2.doc") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		DMDocument.moveToFolderPG(
			bulkSelection = "2 of 3 Selected",
			dmFolderName = "Folder 1");

		Navigator.openURL();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "Folder 1");

		DMDocument.viewPG(dmDocumentTitle = "Document_1.doc");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "Folder 1");

		DMDocument.viewPG(dmDocumentTitle = "Document_2.doc");
	}

	@description = "This test covers LPS-89636. It saves tags without clicking Enter or adding a Comma."
	@priority = "4"
	test SaveTags {
		DMDocument.addMultiplePG(dmDocumentFileList = "Document_1.doc,Document_2.jpg,Document_3.xls,Document_4.txt");

		for (var key_dmDocumentTitle : list "Document_1.doc,Document_2.jpg,Document_3.xls,Document_4.txt") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		Click(locator1 = "Icon#EDIT_TAGS");

		SelectFrame(value1 = "relative=top");

		AssertTextEquals(
			locator1 = "Modal#HEADER",
			value1 = "Edit Tags");

		AssertTextEquals.assertPartialText(
			locator1 = "Modal#BODY",
			value1 = "You are editing the common tags for 4 items. Select edit or replace current tags.");

		Click(locator1 = "Modal#BODY");

		Type(
			locator1 = "DocumentsAndMedia#EDIT_TAGS_FIELD",
			value1 = "tagone");

		Button.clickSave();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "Changes saved.");

		for (var key_dmDocumentTitle : list "Document_1.doc,Document_2.jpg,Document_3.xls,Document_4.txt") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewTag(tagName = "tagone");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}
	}

	@description = "This test covers LPS-91524. It asserts that the Edit Tags icon is disabled when workflow is enabled. "
	@priority = "4"
	test ViewEditTagsIconWithWorkflow {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		DMFolder.addCP(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.addWithWorkflowCP(
			dmDocumentDescription = "DM Document 1 in Folder",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document 1 in Folder");

		DMDocument.addWithWorkflowCP(
			dmDocumentDescription = "DM Document 2 in Folder",
			dmDocumentFile = "Document_2.jpg",
			dmDocumentTitle = "DM Document 2 in Folder");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		for (var key_dmDocumentTitle : list "DM Document 1 in Folder,DM Document 2 in Folder") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		AssertElementPresent(locator1 = "Icon#EDIT_TAGS_DISABLED");
	}

}