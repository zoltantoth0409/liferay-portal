@component-name = "portal-change-list"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Change List";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "5"
	test CannotEnablePublicationsWhenStagingIsTurnedOn {
		property test.name.skip.portal.instance = "ChangeListConfiguration#CannotEnablePublicationsWhenStagingIsTurnedOn";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "Staging",
			site = "Guest");

		Staging.activateStagingCP(siteName = "${siteName}");

		ApplicationsMenu.gotoPortlet(
			category = "Publications",
			panel = "Applications",
			portlet = "Settings");

		Check.checkToggleSwitch(locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH");

		Click(locator1 = "Button#SUBMIT");

		Alert.viewErrorMessage(errorMessage = "Staging is enabled for at least one site in the current virtual instance. Publications and staging cannot be used together.");
	}

	@priority = "5"
	test EnableDisablePublications {
		property test.name.skip.portal.instance = "ChangeListConfiguration#EnableDisablePublications";

		ApplicationsMenu.gotoPortlet(
			category = "Publications",
			panel = "Applications",
			portlet = "Settings");

		ChangeList.enablePublications();

		ApplicationsMenu.gotoPortlet(
			category = "Publications",
			panel = "Applications",
			portlet = "Settings");

		ChangeList.disablePublications();

		AssertElementPresent(locator1 = "ChangeListSettings#DISABLED_SAVE_AND_GO_TO_OVERVIEW_BUTTON");
	}

	@priority = "5"
	test EnablePublicationsAndGoToOverview {
		property test.name.skip.portal.instance = "ChangeListConfiguration#EnablePublicationsAndGoToOverview";

		ApplicationsMenu.gotoPortlet(
			category = "Publications",
			panel = "Applications",
			portlet = "Settings");

		ChangeList.enablePublications(gotoOverview = "true");

		AssertTextEquals(
			locator1 = "ChangeList#MANGE_PUBLICATIONS_PAGE_TITLE",
			value1 = "Publications");

		AssertTextPresent(
			key_publicationName = "Production",
			locator1 = "ChangeList#CHANGE_LIST_INDICATOR_TITLE",
			value1 = "Production");

		Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

		for (var menuItem : list "Select a Publication,Create New Publication") {
			MenuItem.viewPresent(menuItem = "${menuItem}");
		}

		for (var menuItem : list "Review Changes,Prepare to Publish") {
			MenuItem.viewNotPresent(menuItem = "${menuItem}");
		}

		Navigator.openURL();

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "Publishing");

		AssertElementNotPresent(
			key_portlet = "Staging",
			locator1 = "ProductMenu#PORTLET");
	}

}