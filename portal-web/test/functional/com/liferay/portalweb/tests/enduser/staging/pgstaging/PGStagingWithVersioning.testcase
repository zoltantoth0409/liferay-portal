@component-name = "portal-staging"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Staging";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Staging Test Page");

		Navigator.openSiteURL(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		Staging.activateStagingCP(
			pageVersioningEnabled = "true",
			siteName = "Site Name");

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}
	}

	@priority = "4"
	test AddEventRepeatDaily {
		Staging.addEventRepeatDaily(
			scheduleTitleName = "Event Daily Name",
			siteName = "Site Name");
	}

	@priority = "5"
	test AddPageVariation {
		property portal.acceptance = "true";
		property test.assert.warning.exceptions = "true";
		property testray.component.names = "Training";

		Staging.addPageVariationPG(pageVariationName = "Page Variation Name");

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name");

		Staging.viewPageVariationPG(pageVariationName = "Page Variation Name");

		Navigator.openStagingSiteURL(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Staging Test Page");

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "de-DE",
			localizedFriendlyURL = "/german");

		PortletEntry.inputName(name = "German Staging Test Page");

		Button.click(button = "Save");

		Alert.viewSuccessMessage();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Staging Test Page");

		Page.viewPageFriendlyURLPG(
			friendlyUrlLocalized = "de_DE",
			pageName = "german");
	}

	@priority = "5"
	test AddPageWithPageVersioningEnabled {
		Navigator.openSiteURL(siteName = "Site Name");

		ProductMenu.gotoStagingSite();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Staging Page Versioning");

		Navigator.gotoStagedSitePage(
			pageName = "Staging Page Versioning",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Hello World");

		Staging.markAsReadyForPublication();

		Navigator.openSiteURL(siteName = "Site Name");

		Navigator.gotoStagedView();

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		Navigator.gotoStagedSitePage(
			pageName = "Staging Page Versioning",
			siteName = "Site Name");

		Portlet.viewSpecificPG(portletName = "Hello World");
	}

	@priority = "5"
	test AddSitePagesVariation {
		Staging.addSitePagesVariationPG(sitePagesVariationName = "Site Pages Variation Name");

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name");

		Staging.viewSitePagesVariationPG(sitePagesVariationName = "Site Pages Variation Name");
	}

}