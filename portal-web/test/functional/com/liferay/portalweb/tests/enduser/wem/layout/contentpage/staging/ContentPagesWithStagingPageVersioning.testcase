@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Activate local live staging with page versioning enabled") {
			Staging.openStagingAdmin(siteURLKey = "test-site-name");

			Staging.activateStagingCP(
				pageVersioningEnabled = "true",
				siteName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-125755. Can switch to specific Site Pages Variation on content page when enable page versioning."
	@priority = "5"
	test ChangeSitePagesVariationInViewMode {
		task ("Add a public content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

			ContentPages.addPage(pageName = "Test Page Name");
		}

		task ("Add a Card fragment to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Card");

			PageEditor.clickPublish();
		}

		task ("Navigate to the content page in the staging site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");
		}

		task ("Assert the Card fragment is shown") {
			AssertTextEquals.assertPartialText(
				key_fragmentName = "card",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "Card Title Example");
		}

		task ("Assert the Main Variation is selected in Site Pages Variation field") {
			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Main Variation");
		}

		task ("Add a new site pages variation") {
			Staging.addSitePagesVariationPG(sitePagesVariationName = "Test Site Pages Variation");
		}

		task ("Assert the new site pages variation is selected in Site Pages Variation field") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");

			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Test Site Pages Variation");
		}

		task ("Assert the Card fragment is shown") {
			AssertTextEquals.assertPartialText(
				key_fragmentName = "card",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "Card Title Example");
		}

		task ("Change to Main Variation") {
			Staging.changeSitePageVariationPG(
				sitePagesVariationMenulistName = "Main Variation",
				sitePagesVariationName = "Test Site Pages Variation");
		}

		task ("Assert the Main Variation is selected in Site Pages Variation field") {
			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Main Variation");
		}

		task ("Assert the Card fragment is shown") {
			AssertTextEquals.assertPartialText(
				key_fragmentName = "card",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "Card Title Example");
		}
	}

	@description = "This is a test for LPS-125755. Can manage Site Pages Navigation on content page when enable page versioning."
	@priority = "5"
	test ManageSitePagesVariationInViewMode {
		task ("Add a public content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

			ContentPages.addPage(pageName = "Test Page Name");
		}

		task ("Add a Paragraph fragment to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Paragraph");

			PageEditor.clickPublish();
		}

		task ("Navigate to the content page in the staging site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");
		}

		task ("Assert the Paragraph fragment is shown") {
			AssertTextEquals.assertPartialText(
				key_fragmentName = "paragraph",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "A paragraph is a self-contained unit of a discourse in writing dealing with a particular point or idea.");
		}

		task ("Assert the Main Variation is selected in Site Pages Variation field") {
			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Main Variation");
		}

		task ("Open Site Pages Variation modal") {
			Staging.navigateToSitePageVariation();
		}

		task ("Assert actions of the Main Variation") {
			LexiconEntry.openEntryMenu(rowEntry = "Main Variation");

			for (var menuItem : list "Edit,Permissions,Merge") {
				MenuItem.viewVisible(menuItem = "${menuItem}");
			}

			MenuItem.viewNotPresent(menuItem = "Delete");
		}

		task ("Rename the Main Variation") {
			MenuItem.click(menuItem = "Edit");

			Type(
				locator1 = "StagingManageVariations#MANAGE_VARIATION",
				value1 = "Default Variation");

			Click(locator1 = "StagingManageVariations#UPDATE_BUTTON");

			Alert.viewSuccessMessage();
		}

		task ("Assert the Main Variation is renamed") {
			LexiconEntry.viewEntryName(rowEntry = "Default Variation");
		}

		task ("Add a new site pages variation") {
			Click(locator1 = "StagingManageVariations#ADD_SITE_PAGES_VARIATIONS_BUTTON");

			Type(
				locator1 = "StagingManageVariations#MANAGE_VARIATION",
				value1 = "Test Site Pages Variation");

			Click(locator1 = "StagingManageVariations#ADD");

			Alert.viewSuccessMessage();
		}

		task ("Assert actions of the new site pages variation") {
			LexiconEntry.openEntryMenu(rowEntry = "Test Site Pages Variation");

			for (var menuItem : list "Edit,Permissions,Merge,Delete") {
				MenuItem.viewVisible(menuItem = "${menuItem}");
			}
		}

		task ("Delete the new site pages variation") {
			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm.assertPartialConfirmation(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}

		task ("Assert the new site pages variation is deleted") {
			LexiconEntry.viewNoEntry(rowEntry = "Test Site Pages Variation");
		}

		task ("Assert the Default Variation is selected in Site Pages Variation field") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");

			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Default Variation");
		}

		task ("Assert the Paragraph fragment is shown") {
			AssertTextEquals.assertPartialText(
				key_fragmentName = "paragraph",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "A paragraph is a self-contained unit of a discourse in writing dealing with a particular point or idea.");
		}
	}

	@description = "This is a test for LPS-125755. The Site Pages Variation is enabled on content page in view mode when enable page versioning."
	@priority = "5"
	test PublishContentPage {
		property portal.acceptance = "true";

		task ("Add a public content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

			ContentPages.addPage(pageName = "Public Content Page Name");
		}

		task ("Add a Heading fragment to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");

			PageEditor.clickPublish();
		}

		task ("Navigate to the public content page in the staging site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Public Content Page Name",
				siteName = "Test Site Name Staging");
		}

		task ("Assert the Heading fragment is shown") {
			AssertTextEquals(
				key_fragmentName = "heading",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "Heading Example");
		}

		task ("Assert Live item is not shown on navbar") {
			AssertElementNotPresent(locator1 = "Staging#LIVE_BUTTON");
		}

		task ("Assert the Site Page Variation is shown in staging bar") {
			AssertVisible(
				key_stagingVariationLabel = "Site Pages Variation",
				locator1 = "Staging#STAGING_VARIATION_LABEL");
		}

		task ("Assert the Main Variation is selected in Site Pages Variation field") {
			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Main Variation");
		}

		task ("Assert the workflow status is Approved") {
			AssertTextEquals(
				locator1 = "Staging#WORKFLOW_STATUS",
				value1 = "Approved");
		}

		task ("Switch Ready for Publish Process to Yes") {
			Staging.markAsReadyForPublication();
		}

		task ("Assert the workflow status is Ready for Publish Process") {
			Pause(locator1 = "3000");

			AssertTextEquals(
				locator1 = "Staging#WORKFLOW_STATUS",
				value1 = "Ready for Publish Process");
		}

		task ("Publish to Live") {
			Staging.gotoPublishToLive();

			Staging.publishToLive();
		}

		task ("Assert the workflow status is In Live") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Public Content Page Name",
				siteName = "Test Site Name Staging");

			AssertTextEquals(
				locator1 = "Staging#WORKFLOW_STATUS",
				value1 = "In Live");
		}

		task ("Navigate to the public content page in the live site") {
			Click(locator1 = "Staging#LIVE_BUTTON");
		}

		task ("Assert the Heading fragment is shown") {
			AssertTextEquals(
				key_fragmentName = "heading",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "Heading Example");
		}

		task ("Add a private content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

			ContentPages.addPrivatePage(pageName = "Private Content Page Name");
		}

		task ("Add a Button fragment to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Button");

			PageEditor.clickPublish();
		}

		task ("Navigate to the private content page in the staging site") {
			Navigator.openWithAppendToBaseURL(urlAppend = "group/test-site-name-staging/private-content-page-name");
		}

		task ("Assert the Button fragment is shown") {
			AssertTextEquals(
				key_fragmentName = "button",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "Go Somewhere");
		}

		task ("Assert Live item is not shown on navbar") {
			AssertElementNotPresent(locator1 = "Staging#LIVE_BUTTON");
		}

		task ("Assert the Site Page Variation is shown in staging bar") {
			AssertVisible(
				key_stagingVariationLabel = "Site Pages Variation",
				locator1 = "Staging#STAGING_VARIATION_LABEL");
		}

		task ("Assert the Main Variation is selected in Site Pages Variation field") {
			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Main Variation");
		}

		task ("Assert the workflow status is Approved") {
			AssertTextEquals(
				locator1 = "Staging#WORKFLOW_STATUS",
				value1 = "Approved");
		}

		task ("Switch Ready for Publish Process to Yes") {
			Staging.markAsReadyForPublication();
		}

		task ("Assert the workflow status is Ready for Publish Process") {
			Pause(locator1 = "3000");

			AssertTextEquals(
				locator1 = "Staging#WORKFLOW_STATUS",
				value1 = "Ready for Publish Process");
		}

		task ("Publish to Live") {
			Staging.gotoPublishToLive();

			Staging.publishToLive();
		}

		task ("Assert the workflow status is In Live") {
			Navigator.openWithAppendToBaseURL(urlAppend = "group/test-site-name-staging/private-content-page-name");

			AssertTextEquals(
				locator1 = "Staging#WORKFLOW_STATUS",
				value1 = "In Live");
		}

		task ("Navigate to the private content page in the live site") {
			Click(locator1 = "Staging#LIVE_BUTTON");
		}

		task ("Assert the Button fragment is shown") {
			AssertTextEquals(
				key_fragmentName = "button",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "Go Somewhere");
		}
	}

	@description = "This is a test for LPS-125755. The Site Pages Variation is not shown on content page in edit mode when enable page versioning."
	@priority = "4"
	test SitePagesVariationOnlyShownInViewMode {
		task ("Add a public content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

			ContentPages.addPage(pageName = "Test Page Name");
		}

		task ("Assert the Site Page Variation is not shown in management bar") {
			AssertElementNotPresent(
				key_stagingVariationLabel = "Site Pages Variation",
				locator1 = "Staging#STAGING_VARIATION_LABEL");

			AssertElementNotPresent(
				key_sitePagesVariationName = "Main Variation",
				locator1 = "Staging#SITE_PAGES_VARIATION_DROPDOWN");
		}

		task ("Assert the Experience is shown in management bar") {
			AssertVisible(locator1 = "PageEditor#EXPERIENCE_LABEL");
		}

		task ("Add a HTML fragment to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "HTML");

			PageEditor.clickPublish();
		}

		task ("Navigate to the content page in the staging site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");
		}

		task ("Assert the Site Page Variation is shown in staging bar") {
			AssertVisible(
				key_stagingVariationLabel = "Site Pages Variation",
				locator1 = "Staging#STAGING_VARIATION_LABEL");
		}

		task ("Assert the Main Variation is selected in Site Pages Variation field") {
			Staging.viewSitePagesVariationPG(sitePagesVariationName = "Main Variation");
		}

		task ("Assert the HTML fragment is shown") {
			AssertTextEquals.assertPartialText(
				key_fragmentName = "html",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT",
				value1 = "HTML Example");
		}

		task ("Access to content page in edit mode") {
			ContentPages.clickPencil();
		}

		task ("Assert the Site Page Variation is not shown in management bar") {
			AssertElementNotPresent(
				key_stagingVariationLabel = "Site Pages Variation",
				locator1 = "Staging#STAGING_VARIATION_LABEL");

			AssertElementNotPresent(
				key_sitePagesVariationName = "Main Variation",
				locator1 = "Staging#SITE_PAGES_VARIATION_DROPDOWN");
		}

		task ("Assert the Experience is shown in management bar") {
			AssertVisible(locator1 = "PageEditor#EXPERIENCE_LABEL");
		}
	}

}