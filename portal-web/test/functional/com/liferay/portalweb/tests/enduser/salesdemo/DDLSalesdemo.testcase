@component-name = "portal-web-forms-and-data-lists"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Dynamic Data Lists";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			DDLList.tearDownCP();

			DDLDataDefinition.tearDownCP();

			Page.tearDownCP();
		}
	}

	@priority = "3"
	test Demo1 {
		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Company Picnic");

		SitePages.configurePage(
			columnName = "1 Column",
			pageName = "Company Picnic",
			privacy = "Public Pages",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Company Picnic");

		Portlet.addPG(portletName = "Dynamic Data Lists Display");

		DDLList.addViaDDLDisplayPG(
			ddlListDescription = "Sign up list for our company picnic.",
			ddlListName = "Potluck Sign Up List");

		DDLDataDefinition.addViaDDLDisplayPG(
			ddlDataDefinitionDescription = "List for Potlucks",
			ddlDataDefinitionName = "Potluck Items");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "First Name",
			fieldName = "FirstName");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "Last Name",
			fieldName = "LastName");

		DynamicDataMapping.addField(
			field = "Select",
			fieldFieldLabel = "What are you bringing?",
			fieldName = "WhatAreYouBringing");

		DynamicDataMapping.editFieldOptions(
			field = "Select",
			fieldFieldLabel = "What are you bringing?",
			fieldName = "WhatAreYouBringing",
			fieldOptionsNames = "Carrots,Beef Jerky,Chips",
			fieldOptionsValues = "Carrots,Beef Jerky,Chips");

		DDLDataDefinition.saveViaDDLDisplayPG();

		DDLDataDefinition.selectViaDDLDisplayPG(ddlDataDefinitionName = "Potluck Items");

		DDLList.saveViaDDLDisplayPG();

		Navigator.gotoPage(pageName = "Company Picnic");

		DDLRecord.addViaDDLDisplayPG(ddlDataDefinitionName = "Potluck Items");

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "John",
			fieldFieldLabel = "First Name",
			fieldName = "FirstName");

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "Doe",
			fieldFieldLabel = "Last Name",
			fieldName = "LastName");

		DDLRecord.populateFieldSelect(
			ddlRecordFieldData = "Beef Jerky",
			fieldFieldLabel = "What are you bringing?",
			fieldName = "WhatAreYouBringing");

		PortletEntry.publish();

		DDLRecord.addViaDDLDisplayPG(ddlDataDefinitionName = "Potluck Items");

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "Jane",
			fieldFieldLabel = "First Name",
			fieldName = "FirstName");

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "Smith",
			fieldFieldLabel = "Last Name",
			fieldName = "LastName");

		DDLRecord.populateFieldSelect(
			ddlRecordFieldData = "Carrots",
			fieldFieldLabel = "What are you bringing?",
			fieldName = "WhatAreYouBringing");

		PortletEntry.publish();

		for (var ddlRecordFieldData : list "John,Jane") {
			DDLRecord.viewViaDDLDisplayTable(
				ddlRecordFieldData = "${ddlRecordFieldData}",
				fieldFieldLabel = "First Name");
		}

		for (var ddlRecordFieldData : list "Doe,Smith") {
			DDLRecord.viewViaDDLDisplayTable(
				ddlRecordFieldData = "${ddlRecordFieldData}",
				fieldFieldLabel = "Last Name");
		}

		for (var ddlRecordFieldData : list "Beef Jerky,Carrots") {
			DDLRecord.viewViaDDLDisplayTable(
				ddlRecordFieldData = "${ddlRecordFieldData}",
				fieldFieldLabel = "What are you bringing");
		}

		DDLRecord.viewStatusViaDDLDisplayTable(
			fieldFieldLabel = "Status",
			workflowStatus = "Approved");

		DDLRecord.viewViaDDLDisplayTable(
			ddlRecordFieldData = "Test Test",
			fieldFieldLabel = "Author");

		DDLDisplayPortlet.configureListViewPG(
			configureCheckBoxName = "TEMPLATES_SPREADSHEET_VIEW_CHECKBOX",
			configureChecked = "true");

		PortletEntry.save();

		Navigator.gotoPage(pageName = "Company Picnic");

		DDLRecord.populateFieldTextViaSpreadsheetView(
			ddlRecordFieldData = "Joe",
			ddlRecordRow = "3",
			fieldName = "FirstName");

		DDLRecord.populateFieldTextViaSpreadsheetView(
			ddlRecordFieldData = "Perkins",
			ddlRecordRow = "3",
			fieldName = "LastName");

		DDLRecord.populateFieldSelectViaSpreadsheetView(
			ddlRecordFieldData = "Chips",
			ddlRecordRow = "3",
			fieldName = "WhatAreYouBringing");
	}

}