@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Import test fragments from dependencies folder") {
			FragmentsAdmin.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdmin.importCollectionFromDependenciesFolder(
				collectionZipFile = "fragment-collection.zip"
			);
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "Add a content page with fragments."
	@priority = "5"
	test AddContentPage {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		task ("Add a content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Content Page Name");
		}

		task ("Add a Title fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title"
			);
		}

		task ("Add a Rich Text fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text"
			);
		}

		task ("Add a Button fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Button"
			);
		}

		task ("Publish the content page") {
			PageEditor.clickPublish();
		}

		task ("Navigate to the content page") {
			Navigator.openWithAppendToBaseURL(
				urlAppend = "web/test-site-name/test-content-page-name"
			);
		}

		task ("Assert the Title fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1"
			);
		}

		task ("Assert the Rich Text fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum.",
				key_fragmentClass = "rich-text",
				key_position = "2"
			);
		}

		task ("Assert the Button fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Submit",
				key_fragmentClass = "footer",
				key_position = "3"
			);

			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Cancel",
				key_fragmentClass = "footer",
				key_position = "3"
			);
		}

		task ("Assert the Submit and Cancel buttons in the Button fragment is present") {
			for (var button : list "Submit,Cancel") {
				Button.viewPresent(button = "${button}");
			}
		}
	}

	@description = "Create a content page from a page template with fragments."
	@priority = "5"
	test AddContentPageFromPageTemplate {
		property portal.acceptance = "true";

		task ("Add a page template collection") {
			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			PageTemplatesAdmin.addCollection(collectionName = "Page Template Collection");
		}

		task ("Add a page template with a Button fragment") {
			PageTemplatesAdmin.addPageTemplate(pageTemplateName = "Page Template Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Button"
			);

			Button.clickPublish();
		}

		task ("Add a content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				collectionName = "Page Template Collection",
				pageName = "Test Content Page Name",
				pageTemplateName = "Page Template Name"
			);
		}

		task ("Publish the content page") {
			PageEditor.clickPublish();
		}

		task ("Navigate to the content page") {
			Navigator.openWithAppendToBaseURL(
				urlAppend = "web/test-site-name/test-content-page-name"
			);
		}

		task ("Assert the Button fragment is present and the buttons work") {
			for (var button : list "Submit,Cancel") {
				Button.viewPresent(button = "${button}");
			}

			Button.clickNoError(button = "Submit");

			AssertConfirm(value1 = "Form submitted");
		}
	}

	@description = "Add a content page and edit its fragment immediately. This is a variation of the steps from LPS-85872."
	@priority = "5"
	test AddContentPageWithEditedFragmentText {
		property testray.component.names = "Training";

		task ("Add a content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Content Page Name");
		}

		task ("Add a Content fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Content"
			);
		}

		task ("Edit the Content fragment editable text") {
			PageEditor.editFragmentText(
				fragmentName = "Content",
				id = "content",
				text = "New editable fragment text"
			);
		}

		task ("Publish the content page") {
			PageEditor.clickPublish();
		}

		task ("Navigate to the content page") {
			Navigator.openWithAppendToBaseURL(
				urlAppend = "web/test-site-name/test-content-page-name"
			);
		}

		task ("Assert the Content fragment has the updated editable text") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "New editable fragment text",
				key_fragmentClass = "body",
				key_position = "1"
			);
		}
	}

	@description = "Users can only edit content pages in edit mode."
	@priority = "5"
	test CannotEditInViewMode {
		task ("Add a content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Content Page Name");
		}

		task ("Navigate to the content page") {
			Navigator.openWithAppendToBaseURL(
				urlAppend = "web/test-site-name/test-content-page-name"
			);
		}

		task ("Assert the page cannot be edited in 'View Mode' by asserting the fragments side bar is not present") {
			AssertElementNotPresent(locator1 = "PageEditor#FRAGMENT_SIDEBAR");
		}

		task ("Navigate to the content page's 'Edit Mode'") {
			ContentPages.clickPencil();
		}

		task ("Assert the content page can be edited in 'Edit Mode' by asserting the fragments side bar is present") {
			AssertVisible(locator1 = "PageEditor#FRAGMENT_SIDEBAR");
		}

		task ("Navigate back to the content page") {
			PageEditor.clickBack();
		}

		task ("Assert the page cannot be edited by asserting the fragments side bar is not present") {
			AssertElementNotPresent(locator1 = "PageEditor#FRAGMENT_SIDEBAR");
		}
	}

	@description = "This is a test for LPS-78726. Discarding a draft will revert a content page back to its most recent published version."
	@priority = "5"
	test DiscardContentPageDraft {
		task ("Add a content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Content Page Name");
		}

		task ("Add a Title fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title"
			);
		}

		task ("Discard the draft") {
			PageEditor.clickDiscardDraft();
		}

		task ("Assert the Title fragment is no longer present") {
			AssertElementNotPresent(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1"
			);
		}

		task ("Assert the Discard Draft and Publish buttons are still enabled") {
			AssertVisible(
				locator1 = "Button#ANY_ENABLED",
				key_text = "Discard Draft"
			);

			AssertVisible(
				locator1 = "Button#ANY_ENABLED",
				key_text = "Publish"
			);
		}

		task ("Add a Title fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title"
			);
		}

		task ("Publish the content page") {
			PageEditor.clickPublish();
		}

		task ("Navigate to the content page") {
			Navigator.openWithAppendToBaseURL(
				urlAppend = "web/test-site-name/test-content-page-name"
			);
		}

		task ("Navigate to the content page's 'Edit Mode'") {
			ContentPages.clickPencil();
		}

		task ("Assert the Discard Draft and Publish buttons are now disabled") {
			AssertVisible(
				locator1 = "Button#ANY_DISABLED",
				key_text = "Discard Draft"
			);

			AssertVisible(
				locator1 = "Button#ANY_DISABLED",
				key_text = "Publish"
			);
		}

		task ("Edit the Title fragment's editable text") {
			PageEditor.editFragmentText(
				fragmentName = "Title",
				id = "title",
				text = "New editable fragment text"
			);
		}

		task ("Assert the Discard Draft and Publish buttons are now enabled") {
			AssertVisible(
				locator1 = "Button#ANY_ENABLED",
				key_text = "Discard Draft"
			);

			AssertVisible(
				locator1 = "Button#ANY_ENABLED",
				key_text = "Publish"
			);
		}

		task ("Discard the draft") {
			PageEditor.clickDiscardDraft();
		}

		task ("Assert the Title fragment's text has reverted back to what it was before") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1"
			);
		}

		task ("Assert the Discard Draft and Publish buttons are now disabled") {
			AssertVisible(
				locator1 = "Button#ANY_DISABLED",
				key_text = "Discard Draft"
			);

			AssertVisible(
				locator1 = "Button#ANY_DISABLED",
				key_text = "Publish"
			);
		}

		task ("Add a Rich Text fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text"
			);
		}

		task ("Assert the Discard Draft and Publish buttons are now enabled") {
			AssertVisible(
				locator1 = "Button#ANY_ENABLED",
				key_text = "Discard Draft"
			);

			AssertVisible(
				locator1 = "Button#ANY_ENABLED",
				key_text = "Publish"
			);
		}

		task ("Discard the draft") {
			PageEditor.clickDiscardDraft();
		}

		task ("Assert the Rich Text fragment has been removed") {
			AssertElementNotPresent(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum.",
				key_fragmentClass = "rich-text",
				key_position = "2"
			);
		}

		task ("Assert the Discard Draft and Publish buttons are now disabled") {
			AssertVisible(
				locator1 = "Button#ANY_DISABLED",
				key_text = "Discard Draft"
			);

			AssertVisible(
				locator1 = "Button#ANY_DISABLED",
				key_text = "Publish"
			);
		}
	}

	@description = "Edit an existing content page that has fragments. This testcase covers the bug found in LPS-85872. This test has been extended to cover LPS-78726. Changes to content pages only appear after they are published."
	@priority = "5"
	test EditContentPage {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		task ("Add a content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Content Page Name");
		}

		task ("Add a Title fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title"
			);
		}

		task ("Add a Rich Text fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text"
			);
		}

		task ("Navigate to the content page") {
			Navigator.openWithAppendToBaseURL(
				urlAppend = "web/test-site-name/test-content-page-name"
			);
		}

		task ("Assert the Title fragment is not present because it has not been published yet") {
			AssertElementNotPresent(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1"
			);
		}

		task ("Assert the Rich Text fragment is not present because it has not been published yet") {
			AssertElementNotPresent(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum.",
				key_fragmentClass = "rich-text",
				key_position = "2"
			);
		}

		task ("Navigate to the content page's 'Edit Mode'") {
			ContentPages.clickPencil();
		}

		task ("Publish the content page") {
			PageEditor.clickPublish();
		}

		task ("Assert the Title fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1"
			);
		}

		task ("Assert the Rich Text fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum.",
				key_fragmentClass = "rich-text",
				key_position = "2"
			);
		}

		task ("Navigate to the content page's 'Edit Mode'") {
			ContentPages.clickPencil();
		}

		task ("Add a Content fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Content"
			);
		}

		task ("Edit the Rich Text fragment's editable text") {
			PageEditor.editFragmentText(
				fragmentName = "Rich Text",
				id = "rich-text",
				text = "New editable fragment text"
			);
		}

		task ("View the content page") {
			PageEditor.clickBack();
		}

		task ("Assert the Title fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1"
			);
		}

		task ("Assert the Rich Text fragment is still has its original text because the edit has not been published yet") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum.",
				key_fragmentClass = "rich-text",
				key_position = "2"
			);
		}

		task ("Assert the Content fragment is not present because the adding of the fragment has not been published yet") {
			AssertElementNotPresent(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet, consectetur adipiscing elit. Aenean tempus eleifend lacus nec porttitor. Vestibulum pharetra ullamcorper felis volutpat commodo. Nam id ipsum placerat, hendrerit orci eu, congue nisi. Proin.",
				key_fragmentClass = "body",
				key_position = "3"
			);
		}

		task ("Navigate to the content page's edit mode") {
			ContentPages.clickPencil();
		}

		task ("Publish the content page") {
			PageEditor.clickPublish();
		}

		task ("Assert the Title Fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1"
			);
		}

		task ("Assert the Rich Text fragment with updated editable text is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "New editable fragment text",
				key_fragmentClass = "rich-text",
				key_position = "2"
			);
		}

		task ("Assert the Content fragment is present") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Lorem ipsum dolor sit amet, consectetur adipiscing elit. Aenean tempus eleifend lacus nec porttitor. Vestibulum pharetra ullamcorper felis volutpat commodo. Nam id ipsum placerat, hendrerit orci eu, congue nisi. Proin.",
				key_fragmentClass = "body",
				key_position = "3"
			);
		}
	}

	@description = "This is a test for LPS-81870. In edit mode the page should still show the elements of the page that are not from the page itself, like the header, footer, or elements defined by the theme."
	@priority = "5"
	test ViewEditModeHeaderFooterTheme {
		task ("Add a widget page to the site") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Widget Page Name"
			);
		}

		task ("Add a content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Content Page Name");
		}

		task ("Assert the header and footer are disabled") {
			AssertVisible(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_HEADER"
			);

			AssertVisible(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_FOOTER"
			);
		}

		task ("Assert the header and footer contain the elements that are part of the header and footer") {
			AssertVisible(
				locator1 = "Home#DEFAULT_LOGO"
			);

			AssertTextEquals(
				locator1 = "Home#SITE_NAME",
				value1 = "Test Site Name"
			);

			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "This search bar is not visible to users yet. Set up its destination to make it visible."
			);

			AssertVisible(
				locator1 = "UserBar#USER_AVATAR_IMAGE"
			);

			AssertTextEquals(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_FOOTER",
				value1 = "Powered By Liferay"
			);
		}

		task ("Assert the navigation menu widget with the previously created widget page also displays in the disabled part of the header") {
			AssertVisible(
				locator1 = "Home#PAGE",
				key_pageName = "Test Widget Page Name"
			);
		}

		task ("Assert clicking on the disabled header will cause a popover message explaining that the header is disabled") {
			Click(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_HEADER"
			);

			AssertVisible(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_POPOVER"
			);

			AssertTextEquals(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_POPOVER",
				value1 = "This area is defined by the theme. To change theme settings go to page configuration."
			);
		}

		task ("Click away from the header so the popover will disappear") {
			Click(
				locator1 = "PageEditor#EMPTY_MESSAGE"
			);

			AssertElementNotPresent(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_POPOVER"
			);
		}

		task ("Assert clicking on the disabled footer will cause a popover message explaining that the footer is disabled") {
			Click(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_FOOTER"
			);

			AssertVisible(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_POPOVER"
			);

			AssertTextEquals(
				locator1 = "PageEditor#CONTENT_PAGE_DISABLED_POPOVER",
				value1 = "This area is defined by the theme. To change theme settings go to page configuration."
			);
		}
	}
}