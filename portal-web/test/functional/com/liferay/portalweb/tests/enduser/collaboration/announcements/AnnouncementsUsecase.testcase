@component-name = "portal-collaboration"
definition {

	property custom.properties = "default.layout.template.id=1_column";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Announcements";

	var pageName = "Announcements Page";
	var portletName = "Announcements";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Announcements Page");

		JSONLayout.updateLayoutTemplateOfPublicLayout(
			groupName = "Guest",
			layoutName = "Announcements Page",
			layoutTemplate = "1 Column");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Guest",
			layoutName = "Announcements Page",
			widgetName = "Announcements");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.gotoPage(pageName = "Announcements Page");

			AnnouncementsEntry.tearDownPG();

			Page.tearDownCP();

			JSONUser.tearDownNonAdminUsers();

			Site.tearDownCP();
		}
	}

	@priority = "4"
	test ViewAnnouncementsEntryWithDistributedScope {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		Navigator.gotoPage(pageName = "Announcements Page");

		JSONAnnouncement.addAnnouncement(
			announcementContent = "Announcements Entry Content",
			announcementTitle = "Announcements Entry Title",
			announcementURL = "http://www.liferay.com");

		Navigator.gotoPage(pageName = "Announcements Page");

		AnnouncementsEntry.viewPG(
			distributionScope = "General",
			entryContent = "Announcements Entry Content",
			entryTitle = "Announcements Entry Title");

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Site Page Name");

		JSONLayout.updateLayoutTemplateOfPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Site Page Name",
			layoutTemplate = "1 Column");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Site Page Name",
			widgetName = "Announcements");

		Navigator.gotoSitePage(
			pageName = "Site Page Name",
			siteName = "Test Site Name");

		AnnouncementsEntry.viewPG(
			distributionScope = "General",
			entryContent = "Announcements Entry Content",
			entryTitle = "Announcements Entry Title");

		AnnouncementsEntry.addPG(
			distributionScope = "Test Site Name",
			entryContent = "Announcements Entry2 Scoped Content",
			entryTitle = "Announcements Entry2 Scoped Title",
			entryURL = "http://www.yahoo.com");

		AnnouncementsEntry.viewPG(
			distributionScope = "Test Site Name",
			entryContent = "Announcements Entry2 Scoped Content",
			entryTitle = "Announcements Entry2 Scoped Title");

		Navigator.gotoPage(pageName = "Announcements Page");

		AnnouncementsEntry.viewNoPG(entryTitle = "Announcements Entry2 Scoped Title");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Announcements Page");

		AnnouncementsEntry.viewViaEntriesPG(
			distributionScope = "General",
			entryContent = "Announcements Entry Content",
			entryTitle = "Announcements Entry Title");

		AnnouncementsEntry.viewNoPG(entryTitle = "Announcements Entry2 Scoped Title");

		Navigator.gotoSitePage(
			pageName = "Site Page Name",
			siteName = "Test Site Name");

		AnnouncementsEntry.viewViaEntriesPG(
			distributionScope = "General",
			entryContent = "Announcements Entry Content",
			entryTitle = "Announcements Entry Title");

		AnnouncementsEntry.viewViaEntriesPG(
			distributionScope = "Test Site Name",
			entryContent = "Announcements Entry2 Scoped Content",
			entryTitle = "Announcements Entry2 Scoped Title");
	}

	@priority = "3"
	test ViewAnnouncementsPagination {
		for (var entryCount : list "1,2,3,4,5,6,7") {
			JSONAnnouncement.addAnnouncement(
				announcementContent = "Announcements Entry Content ${entryCount}",
				announcementTitle = "Announcements Entry Title ${entryCount}",
				announcementURL = "http://www.liferay.com",
				userEmailAddress = "test@liferay.com",
				userPassword = "test");
		}

		Navigator.gotoPage(pageName = "Announcements Page");

		AnnouncementsPortlet.configureDisplayCountPG(maxDisplayItems = "4");

		Navigator.gotoPage(pageName = "Announcements Page");

		for (var viewEntryCount : list "4,5,6,7") {
			AnnouncementsEntry.viewViaEntriesPG(
				distributionScope = "General",
				entryContent = "Announcements Entry Content ${viewEntryCount}",
				entryTitle = "Announcements Entry Title ${viewEntryCount}",
				entryURL = "http://www.liferay.com");
		}

		for (var viewEntryCount : list "1,2,3") {
			AssertElementNotPresent(
				key_entryTitle = "Announcements Entry Title ${viewEntryCount}",
				locator1 = "Announcements#CARD_VIEW_ENTRY_TITLE");
		}

		AssertClick(
			locator1 = "Pagination#NEXT_LINK",
			value1 = "Next");

		for (var viewEntryCount : list "1,2,3") {
			AnnouncementsEntry.viewViaEntriesPG(
				distributionScope = "General",
				entryContent = "Announcements Entry Content ${viewEntryCount}",
				entryTitle = "Announcements Entry Title ${viewEntryCount}",
				entryURL = "http://www.liferay.com");
		}

		for (var viewEntryCount : list "4,5,6,7") {
			AssertElementNotPresent(
				key_entryTitle = "Announcements Entry Title ${viewEntryCount}",
				locator1 = "Announcements#CARD_VIEW_ENTRY_TITLE");
		}

		AssertClick(
			locator1 = "Pagination#PREVIOUS_LINK",
			value1 = "Previous");

		for (var viewEntryCount : list "4,5,6,7") {
			AnnouncementsEntry.viewViaEntriesPG(
				distributionScope = "General",
				entryContent = "Announcements Entry Content ${viewEntryCount}",
				entryTitle = "Announcements Entry Title ${viewEntryCount}",
				entryURL = "http://www.liferay.com");
		}

		for (var viewEntryCount : list "1,2,3") {
			AssertElementNotPresent(
				key_entryTitle = "Announcements Entry Title ${viewEntryCount}",
				locator1 = "Announcements#CARD_VIEW_ENTRY_TITLE");
		}

		ApplicationsMenu.gotoPortlet(
			category = "Communication",
			panel = "Applications",
			portlet = "Announcements and Alerts");

		AnnouncementsPortlet.configurePaginationCP(itemsPerPage = "4");

		for (var viewEntryCount : list "1,2,3,4") {
			AnnouncementsEntry.viewList(
				distributionScope = "General",
				entryTitle = "Announcements Entry Title ${viewEntryCount}");
		}

		for (var viewEntryCount : list "5,6,7") {
			AssertElementNotPresent(
				key_entryTitle = "Announcements Entry Title ${viewEntryCount}",
				locator1 = "Blogs#ENTRY_TABLE_TITLE");
		}

		Click(locator1 = "Pagination#NEXT_LINK");

		for (var viewEntryCount : list "5,6,7") {
			AnnouncementsEntry.viewList(
				distributionScope = "General",
				entryTitle = "Announcements Entry Title ${viewEntryCount}");
		}

		for (var viewEntryCount : list "1,2,3,4") {
			AssertElementNotPresent(
				key_entryTitle = "Announcements Entry Title ${viewEntryCount}",
				locator1 = "Blogs#ENTRY_TABLE_TITLE");
		}

		Click(locator1 = "Pagination#PREVIOUS_LINK");

		for (var viewEntryCount : list "1,2,3,4") {
			AnnouncementsEntry.viewList(
				distributionScope = "General",
				entryTitle = "Announcements Entry Title ${viewEntryCount}");
		}

		for (var viewEntryCount : list "5,6,7") {
			AssertElementNotPresent(
				key_entryTitle = "Announcements Entry Title ${viewEntryCount}",
				locator1 = "Blogs#ENTRY_TABLE_TITLE");
		}
	}

}