@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Page Templates";
	property testray.main.component.name = "Page Templates";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a custom master") {
			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");

			MastersPageTemplatesAdmin.addMasters(masterLayoutName = "Custom Masters Page Template");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-102566. There's a custom masters page created in site, when add a new page, there's an option to select the created masters, and the common elements added in masters page are added in page"
	@priority = "5"
	test AddPageBasedOnCustomMastersAndInheritElementsFromCustomMasters {
		task ("Add a custom masters with Button fragment") {
			MastersPageTemplatesAdmin.gotoMastersEllipsisItem(
				item = "Edit",
				masterLayoutName = "Custom Masters Page Template");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Button");

			Button.click(button = "Publish Master");
		}

		task ("Assert custom masters as an option when add a new page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Custom Masters Page Template",
				pageName = "Test Page Name");
		}

		task ("Assert the new page inherits elements from custom masters") {
			AssertVisible(
				key_fragmentName = "button",
				locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");
		}
	}

	@description = "This is a test for LPS-102208. As a master pages administrator I can duplicate a masters template"
	@priority = "5"
	test CanDuplicateMastersPageTemplate {
		MastersPageTemplatesAdmin.gotoMastersEllipsisItem(
			item = "Make a Copy",
			masterLayoutName = "Custom Masters Page Template");

		MastersPageTemplatesAdmin.viewMastersPresent(masterLayoutName = "Custom Masters Page Template (Copy)");
	}

	@description = "This is for LPS-103687. Validate if the Blank page template can not be deleted."
	@priority = "5"
	test CannotDeleteBlankPageTemplate {
		task ("Assert the Blank page template exist") {
			MastersPageTemplatesAdmin.viewMastersPresent(masterLayoutName = "Blank");
		}

		task ("Assert the Blank page template without ellipsis icon button") {
			AssertElementNotPresent(
				key_card = "Blank",
				locator1 = "LexiconCard#CARD_ELLIPSIS");
		}

		task ("Assert the Blank page template without checkbox") {
			AssertElementNotPresent(
				key_card = "Blank",
				locator1 = "LexiconCard#CARD_CHECKBOX");
		}
	}

	@description = "This is for LPS-103687. Validate if the Blank page template can not be edited."
	@priority = "5"
	test CannotEditBlankPageTemplate {
		task ("Assert the Blank page template exist") {
			MastersPageTemplatesAdmin.viewMastersPresent(masterLayoutName = "Blank");
		}

		task ("Assert the Blank page template without ellipsis icon button") {
			AssertElementNotPresent(
				key_card = "Blank",
				locator1 = "LexiconCard#CARD_ELLIPSIS");
		}
	}

	@description = "This is for LPS-102200. Can change master of a content page among any existing masters."
	@priority = "5"
	test ChangeMasterOfContentPageAmongExistingMasters {
		task ("Delete the existing custom masters") {
			if (IsElementPresent(locator1 = "Button#CLOSE_MESSAGE_FLOAT")) {
				Click(locator1 = "Button#CLOSE_MESSAGE_FLOAT");
			}

			MastersPageTemplatesAdmin.deleteMasters(masterLayoutName = "Custom Masters Page Template");

			Alert.viewSuccessMessage();
		}

		task ("Import the site template with custom masters") {
			LAR.importSiteCP(
				larFileName = "custom-masters-site-template.lar",
				siteName = "Test Site Name",
				uploadFrom = "dependencies");
		}

		task ("Add a content page based on Blank") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Page Name 1");
		}

		task ("Assert can change master from Blank to custom master") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertTextEquals(
				key_masterLayoutName = "Blank",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Blank");

			AssertClick(
				key_text = "Change Master",
				locator1 = "Button#ANY",
				value1 = "Change Master");

			Alert.viewSuccessMessage();

			MastersPageTemplates.selectMaster(masterLayoutName = "Custom Masters Page Template");

			AssertTextEquals(
				key_masterLayoutName = "Custom Masters Page Template",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Custom Masters Page Template");

			AssertElementNotPresent(
				key_sectionTitle = "Theme",
				locator1 = "SitePagesEditPage#SECTION_TITLE");

			Button.clickSave();

			Alert.viewSuccessMessage();

			AssertVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			Navigator.gotoBack();

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");

			PageEditor.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("Add a content page based on custom master") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Custom Masters Page Template",
				pageName = "Test Page Name 2");
		}

		task ("Assert can change master from custom master to Blank") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertTextEquals(
				key_masterLayoutName = "Custom Masters Page Template",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Custom Masters Page Template");

			AssertVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			AssertClick(
				key_text = "Change Master",
				locator1 = "Button#ANY",
				value1 = "Change Master");

			Alert.viewSuccessMessage();

			MastersPageTemplates.selectMaster(masterLayoutName = "Blank");

			AssertTextEquals(
				key_masterLayoutName = "Blank",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Blank");

			AssertVisible(
				key_sectionTitle = "Theme",
				locator1 = "SitePagesEditPage#SECTION_TITLE");

			AssertNotVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			Button.clickSave();

			Alert.viewSuccessMessage();

			Navigator.gotoBack();

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");
		}
	}

	@description = "This is for LPS-102200. Can change master of a content page template among any existing masters"
	@priority = "5"
	test ChangeMasterOfContentPageTemplateAmongExistingMasters {
		task ("Delete the existing custom masters") {
			if (IsElementPresent(locator1 = "Button#CLOSE_MESSAGE_FLOAT")) {
				Click(locator1 = "Button#CLOSE_MESSAGE_FLOAT");
			}

			MastersPageTemplatesAdmin.deleteMasters(masterLayoutName = "Custom Masters Page Template");

			Alert.viewSuccessMessage();
		}

		task ("Import the site template with custom masters") {
			LAR.importSiteCP(
				larFileName = "custom-masters-site-template.lar",
				siteName = "Test Site Name",
				uploadFrom = "dependencies");
		}

		task ("Add a page template collection") {
			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			PageTemplatesAdmin.addCollection(collectionName = "Page Template Collection");
		}

		task ("Add a content page template based on Blank") {
			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.addPageTemplate(pageTemplateName = "Page Template Name 1");
		}

		task ("Assert can change master from Blank to custom master") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertTextEquals(
				key_masterLayoutName = "Blank",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Blank");

			AssertClick(
				key_text = "Change Master",
				locator1 = "Button#ANY",
				value1 = "Change Master");

			Alert.viewSuccessMessage();

			MastersPageTemplates.selectMaster(masterLayoutName = "Custom Masters Page Template");

			AssertTextEquals(
				key_masterLayoutName = "Custom Masters Page Template",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Custom Masters Page Template");

			AssertElementNotPresent(
				key_sectionTitle = "Theme",
				locator1 = "SitePagesEditPage#SECTION_TITLE");

			Button.clickSave();

			Alert.viewSuccessMessage();

			AssertVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			Navigator.gotoBack();

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");

			PageEditor.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("Add a content page template based on custom master") {
			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.addPageTemplate(
				masterLayoutName = "Custom Masters Page Template",
				pageTemplateName = "Page Template Name 2");
		}

		task ("Assert can change master from custom master to Blank") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertTextEquals(
				key_masterLayoutName = "Custom Masters Page Template",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Custom Masters Page Template");

			AssertVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			AssertClick(
				key_text = "Change Master",
				locator1 = "Button#ANY",
				value1 = "Change Master");

			Alert.viewSuccessMessage();

			MastersPageTemplates.selectMaster(masterLayoutName = "Blank");

			AssertTextEquals(
				key_masterLayoutName = "Blank",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Blank");

			AssertVisible(
				key_sectionTitle = "Theme",
				locator1 = "SitePagesEditPage#SECTION_TITLE");

			AssertNotVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			Button.clickSave();

			Alert.viewSuccessMessage();

			Navigator.gotoBack();

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");
		}
	}

	@description = "This is for LPS-102200. Can change master of a display page template among any existing masters."
	@priority = "5"
	test ChangeMasterOfDisplayPageTemplateAmongExistingMasters {
		task ("Delete the existing custom masters") {
			if (IsElementPresent(locator1 = "Button#CLOSE_MESSAGE_FLOAT")) {
				Click(locator1 = "Button#CLOSE_MESSAGE_FLOAT");
			}

			MastersPageTemplatesAdmin.deleteMasters(masterLayoutName = "Custom Masters Page Template");

			Alert.viewSuccessMessage();
		}

		task ("Import the site template with custom masters") {
			LAR.importSiteCP(
				larFileName = "custom-masters-site-template.lar",
				siteName = "Test Site Name",
				uploadFrom = "dependencies");
		}

		task ("Add a display page template based on Blank") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.addDisplayPage(displayPageName = "Display Page Name 1");
		}

		task ("Assert can change master from Blank to custom master") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertTextEquals(
				key_masterLayoutName = "Blank",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Blank");

			AssertClick(
				key_text = "Change Master",
				locator1 = "Button#ANY",
				value1 = "Change Master");

			Alert.viewSuccessMessage();

			MastersPageTemplates.selectMaster(masterLayoutName = "Custom Masters Page Template");

			AssertTextEquals(
				key_masterLayoutName = "Custom Masters Page Template",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Custom Masters Page Template");

			AssertElementNotPresent(
				key_sectionTitle = "Theme",
				locator1 = "SitePagesEditPage#SECTION_TITLE");

			Button.clickSave();

			Alert.viewSuccessMessage();

			AssertVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			Navigator.gotoBack();

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");

			PageEditor.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("Add a display page template based on custom masters") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.addDisplayPage(
				displayPageName = "Display Page Name 2",
				masterLayoutName = "Custom Masters Page Template");
		}

		task ("Assert can change master from custom master to Blank") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertTextEquals(
				key_masterLayoutName = "Custom Masters Page Template",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Custom Masters Page Template");

			AssertVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			AssertClick(
				key_text = "Change Master",
				locator1 = "Button#ANY",
				value1 = "Change Master");

			Alert.viewSuccessMessage();

			MastersPageTemplates.selectMaster(masterLayoutName = "Blank");

			AssertTextEquals(
				key_masterLayoutName = "Blank",
				locator1 = "SitePagesEditPage#MASTER_LAYOUT_NAME",
				value1 = "Blank");

			AssertVisible(
				key_sectionTitle = "Theme",
				locator1 = "SitePagesEditPage#SECTION_TITLE");

			AssertNotVisible(
				key_text = "Edit Master",
				locator1 = "Button#ANY");

			Button.clickSave();

			Alert.viewSuccessMessage();

			Navigator.gotoBack();

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");
		}
	}

	@description = "This is for LPS-102200. Assert the content in Drop Zone is not changed when change master of page."
	@priority = "3"
	test ContentInDropZoneIsNotChangedWhenChangeMasterofPage {
		task ("Delete the existing custom masters") {
			MastersPageTemplatesAdmin.deleteMasters(masterLayoutName = "Custom Masters Page Template");
		}

		task ("Import the site template with custom masters") {
			LAR.importSiteCP(
				larFileName = "custom-masters-site-template.lar",
				siteName = "Test Site Name",
				uploadFrom = "dependencies");
		}

		task ("Add a content page based on custom master") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Custom Masters Page Template",
				pageName = "Test Page Name");

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertVisible(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");
		}

		task ("Add a Heading fragment to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Change master from custom master to Blank") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertClick(
				key_text = "Change Master",
				locator1 = "Button#ANY",
				value1 = "Change Master");

			MastersPageTemplates.selectMaster(masterLayoutName = "Blank");

			Button.clickSave();

			Navigator.gotoBack();

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#NAVIGATION_BARS");

			AssertElementNotPresent(
				key_type = "dark",
				locator1 = "PageEditor#FOOTERS");
		}

		task ("Assert the content in drop zone is not changed") {
			PageEditor.viewFragment(fragmentName = "Heading");
		}
	}

	@description = "This is for LPS-102200. Can edit master page template elements from  "Look and Feel" of page editor."
	@priority = "5"
	test EditMasterPageTemplateViaPageEditorLookAndFeel {
		MastersPageTemplatesAdmin.gotoMastersEllipsisItem(
			item = "Edit",
			masterLayoutName = "Custom Masters Page Template");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Button");

		PageEditor.addWidget(portletName = "Asset Publisher");

		Button.click(button = "Publish Master");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		ContentPages.addPage(
			masterLayoutName = "Custom Masters Page Template",
			pageName = "Test Content Page Name");

		PageEditor.gotoTab(tabName = "Look and Feel");

		Click(
			key_text = "Edit Master",
			locator1 = "Button#ANY");

		PageEditor.viewFragment(fragmentName = "Button");

		PageEditor.viewFragment(fragmentName = "Asset Publisher");

		PageEditor.addWidget(portletName = "Web Content Display");

		MastersPageTemplates.propagateChange();

		WebContentDisplayPortlet.viewDefaultMessage();

		AssertVisible(locator1 = "PageEditor#FRAGMENT_SIDEBAR");
	}

	@description = "This is a test for LPS-102566. The new elements can only be added into Drop Zone in a page based on custom master and the elements inherited from the custom master are changless."
	@priority = "3"
	test OnlyAddElementsInDropZoneOfPageAndElementsInheritedAreChangeless {
		task ("Add a custom master with Heading fragment and Asset Publisher portlet") {
			MastersPageTemplatesAdmin.gotoMastersEllipsisItem(
				item = "Edit",
				masterLayoutName = "Custom Masters Page Template");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");

			PageEditor.addWidget(portletName = "Asset Publisher");

			Button.click(button = "Publish Master");
		}

		task ("Assert elements inherited from custom masters are changeless") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Custom Masters Page Template",
				pageName = "Test Page Name");

			Click(
				key_position = "2",
				locator1 = "PageEditor#SECTION_CONTAINER");

			AssertElementNotPresent(locator1 = "FragmentEditor#FRAGMENTS_EDITOR_FLOATING_TOOLBAR");

			Click(
				key_fragmentName = "heading",
				locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");

			AssertElementNotPresent(locator1 = "FragmentEditor#FRAGMENTS_EDITOR_FLOATING_TOOLBAR");

			Click(
				key_fragmentName = "Heading",
				key_id = "element-text",
				locator1 = "Fragment#EDITABLE_FIELD");

			AssertElementNotPresent(locator1 = "FragmentEditor#FRAGMENTS_EDITOR_FLOATING_TOOLBAR");

			Click(
				key_position = "3",
				locator1 = "PageEditor#SECTION_CONTAINER");

			AssertElementNotPresent(locator1 = "FragmentEditor#FRAGMENTS_EDITOR_FLOATING_TOOLBAR");

			Click(
				key_portletName = "Asset Publisher",
				locator1 = "PageEditor#PORTLET_INHERITED_FROM_CUSTOM_MASTERS");

			AssertElementNotPresent(locator1 = "FragmentEditor#FRAGMENTS_EDITOR_FLOATING_TOOLBAR");

			AssertNotVisible(
				key_portletName = "Asset Publisher",
				locator1 = "Portlet#SPECIFIC_BORDERLESS_ELLIPSIS_ICON");
		}

		task ("Add a Button fragment to drop zone") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Button");
		}

		task ("Assert can not add a Paragraph fragment to outside of drop zone") {
			PageEditor.viewCannotAddFragmentToOutsideOfDropZone(
				collectionName = "Basic Components",
				fragmentName = "Paragraph");
		}
	}

	@description = "This is a test for LPS-102566. Master page can be used in page, page template and display page, changes in master page will be propagated to page, page template and display page"
	@priority = "5"
	test PropagateChangesFromCustomMastersToAllBasedOnCustomMasters {
		task ("Add a page based on custom masters") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Custom Masters Page Template",
				pageName = "Test Page Name");

			PageEditor.clickPublish();
		}

		task ("Add a content page template based on custom masters") {
			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			PageTemplatesAdmin.addCollection(collectionName = "Page Template Collection");

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.addPageTemplate(
				masterLayoutName = "Custom Masters Page Template",
				pageTemplateName = "Page Template Name");

			PageEditor.clickPublish();
		}

		task ("Add a display page template based on custom masters") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.addDisplayPage(
				displayPageName = "Display Page Name",
				masterLayoutName = "Custom Masters Page Template");

			PageEditor.clickPublish();
		}

		task ("Add a Heading fragment to custom masters") {
			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");

			MastersPageTemplatesAdmin.gotoMastersEllipsisItem(
				item = "Edit",
				masterLayoutName = "Custom Masters Page Template");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");

			MastersPageTemplates.propagateChange();
		}

		task ("Assert the Heading fragment exist in page based on custom masters") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/test-page-name");

			AssertVisible(
				key_fragmentName = "heading",
				locator1 = "Fragment#FRAGMENT_BASIC_COMPONENT");

			ContentPages.clickPencil();

			AssertVisible(
				key_fragmentName = "heading",
				locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");
		}

		task ("Assert the Heading fragment exist in content page template based on custom masters") {
			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.gotoPageTemplateEllipsisItem(
				item = "Edit",
				pageTemplateName = "Page Template Name");

			AssertVisible(
				key_fragmentName = "heading",
				locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");
		}

		task ("Assert the Heading fragment exist in display page template based on custom masters") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.gotoDisplayPageEllipsisItem(
				displayPageName = "Display Page Name",
				item = "Edit");

			AssertVisible(
				key_fragmentName = "heading",
				locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");
		}
	}

}