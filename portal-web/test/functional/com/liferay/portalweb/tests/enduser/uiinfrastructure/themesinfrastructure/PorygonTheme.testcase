@component-name = "portal-user-interface"
definition {
	property osgi.app.includes = "frontend-theme-porygon";
	property plugins.deployment.type = "osgi";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "5"
	test AddThemeSite {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "PorygonTheme#AddThemeSite";
		var themeContainsResources = PropsUtil.get("theme.contains.resources");

		if ("${themeContainsResources}" == "true") {
			ProductMenu.gotoPortlet(
				category = "Sites",
				panel = "Control Panel",
				portlet = "Sites"
			);

			Site.addSiteTemplateCP(
				siteName = "Porygon Theme Site",
				siteTemplateName = "Porygon Theme"
			);

			ProductMenu.gotoSite(site = "Porygon Theme Site");

			AssertClick(locator1 = "Home#PAGE", value1 = "HOME", key_pageName = "home");

			AssertTextEquals(
				locator1 = "Header#H2_TITLE",
				value1 = "The Earth is all we have in common",
				key_title = "The Earth is all we have in common"
			);
		}
		else {
			var appName = "porygon-theme";

			ProductMenu.gotoPortlet(
				category = "Apps",
				panel = "Control Panel",
				portlet = "App Manager"
			);

			Type(locator1 = "TextInput#SEARCH", value1 = "${appName}");

			Click(locator1 = "Icon#BASIC_SEARCH");

			AssertVisible(
				locator1 = "ListGroupItem#ITEM_NAME_LINK",
				key_itemName = "porygon-theme"
			);
		}
	}
}