@component-name = "portal-collaboration"
definition {

	property portal.release = "false";
	property portal.upstream = "true";
	property custom.properties = "default.layout.template.id=1_column";
	property testray.main.component.name = "Microblogs";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(
			pageName = "Microblogs Page",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Microblogs Page");

		Portlet.addPG(portletName = "Microblogs");

		Navigator.gotoPage(pageName = "Microblogs Page");

		AssertTextEquals(
			locator1 = "Portlet#H2_HEADER",
			value1 = "Microblogs");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "4"
	test AddEntry {
		Microblogs.updateStatus(entryContent = "Microblog Entry Content");

		Microblogs.gotoTimelineTab();

		Microblogs.viewEntry(
			entryContent = "Microblog Entry Content",
			userName = "Test Test");
	}

	@priority = "5"
	test DeleteEntry {
		property portal.acceptance = "true";

		Microblogs.updateStatus(entryContent = "Microblog Entry Content");

		Microblogs.gotoTimelineTab();

		Microblogs.deleteStatus(entryContent = "Microblog Entry Content");

		Microblogs.gotoTimelineTab();

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "There are no microblog entries.");
	}

	@priority = "5"
	test EditEntry {
		property portal.acceptance = "true";

		Microblogs.updateStatus(entryContent = "Microblog Entry Content");

		Microblogs.gotoTimelineTab();

		Microblogs.editStatus(
			entryContent = "Microblog Entry Content",
			entryContentEdit = "Edited Microblogs Content");

		Microblogs.gotoTimelineTab();

		Microblogs.viewEntry(
			entryContent = "Edited Microblogs Content",
			userName = "Test Test");
	}

}