@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Documents Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Staging DM Page");

		Navigator.openSiteURL(siteName = "Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}
	}

	@priority = "5"
	test StagingDemoDocumentsAndMedia {
		property custom.properties = "default.layout.template.id=1_column";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "Staging#StagingDemoDocumentsAndMedia";
		property testray.component.names = "Training";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Documents and Media",
			configurationName = "Service",
			configurationScope = "System Scope");

		Type(
			key_fieldLabel = "Maximum File Size",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "30000000");

		ScrollWebElementIntoView(locator1 = "Button#SAVE");

		PortletEntry.save();

		Navigator.gotoSitePage(
			pageName = "Staging DM Page",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Documents and Media");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Staging DM Page",
			siteName = "Site Name");

		DMFolder.addPG(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		DMFolder.addPG(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name Empty");

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Documents and Media");

		DMNavigator.gotoMetadataSets();

		DMMetadataSet.addTitle(metadataSetName = "DM Metadata Set Name");

		DataEngine.addFields(fieldNameList = "Numeric,Text");

		DDLDataDefinition.save();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Documents and Media");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Date",
			dmDocumentTypeName = "DM Document Type Name",
			metadataSetName = "DM Metadata Set Name");

		Navigator.openSiteURL(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "Staging");

		Staging.activateStagingCP(
			pageVersioningEnabled = "true",
			siteName = "Site Name");

		Navigator.gotoSitePage(
			pageName = "Staging DM Page",
			siteName = "Site Name");

		DMFolder.viewCP(dmFolderName = "DM Folder Name Empty");

		Navigator.gotoStagedSitePage(
			pageName = "Staging DM Page",
			siteName = "Site Name");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.addDocumentTypePG(
			dateValue = "01/01/2022",
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.mp3",
			dmDocumentTitle = "DM Document Title",
			dmDocumentTypeName = "DM Document Type Name",
			numericField = "1234",
			textField = "Text");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		Navigator.gotoSitePage(
			pageName = "Staging DM Page",
			siteName = "Site Name");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title");

		Navigator.gotoSitePage(
			pageName = "Staging DM Page",
			siteName = "Site Name");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMNavigator.gotoDocumentPG(dmDocumentTitle = "DM Document Title");

		DMDocument.viewDocumentTypePG(numericValue = "1234");

		DMDocument.viewDocumentTypePG(textContent = "Text");

		DMDocument.viewDocumentTypePG(dateValue = "01/01/2022");
	}

}