@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Navigation";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "5"
	test AddNavigationMenuEntriesAllTypes {
		property portal.acceptance = "true";

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.addPublicPage(pageName = "Test Page Name");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntry(rowEntry = "Primary Menu Name 1");

		NavigationMenusAdmin.addItem(item = "Page", pageNames = "Test Page Name");

		NavigationMenusAdmin.viewItem(itemName = "Test Page Name");

		NavigationMenusAdmin.addItem(item = "Submenu", submenuName = "Submenu Name");

		NavigationMenusAdmin.viewItem(itemName = "Submenu Name");

		NavigationMenusAdmin.addItem(
			item = "URL",
			url = "https://www.liferay.com/",
			urlName = "Liferay"
		);

		NavigationMenusAdmin.viewItem(itemName = "Liferay");
	}

	@priority = "5"
	test AddNavigationMenuEntriesNestItems {
		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		for (var index : list "1,2") {
			SitePages.addPublicPage(pageName = "Test Page Name ${index}");
		}

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntry(rowEntry = "Primary Menu Name 1");

		NavigationMenusAdmin.addItem(
			item = "Page",
			pageNames = "Test Page Name 1,Test Page Name 2"
		);

		for (var index : list "1,2") {
			NavigationMenusAdmin.viewItemOrder(
				index = "${index}",
				itemName = "Test Page Name ${index}"
			);
		}

		NavigationMenusAdmin.viewItemNotNested(itemName = "Test Page Name 2");

		NavigationMenusAdmin.moveItem(direction = "in", itemName = "Test Page Name 2");

		NavigationMenusAdmin.viewItemNested(itemName = "Test Page Name 2");
	}

	@priority = "5"
	test AddNavigationMenuEntriesSortViewOrder {
		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		for (var index : list "1,2,3,4") {
			SitePages.addPublicPage(pageName = "Test Page Name ${index}");
		}

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntry(rowEntry = "Primary Menu Name 1");

		NavigationMenusAdmin.addItem(
			item = "Page",
			pageNames = "Test Page Name 1,Test Page Name 2,Test Page Name 3,Test Page Name 4"
		);

		for (var index : list "1,2,3,4") {
			NavigationMenusAdmin.viewItemOrder(
				index = "${index}",
				itemName = "Test Page Name ${index}"
			);
		}

		NavigationMenusAdmin.moveItem(direction = "up", itemName = "Test Page Name 2");

		NavigationMenusAdmin.viewItemOrder(index = "1", itemName = "Test Page Name 2");

		NavigationMenusAdmin.viewItemOrder(index = "2", itemName = "Test Page Name 1");
	}

	@priority = "3"
	test AddPageToNavigationMenuUponCreation {
		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.addPublicPage(pageName = "Test Page Name");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 2");

		NavigationMenusAdmin.addItem(item = "Page", pageNames = "Test Page Name");

		NavigationMenusAdmin.viewItem(itemName = "Test Page Name");
	}

	@description = "This is a use case for LPS-75061"
	@priority = "5"
	test AddSubmenuNavigationMenu {
		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page Name"
		);

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntry(rowEntry = "Primary Menu Name 1");

		NavigationMenusAdmin.addItem(item = "Submenu", submenuName = "Test Submenu");

		NavigationMenusAdmin.viewItem(itemName = "Test Submenu");

		NavigationMenusAdmin.addItem(
			item = "URL",
			url = "https://www.liferay.com/",
			urlName = "Liferay"
		);

		NavigationMenusAdmin.viewItem(itemName = "Liferay");

		NavigationMenusAdmin.moveItem(direction = "up", itemName = "Liferay");

		NavigationMenusAdmin.moveItem(direction = "in", itemName = "Liferay");

		NavigationMenusAdmin.viewItemNested(itemName = "Liferay");

		ProductMenu.gotoSite(site = "Test Site Name");

		NavigationMenusWidget.gotoNavigationMenuWidgetConfig();

		NavigationMenusWidget.chooseMenu(menuName = "Primary Menu Name 1");

		Portlet.saveAndCloseConfig();

		Page.gotoChildPagePG(childPageName = "Liferay", pageName = "Test Submenu");
	}

	@description = "This is a use case for LPS-75059."
	@priority = "4"
	test AddURLNavigationMenu {
		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.addPublicPage(pageName = "Test Page Name");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Test Navigation Menu Name");

		NavigationMenusAdmin.addItem(
			item = "URL",
			url = "https://www.liferay.com/",
			urlName = "Liferay"
		);

		NavigationMenusAdmin.viewItem(itemName = "Liferay");

		Navigator.openSiteURL(siteName = "Test Site Name");

		NavigationMenusWidget.gotoNavigationMenuWidgetConfig();

		NavigationMenusWidget.chooseMenu(menuName = "Test Navigation Menu Name");

		Portlet.saveAndCloseConfig();

		AssertClick(
			locator1 = "Home#PAGE",
			value1 = "Liferay",
			key_pageName = "Liferay"
		);

		AssertLocation(value1 = "https://www.liferay.com/");
	}

	@priority = "4"
	test ConfigurePrimaryNavigationMenu {
		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 2");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Primary Navigation",
			rowEntry = "Primary Menu Name 2"
		);

		NavigationMenusAdmin.configureMenuType(
			currentPrimaryNavigationMenu = "Primary Menu Name 1",
			menuType = "Primary Navigation",
			navigationMenu = "Primary Menu Name 2"
		);

		NavigationMenusAdmin.viewMenuType(
			menuType = "Primary Navigation",
			rowEntry = "Primary Menu Name 2"
		);
	}

	@priority = "4"
	test ConfigureSecondaryNavigationMenu {
		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 2");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.configureMenuType(
			menuType = "Secondary Navigation",
			navigationMenu = "Primary Menu Name 2"
		);

		NavigationMenusAdmin.viewMenuType(
			menuType = "Secondary Navigation",
			rowEntry = "Primary Menu Name 2"
		);
	}

	@priority = "4"
	test ConfigureSocialNavigationMenu {
		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 2");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.configureMenuType(
			menuType = "Social Navigation",
			navigationMenu = "Primary Menu Name 2"
		);

		NavigationMenusAdmin.viewMenuType(
			menuType = "Social Navigation",
			rowEntry = "Primary Menu Name 2"
		);
	}

	@priority = "5"
	test DeleteNavigationMenu {
		property portal.acceptance = "true";

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.delete(menuItem = "Delete", rowEntry = "Primary Menu Name 1");

		LexiconEntry.viewNoEntry(rowEntry = "Primary Menu Name 1");
	}

	@description = "This is a usecase for LPS-76658"
	@priority = "5"
	test EditNavigationMenuPageItemName {
		property portal.acceptance = "quarantine";

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		for (var index : list "1,2") {
			SitePages.addPublicPage(pageName = "Site Page Name ${index}");
		}

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Test Navigation Menu Name");

		NavigationMenusAdmin.addItem(
			item = "Page",
			pageNames = "Site Page Name 1,Site Page Name 2"
		);

		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Navigation Menus"
		);

		LexiconEntry.gotoEntry(rowEntry = "Test Navigation Menu Name");

		NavigationMenusAdmin.editItem(
			item = "Page",
			itemName = "Site Page Name 2",
			name = "Site Page Name 3"
		);

		NavigationMenusAdmin.viewItem(itemName = "Site Page Name 3");

		Navigator.gotoSitePage(
			pageName = "Site Page Name 1",
			siteName = "Test Site Name"
		);

		NavigationMenusWidget.gotoNavigationMenuWidgetConfig();

		NavigationMenusWidget.selectNavigation(menuType = "Primary Navigation");

		Portlet.saveAndCloseConfig();

		Navigator.gotoSitePage(
			pageName = "Site Page Name 3",
			siteName = "Test Site Name"
		);

		var portalURL = PropsUtil.get("portal.url");

		AssertLocation(value1 = "${portalURL}/web/test-site-name/site-page-name-2");
	}

	@priority = "5"
	test RenameNavigationMenu {
		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Rename",
			rowEntry = "Primary Menu Name 1"
		);

		PortletEntry.inputName(name = "Primary Menu Name 1 Edit");

		Modal.save();

		LexiconEntry.viewEntryName(rowEntry = "Primary Menu Name 1 Edit");
	}

	@description = "This is a use case for LPS-67191."
	@priority = "5"
	test ViewNavigationMenuPropertiesSidebar {
		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntry(rowEntry = "Primary Menu Name 1");

		NavigationMenusAdmin.addItem(
			item = "URL",
			url = "https://www.liferay.com/",
			urlName = "Liferay"
		);

		NavigationMenusAdmin.viewItem(itemName = "Liferay");

		NavigationMenusAdmin.openItemSidebar(itemName = "Liferay");
	}

	@description = "This is a test case for LPS-77912."
	@priority = "4"
	test ViewSuccessMessageAfterAddingMenuItem {
		property portal.acceptance = "true";

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.addPublicPage(pageName = "Test Page Name");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		NavigationMenusAdmin.addMenu(menuName = "Primary Menu Name 1");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "test-site-name");

		LexiconEntry.gotoEntry(rowEntry = "Primary Menu Name 1");

		NavigationMenusAdmin.addItem(item = "Page", pageNames = "Test Page Name");

		Alert.viewSuccessMessage();
	}
}