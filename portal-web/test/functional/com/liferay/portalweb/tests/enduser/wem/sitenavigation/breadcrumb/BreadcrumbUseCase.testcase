@component-name = "portal-breadcrumb"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Breadcrumb,Navigation";
	property testray.main.component.name = "Navigation";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Page.tearDownCP();

			Site.tearDownCP();
		}
	}

	@priority = "3"
	test BreadcrumbConfiguration {
		property test.name.skip.portal.instance = "BreadcrumbUsecase#BreadcrumbConfiguration";

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Parent Page Name");

		SitePages.addPublicChildPage(
			childPageName = "Child Page Name",
			parentPageName = "Parent Page Name");

		Navigator.openSiteURL(siteName = "Site Name");

		Page.gotoChildPagePG(
			childPageName = "Child Page Name",
			pageName = "Parent Page Name");

		Portlet.addPG(portletName = "Breadcrumb");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		Breadcrumb.viewConfigurationPreview(breadcrumbListVisible = "Site Name,Parent Page Name,Child Page Name");

		Breadcrumb.viewConfigurationCheckboxes(
			checkSetupName = "Show Current Site,Show Page,Show Parent Sites,Show Application Breadcrumb",
			uncheckSetupName = "Show Guest Site");

		Breadcrumb.configureBreadcrumb(
			checkSetupName = "Show Guest Site,Show Application Breadcrumb",
			uncheckSetupName = "Show Current Site,Show Page,Show Parent Sites");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		Breadcrumb.viewConfigurationPreview(
			breadcrumbListNotVisible = "Site Name,Parent Page Name,Child Page Name",
			breadcrumbListVisible = "${siteName}");

		Navigator.openSiteURL(siteName = "Site Name");

		Page.gotoChildPagePG(
			childPageName = "Child Page Name",
			pageName = "Parent Page Name");

		Breadcrumb.viewPG(
			breadcrumbListNotVisible = "Site Name,Parent Page Name,Child Page Name",
			breadcrumbListVisible = "${siteName}",
			pathLocator = "PAGE");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		Breadcrumb.configureBreadcrumb(
			checkSetupName = "Show Current Site,Show Application Breadcrumb",
			uncheckSetupName = "Show Guest Site,Show Page,Show Parent Sites");

		Breadcrumb.viewConfigurationPreview(breadcrumbListNotVisible = "Parent Page Name,Child Page Name");

		Navigator.openSiteURL(siteName = "Site Name");

		Page.gotoChildPagePG(
			childPageName = "Child Page Name",
			pageName = "Parent Page Name");

		Breadcrumb.viewPG(
			breadcrumbListNotVisible = "Parent Page Name,Child Page Name",
			pathLocator = "PAGE");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		Breadcrumb.configureBreadcrumb(checkSetupName = "Show Current Site,Show Application Breadcrumb,Show Guest Site,Show Page");

		Breadcrumb.viewConfigurationPreview(breadcrumbListVisible = "${siteName},Site Name,Parent Page Name,Child Page Name");

		Navigator.openSiteURL(siteName = "Site Name");

		Page.gotoChildPagePG(
			childPageName = "Child Page Name",
			pageName = "Parent Page Name");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "${siteName},Site Name,Parent Page Name,Child Page Name",
			pathLocator = "PAGE");
	}

	@priority = "4"
	test BreadcrumbLinkPointsToLocalhost {
		property test.name.skip.portal.instance = "BreadcrumbUsecase#BreadcrumbLinkPointsToLocalhost";
		var portalURL = PropsUtil.get("portal.url");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Site Page Name");

		Navigator.gotoSpecificURLPage(
			pageName = "Site Page Name",
			url = "${portalURL}/web/site-name");

		Portlet.addPG(portletName = "Breadcrumb");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		Breadcrumb.configureBreadcrumb(checkSetupName = "Show Guest Site");

		Navigator.gotoSpecificURLPage(
			pageName = "Site Page Name",
			url = "http://127.0.0.1:8080/web/site-name");

		AssertLocation(value1 = "http://127.0.0.1:8080/web/site-name");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "${siteName},Site Name,Site Page Name",
			pathLocator = "PAGE");

		var key_URL = "${portalURL}";

		AssertElementNotPresent(locator1 = "BreadcrumbPortlet#BREADCRUMB_PAGE_LINK");

		var key_URL = "/web/guest";

		AssertTextEquals(
			locator1 = "BreadcrumbPortlet#BREADCRUMB_PAGE_LINK",
			value1 = "${siteName}");
	}

}