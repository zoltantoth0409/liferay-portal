@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "5"
	test AddStructures {
		property portal.acceptance = "true";
		property testray.component.names = "Training,Web Content Administration";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name 1");

		DataEngine.addFields(fieldNameList = "Date,Image,Upload");

		WebContentStructures.saveCP(structureName = "WC Structure Name 1");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name 2");

		DataEngine.addFields(fieldNameList = "Grid,Link to Page");

		WebContentStructures.saveCP(structureName = "WC Structure Name 2");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name 3");

		DataEngine.addFields(fieldNameList = "Numeric,Single Selection,Select from List,Separator,Text,Rich Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name 3");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.viewCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name 1");

		LexiconEntry.gotoEntry(rowEntry = "WC Structure Name 1");

		for (var structureFieldName : list "Date,Image,Upload") {
			DataEngine.viewField(
				fieldFieldLabel = "${structureFieldName}",
				fieldName = "${structureFieldName}");
		}

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.viewCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name 2");

		LexiconEntry.gotoEntry(rowEntry = "WC Structure Name 2");

		for (var structureFieldName : list "Grid,Link to Page") {
			DataEngine.viewField(
				fieldFieldLabel = "${structureFieldName}",
				fieldName = "${structureFieldName}");
		}

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.viewCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name 3");

		LexiconEntry.gotoEntry(rowEntry = "WC Structure Name 3");

		for (var structureFieldName : list "Numeric,Single Selection,Select from List,Separator,Text,Rich Text") {
			DataEngine.viewField(
				fieldFieldLabel = "${structureFieldName}",
				fieldName = "${structureFieldName}");
		}
	}

	@description = "This is a use case for LPS-77387."
	@priority = "4"
	test AddStructureViaLocaleURL {
		property test.name.skip.portal.instance = "WebContentStructures#AddStructureViaLocaleURL";

		var portalURL = PropsUtil.get("portal.url");

		Navigator.openSpecificURL(url = "${portalURL}/en/");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		Navigator.ensureLocaleURL(locale = "en");

		WebContentStructures.addName(structureName = "WC Structure Name");

		Navigator.gotoSidebarTab(tabName = "Properties");

		PortletEntry.inputDescriptionTextInput(description = "WC Structure Description");

		SelectFrame(value1 = "relative=top");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		AssertElementNotPresent(
			key_ddlDataDefinitionName = "WC Structure Name",
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_2");
	}

	@description = "This is a use case for LPS-126248. Edit field reference of field options in structure editor."
	@priority = "4"
	test EditFieldReferenceOfFieldOption {
		task ("Add a web content structure with a Select from List field") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = "WC Structure Name");

			DataEngine.addField(
				fieldFieldLabel = "Select from List",
				fieldName = "Select from List");
		}

		task ("Add two field options for Select from List") {
			var index = "1";

			for (var optionValue : list "Option 1,Option 2") {
				DataEngine.addFieldOption(
					fieldFieldLabel = "Select from List",
					index = "${index}",
					optionValue = "${optionValue}");

				var index = ${index} + 1;
			}
		}

		task ("Edit field reference of field options") {
			var index = "1";

			for (var fieldReferenceOfFieldOption : list "Value1,Value2") {
				DataEngine.editFieldReferenceOfFieldOption(
					fieldFieldLabel = "Select from List",
					fieldReferenceOfFieldOption = "${fieldReferenceOfFieldOption}",
					index = "${index}");

				var index = ${index} + 1;
			}
		}

		task ("Assert the field reference of field options") {
			Click(
				key_fieldFieldLabel = "Select from List",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER");

			Navigator.gotoNavTab(navTab = "Basic");

			var index = "1";

			for (var fieldReferenceOfFieldOption : list "Value1,Value2") {
				AssertTextEquals(
					index = "${index}",
					locator1 = "Sidebar#OPTION_WITHOUT_LABEL_FIELD_REFERENCE_INPUT",
					value1 = "${fieldReferenceOfFieldOption}");

				var index = ${index} + 1;
			}

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Assert the field reference of field options saved") {
			WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

			Click(
				key_fieldFieldLabel = "Select from List",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER");

			Navigator.gotoNavTab(navTab = "Basic");

			var index = "1";

			for (var fieldReferenceOfFieldOption : list "Value1,Value2") {
				AssertTextEquals(
					index = "${index}",
					locator1 = "Sidebar#OPTION_WITHOUT_LABEL_FIELD_REFERENCE_INPUT",
					value1 = "${fieldReferenceOfFieldOption}");

				var index = ${index} + 1;
			}
		}
	}

	@priority = "4"
	test EditStructureDefaultValuesForFieldsets {
		property testray.component.names = "Web Content Administration";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure 1");

		DataEngine.addFields(fieldNameList = "Text,Rich Text,Image,Upload");

		WebContentStructures.saveCP(structureName = "WC Structure 1");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		LexiconEntry.gotoAdd();

		WebContentStructures.addName(structureName = "WC Structure 2");

		Navigator.gotoSidebarTab(tabName = "Properties");

		PortletEntry.inputDescriptionTextInput(description = "WC Structure Description");

		DataEngine.addFieldsets(
			fieldsetsLabel = "WC Structure 1",
			fieldsetsName = "WC Structure 1");

		DataEngine.addField(
			fieldFieldLabel = "Numeric",
			fieldName = "Numeric");

		WebContentStructures.saveCP(structureName = "WC Structure 1");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.editStructureDefaultValuesCP(structureName = "WC Structure 2");

		WebContent.addWithStructureCP(webContentText = "Edited Text Field");

		PortletEntry.save();
	}

	@description = "This is a use case for LRQA-61632. Can select different type pages in Link to Page field."
	@priority = "4"
	test SelectDifferentTypePagesInLinkToPageField {
		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Widget Page");
		}

		task ("Add a page based on Blank") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(pageName = "Test Content Page");

			PageEditor.clickPublish();
		}

		task ("Add a page based on Search") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(
				collectionName = "Global Templates",
				pageName = "Test Search",
				pageTemplateName = "Search");
		}

		task ("Add a page based on Full Page Application") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(
				pageName = "Test Full Page Application",
				sitePageType = "Full Page Application");
		}

		task ("Add a page based on Page Set") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(
				pageName = "Test Page Set",
				sitePageType = "Page Set");

			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Child Page",
				parentLayoutName = "Test Page Set");
		}

		task ("Add a page based on Embedded") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(
				pageName = "Test Embedded",
				pageTypeURL = "https://www.liferay.com",
				sitePageType = "Embedded");
		}

		task ("Add a page based on Link to a Page of This Site") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(
				linkPageName = "Test Content Page",
				pageName = "Test Link To A Page Of This Site",
				sitePageType = "Link to a Page of This Site");
		}

		task ("Add a page based on Panel") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(
				componentName = "Collaboration",
				pageName = "Test Panel",
				sitePageType = "Panel");
		}

		task ("Add a page based on Link to URL") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(
				pageName = "Test Link To URL",
				pageTypeURL = "https://www.liferay.com",
				sitePageType = "Link to URL");
		}

		task ("Add a structure with 9 Link to Page fields") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = "WC Structure Name");

			for (var fieldLabel : list "Widget Page,Content Page,Search,Full Page Application,Page Set,Embedded,Link To A Page Of This Site,Panel,Link To URL") {
				DataEngine.addField(
					fieldFieldLabel = "${fieldLabel}",
					fieldName = "Link to Page");
			}

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Add a web content based on new structure") {
			NavItem.click(navItem = "Web Content");

			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");
		}

		task ("Select the relevant page in each field") {
			AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

			PortletEntry.inputTitle(title = "Web Content Title");

			for (var fieldLabel : list "Widget Page,Content Page,Search,Full Page Application,Embedded,Link To A Page Of This Site,Panel,Link To URL") {
				WebContent.addWithStructureCP(
					webContentLinkToPage = "Test ${fieldLabel}",
					webContentLinkToPageFieldLabel = "${fieldLabel}");
			}

			WebContent.addWithStructureCP(
				webContentLinkToPage = "Test Child Page",
				webContentLinkToPageFieldLabel = "Page Set");

			PortletEntry.publish();
		}

		task ("Add a Web Content Display to Test Widget Page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Widget Page",
				widgetName = "Web Content Display");
		}

		task ("Select the web content article") {
			Navigator.gotoSitePage(
				pageName = "Test Widget Page",
				siteName = "Test Site Name");

			WebContentDisplayPortlet.selectWebContent(webContentTitle = "Web Content Title");
		}

		task ("Assert access to given pages via Web Content Display") {
			for (var fieldLabel : list "Widget Page,Content Page,Search,Full Page Application,Embedded,Panel") {
				Click(
					key_contentText = "${fieldLabel}",
					locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL");

				ControlMenu.viewHeaderTitle(headerTitle = "Test ${fieldLabel}");

				Navigator.gotoSitePage(
					pageName = "Test Widget Page",
					siteName = "Test Site Name");
			}

			Click(
				key_contentText = "Link To A Page Of This Site",
				locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL");

			ControlMenu.viewHeaderTitle(headerTitle = "Test Content Page");

			Navigator.gotoSitePage(
				pageName = "Test Widget Page",
				siteName = "Test Site Name");

			Click(
				key_contentText = "Page Set",
				locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL");

			ControlMenu.viewHeaderTitle(headerTitle = "Test Child Page");

			Navigator.gotoSitePage(
				pageName = "Test Widget Page",
				siteName = "Test Site Name");

			Click(
				key_contentText = "Link To URL",
				locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL");

			AssertLocation.assertPartialLocation(value1 = "https://www.liferay.com");
		}
	}

	@description = "This is a use case for LRQA-61632. Can select a great grand child page in Link to Page field."
	@priority = "4"
	test SelectGreatGrandChildPageInLinkToPageField {
		task ("Add a parent page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Parent Page");
		}

		task ("Add a child page under parent page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Child Page",
				parentLayoutName = "Test Parent Page");
		}

		task ("Add 12 grand child pages under child page") {
			for (var n : list "1,2,3,4,5,6,7,8,9,10,11,12") {
				JSONLayout.addPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Grand Child Page ${n}",
					parentLayoutName = "Test Child Page");
			}
		}

		task ("Add a great grand page under the 12th grand child page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Great Grand Child Page",
				parentLayoutName = "Test Grand Child Page 12");
		}

		task ("Add a structure with Link to Page field") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = "WC Structure Name");

			DataEngine.addField(
				fieldFieldLabel = "Link to Page",
				fieldName = "Link to Page");

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Add a web content based on new structure") {
			NavItem.click(navItem = "Web Content");

			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");
		}

		task ("Select the great grand child page in Link to Page field") {
			WebContent.addWithStructureCP(
				webContentLinkToPage = "Test Great Grand Child Page",
				webContentTitle = "Web Content Title");

			Pause(locator1 = "3000");

			WebContent.viewWithStructureCP(
				predefinedValue = "true",
				webContentLinkToPage = "Test Great Grand Child Page");

			PortletEntry.publish();
		}

		task ("Add a Web Content Display to parent page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Parent Page",
				widgetName = "Web Content Display");
		}

		task ("Select the web content article") {
			Navigator.gotoSitePage(
				pageName = "Test Parent Page",
				siteName = "Test Site Name");

			WebContentDisplayPortlet.selectWebContent(webContentTitle = "Web Content Title");
		}

		task ("Click the link in Web Content Display") {
			WebContent.viewPGViaWCD(
				linkName = "Link to Page",
				linkToPage = "true");

			Click(
				key_contentText = "Link to Page",
				locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL");
		}

		task ("Assert the page redirects to the great grand child page") {
			ControlMenu.viewHeaderTitle(headerTitle = "Test Great Grand Child Page");
		}
	}

	@description = "This is a use case for LRQA-61632. Can select the image from Documents and Media in Image field."
	@priority = "4"
	test SelectImageFromDocumentsAndMediaInImageField {
		task ("Add a document with an image") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentTitle = "Document Title",
				groupName = "Test Site Name",
				mimeType = "image/jpeg",
				sourceFileName = "Document_1.jpg");
		}

		task ("Add a web content structure with an Image field") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = "WC Structure Name");

			DataEngine.addField(
				fieldFieldLabel = "Image",
				fieldName = "Image");

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Add a web content article based on new structure") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

			WebContent.addWithStructureCP(
				imageFileName = "Document Title",
				navTab = "Documents and Media",
				structureName = "WC Structure Name",
				webContentImage = "Document Title",
				webContentTitle = "Web Content Title");

			PortletEntry.publish();
		}

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}

		task ("Add a Web Content Display to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Web Content Display");
		}

		task ("Select the web content article in Web Content Display") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			WebContentDisplayPortlet.selectWebContent(webContentTitle = "Web Content Title");

			IFrame.closeFrame();
		}

		task ("Assert the image is shown in Web Content Display") {
			WebContent.viewPGViaWCD(
				image = "true",
				key_image = "Document_1.jpg");
		}
	}

	@priority = "4"
	test ValidateDDMNavigation {
		property test.name.skip.portal.instance = "WebContentStructures#ValidateDDMNavigation";
		property testray.component.names = "Web Content Administration";

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Search");

		SearchAdministration.executeSearchActions(actionsDescription = "Reindex all search indexes.");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "global");

		NavItem.gotoStructures();

		WebContentNavigator.gotoManageTemplatesViaStructures(structureName = "Basic Web Content");

		WebContentTemplates.addCP(
			templateDescription = "Test WC Template Description",
			templateName = "Test WC Template");

		WebContentTemplates.gotoTemplateEllipsisItem(
			item = "Edit",
			templateName = "Test WC Template");

		Navigator.gotoBack();

		SiteAdmin.viewPortletTitle(portletTitle = "Templates for Structure: Basic Web Content");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		WebContentTemplates.viewTableEntryCP(
			templateDescription = "Test WC Template Description",
			templateName = "Test WC Template");

		WebContentTemplates.viewTableEntryCP(
			templateDescription = "",
			templateName = "Basic Web Content");

		NavItem.gotoStructures();

		SiteAdmin.viewPortletTitle(portletTitle = "Web Content");

		WebContentStructures.viewTableEntryCP(
			structureDescription = "Basic Web Content",
			structureName = "Basic Web Content");
	}

	@priority = "4"
	test ViewFavoriteStructure {
		property testray.component.names = "Web Content Administration";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		var n = "0";

		while ("${n}" != "15") {
			WebContentStructures.addCP(
				structureDescription = "WC Structure Description",
				structureName = "WC Structure Name ${n}");

			DataEngine.addField(
				fieldFieldLabel = "Text",
				fieldName = "Text");

			WebContentStructures.saveCP(structureName = "${structureName}");

			var n = ${n} + 1;
		}

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoMoreStructuresViaPlusButton(pageMod = "8 of 17");

		for (var structureName : list "Basic Web Content,WC Structure Name 0,WC Structure Name 1,WC Structure Name 2,WC Structure Name 3,WC Structure Name 4,WC Structure Name 5,WC Structure Name 6,WC Structure Name 7,WC Structure Name 8,WC Structure Name 9,WC Structure Name 10,WC Structure Name 11,WC Structure Name 12,WC Structure Name 13,WC Structure Name 14") {
			WebContentStructures.viewStructuresViaMoreIFrame(structureName = "${structureName}");
		}

		WebContentStructures.viewNoFavoriteStructure();

		WebContentStructures.addFavoriteStructure(structureName = "WC Structure Name 9");

		WebContentStructures.viewFavoriteStructureViaIFrame(
			indexNumber = "1",
			structureName = "WC Structure Name 9");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentStructures.viewFavoriteStructureViaPlusButton(structureName = "WC Structure Name 9");
	}

}