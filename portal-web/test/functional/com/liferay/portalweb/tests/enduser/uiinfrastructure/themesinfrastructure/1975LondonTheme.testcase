@component-name = "portal-user-interface"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "5"
	test AddThemeSite {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "1975LondonTheme#AddThemeSite";
		property theme.file.name = "1975-london-theme.war";
		var themeContainsResources = PropsUtil.get("theme.contains.resources");

		if ("${themeContainsResources}" == "true") {
			ProductMenu.gotoPortlet(
				category = "Sites",
				panel = "Control Panel",
				portlet = "Sites");

			Site.addSiteTemplateCP(
				siteName = "1975 London Theme Site",
				siteTemplateName = "1975 London Theme");

			Navigator.openSiteURL(siteName = "1975 London Theme Site");

			ProductMenu.gotoPublicPages();

			AssertClick(
				key_page = "home",
				locator1 = "ProductMenu#PAGES",
				value1 = "home");

			AssertTextEquals(
				key_title = "We love Music",
				locator1 = "Header#H3_TITLE",
				value1 = "We love Music");
		}
		else {
			var appName = "1975-london-theme";

			ProductMenu.gotoPortlet(
				category = "Apps",
				panel = "Control Panel",
				portlet = "App Manager");

			Type(
				locator1 = "TextInput#SEARCH",
				value1 = "${appName}");

			Click(locator1 = "Icon#BASIC_SEARCH");

			AssertVisible(
				key_itemName = "1975-london-theme",
				locator1 = "ListGroupItem#ITEM_NAME_LINK");
		}
	}

	@priority = "5"
	test AddThemeSitePage {
		property test.name.skip.portal.instance = "1975LondonTheme#AddThemeSitePage";
		property theme.file.name = "1975-london-theme.war";

		Page.add(pageName = "Themes Page");

		SitePageNavigator.gotoEditPage(page = "Themes Page");

		Navigator.gotoNavTab(navTab = "Look and Feel");

		Click(locator1 = "Radio#SPECIFIC_LOOK");

		Page.changeCurrentTheme(themeName = "1975 London");

		Page.viewCurrentTheme(
			navTab = "Look and Feel",
			page = "Welcome",
			themeName = "Classic Theme");

		Navigator.gotoPage(pageName = "Welcome");

		AssertElementNotPresent(locator1 = "//footer//div[contains(@class,'navbar')]");

		Page.viewCurrentTheme(
			navTab = "Look and Feel",
			page = "Themes Page",
			themeName = "1975 London");

		Navigator.gotoPage(pageName = "Themes Page");

		AssertElementPresent(locator1 = "//footer//div[contains(@class,'navbar')]");
	}

}