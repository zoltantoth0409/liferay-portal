@component-name = "portal-segmentation"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Segmentation";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "User Authentication",
			configurationName = "General",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.uncheckStrangersVerifyEmailCheckbox();

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ApplicationsMenu.gotoSite(site = "Global");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Segments");

		LexiconEntry.gotoAdd();

		Segmentation.addProperties(propertyOption = "Screen Name");

		Segmentation.fillTitle(titleField = "Segment usersn");

		Segmentation.fillProperty(PropertyField = "usersn");

		Button.clickSave();

		JSONRole.addRegularRole(
			roleKey = "Role User Segments",
			roleTitle = "Role User Segments");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_content_dashboard_web_portlet_ContentDashboardAdminPortlet",
			roleTitle = "Role User Segments");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_CONTROL_PANEL",
			resourceName = "90",
			roleTitle = "Role User Segments");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Roles");

		RoleNavigator.gotoAssignees(roleTitle = "Role User Segments");

		RoleNavigator.gotoAssigneeType(assigneeType = "Segments");

		Role.addAssignees(assigneeName = "Segment usersn");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();

			User.loginPG(
				password = "test",
				userEmailAddress = "test@liferay.com");

			ApplicationsMenu.gotoSite(site = "Global");

			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			User.deactivateCP(userFirstName = "userfn");

			User.deleteCP();

			JSONRole.deleteRole(roleTitle = "Role User Segments");

			Navigator.gotoPage(pageName = "Home");

			ProductMenu.gotoPortlet(
				category = "People",
				portlet = "Segments");

			if (IsElementPresent(locator1 = "Segmentation#SEGMENT_LIST")) {
				Segmentation.tearDown();
			}
		}
	}

	@description = "Validate if, with the option Enable Assign roles by segments disabled, the role is not working for the user"
	@priority = "5"
	test DisableAssignRolesBySegment {
		property portal.acceptance = "true";

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		AssertElementNotPresent(locator1 = "ApplicationsMenu#PORTLET");
	}

	@description = "Validate if, with the option Enable Assign roles by segments enabled, the role is working for the user"
	@priority = "5"
	test EnableAssignRolesBySegment {
		property portal.acceptance = "true";

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Segments",
			configurationName = "Segments Service",
			configurationScope = "System Scope");

		Segmentation.enableAssingRolesBySegmentsCheckbox();

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenuHelper.openApplicationsMenu();

		ApplicationsMenu.gotoPanel(panel = "Applications");

		AssertElementPresent(
			key_category = "Content",
			key_portlet = "Content Dashboard",
			locator1 = "ApplicationsMenu#PORTLET");
	}

}