@component-name = "portal-wcm"
definition {

	property portal.release = "false";
	property portal.upstream = "true";
	property testray.component.names = "YouTube";
	property testray.main.component.name = "YouTube";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Test Page Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Page.tearDownCP();
		}
	}

	@priority = "4"
	test AddYoutubeVideo {
		Navigator.gotoPage(pageName = "Test Page Name");

		Portlet.addPG(portletName = "YouTube");

		Portlet.gotoPortletOptions(
			portletName = "YouTube",
			portletOption = "Configuration");

		YoutubePortlet.addVideoId(videoId = "PiObSymqSdo");

		PortletEntry.save();

		Navigator.gotoPage(pageName = "Test Page Name");

		YoutubePortlet.assertVideoTitle(videoTitle = "The Gospel in 6 Minutes - John Piper");

		Portlet.gotoPortletOptions(
			portletName = "YouTube",
			portletOption = "Configuration");

		YoutubePortlet.viewPortletConfiguration(videoId = "PiObSymqSdo");

		IFrame.closeFrame();
	}

	@priority = "4"
	test YoutubePortletSmoke {
		Navigator.gotoPage(pageName = "Test Page Name");

		Portlet.addPG(portletName = "YouTube");

		Portlet.viewPG(
			portletBody = "Please configure this portlet to make it visible to all users.",
			portletName = "YouTube");
	}

}