@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Asset Publisher,Most Viewed Assets";
	property testray.main.component.name = "Most Viewed Assets";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "4"
	test AssertMostViewedAsset {
		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		for (var n : list "1,2") {
			SitePages.addPublicPage(pageName = "Test Page ${n}");
		}

		Navigator.gotoSitePage(
			pageName = "Test Page 1",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Most Viewed Assets");

		AssetPublisherPortlet.configureShowMetadataFieldsPG(
			addMetadata = "true",
			metadataFieldList = "View Count",
			portletName = "Most Viewed Assets");

		Navigator.gotoSitePage(
			pageName = "Test Page 2",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Asset Publisher");

		AssetPublisherPortlet.configureDisplaySettings(displaySettings = "View Count Increment");

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content");

		for (var n : list "1,2") {
			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "Web Content Content",
				webContentTitle = "Web Content Title ${n}");

			PortletEntry.publish();
		}

		var count = "0";

		while ("${count}" != "3") {
			Navigator.gotoSitePage(
				pageName = "Test Page 2",
				siteName = "Site Name");

			AssetPublisherPortlet.viewAssetDetailsPG(
				assetContent = "Web Content Content",
				assetTitle = "Web Content Title 1");

			var count = ${count} + 1;
		}

		Navigator.gotoSitePage(
			pageName = "Test Page 1",
			siteName = "Site Name");

		AssetPublisherPortlet.viewAssetViewCount(
			assetTitle = "Web Content Title 1",
			viewCount = "3 View");

		AssetPublisherPortlet.viewAssetViewCount(
			assetTitle = "Web Content Title 2",
			viewCount = "0 View");

		var count = "0";

		while ("${count}" != "5") {
			Navigator.gotoSitePage(
				pageName = "Test Page 2",
				siteName = "Site Name");

			AssetPublisherPortlet.viewAssetDetailsPG(
				assetContent = "Web Content Content",
				assetTitle = "Web Content Title 2");

			var count = ${count} + 1;
		}

		Navigator.gotoSitePage(
			pageName = "Test Page 1",
			siteName = "Site Name");

		AssetPublisherPortlet.viewAssetViewCount(
			assetTitle = "Web Content Title 2",
			viewCount = "5 View");

		AssetPublisherPortlet.viewAssetViewCount(
			assetTitle = "Web Content Title 1",
			viewCount = "3 View");
	}

}