@component-name = "portal-wcm"
definition {

	property portal.upstream = "true";
	property testray.component.names = "Navigation";
	property testray.main.component.name = "Navigation";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();
		}
	}

	@priority = "5"
	test DisplayStyleOptions {
		property portal.acceptance = "true";

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page",
			widgetName = "Sites Directory");

		for (var n : list "1,2,3") {
			JSONGroup.addChildSite(
				groupName = "Child SiteName ${n}",
				parentGroupName = "Site Name");

			JSONLayout.addPublicLayout(
				groupName = "Child SiteName ${n}",
				layoutName = "Site Page",
				parentGroupName = "Site Name");
		}

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Site Name");

		SitesDirectory.viewDefaultConfigurationPG();

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Site Name");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SitesDirectory.viewDescriptivePG(siteName = "${siteName}");

		SitesDirectory.viewDescriptivePG(siteName = "Site Name");

		SitesDirectory.configureSetupPG(directorySites = "Parent Level");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Site Name");

		AssertTextEquals(
			locator1 = "Message#INFO_2",
			value1 = "No sites were found.");

		SitesDirectory.configureSetupPG(
			directoryDisplayStyle = "List Hierarchy",
			directorySites = "Top Level");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Site Name");

		SitesDirectory.viewListHierarchyPG(
			availableSiteName = "${siteName}",
			childSiteName1 = "Child SiteName 1",
			childSiteName2 = "Child SiteName 2",
			childSiteName3 = "Child SiteName 3",
			currentSiteName = "Site Name");
	}

}