@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "5"
	test AddTemplateWithStructure {
		property portal.acceptance = "true";
		property testray.component.names = "Web Content Administration";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoTemplates();

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Text",
			templateName = "WC Template Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		Navigator.gotoNavItem(navItem = "Templates");

		WebContentTemplates.viewCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateName = "WC Template Name",
			templateScript = "Text.getData()");
	}

	@description = "This is a use case for LPS-116515. The Add FreeMarker button should be hidden when disable template creation in Dynamic Data Mapping."
	@priority = "5"
	test CannotAddTemplateWhenDisableTemplateCreation {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "WebContentTemplates#CannotAddTemplateWhenDisableTemplateCreation";

		task ("Disable Template Creation") {
			SystemSettings.openSystemSettingsAdmin();

			SystemSettings.gotoConfiguration(
				configurationCategory = "Dynamic Data Mapping",
				configurationName = "Dynamic Data Mapping Web",
				configurationScope = "System Scope");

			FormFields.disableCheckbox(fieldName = "Enable Template Creation");

			if (IsElementPresent(locator1 = "Button#UPDATE")) {
				PortletEntry.update();
			}
			else if (IsElementPresent(locator1 = "Button#SAVE")) {
				PortletEntry.save();
			}
		}

		task ("Assert Add FreeMarker button doesn't present") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			var browserType = PropsUtil.get("browser.type");

			if ("${browserType}" == "chrome") {
				MetalComponent.waitForManagementBar();
			}

			AssertElementNotPresent(locator1 = "Button#PLUS");
		}

		task ("Enable Template Creation") {
			SystemSettings.openSystemSettingsAdmin();

			SystemSettings.gotoConfiguration(
				configurationCategory = "Dynamic Data Mapping",
				configurationName = "Dynamic Data Mapping Web",
				configurationScope = "System Scope");

			FormFields.enableCheckbox(fieldName = "Enable Template Creation");

			PortletEntry.update();
		}
	}

	@description = "This is a use case for LPS-101248. Cannot delete a web content template used in Content Display fragment."
	@priority = "2"
	test CannotDeleteTemplateUsedInContentDisplay {
		task ("Add a web content based on Basic Web Content") {
			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				groupName = "Test Site Name",
				title = "Web Content Title");
		}

		task ("Add a web content template for Basic Web Content") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			var templateScript = '''<a>${content.getData()}</a>''';

			WebContentTemplates.addCP(
				structureName = "Basic Web Content",
				templateName = "WC Template Name",
				templateScript = "${templateScript}");
		}

		task ("Add a content page on Blank") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}

		task ("Add a Content Display to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Content Display");
		}

		task ("Select the web content and custom template in Content Display") {
			PageEditor.editContentDisplay(
				customWebcontenttemplate = "WC Template Name",
				fragmentName = "Content Display",
				webcontent = "true",
				webContentTitle = "Web Content Title");

			AssertVisible(
				key_content = "//a[contains(.,'Web Content Content')]",
				locator1 = "WCD#WEB_CONTENT_CONTENT_ANY");
		}

		task ("Assert cannot delete web content template") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			WebContentTemplates.deleteCP(
				templateName = "WC Template Name",
				webContentHasReference = "true");
		}
	}

	@description = "This is a use case for LPS-116515. The user is able to save changes in existing template when disable template creation in Dynamic Data Mapping."
	@priority = "5"
	test SaveChangesInExistingTemplateWhenDisableTemplateCreation {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "WebContentTemplates#SaveChangesInExistingTemplateWhenDisableTemplateCreation";

		task ("Add a template for Basic Web Content Structure") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			WebContentTemplates.addCP(
				structureName = "Basic Web Content",
				templateDescription = "WC Template Description",
				templateFieldNames = "Content",
				templateName = "WC Template Name");

			LexiconCard.viewCardTitle(card = "WC Template Name");
		}

		task ("Disable Template Creation") {
			SystemSettings.openSystemSettingsAdmin();

			SystemSettings.gotoConfiguration(
				configurationCategory = "Dynamic Data Mapping",
				configurationName = "Dynamic Data Mapping Web",
				configurationScope = "System Scope");

			FormFields.disableCheckbox(fieldName = "Enable Template Creation");

			if (IsElementPresent(locator1 = "Button#UPDATE")) {
				PortletEntry.update();
			}
			else if (IsElementPresent(locator1 = "Button#SAVE")) {
				PortletEntry.save();
			}
		}

		task ("Assert can save changes in existing template") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			WebContentTemplates.modifyTemplate(
				templateName = "WC Template Name Edit",
				templatePreviousName = "WC Template Name",
				templateScript = "${content.getName()}: ${content.getData()}");

			LexiconCard.viewCardTitle(card = "WC Template Name Edit");
		}

		task ("Enable Template Creation") {
			SystemSettings.openSystemSettingsAdmin();

			SystemSettings.gotoConfiguration(
				configurationCategory = "Dynamic Data Mapping",
				configurationName = "Dynamic Data Mapping Web",
				configurationScope = "System Scope");

			FormFields.enableCheckbox(fieldName = "Enable Template Creation");

			PortletEntry.update();
		}
	}

	@description = "This is a test for LPS-125933. Create, read, update and delete a Template based on a Structure with all possible fields."
	@ignore = "true"
	@priority = "4"
	test ViewAndEditAsWellAsDeleteTemplateWithAllFieldsStrucrure {
		property portal.acceptance = "false";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addFields(fieldNameList = "Text,Select from List,Single Selection,Multiple Selection,Grid,Date,Numeric,Image,Rich Text,Upload,Color,Web Content,Geolocation,Link to Page,Separator");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoTemplates();

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Text",
			templateName = "WC Template Name");

		WebContentTemplates.modifyTemplate(
			templateName = "WC Template Name Edit",
			templatePreviousName = "WC Template Name");


		WebContentTemplates.deleteCP(templateName = "WC Template Name");
	}

	@description = "This is a use case for LPS-116515. The creation of template is not allowed when disable template creation in Dynamic Data Mapping."
	@priority = "4"
	test ViewTemplateCreationFailureWhenDisableTemplateCreation {
		property test.name.skip.portal.instance = "WebContentTemplates#ViewTemplateCreationFailureWhenDisableTemplateCreation";

		task ("Add a template for Basic Web Content structure but not save") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			LexiconEntry.gotoAdd();

			Pause(locator1 = "3000");
		}

		task ("Disable Template Creation") {
			Open.openInTheNewTab(locator1 = "http://localhost:8080");

			var siteName = TestCase.getSiteName(siteName = "${siteName}");

			SelectWindow(locator1 = "title=Home - ${siteName}");

			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Dynamic Data Mapping",
				configurationName = "Dynamic Data Mapping Web",
				configurationScope = "System Scope");

			FormFields.disableCheckbox(fieldName = "Enable Template Creation");

			if (IsElementPresent(locator1 = "Button#UPDATE")) {
				PortletEntry.update();
			}
			else if (IsElementPresent(locator1 = "Button#SAVE")) {
				PortletEntry.save();
			}
		}

		task ("View template creation failure") {
			Close.closeAndSelectWindow(locator1 = "title=New Template - Test Site Name - ${siteName}");

			WebContentTemplates.inputTemplateAddScreenFields(
				structureName = "Basic Web Content",
				templateDescription = "WC Template Description",
				templateFieldNames = "Content",
				templateName = "WC Template Name");

			ClickNoError.mouseDownMouseUp(locator1 = "//span[contains(@class,'btn')][normalize-space(text())='Save']/..");

			Alert.viewErrorMessage(errorMessage = "Your request failed to complete.");
		}

		task ("Enable Template Creation") {
			SystemSettings.openSystemSettingsAdmin();

			SystemSettings.gotoConfiguration(
				configurationCategory = "Dynamic Data Mapping",
				configurationName = "Dynamic Data Mapping Web",
				configurationScope = "System Scope");

			FormFields.enableCheckbox(fieldName = "Enable Template Creation");

			PortletEntry.update();
		}
	}

}