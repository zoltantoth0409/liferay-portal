@component-name = "portal-calendar"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property custom.properties = "default.layout.template.id=1_column${line.separator}company.default.time.zone=America/Los_Angeles";
	property testray.main.component.name = "Calendar";
	var pageName = "Calendar Page";
	var portletName = "Calendar";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(
			pageName = "Calendar Page",
			sitePageType = "Widget Page");

		SitePages.configurePage(
			columnName = "1 Column",
			pageName = "Calendar Page",
			privacy = "Public Pages",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Calendar Page");

		Portlet.addPG(
			pageName = "Calendar Page",
			portletName = "Calendar");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.gotoPage(pageName = "Calendar Page");

			if (IsElementPresent(locator1 = "Calendar#SCHEDULER_EVENT_GENERIC")) {
				CalendarEvent.tearDown();
			}

			Calendar.tearDown();

			CalendarConfiguration.tearDown();

			Page.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-52517."
	@priority = "3"
	test ViewCorrectNumberOfWeeksMonthView {
		Calendar.viewMonth(dateMonth = "April 2016");

		Calendar.viewWeekCountViaMonthView(weekNumber = "5");

		Calendar.viewMonth(dateMonth = "July 2016");

		Calendar.viewWeekCountViaMonthView(weekNumber = "6");
	}

	@description = "This is a use case for LPS-67400."
	@priority = "2"
	test ViewCreatedEventVisibleOnScheduler {
		CalendarNavigator.gotoAddEvent();

		var endDateTime = selenium.getElementValue("CalendarEditEvent#END_DATE_TIME");
		var startDateTime = selenium.getElementValue("CalendarEditEvent#START_DATE_TIME");

		CalendarEvent.viewTimes(
			endDateTime = "${endDateTime}",
			startDateTime = "${startDateTime}");

		CalendarEvent.editTitle(eventTitle = "Calendar Event");

		CalendarEvent.publish();

		IFrame.closeFrame();

		CalendarScheduler.selectSpecificEvent(eventTitle = "Calendar Event");
	}

	@priority = "3"
	test ViewDefaultDurationsViaScheduler {
		for (var minutes : list "15,30,60,120") {
			Navigator.gotoPage(pageName = "Calendar Page");

			CalendarNavigator.gotoConfiguration();

			CalendarConfiguration.selectDefaultDuration(defaultDuration = "${minutes} Minutes");

			CalendarConfiguration.save();

			Navigator.gotoPage(pageName = "Calendar Page");

			CalendarNavigator.gotoView(viewType = "Day");

			CalendarEvent.addViaDayView(eventTitle = "Calendar Event ${minutes} Minutes Title");

			CalendarScheduler.viewEventDuration(
				eventTitle = "Calendar Event ${minutes} Minutes Title",
				minutes = "${minutes}");

			CalendarEvent.tearDown();
		}
	}

	@description = "This is a use case for LPS-59251."
	@priority = "3"
	test ViewDisplayedSiteCalendarEventsAsGuest {
		property test.name.skip.portal.instance = "CalendarScheduler#ViewDisplayedSiteCalendarEventsAsGuest";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		CalendarNavigator.gotoAddEvent();

		CalendarEvent.editTitle(eventTitle = "Liferay Calendar Event Title");

		CalendarEvent.setTimes(
			endDateTime = "04:00 AM",
			startDateTime = "03:00 AM");

		CalendarEvent.selectCalendar(calendarName = "${siteName}");

		CalendarEvent.publish();

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarNavigator.gotoAddEvent();

		CalendarEvent.editTitle(eventTitle = "Test Test Calendar Event Title");

		CalendarEvent.setTimes(
			endDateTime = "09:00 PM",
			startDateTime = "08:00 PM");

		CalendarEvent.selectCalendar(calendarName = "Test Test");

		CalendarEvent.publish();

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarScheduler.viewWeek(eventTitle = "Liferay Calendar Event Title");

		CalendarScheduler.viewWeek(eventTitle = "Test Test Calendar Event Title");

		Navigator.gotoPersonalSite();

		Portlet.addPG(portletName = "Calendar");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarEvent.viewSiteCalendarEventWithDefaultPermissions(
			calendarName = "${siteName}",
			endDateTime = "04:00 AM",
			eventTitle = "Liferay Calendar Event Title",
			siteName = "${siteName}",
			startDateTime = "03:00 AM");

		CalendarEvent.viewSiteCalendarEventNotPresent(
			endDateTime = "09:00 PM",
			siteName = "Test Test",
			startDateTime = "08:00 PM");

		Navigator.gotoSpecificURLPage(
			pageName = "My Profile",
			url = "localhost:8080/web/test");

		CalendarEvent.viewSiteCalendarEventWithDefaultPermissions(
			calendarName = "Test Test",
			endDateTime = "09:00 PM",
			eventTitle = "Test Test Calendar Event Title",
			siteName = "Test Test",
			startDateTime = "08:00 PM");

		CalendarEvent.viewSiteCalendarEventNotPresent(
			endDateTime = "04:00 AM",
			siteName = "${siteName}",
			startDateTime = "03:00 AM");
	}

}