@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Fragments";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Navigate to fragments admin") {
			FragmentsAdmin.openFragmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Add a fragment collection") {
			FragmentsAdmin.addCollection(collectionName = "Collection Name");
		}

		task ("Navigate to the fragment collection") {
			Navigator.gotoNavNested(navNested = "Collection Name");
		}

		task ("Add a section fragment that uses the Button fragment code") {
			FragmentsAdmin.addSection(fragmentName = "Fragment Name");

			var htmlSnippet = TestCase.getFileContent(fileName = "fragment_button.html");

			Type.typeAceEditorNoError(
				locator1 = "FragmentEditor#HTML_EDITOR",
				value1 = "${htmlSnippet}"
			);

			var cssSnippet = TestCase.getFileContent(fileName = "fragment_button.css");

			Type.typeAceEditorNoError(
				locator1 = "FragmentEditor#CSS_EDITOR",
				value1 = "${cssSnippet}"
			);

			var javascriptSnippet = TestCase.getFileContent(fileName = "fragment_button.js");

			Type.typeAceEditorNoError(
				locator1 = "FragmentEditor#JAVASCRIPT_EDITOR",
				value1 = "${javascriptSnippet}"
			);
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "Add a fragment in the UI and then assert that it displays properly when added to a content page."
	@priority = "5"
	test AddFragmentToContentPage {
		property app.server.types = "jboss,resin,tcserver,tomcat,weblogic,websphere,wildfly";
		property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property environment.acceptance = "true";
		property portal.acceptance = "true";

		task ("Publish the section fragment") {
			PortletEntry.publish();
		}

		task ("Navigate to pages admin") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("Add a content page") {
			ContentPages.addPage(pageName = "Test Content Page Name");
		}

		task ("Add the section fragment to the content page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Fragment Name"
			);
		}

		task ("Navigate to the content page") {
			Navigator.openWithAppendToBaseURL(
				urlAppend = "web/test-site-name/test-content-page-name"
			);
		}

		task ("Assert the section fragment is present by asserting the elements from the Button fragment") {
			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Submit",
				key_fragmentClass = "footer",
				key_position = "1"
			);

			AssertVisible(
				locator1 = "Fragment#FRAGMENT",
				key_content = "Cancel",
				key_fragmentClass = "footer",
				key_position = "1"
			);

			AssertElementPresent(locator1 = "Button#SUBMIT");

			AssertElementPresent(locator1 = "Button#CANCEL");

			AssertClickNoError(locator1 = "Button#SUBMIT", value1 = "Submit");

			AssertConfirm(value1 = "Form submitted");
		}
	}

	@description = "Edit a fragment and assert the edits persist."
	@priority = "5"
	test EditFragment {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		task ("Publish the section fragment") {
			PortletEntry.publish();
		}

		task ("Edit the section fragment to have the Title fragment code") {
			LexiconEntry.gotoEntryMenuItem(menuItem = "Edit", rowEntry = "Fragment Name");

			var htmlSnippet = TestCase.getFileContent(fileName = "fragment_title.html");

			Type.typeAceEditorNoError(
				locator1 = "FragmentEditor#HTML_EDITOR",
				value1 = "${htmlSnippet}"
			);

			var cssSnippet = TestCase.getFileContent(fileName = "fragment_title.css");

			Type.typeAceEditorNoError(
				locator1 = "FragmentEditor#CSS_EDITOR",
				value1 = "${cssSnippet}"
			);

			Type.typeAceEditorNoError(
				locator1 = "FragmentEditor#JAVASCRIPT_EDITOR",
				value1 = ""
			);

			SelectFrame(locator1 = "FragmentEditor#PREVIEW_IFRAME");

			AssertTextEquals(
				locator1 = "//div[@class='fragment-header']/h1[@class='title']",
				value1 = "LOREM IPSUM DOLOR SIT AMET"
			);

			SelectFrameTop();

			PortletEntry.publish();
		}

		task ("Navigate to the section fragment") {
			LexiconEntry.gotoEntryMenuItem(menuItem = "Edit", rowEntry = "Fragment Name");
		}

		task ("Assert the edits to the section fragment persist by asserting that the preview displays the Title fragment") {
			SelectFrame(locator1 = "FragmentEditor#PREVIEW_IFRAME");

			AssertTextEquals(
				locator1 = "//div[@class='fragment-header']/h1[@class='title']",
				value1 = "LOREM IPSUM DOLOR SIT AMET"
			);

			SelectFrameTop();
		}
	}

	@description = "Fragment editor should show an accurate preview of the fragment."
	@priority = "5"
	test ViewFragmentEditorPreview {
		property portal.acceptance = "true";

		task ("Assert the preview properly displays the Button fragment") {
			SelectFrame(locator1 = "FragmentEditor#PREVIEW_IFRAME");

			AssertElementPresent(locator1 = "Button#SUBMIT");

			AssertElementPresent(locator1 = "Button#CANCEL");

			AssertClickNoError(locator1 = "Button#SUBMIT", value1 = "Submit");

			AssertConfirm(value1 = "Form submitted");

			SelectFrameTop();
		}
	}
}