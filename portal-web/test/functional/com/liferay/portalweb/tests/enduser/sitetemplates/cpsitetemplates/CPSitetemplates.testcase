@component-name = "portal-staging"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Site Templates";
	property testray.main.component.name = "Site Templates";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();

			SiteTemplates.tearDownCP();
		}
	}

	@priority = "4"
	test AddSiteTemplate {
		property portal.acceptance = "true";

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		SiteTemplates.viewCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");
	}

	@priority = "4"
	test AddSiteTemplateApplySiteTemplateChangesToSite {
		property portal.acceptance = "true";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		Site.openSitesAdmin();

		Site.addCP(
			siteName = "Site Template Site Name",
			siteTemplateName = "Site Template Name",
			siteType = "Site Template");

		Navigator.openSiteURL(siteName = "Site Template Site Name");

		Portlet.viewNoPortletsPresentPG();

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Home",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		ContentPages.clickPencil();

		PageEditor.addWidget(portletName = "Hello World");

		PageEditor.clickPublish();

		Navigator.openSiteURL(siteName = "Site Template Site Name");

		if (IsElementNotPresent(locator1 = "Portlet#PORTLET_CONTENT")) {
			Refresh();
		}

		Portlet.viewPortletPresentPG(portletContent = "Welcome to Liferay");
	}

	@priority = "4"
	test AddSiteTemplateDisablePageModification {
		property portal.acceptance = "true";

		SiteTemplates.addCP(
			disableChanges = "true",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addCP(
			siteName = "Site Template Site Name",
			siteTemplateName = "Site Template Name",
			siteType = "Site Template");

		Navigator.openSiteURL(siteName = "Site Template Site Name");

		Page.viewPageLockCheck();

		AssertElementNotPresent(locator1 = "ControlMenu#ADD");
	}

	@description = "This is a use case for LPS-121137."
	@priority = "4"
	test AdminCanControlWhenPropagateSiteTemplateChanges {
		property portal.acceptance = "true";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(
			disablePropagation = "true",
			siteTemplateName = "Site Template Name");

		Site.openSitesAdmin();

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Home",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		ContentPages.clickPencil();

		PageEditor.addWidget(portletName = "Hello World");

		PageEditor.clickPublish();

		Navigator.openSiteURL(siteName = "Site Name");

		Refresh();

		Portlet.viewNoPortletsPresentPG();

		SiteTemplatesNavigator.openSiteTemplatesAdmin();

		SiteTemplates.enablePropagation(siteTemplateName = "Site Template Name");

		Navigator.openSiteURL(siteName = "Site Name");

		Refresh();

		Portlet.viewPortletPresentPG(portletContent = "Welcome to Liferay");

		SiteTemplatesNavigator.openSiteTemplatesAdmin();

		SiteTemplates.disablePropagation(siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Home",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		ContentPages.clickPencil();

		PageEditor.addWidget(
			portletName = "Asset Publisher",
			targetFragmentName = "Hello World");

		PageEditor.clickPublish();

		Navigator.openSiteURL(siteName = "Site Name");

		Refresh();

		AssertElementNotPresent(
			key_portletName = "AssetPublisher",
			locator1 = "Portlet#SPECIFIC_FRAGMENTS_AND_WIDGETS");
	}

	@description = "This is a use case for LPS-118486."
	@priority = "3"
	test AssertNoChildPageOptionForPageDerivedFromSiteTemplate {
		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		Navigator.openSiteURL(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Test Page");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		MouseOver(
			key_itemName = "Home",
			locator1 = "SitePages#LIST_GROUP_ITEM");

		AssertElementNotPresent(
			key_pageName = "Home",
			locator1 = "SitePages#LIST_GROUP_ITEM_PLUS_ICON");

		Click(
			key_itemName = "Home",
			locator1 = "SitePages#LIST_GROUP_ITEM");

		AssertElementNotPresent(locator1 = "Button#NEW");

		Click(
			key_itemName = "Test Page",
			locator1 = "SitePages#LIST_GROUP_ITEM");

		Click(locator1 = "Button#NEW");

		AssertElementPresent(
			key_menuItem = "Add Child Page of Test Page",
			locator1 = "MenuItem#ANY_MENU_ITEM");
	}

	@description = "This is a use case for LPS-84497."
	@priority = "3"
	test DeactivateActivateSiteTemplate {
		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		SiteTemplates.viewCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.deactivateCP(siteTemplateName = "Site Template Name");

		SiteTemplates.viewTemplateStatusCP(
			siteTemplateActiveEdit = "No",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.activateCP(siteTemplateName = "Site Template Name");

		SiteTemplates.viewTemplateStatusCP(
			siteTemplateActiveEdit = "Yes",
			siteTemplateName = "Site Template Name");
	}

	@description = "This is a use case for LPS-87081."
	@priority = "5"
	test DeleteSiteTemplate {
		property portal.acceptance = "true";

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.deleteCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.viewNoTemplatesDisplay(siteTemplateName = "Site Template Name");

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		SiteTemplates.deleteViaDeleteIconCP(siteTemplateName = "Site Template Name");

		SiteTemplates.viewNoTemplatesDisplay(siteTemplateName = "Site Template Name");
	}

	@priority = "4"
	test DeleteSiteTemplateAssociatedWithASite {
		property portal.acceptance = "true";

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.deleteCP(
			associatedToSite = "true",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.viewCP(siteTemplateName = "Site Template Name");
	}

	@description = "This is a use case for LPS-98548."
	@priority = "4"
	test DeleteSiteTemplateImportedToVirtualInstance {
		property test.name.skip.portal.instance = "CPSitetemplates#DeleteSiteTemplateImportedToVirtualInstance";

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		Site.openSitesAdmin();

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.openSiteTemplatesAdmin();

		LAR.exportPortlet(
			larFileName = "site-template-name.lar",
			portletName = "Site Templates");

		PortalInstances.openVirtualInstancesAdmin();

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		SiteTemplatesNavigator.openSiteTemplatesAdmin(baseURL = "http://www.able.com:8080");

		LAR.importPortlet(larFileName = "site-template-name.lar");

		SiteTemplates.viewCP(siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.openSiteTemplatesAdmin(baseURL = "http://www.able.com:8080");

		SiteTemplates.deleteCP(siteTemplateName = "Site Template Name");

		SiteTemplates.viewNoTemplatesDisplay(siteTemplateName = "Site Template Name");
	}

	@priority = "4"
	test EditSiteTemplate {
		property portal.acceptance = "true";

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		SiteTemplates.viewCP(
			siteTemplateDescription = "This is a site template description",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.editCP(
			siteTemplateDescription = "Site Template Description Edit",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.viewCP(
			siteTemplateDescription = "Site Template Description Edit",
			siteTemplateName = "Site Template Name Edit");
	}

	@priority = "5"
	test EditSiteTemplateStatus {
		property portal.acceptance = "true";

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.editTemplateStatusCP(
			siteTemplateActiveEdit = "No",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.viewTemplateStatusCP(
			siteTemplateActiveEdit = "No",
			siteTemplateName = "Site Template Name");
	}

	@priority = "4"
	test LocalizeSiteTemplatesAdmin {
		property test.name.skip.portal.instance = "CPSitetemplates#LocalizeSiteTemplatesAdmin";

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		SiteTemplates.viewCP(siteTemplateName = "Site Template Name");

		Navigator.gotoAccountSettings();

		MyAccount.changeUserLanguage(
			languageName = "español (España)",
			saveButton = "Save",
			successMessage = "Su petición ha terminado con éxito.");

		Navigator.openURL();

		ProductMenuHelper.closeProductMenu();

		ApplicationsMenu.gotoPortlet(
			category = "Sitios web",
			panel = "Panel de control",
			portlet = "Plantillas de sitio web");

		SiteTemplates.viewDataLocalizedCP(siteTemplateNameList = "Site Template Name");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Mi cuenta");

		MyAccount.changeUserLanguage(
			languageName = "English (United States)",
			saveButton = "Guardar",
			successMessage = "Your request completed successfully.");

		Navigator.openURL();

		ProductMenuHelper.closeProductMenu();

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.viewCP(siteTemplateName = "Site Template Name");
	}

	@priority = "4"
	test OnlySiteAdminCanModifyPageAssociatedWithSiteTemplate {
		property portal.acceptance = "true";

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addCP(
			siteName = "Site Template Site Name",
			siteTemplateName = "Site Template Name",
			siteType = "Site Template");

		Navigator.openSiteURL(siteName = "Site Template Site Name");

		AssertElementPresent(locator1 = "Icon#EDIT_PENCIL");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		JSONUser.addUserToSite(
			groupName = "Site Template Site Name",
			userEmailAddress = "userea@liferay.com");

		User.logoutUserPG();

		User.loginUserPG(userEmailAddress = "userea@liferay.com");

		Navigator.gotoSitePage(
			pageName = "Home",
			siteName = "Site Template Site Name");

		AssertElementNotPresent(locator1 = "Icon#EDIT_PENCIL");

		User.logoutUserPG();

		User.loginUserPG();
	}

	@priority = "5"
	test ResetPageChanges {
		property portal.acceptance = "quarantine";
		property portal.upstream = "quarantine";
		property test.name.skip.portal.instance = "CPSitetemplates#ResetPageChanges";
		property testray.component.names = "Training";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Site Page Name",
			siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Site Page Name",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		Portlet.addPG(portletName = "Asset Publisher");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Site Page Name",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		Portlet.dragAndDropPortletToColumnPG(
			columnNumberFrom = "1",
			columnNumberTo = "2",
			portletName = "Asset Publisher");

		Close.closeAndSelectWindow(locator1 = "Site Templates - ${siteName}");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		Navigator.gotoSitePage(
			pageName = "Site Page Name",
			siteName = "Site Name");

		Portlet.dragAndDropPortletToColumnPG(
			columnNumberFrom = "2",
			columnNumberTo = "1",
			portletName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Site Page Name",
			siteName = "Site Name");

		SiteTemplates.viewResetPageChanges(
			columnNumberFrom = "1",
			portletName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Site Page Name",
			siteName = "Site Name");

		SiteTemplates.resetPageChanges(
			columnNumberTo = "1",
			portletName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Site Page Name",
			siteName = "Site Name");

		SiteTemplates.viewResetPageChanges(
			columnNumberFrom = "2",
			portletName = "Asset Publisher");
	}

	@description = "This is a use case for LPS-44550"
	@priority = "4"
	test SiteTemplateXSS {
		property portal.upstream = "quarantine";

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = '''<script>alert(123);</script>''');

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = '''<script>alert(123);</script>''',
			siteType = "Site Template",
			uncheckPropagation = "true");

		SitePages.openPagesAdmin(siteURLKey = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		AssertAlertNotPresent();

		var actualScript = '''<script>alert(123);</script>''';

		var escapedScript = '''&lt;script&gt;alert(123);&lt;/script&gt;''';

		AssertHTMLSourceTextNotPresent(value1 = "${actualScript}");

		AssertHTMLSourceTextPresent(value1 = "${escapedScript}");
	}

}