apply plugin: "com.liferay.maven.plugin.builder"

buildPluginDescriptor {
	ext {
		autoClean = false
	}

	finalizedBy formatSource

	forcedExclusions "com.ibm.icu:icu4j:2.6.1"
	goalPrefix = "service-builder"
}

configurations {
	xmltask
}

dependencies {
	compile group: "com.puppycrawl.tools", name: "checkstyle", version: "8.11"
	compile group: "com.thoughtworks.qdox", name: "qdox", version: "2.0-M5"
	compile group: "commons-io", name: "commons-io", version: "2.5"
	compile group: "dom4j", name: "dom4j", transitive: false, version: "1.6.1"
	compile group: "jalopy", name: "jalopy", transitive: false, version: "1.5rc3"
	compile group: "jaxen", name: "jaxen", transitive: false, version: "1.1.1"
	compile group: "log4j", name: "log4j", version: "1.2.17"
	compile group: "org.freemarker", name: "freemarker", transitive: false, version: "2.3.23"

	compileOnly group: "com.liferay", name: "com.liferay.petra.concurrent", version: "1.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.lang", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.memory", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.reflect", version: "1.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "4.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.xml", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.tools.java.parser", version: "1.0.25"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "2.65.8"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.java", version: "2.5.0"
	compileOnly group: "org.apache.ant", name: "ant", transitive: false, version: "1.9.14"
	compileOnly group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.0.4"
	compileOnly group: "xerces", name: "xercesImpl", transitive: false, version: "2.11.0"

	xmltask group: "com.oopsconsultancy", name: "xmltask", version: "1.16"
}

liferay {
	deployDir = "../../../tools/sdk/dependencies/com.liferay.portal.tools.service.builder/lib"
}

processResources {
	doLast {
		File portalModelHintsXmlFile = new File(destinationDir, "META-INF/portal-model-hints.xml")

		ant.taskdef classname: "com.oopsconsultancy.xmltask.ant.XmlTask", classpath: configurations.xmltask.asPath, name: "xmltask"

		ant.xmltask(dest: portalModelHintsXmlFile, outputter: "simple:4", source: portalModelHintsXmlFile) {
			remove path: "/model-hints/model/*"
		}

		String portalModelHintsXml = portalModelHintsXmlFile.text.trim()

		portalModelHintsXml = portalModelHintsXml.replace("<model-hints>", '''\
<!--
This file DOES NOT represent Liferay's complete portal-model-hints.xml file. It
is a fragment that contains only the hint collection and model definitions
needed by Service Builder to generate model services.
-->

<model-hints>''')

		portalModelHintsXmlFile.text = portalModelHintsXml.replace(' ' * 4, '\t')
	}

	from {
		File portalImplJarFile = configurations.compileOnly.find {
			it.name.startsWith "com.liferay.portal.impl-"
		}

		zipTree(portalImplJarFile)
	} {
		include "META-INF/portal-model-hints.xml"
	}

	into("com/liferay/blogs") {
		from "../../../modules/apps/blogs/blogs-service/service.xml"
	}
}

updateFileVersions {
	match(/\t\t\t\t<version>(\d.+)<\/version>/, "samples/pom.xml")
	match(/com\.liferay\.portal\.tools\.service\.builder=(\d.+)/, "../../sdk/gradle-plugins/src/main/resources/com/liferay/gradle/plugins/dependencies/portal-tools.properties")
}