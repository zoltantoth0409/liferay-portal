import org.gradle.api.artifacts.maven.Conf2ScopeMappingContainer

apply plugin: "com.liferay.maven.plugin.builder"

task writeParentThemeDependencies
task writeTestThemeDependencies

buildPluginDescriptor {
	configurationScopeMapping "mavenCompile", Conf2ScopeMappingContainer.COMPILE

	ext {
		autoClean = false
	}

	finalizedBy formatSource
	finalizedBy formatXML

	goalPrefix = "theme-builder"
}

configurations {
	mavenCompile {
		extendsFrom compile
	}

	mavenDistribution
	parentThemes
	testThemes
}

dependencies {
	compileInclude group: "com.beust", name: "jcommander", version: "1.48"
	compileInclude group: "net.coobird", name: "thumbnailator", version: "0.4.8"

	compileOnly group: "org.apache.ant", name: "ant", transitive: false, version: "1.9.7"
	compileOnly group: "org.apache.maven", name: "maven-artifact", version: "3.2.5"
	compileOnly group: "org.apache.maven", name: "maven-core", version: "3.2.5"
	compileOnly group: "org.apache.maven", name: "maven-plugin-api", version: "3.2.5"

	mavenCompile group: "com.liferay", name: "com.liferay.frontend.theme.styled", version: "3.0.4"
	mavenCompile group: "com.liferay", name: "com.liferay.frontend.theme.unstyled", version: "3.0.4"

	mavenDistribution classifier: "bin", ext: "zip", group: "org.apache.maven", name: "apache-maven", version: "3.3.9"

	parentThemes group: "com.liferay.plugins", name: "classic-theme", version: "1.0.2"
	parentThemes project(":apps:frontend-theme:frontend-theme-styled")
	parentThemes project(":apps:frontend-theme:frontend-theme-unstyled")

	testCompile group: "com.googlecode.json-simple", name: "json-simple", version: "1.1"
	testCompile group: "com.liferay", name: "com.liferay.maven.executor", version: "1.0.0"
	testCompile group: "org.apache.ant", name: "ant-testutil", version: "1.9.7"

	testThemes project(":apps:frontend-theme:frontend-theme-admin")
	testThemes project(":apps:frontend-theme:frontend-theme-classic")
}

formatSource {
	mustRunAfter formatXML
}

liferay {
	deployDir = "../../../tools/sdk/dependencies/com.liferay.portal.tools.theme.builder/lib"
}

liferayOSGi {
	expandCompileInclude = true
}

test {
	dependsOn install
	dependsOn writeParentThemeDependencies
	dependsOn writeTestThemeDependencies

	doFirst {
		systemProperty "maven.distribution.file.name", configurations.mavenDistribution.singleFile
	}

	systemProperty "http.proxyHost", System.properties["http.proxyHost"]
	systemProperty "http.proxyPort", System.properties["http.proxyPort"]
	systemProperty "maven.debug", System.properties["maven.debug"]
	systemProperty "maven.repo.local", System.properties["maven.repo.local"]
	systemProperty "repository.url", System.properties["repository.url"]
	systemProperty "theme.builder.version", jar.version
}

updateFileVersions {
	match(/\t\t\t\t<version>(\d.+)<\/version>/, "samples/pom.xml")
}

writeParentThemeDependencies {
	configurations.parentThemes.dependencies.withType(ProjectDependency) {
		dependsOn "${dependencyProject.path}:jar"
	}

	doLast {
		File parentThemeDependenciesFile = new File(buildDir, "parent-theme-dependencies.txt")

		parentThemeDependenciesFile.parentFile.mkdirs()

		parentThemeDependenciesFile.text = configurations.parentThemes.join(System.lineSeparator())
	}
}

writeTestThemeDependencies {
	configurations.testThemes.dependencies.withType(ProjectDependency) {
		dependsOn "${dependencyProject.path}:assemble"
	}

	doLast {
		Properties properties = new Properties()

		for (ProjectDependency projectDependency in configurations.testThemes.dependencies.withType(ProjectDependency)) {
			Project themeProject = projectDependency.dependencyProject

			properties.setProperty themeProject.projectDir.absolutePath, themeProject.configurations.archives.artifacts.files.singleFile.absolutePath
		}

		File testThemeDependenciesFile = new File(buildDir, "test-theme-dependencies.properties")

		testThemeDependenciesFile.parentFile.mkdirs()

		testThemeDependenciesFile.withOutputStream {
			properties.store it, null
		}
	}
}