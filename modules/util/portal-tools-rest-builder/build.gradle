apply plugin: "com.liferay.maven.plugin.builder"

buildPluginDescriptor {
	ext {
		autoClean = false
	}

	finalizedBy formatSource
	finalizedBy formatXML

	goalPrefix = "rest-builder"
}

configurations {
	compile {
		extendsFrom sourceFormatter
	}

	sourceFormatter
}

dependencies {
	compile group: "org.freemarker", name: "freemarker", transitive: false, version: "2.3.28"
	compile group: "org.yaml", name: "snakeyaml", version: "1.23"

	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "2.6.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.49.0"
	compileOnly group: "org.apache.ant", name: "ant", transitive: false, version: "1.9.4"
	compileOnly group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.0.4"

	sourceFormatter group: "com.liferay", name: "com.liferay.source.formatter", version: "1.0.779"
}

deployDependencies {
	from (configurations.compile - configurations.sourceFormatter)
	from "../../../tools/sdk/dependencies/com.liferay.source.formatter/lib"
}

formatSource {
	mustRunAfter formatXML
}

liferay {
	deployDir = "../../../tools/sdk/dependencies/com.liferay.portal.tools.rest.builder/lib"
}

updateFileVersions {
	match(/\t\t\t\t<version>(\d.+)<\/version>/, "samples/pom.xml")
	match(/com\.liferay\.portal\.tools\.rest\.builder=(\d.+)/, "../../sdk/gradle-plugins/src/main/resources/com/liferay/gradle/plugins/dependencies/portal-tools.properties")
}