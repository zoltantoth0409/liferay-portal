import com.liferay.gradle.util.copy.StripPathSegmentsAction

apply plugin: "com.liferay.maven.plugin.builder"

task copyPortalCommonCSSJar(type: Copy)
task unzipPortalCommonCSS(type: Copy)

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

buildPluginDescriptor {
	configurationScopeMapping "mavenCompile", Conf2ScopeMappingContainer.COMPILE

	ext {
		autoClean = false
	}

	finalizedBy formatSource
	finalizedBy formatXML

	goalPrefix = "css-builder"
}

copyPortalCommonCSSJar {
	from configurations.portalCommonCSS
	into new File(buildDir, "portal-common-css-jar")

	rename {
		"com.liferay.frontend.css.common.jar"
	}
}

configurations {
	mavenCompile {
		extendsFrom compile
	}

	mavenDistribution
}

dependencies {
	compile group: "com.beust", name: "jcommander", version: "1.72"
	compile group: "com.liferay", name: "com.liferay.rtl.css", version: "1.0.5"
	compile group: "com.liferay", name: "com.liferay.sass.compiler.jni", version: "2.0.6"
	compile group: "com.liferay", name: "com.liferay.sass.compiler.ruby", version: "2.0.4"
	compile group: "org.apache.ant", name: "ant", version: "1.9.4"
	compile group: "org.codehaus.plexus", name: "plexus-utils", transitive: false, version: "3.0.15"
	compile group: "org.slf4j", name: "slf4j-log4j12", version: "1.7.2"
	compile group: "org.sonatype.plexus", name: "plexus-build-api", transitive: false, version: "0.0.7"

	mavenCompile group: "com.liferay", name: "com.liferay.frontend.css.common", version: "2.0.4"

	mavenDistribution classifier: "bin", ext: "zip", group: "org.apache.maven", name: "apache-maven", version: "3.3.9"

	provided group: "com.liferay", name: "com.liferay.petra.string", version: "1.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.49.0"
	provided group: "org.apache.maven", name: "maven-artifact", version: "3.2.5"
	provided group: "org.apache.maven", name: "maven-core", version: "3.2.5"
	provided group: "org.apache.maven", name: "maven-plugin-api", version: "3.2.5"

	testCompile group: "org.apache.ant", name: "ant-testutil", version: "1.9.7"
	testCompile project(":test:maven-executor")
}

formatSource {
	mustRunAfter formatXML
}

liferay {
	deployDir = "../../../tools/sdk/dependencies/com.liferay.css.builder/lib"
}

test {
	dependsOn copyPortalCommonCSSJar
	dependsOn install
	dependsOn unzipPortalCommonCSS

	doFirst {
		systemProperty "maven.distribution.file.name", configurations.mavenDistribution.singleFile
	}

	systemProperty "css.builder.version", jar.version
	systemProperty "http.proxyHost", System.properties["http.proxyHost"]
	systemProperty "http.proxyPort", System.properties["http.proxyPort"]
	systemProperty "maven.debug", System.properties["maven.debug"]
	systemProperty "maven.repo.local", System.properties["maven.repo.local"]
	systemProperty "repository.url", System.properties["repository.url"]
}

unzipPortalCommonCSS {
	eachFile new StripPathSegmentsAction(2)

	from {
		zipTree(configurations.portalCommonCSS.singleFile)
	}

	include "META-INF/resources/**"
	into new File(buildDir, "portal-common-css")
	includeEmptyDirs = false
}

updateFileVersions {
	match(/\t\t\t\t<version>(\d.+)<\/version>/, "samples/pom.xml")
	match(/com\.liferay\.css\.builder=(\d.+)/, "../../sdk/gradle-plugins/src/main/resources/com/liferay/gradle/plugins/dependencies/portal-tools.properties")
}