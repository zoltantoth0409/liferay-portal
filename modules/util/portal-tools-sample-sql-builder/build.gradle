import java.util.jar.Attributes
import java.util.jar.JarFile

task writeReleases

classes {
	dependsOn writeReleases
}

dependencies {
	compile group: "com.liferay.portal", name: "com.liferay.util.java", version: "default"
	compile group: "javax.portlet", name: "portlet-api", version: "2.0"
	compile project(":apps:collaboration:blogs:blogs-api")
	compile project(":apps:collaboration:blogs:blogs-service")
	compile project(":apps:collaboration:document-library:document-library-api")
	compile project(":apps:collaboration:message-boards:message-boards-api")
	compile project(":apps:collaboration:message-boards:message-boards-service")
	compile project(":apps:collaboration:subscription:subscription-api")
	compile project(":apps:collaboration:subscription:subscription-service")
	compile project(":apps:collaboration:wiki:wiki-api")
	compile project(":apps:collaboration:wiki:wiki-service")
	compile project(":apps:forms-and-workflow:dynamic-data-lists:dynamic-data-lists-api")
	compile project(":apps:forms-and-workflow:dynamic-data-lists:dynamic-data-lists-service")
	compile project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-api")
	compile project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-service")
	compile project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-web")
	compile project(":apps:foundation:friendly-url:friendly-url-api")
	compile project(":apps:foundation:friendly-url:friendly-url-service")
	compile project(":apps:web-experience:asset:asset-entry-rel-service")
	compile project(":apps:web-experience:asset:asset-tag-stats-service")
	compile project(":apps:web-experience:journal:journal-api")
	compile project(":apps:web-experience:journal:journal-service")

	provided group: "com.liferay", name: "org.freemarker", version: "2.3.23.LIFERAY-PATCHED-4"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	provided project(":core:petra:petra-reflect")
	provided project(":core:petra:petra-string")

	testCompile classifier: "jdk6debug", group: "org.hsqldb", name: "hsqldb", version: "2.3.3"
	testCompile group: "com.liferay", name: "com.ibm.icu4j", version: "54.1.1.LIFERAY-PATCHED-2"
	testCompile group: "com.liferay", name: "com.sun.syndication", version: "1.0.LIFERAY-PATCHED-1"
	testCompile group: "com.liferay", name: "org.jabsorb", version: "1.3.2.LIFERAY-PATCHED-1"
	testCompile group: "com.mchange", name: "c3p0", version: "0.9.5"
	testCompile group: "com.redhat.qe", name: "json-java", version: "20110202"
	testCompile group: "commons-io", name: "commons-io", version: "2.5"
	testCompile group: "easyconf", name: "easyconf", version: "0.9.5"
	testCompile group: "log4j", name: "apache-log4j-extras", version: "1.1"
	testCompile group: "org.apache.tika", name: "tika-core", version: "1.14"
	testCompile group: "org.apache.tomcat", name: "tomcat-jdbc", version: "8.0.32"
	testCompile group: "org.jodd", name: "jodd-json", version: "3.6.4"
	testCompile group: "org.slf4j", name: "slf4j-api", version: "1.7.2"
	testCompile group: "org.springframework", name: "spring-context", version: "4.1.9.RELEASE"
	testCompile project(":apps:foundation:petra:petra-log4j")
	testCompile project(":core:osgi-service-tracker-collections")
	testCompile project(":core:petra:petra-concurrent")
	testCompile project(":core:petra:petra-lang")
	testCompile project(":core:petra:petra-memory")
	testCompile project(":core:petra:petra-nio")
	testCompile project(":core:registry-api")
}

writeReleases {
	dependsOn configurations.compile

	doLast {
		File releasesTxtFile = new File(processResources.destinationDir, "com/liferay/portal/tools/sample/sql/builder/dependencies/releases.txt")

		releasesTxtFile.withWriter("UTF-8") {
			Writer writer ->

			configurations.compile.each {
				println "Adding release information from ${it} into ${releasesTxtFile}"

				JarFile jarFile = new JarFile(it)

				Attributes mainAttributes = jarFile.manifest.mainAttributes

				String schemaVersion = mainAttributes.getValue("Liferay-Require-SchemaVersion")

				if (!schemaVersion) {
					return
				}

				String symbolicName = mainAttributes.getValue("Bundle-SymbolicName")

				writer.writeLine "${symbolicName}:${schemaVersion}"
			}
		}
	}
}