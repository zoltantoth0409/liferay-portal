import java.util.jar.Attributes
import java.util.jar.JarFile

import org.gradle.util.VersionNumber

task writeReleases

baseline {
	onlyIf {
		Properties properties = new Properties()

		File bndBndFile = project.file("bnd.bnd")

		bndBndFile.withInputStream {
			properties.load it
		}

		return VersionNumber.parse(properties["Bundle-Version"]) > VersionNumber.parse("2.0.0")
	}
}

classes {
	dependsOn writeReleases
}

dependencies {
	compile group: "com.liferay.portal", name: "com.liferay.util.java", version: "default"
	compile group: "javax.portlet", name: "portlet-api", transitive: false, version: "3.0.1"

	compileInclude project(":apps:asset:asset-entry-rel-service")
	compileInclude project(":apps:blogs:blogs-api")
	compileInclude project(":apps:blogs:blogs-service")
	compileInclude project(":apps:commerce:commerce-currency-api")
	compileInclude project(":apps:commerce:commerce-currency-service")
	compileInclude project(":apps:commerce:commerce-product-api")
	compileInclude project(":apps:commerce:commerce-product-service")
	compileInclude project(":apps:document-library:document-library-api")
	compileInclude project(":apps:dynamic-data-lists:dynamic-data-lists-api")
	compileInclude project(":apps:dynamic-data-lists:dynamic-data-lists-service")
	compileInclude project(":apps:dynamic-data-lists:dynamic-data-lists-web")
	compileInclude project(":apps:dynamic-data-mapping:dynamic-data-mapping-api")
	compileInclude project(":apps:dynamic-data-mapping:dynamic-data-mapping-service")
	compileInclude project(":apps:dynamic-data-mapping:dynamic-data-mapping-web")
	compileInclude project(":apps:fragment:fragment-api")
	compileInclude project(":apps:fragment:fragment-service")
	compileInclude project(":apps:friendly-url:friendly-url-api")
	compileInclude project(":apps:friendly-url:friendly-url-service")
	compileInclude project(":apps:hello-world:hello-world-web")
	compileInclude project(":apps:journal:journal-api")
	compileInclude project(":apps:journal:journal-service")
	compileInclude project(":apps:layout:layout-api")
	compileInclude project(":apps:layout:layout-page-template-api")
	compileInclude project(":apps:layout:layout-page-template-service")
	compileInclude project(":apps:layout:layout-service")
	compileInclude project(":apps:login:login-web-api")
	compileInclude project(":apps:message-boards:message-boards-api")
	compileInclude project(":apps:message-boards:message-boards-service")
	compileInclude project(":apps:portal-search:portal-search-web")
	compileInclude project(":apps:portal-search:portal-search-web-api")
	compileInclude project(":apps:segments:segments-api")
	compileInclude project(":apps:segments:segments-service")
	compileInclude project(":apps:subscription:subscription-api")
	compileInclude project(":apps:subscription:subscription-service")
	compileInclude project(":apps:view-count:view-count-api")
	compileInclude project(":apps:view-count:view-count-service")
	compileInclude project(":apps:wiki:wiki-api")
	compileInclude project(":apps:wiki:wiki-service")

	compileOnly group: "com.liferay", name: "org.freemarker", version: "2.3.29.LIFERAY-PATCHED-2"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly project(":core:petra:petra-function")
	compileOnly project(":core:petra:petra-io")
	compileOnly project(":core:petra:petra-reflect")
	compileOnly project(":core:petra:petra-string")

	testCompile classifier: "jdk6debug", group: "org.hsqldb", name: "hsqldb", version: "2.3.3"
	testCompile group: "com.liferay", name: "com.ibm.icu4j", version: "54.1.1.LIFERAY-PATCHED-2"
	testCompile group: "com.liferay", name: "com.sun.syndication", version: "1.0.LIFERAY-PATCHED-1"
	testCompile group: "com.liferay", name: "org.jabsorb", version: "1.3.2.LIFERAY-PATCHED-1"
	testCompile group: "commons-io", name: "commons-io", version: "2.5"
	testCompile group: "log4j", name: "apache-log4j-extras", version: "1.2.17"
	testCompile group: "org.apache.tika", name: "tika-core", version: "1.24.1"
	testCompile group: "org.apache.tomcat", name: "tomcat-jdbc", version: "8.0.32"
	testCompile group: "org.jodd", name: "jodd-json", version: "3.6.4"
	testCompile group: "org.json", name: "json", version: "20180813"
	testCompile group: "org.slf4j", name: "slf4j-api", version: "1.7.26"
	testCompile group: "org.springframework", name: "spring-context", version: "5.2.2.RELEASE"
	testCompile project(":apps:petra:petra-log4j")
	testCompile project(":core:osgi-service-tracker-collections")
	testCompile project(":core:petra:petra-concurrent")
	testCompile project(":core:petra:petra-lang")
	testCompile project(":core:petra:petra-memory")
	testCompile project(":core:petra:petra-nio")
	testCompile project(":core:registry-api")
}

writeReleases {
	dependsOn configurations.compile

	doLast {
		File releasesTxtFile = new File(processResources.destinationDir, "com/liferay/portal/tools/sample/sql/builder/dependencies/releases.txt")

		List<String> lines = []

		(configurations.compile + configurations.compileInclude).each {
			JarFile jarFile = new JarFile(it)

			Attributes mainAttributes = jarFile.manifest.mainAttributes

			String schemaVersion = mainAttributes.getValue("Liferay-Require-SchemaVersion")

			if (!schemaVersion) {
				return
			}

			logger.lifecycle "Adding release information from {} into {}.", it, releasesTxtFile

			String symbolicName = mainAttributes.getValue("Bundle-SymbolicName")

			lines.add("${symbolicName}:${schemaVersion}")
		}

		lines.sort()

		if (!releasesTxtFile.exists()) {
			mkdir releasesTxtFile.getParentFile()

			releasesTxtFile.createNewFile()
		}

		releasesTxtFile.setText lines.join('\n'), "UTF-8"
	}
}