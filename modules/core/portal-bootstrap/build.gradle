import aQute.bnd.osgi.Domain

import com.liferay.gradle.util.copy.RenameDependencyClosure

import org.dm.gradle.plugins.bundle.JarBuilder

task copySystemPackagesExtraLibs(type: Copy)
task copySystemPackagesExtraManifest(type: Copy)

String buildProfile = System.properties["build.profile"]

File systemPackagesExtraManifestFile = file("system.packages.extra.mf")

if (!System.getenv("JENKINS_HOME") || !systemPackagesExtraManifestFile.exists()) {
	systemPackagesExtraManifestFile = new File(buildDir, "system.packages.extra.mf")

	task buildSystemPackagesExtraManifest

	buildSystemPackagesExtraManifest {
		dependsOn classes
		dependsOn copySystemPackagesExtraLibs

		doLast {
			Clock clock = new Clock()

			JarBuilder jarBuilder = bundle.jarBuilderFactory.create()

			jarBuilder.withBase projectDir

			Map<String, String> properties = liferayOSGi.bundleDefaultInstructions

			properties["-check"] = "imports"
			properties["-fixupmessages"] = '"Classpath is empty. Private-Package ...";is:=ignore'
			properties["-include"] = "system.packages.extra.bnd"

			File portalImplJarFile = configurations.compileOnly.find {
				it.name.startsWith "com.liferay.portal.impl-"
			}

			Domain portalImplJarDomain = Domain.domain(portalImplJarFile)

			properties["Provide-Capability"] = portalImplJarDomain.provideCapability.toString()

			jarBuilder.withClasspath new File[0]
			jarBuilder.withFailOnError true
			jarBuilder.withName "system.packages.extra"
			jarBuilder.withProperties properties
			jarBuilder.withResources new File[0]

			jarBuilder.withSourcepath sourceSets.main.allSource.srcDirs.findAll {
				it.directory
			}

			jarBuilder.withTrace false
			jarBuilder.withVersion project.version

			systemPackagesExtraManifestFile.parentFile.mkdirs()

			systemPackagesExtraManifestFile.withOutputStream {
				jarBuilder.writeManifestTo it
			}

			logger.lifecycle "Building the system.packages.extra.mf file took {}.", clock.time
		}
	}

	jar {
		dependsOn buildSystemPackagesExtraManifest
	}
}
else {
	logger.lifecycle "Reusing file {}.", systemPackagesExtraManifestFile.absolutePath
}

bundle {
	instruction "system.packages.extra.manifest.file", relativePath(systemPackagesExtraManifestFile)
}

copySystemPackagesExtraLibs {
	from configurations.compileOnly
	into "lib"
	rename new RenameDependencyClosure(project, configurations.compileOnly.name)
}

dependencies {
	compileOnly group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.jaxws.osgi.bridge", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.osgi.felix.util", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.concurrent", version: "1.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.content", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.encryptor", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.executor", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.function", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.io", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.lang", version: "1.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.log4j", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.mail", version: "1.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.memory", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.nio", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.process", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.reflect", version: "1.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.xml", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.registry.api", version: "1.2.0"
	compileOnly group: "com.liferay", name: "com.liferay.registry.impl", version: "1.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	compileOnly group: "org.springframework", name: "spring-beans", version: "4.1.9.RELEASE"
	compileOnly group: "org.springframework", name: "spring-context", version: "4.1.9.RELEASE"
	compileOnly group: "org.springframework", name: "spring-core", version: "4.1.9.RELEASE"
	compileOnly project(":core:portal-app-license-api")
}

if (buildProfile != "slim") {
	dependencies {
	}
}

liferay {
	deployDir = file("${liferayHome}/osgi/core")
}