import aQute.bnd.osgi.Domain

import com.liferay.gradle.plugins.tasks.ExecuteBndTask
import com.liferay.gradle.util.copy.RenameDependencyClosure

import org.gradle.util.GUtil

task copySystemPackagesExtraLibs(type: Copy)
task copySystemPackagesExtraManifest(type: Copy)

String buildProfile = System.properties["build.profile"]

File systemPackagesExtraManifestFile = file("system.packages.extra.mf")

if (!System.getenv("JENKINS_HOME") || !systemPackagesExtraManifestFile.exists()) {
	systemPackagesExtraManifestFile = new File(buildDir, "system.packages.extra.mf")

	task buildSystemPackagesExtraManifest(type: ExecuteBndTask)

	buildSystemPackagesExtraManifest {
		classpath = files()

		dependsOn classes
		dependsOn copySystemPackagesExtraLibs

		outputFile = systemPackagesExtraManifestFile

		properties liferayOSGi.bundleDefaultInstructions

		property "-check", "IMPORTS"
		property "-fixupmessages.liferay.services", "This bundle contains Liferay services"
		property "-include", "system.packages.extra.bnd"

		property "Bundle-SymbolicName", {
			Properties bndProperties = GUtil.loadProperties(file("system.packages.extra.bnd"))

			bndProperties.getProperty("Bundle-SymbolicName")
		}

		property "Bundle-Version", {
			project.version
		}

		property "Provide-Capability", {
			File portalImplJarFile = configurations.compileOnly.find {
				it.name.startsWith "com.liferay.portal.impl-"
			}

			Domain portalImplJarDomain = Domain.domain(portalImplJarFile)

			portalImplJarDomain.provideCapability.toString()
		}

		resourceDirs = files()
		sourceDirs = files(sourceSets.main.allSource.srcDirs.findAll { it.directory })
		writeManifest = true
	}

	jar {
		dependsOn buildSystemPackagesExtraManifest
	}
}
else {
	logger.lifecycle "Reusing file {}.", systemPackagesExtraManifestFile.absolutePath
}

bundle {
	String systemPackagesExtraManifestPath = relativePath(systemPackagesExtraManifestFile)

	instruction "system.packages.extra.manifest.file", systemPackagesExtraManifestPath.replace(File.separatorChar, '/' as char)
}

copySystemPackagesExtraLibs {
	from configurations.compileOnly
	into "lib"
	rename new RenameDependencyClosure(project, configurations.compileOnly.name)
}

dependencies {
	compileOnly group: "com.liferay", name: "biz.aQute.bnd.annotation", version: "4.2.0.LIFERAY-PATCHED-1"
	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.9.10"
	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.9.10"
	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.10.1"
	compileOnly group: "com.liferay", name: "org.eclipse.osgi", version: "3.13.0.LIFERAY-PATCHED-8"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly group: "org.springframework", name: "spring-beans", version: "5.2.2.RELEASE"
	compileOnly group: "org.springframework", name: "spring-context", version: "5.2.2.RELEASE"
	compileOnly group: "org.springframework", name: "spring-core", version: "5.2.2.RELEASE"
	compileOnly project(":apps:petra:petra-content")
	compileOnly project(":apps:petra:petra-encryptor")
	compileOnly project(":apps:petra:petra-log4j")
	compileOnly project(":apps:petra:petra-mail")
	compileOnly project(":apps:petra:petra-xml")
	compileOnly project(":core:jaxws-osgi-bridge")
	compileOnly project(":core:osgi-service-tracker-collections")
	compileOnly project(":core:petra:petra-concurrent")
	compileOnly project(":core:petra:petra-executor")
	compileOnly project(":core:petra:petra-function")
	compileOnly project(":core:petra:petra-io")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-memory")
	compileOnly project(":core:petra:petra-nio")
	compileOnly project(":core:petra:petra-process")
	compileOnly project(":core:petra:petra-reflect")
	compileOnly project(":core:petra:petra-string")
	compileOnly project(":core:portal-app-license-api")
	compileOnly project(":core:registry-api")
	compileOnly project(":core:registry-impl")
}

liferay {
	deployDir = file("${liferayHome}/osgi/core")
}