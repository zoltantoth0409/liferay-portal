import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildAlloyEditor(type: Copy)
task buildCreateReactClass(type: Copy)

File editorDestinationDir = file("tmp/META-INF/resources")
File createReactClassDestinationDir = file("classes/META-INF/resources/js")

buildAlloyEditor {
	File alloyEditorDestinationDir = new File(editorDestinationDir, "alloyeditor")

	dependsOn npmInstall

	doFirst {
		delete alloyEditorDestinationDir
	}

	eachFile new StripPathSegmentsAction(3)
	from npmInstall.nodeModulesDir
	include "alloyeditor/dist/alloy-editor/"
	includeEmptyDirs = false
	into alloyEditorDestinationDir
}

buildCreateReactClass {
	dependsOn buildAlloyEditor

	filter {
		String line ->

		line.replace('exports.createReactClass=e(require("react")):t.createReactClass=e(t.React)}(this,', 'exports.createClass=e(require("react")):t.React.createClass=e(t.React)}(AlloyEditor,')
	}

	from npmInstall.nodeModulesDir
	include "create-react-class/create-react-class.min.js"
	includeEmptyDirs = false
	into createReactClassDestinationDir
}

classes {
	dependsOn buildAlloyEditor
	dependsOn buildCreateReactClass
}

clean {
	delete "tmp"
}

dependencies {
	compileOnly group: "com.liferay", name: "com.liferay.frontend.editor.api", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.frontend.editor.taglib", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.frontend.taglib", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.item.selector.api", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.item.selector.criteria.api", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.layout.item.selector.api", version: "2.1.0"
	compileOnly group: "com.liferay", name: "com.liferay.message.boards.api", version: "4.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.encryptor", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.lang", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "2.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "3.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.java", version: "3.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "3.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
}