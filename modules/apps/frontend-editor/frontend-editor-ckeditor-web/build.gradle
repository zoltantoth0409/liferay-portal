import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction
import com.liferay.gradle.util.tasks.ConcatenateTask

task buildCKEditor(type: Copy)
task buildCKEditorBBCode(type: ConcatenateTask)
task buildCKEditorPlugins(type: Copy)
task buildCKEditorScayt(type: Copy)
task buildCKEditorWsc(type: Copy)

String ckEditorVersion = "4.11.3"

String ckEditorScaytUrl = "https://ckeditor.com/cke4/sites/default/files/scayt/releases/scayt_${ckEditorVersion}.zip"
String ckEditorWscUrl = "https://ckeditor.com/cke4/sites/default/files/wsc/releases/wsc_${ckEditorVersion}.zip"

File editorDestinationDir = file("tmp/META-INF/resources")
File editorSrcDir = file("src/main/resources/META-INF/resources")

buildCKEditor {
	File ckEditorDestinationDir = new File(editorDestinationDir, "ckeditor")

	doFirst {
		delete ckEditorDestinationDir
	}

	duplicatesStrategy = "exclude"

	eachFile new StripPathSegmentsAction(2)
	from npmInstall.nodeModulesDir
	include "liferay-ckeditor/ckeditor/"
	includeEmptyDirs = false
	into ckEditorDestinationDir
}

buildCKEditorBBCode {
	File bbCodeDir = new File(editorSrcDir, "_diffs/plugins/bbcode")

	destinationFile = new File(bbCodeDir, "bbcode_parser.js")

	ext {
		autoClean = false
	}

	sourceFiles new File(bbCodeDir, "bbcode_utils.js")
	sourceFiles new File(bbCodeDir, "lexer.js")
	sourceFiles new File(bbCodeDir, "parser.js")
	sourceFiles new File(bbCodeDir, "converter.js")
}

buildCKEditorPlugins {
	File ckEditorDestinationDir = new File(editorDestinationDir, "ckeditor")

	dependsOn buildCKEditor, packageRunBuild

	from new File(packageRunBuild.destinationDir, "_diffs")
	includeEmptyDirs = false
	into ckEditorDestinationDir
}

buildCKEditorScayt {
	dependsOn buildCKEditor

	from {
		File ckEditorScaytFile = FileUtil.get(project, ckEditorScaytUrl)

		zipTree(ckEditorScaytFile)
	}

	into new File(editorDestinationDir, "ckeditor/plugins")
}

buildCKEditorWsc {
	dependsOn buildCKEditor

	from {
		File ckEditorWscFile = FileUtil.get(project, ckEditorWscUrl)

		zipTree(ckEditorWscFile)
	}

	into new File(editorDestinationDir, "ckeditor/plugins")
}

classes {
	dependsOn buildCKEditor, buildCKEditorPlugins, buildCKEditorScayt, buildCKEditorWsc
}

clean {
	delete "tmp"
}

dependencies {
	compileOnly group: "com.liferay", name: "com.liferay.frontend.editor.api", version: "4.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.frontend.editor.taglib", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.frontend.taglib", version: "4.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.message.boards.api", version: "5.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.lang", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "3.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "4.34.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "4.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.1"
	compileOnly group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
}