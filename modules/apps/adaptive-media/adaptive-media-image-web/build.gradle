import com.liferay.gradle.util.copy.StripPathSegmentsAction

configurations {
	picturefill
}

task buildPictureFill(type: Copy)

String pictureFillVersion = "3.0.2"

buildPictureFill {
	eachFile new StripPathSegmentsAction(6)

	from {
		zipTree(configurations.picturefill.singleFile)
	}

	include "META-INF/resources/webjars/picturefill/${pictureFillVersion}/dist/picturefill.min.js"
	includeEmptyDirs = false
	into "classes/META-INF/resources"
}

classes {
	dependsOn buildPictureFill
}

dependencies {
	compileOnly group: "com.liferay", name: "com.liferay.adaptive.media.api", version: "4.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.adaptive.media.image.api", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "3.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "4.34.0"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"

	picturefill group: "org.webjars.bower", name: "picturefill", transitive: false, version: pictureFillVersion

	testCompile project(":core:petra:petra-lang")
}