definition {

	macro addDepot {
		LexiconEntry.gotoAdd();

		Type(
			locator1 = "TextInput#NAME",
			value1 = "${depotName}");

		Button.clickSave();

		if ("${depot}" == "Duplicate") {
			AssertTextEquals(
				locator1 = "Message#ERROR_FORM_FIELD_REQUIRED",
				value1 = "Please enter a unique name.");
		}
		else {
			Alert.viewSuccessMessageText(successMessage = "Repository was added.");

			AssertTextEquals(
				locator1 = "ControlMenu#HEADING",
				value1 = "${depotName}");

			if (isSet(depotDescription)) {
				Type(
					locator1 = "TextArea#DESCRIPTION",
					value1 = "${depotDescription}");
			}

			Button.clickSave();

			Alert.viewSuccessMessage();
		}
	}

	macro configureRecycleBin {
		Panel.expandPanel(panel = "Recycle Bin");

		if ("${recycleBin}" == "Enabled") {
			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Enable Recycle Bin",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}
		else if ("${recycleBin}" == "Disabled") {
			Uncheck.uncheckToggleSwitchNoErrorNoAssert(locator1 = "Checkbox#ENABLE_RECYCLE_BIN");

			AssertConfirm(value1 = "Disabling the Recycle Bin prevents the restoring of content that has been moved to the Recycle Bin.");

			AssertNotChecked.assertNotCheckedNotVisible(locator1 = "Checkbox#ENABLE_RECYCLE_BIN");
		}

		PortletEntry.save();
	}

	macro configureSearch {
		for (var siteName : list "${siteList}") {
			DepotNavigator.gotoEdit(depotName = "${depotName}");

			DepotNavigator.gotoSites(depotName = "${depotName}");

			LexiconTable.clickEllipsisItem(
				item = "${item}",
				tableEntry = "${siteName}");

			SelectFrame(value1 = "relative=top");

			Alert.viewSuccessMessage();

			AssertTextEquals(
				key_tableEntry = "${siteName}",
				key_tableEntryText = "${tableEntryText}",
				locator1 = "LexiconTable#TABLE_ENTRY_TEXT",
				value1 = "${tableEntryText}");
		}
	}

	macro configureSharing {
		if ("${sharing}" == "Enabled") {
			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Enabled",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}
		else if ("${sharing}" == "Disabled") {
			Uncheck.uncheckToggleSwitch(
				key_toggleSwitchLabel = "Enabled",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}

		PortletEntry.save();
	}

	macro connectSite {
		for (var siteName : list "${siteList}") {
			Button.clickAdd();

			SelectFrame(locator1 = "IFrame#SELECT_SITES");

			Navigator.gotoNavItem(navItem = "My Sites");

			SiteSelector.selectSite(site = "${siteName}");

			SelectFrame(value1 = "relative=top");

			Alert.viewSuccessMessage();
		}
	}

	macro deleteContentViaBulkEditing {
		if (isSet(recycleBinDisabled)) {
			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(value1 = "Are you sure you want to delete the selected entries? They will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
		else {
			ClickNoError(locator1 = "Icon#TRASH");

			if (isSet(webContent)) {
				AssertConfirm(value1 = "Are you sure you want to move the selected entries to the Recycle Bin?");
			}

			Alert.viewSuccessMessage();
		}
	}

	macro deleteCP {
		if (isSet(deletions)) {
			ClickNoError(locator1 = "Icon#DELETE");
		}

		else {
			LexiconEntry.gotoEntryMenuItemNoError(
				menuItem = "Delete",
				rowEntry = "${depotName}");
		}

		AssertConfirm(value1 = "Removing a repository can affect sites that use the contents stored in it. Are you sure you want to continue removing this repository?");

		Alert.viewSuccessMessage();
	}

	macro disconnectSite {
		for (var siteName : list "${siteList}") {
			LexiconTable.clickEllipsisItem(
				item = "Disconnect",
				tableEntry = "${siteName}");

			AssertConfirm(value1 = "Removing this site connection will not allow the site to consume data from this repository directly.");

			Alert.viewSuccessMessage();
		}
	}

	macro editDepot {
		if (isSet(listView)) {
			LexiconEntry.changeDisplayStyle(displayStyle = "list");

			Click(
				key_listEntry = "${depotName}",
				locator1 = "LexiconList#LIST_ENTRY_ELLIPSIS");

			MenuItem.click(menuItem = "Edit");
		}

		else if (isSet(tableView)) {
			LexiconEntry.changeDisplayStyle(displayStyle = "table");

			Click(
				key_tableEntry = "${depotName}",
				locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

			MenuItem.click(menuItem = "Edit");
		}

		else if (isSet(settings)) {
			AssertElementNotPresent(locator1 = "Button#BACK");
		}

		else {
			DepotNavigator.gotoEdit(depotName = "${depotName}");
		}

		if (isSet(depotNameEdit)) {
			Type(
				locator1 = "TextInput#NAME",
				value1 = "${depotNameEdit}");
		}

		if (isSet(depotDescriptionEdit)) {
			Type(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${depotDescriptionEdit}");
		}

		Button.clickSave();

		if ("${depot}" == "Duplicate") {
			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "Please enter a unique name.");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro viewConnectedSite {
		for (var siteName : list "${siteList}") {
			AssertElementPresent(
				key_tableEntry = "${siteName}",
				locator1 = "LexiconTable#TABLE_ENTRY");

			AssertTextEquals(
				key_tableEntry = "${siteName}",
				key_tableEntryText = "Yes",
				locator1 = "LexiconTable#TABLE_ENTRY_TEXT",
				value1 = "Yes");
		}
	}

	macro viewCurrentLanguages {
		AssertTextEquals(
			key_title = "Available Languages",
			locator1 = "Header#H4_SPECIFIC_TITLE",
			value1 = "Available Languages");

		for (var currentLanguage : list "${currentLanguages}") {
			AssertTextEquals.assertPartialText(
				locator1 = "LanguageConfiguration#LANGUAGES_CURRENT",
				value1 = "${currentLanguage}");
		}
	}

	macro viewDefaultCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No repositories were found.");
	}

	macro viewDefaultLanguage {
		AssertTextEquals(
			key_title = "Default Language",
			locator1 = "Header#H4_SPECIFIC_TITLE",
			value1 = "Default Language");

		PortalSettings.viewDefaultLanguageCP(defaultPortalLanguage = "${defaultDepotLanguage}");
	}

	macro viewDefaultLanguagesConfiguration {
		AssertChecked.assertCheckedNotVisible(
			key_languagesOption = "Use the default language options.",
			locator1 = "Radio#DEPOT_LANGUAGES_OPTION");

		AssertNotChecked.assertNotCheckedNotVisible(
			key_languagesOption = "Define a custom default language and additional available languages for this repository.",
			locator1 = "Radio#DEPOT_LANGUAGES_OPTION");

		AssertTextEquals(
			key_titleName = "Default Language",
			locator1 = "LanguageConfiguration#DEPOT_MUTED_TITLE",
			value1 = "Default Language");

		AssertTextEquals(
			key_language = "English (United States)",
			locator1 = "LanguageConfiguration#DEPOT_MUTED_DEFAULT_LANGUAGE",
			value1 = "English (United States)");

		AssertTextEquals(
			key_titleName = "Available Languages",
			locator1 = "LanguageConfiguration#DEPOT_MUTED_TITLE",
			value1 = "Available Languages");

		AssertTextEquals(
			key_languages = "English (United States), Arabic (Saudi Arabia), Catalan (Spain), Chinese (China), Dutch (Netherlands), Finnish (Finland), French (France), German (Germany), Hungarian (Hungary), Japanese (Japan), Portuguese (Brazil), Spanish (Spain), Swedish (Sweden)",
			locator1 = "LanguageConfiguration#DEPOT_MUTED_AVAILABLE_LANGUAGES",
			value1 = "English (United States), Arabic (Saudi Arabia), Catalan (Spain), Chinese (China), Dutch (Netherlands), Finnish (Finland), French (France), German (Germany), Hungarian (Hungary), Japanese (Japan), Portuguese (Brazil), Spanish (Spain), Swedish (Sweden)");

		AssertElementPresent(locator1 = "Button#SAVE");
	}

	macro viewGeneralLanguageOptions {
		Page.viewFriendlyURLLanguageOptionsPG(
			currentLanguageList = "${currentLanguageList}",
			fieldName = "Name");

		Page.viewFriendlyURLLanguageOptionsPG(
			currentLanguageList = "${currentLanguageList}",
			fieldName = "Description");
	}

	macro viewLanguageOptionsInWebContent {
		Click(locator1 = "Button#LOCALIZATION_GENERIC");

		for (var locale : list "${currentLanguageList}") {
			AssertElementPresent(
				key_locale = "${locale}",
				locator1 = "Translation#LOCALIZATION_NAME");
		}

		Click(
			key_portletName = "JournalPortlet_Menu",
			locator1 = "Button#LANGUAGE_DROPDOWN");

		for (var locale : list "${currentLanguageList}") {
			AssertElementPresent(
				key_locale = "${locale}",
				locator1 = "Translation#LOCALIZATION_NAME");
		}

		Panel.expandPanel(panel = "Basic Information");

		Page.viewFriendlyURLLanguageOptionsPG(
			currentLanguageList = "${currentLanguageList}",
			fieldName = "Summary");

		Panel.expandPanel(panel = "Friendly URL");

		Click(
			key_portletName = "JournalPortlet_friendlyURLMenu",
			locator1 = "Button#LANGUAGE_DROPDOWN");

		for (var locale : list "${currentLanguageList}") {
			AssertElementPresent(
				key_locale = "${locale}",
				locator1 = "Translation#LOCALIZATION_NAME");
		}
	}

	macro viewNoConnectedSiteCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_DEPOT",
			value1 = "No sites are connected yet.");
	}

	macro viewSearchResult {
		if ("${resultCount}" != "1") {
			AssertTextEquals.assertPartialText(
				locator1 = "ManagementBar#SEARCH_RESULT_SUMMARY",
				value1 = "${resultCount} Results for ${searchTerm}");
		}

		else {
			AssertTextEquals.assertPartialText(
				locator1 = "ManagementBar#SEARCH_RESULT_SUMMARY",
				value1 = "1 Result for ${searchTerm}");
		}

		for (var depotName : list "${includedResults}") {
			var key_card = "${depotName}";

			AssertTextEquals(
				locator1 = "LexiconCard#CARD_TITLE",
				value1 = "${depotName}");
		}

		if (isSet(excludedResults)) {
			for (var depotName : list "${excludedResults}") {
				var key_card = "${depotName}";

				AssertElementNotPresent(locator1 = "LexiconCard#CARD_TITLE");
			}
		}
	}

}