@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONDepot.deleteDepot(depotName = "Test Depot Name");

			Site.tearDownCP();

			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This test asserts that Not Found error returns when visiting FTP mode if indexing is disabled."
	@priority = "5"
	test DisableIndexing {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		Navigator.gotoNavTab(navTab = "General");

		Panel.expandPanel(panel = "Documents and Media");

		AssertNotChecked.assertNotCheckedNotVisible(locator1 = "Checkbox#ENABLE_DIRECTORY_INDEXING");

		var portalURL = PropsUtil.get("portal.url");

		Navigator.openSpecificURL(url = "${portalURL}/documents/test depot name");

		AssertTextEquals(
			locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR",
			value1 = "Not Found");
	}

	@description = "This test asserts that a document stored in a depot can be accessed from FTP mode."
	@priority = "5"
	test ViewDocument {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		Navigator.gotoNavTab(navTab = "General");

		Panel.expandPanel(panel = "Documents and Media");

		AssertNotChecked.assertNotCheckedNotVisible(locator1 = "Checkbox#ENABLE_DIRECTORY_INDEXING");

		Depot.configureDirectoryIndexing(directoryIndexing = "Enabled");

		AssertChecked.assertCheckedNotVisible(locator1 = "Checkbox#ENABLE_DIRECTORY_INDEXING");

		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document JPG",
			groupName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document JPG");

		DMDocument.viewCP(dmDocumentTitle = "DM Document JPG");

		Depot.viewDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document JPG");
	}

	@description = "This test asserts that no document shows in FTP mode after deleting it in a depot."
	@priority = "5"
	test ViewDocumentAfterDeleting {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		Navigator.gotoNavTab(navTab = "General");

		Panel.expandPanel(panel = "Documents and Media");

		Depot.configureDirectoryIndexing(directoryIndexing = "Enabled");

		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document PDF",
			groupName = "Test Depot Name");

		Depot.viewDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document PDF");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMDocument.deleteCP(dmDocumentTitle = "DM Document PDF");

		Depot.viewNoDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document PDF");
	}

	@description = "This test asserts that an edited document stored in a depot can be accessed from FTP mode."
	@priority = "5"
	test ViewDocumentAfterEditing {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		Navigator.gotoNavTab(navTab = "General");

		Panel.expandPanel(panel = "Documents and Media");

		Depot.configureDirectoryIndexing(directoryIndexing = "Enabled");

		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document TXT",
			groupName = "Test Depot Name");

		Depot.viewDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document TXT");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMDocument.editCP(
			dmDocumentTitle = "DM Document TXT",
			dmDocumentTitleEdit = "DM Document TXT Edit");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document TXT Edit");

		DMDocument.viewCP(dmDocumentTitle = "DM Document TXT Edit");

		Depot.viewDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document TXT Edit");
	}

	@description = "This test asserts that a document in a folder stored in a depot can be accessed from FTP mode."
	@priority = "5"
	test ViewDocumentInFolder {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		Navigator.gotoNavTab(navTab = "General");

		Panel.expandPanel(panel = "Documents and Media");

		Depot.configureDirectoryIndexing(directoryIndexing = "Enabled");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMFolder.addCP(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document DOC",
			folderName = "DM Folder Name",
			groupName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMFolder.gotoEntry(dmFolderName = "DM Folder Name");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document DOC");

		DMDocument.viewCP(dmDocumentTitle = "DM Document DOC");

		Depot.viewDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document DOC",
			dmFolderName = "DM Folder Name");
	}

	@description = "This test asserts that the approved document in a depot can be accessed from FTP mode."
	@priority = "4"
	test ViewDocumentInWorkflow {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		Navigator.gotoNavTab(navTab = "General");

		Panel.expandPanel(panel = "Documents and Media");

		Depot.configureDirectoryIndexing(directoryIndexing = "Enabled");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMFolder.addCP(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.addWithWorkflowCP(
			dmDocumentFile = "Document_1.xls",
			dmDocumentTitle = "DM Document XLS");

		Depot.viewNoDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document XLS",
			dmFolderName = "DM Folder Name");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "DM Document XLS",
			workflowAssetType = "Document",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "DM Document XLS",
			workflowAssetType = "Document",
			workflowTask = "Review");

		Depot.viewDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document XLS",
			dmFolderName = "DM Folder Name");
	}

	@description = "This test asserts that a document without guest permissions in a depot can not be accessed from FTP mode."
	@priority = "4"
	test ViewDocumentWithoutPermissions {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		Navigator.gotoNavTab(navTab = "General");

		Panel.expandPanel(panel = "Documents and Media");

		Depot.configureDirectoryIndexing(directoryIndexing = "Enabled");

		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document SVG",
			groupName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMNavigator.gotoDocumentPermissionsCP(dmDocumentTitle = "DM Document SVG");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Remove",
			roleList = "Guest");

		Depot.viewDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document SVG");

		Navigator.openURL();

		User.logoutPG();

		Depot.viewNoDocumentInFTP(
			depotName = "Test Depot Name",
			dmDocumentTitle = "DM Document SVG");

		User.loginPG();
	}

}