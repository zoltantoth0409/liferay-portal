@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONDepot.deleteDepot(depotName = "Test Depot Name");

			JSONGroup.deleteGroupByName(groupName = "Site Name");

			User.logoutPG();
		}
	}

	@description = "This ensures that a user is able to embed a depot external video in a blog entry on a connected site."
	@priority = "5"
	test AddBlogEntryWithEmbeddedVideo {
		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Blogs");

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		VideoShortcut.addCP(
			videoShortcutTitle = "Test YouTube Video",
			videoURL = "https://www.youtube.com/watch?v=HOdbzGCI5ME");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		Navigator.openSiteURL(siteName = "Site Name");

		Blogs.addEntry(
			depotName = "Test Depot Name",
			embedVideo = "true",
			entryContent = "video",
			entryTitle = "Blogs Entry Title",
			navTab = "Documents and Media",
			videoShortcutTitle = "Test YouTube Video");

		Navigator.openSiteURL(siteName = "Site Name");

		BlogsNavigator.gotoEntryPG(
			entryContent = "video",
			entryTitle = "Blogs Entry Title");

		BlogsEntry.viewEntryPG(
			embedVideo = "true",
			entryContent = "video",
			entryTitle = "Blogs Entry Title");
	}

	@description = "This test ensures that a YouTube shortened URL video added via item selector can be embedded when adding a depot WC."
	@priority = "4"
	test AddWithShortenedURLToWCViaItemSelector {
		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			embedVideo = "true",
			extractedVideoShortcutTitle = "Liferay Digital Experience Platform",
			videoShortcutDescription = "Video Shortcut Description",
			videoShortcutPreview = "https://www.youtube.com",
			videoShortcutTitle = "Test YouTube Video",
			videoURL = "https://youtu.be/HOdbzGCI5ME",
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		PortletEntry.publish();

		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		WebContentNavigator.gotoPreviewEntryCP(webContentTitle = "WC WebContent Title");

		Pause(locator1 = "3000");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertVisible(
			key_service = "https://www.youtube.com",
			locator1 = "WCPreview#BODY_FIELD_VIDEO");
	}

	@description = "This ensures that a depot video shortcut associated with a tag can be searched from the connected site."
	@priority = "5"
	test SearchForTag {
		property test.name.skip.portal.instance = "DepotVideoShortcut#SearchForTag";

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Search Bar");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Search Results");

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		VideoShortcut.addCP(
			tagNameList = "depot",
			videoShortcutTitle = "Test YouTube Video",
			videoURL = "https://www.youtube.com/watch?v=HOdbzGCI5ME");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		Navigator.gotoSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		Search.searchCP(searchTerm = "depot");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Test YouTube Video",
			searchAssetType = "Document");
	}

	@description = "This ensures that a depot video shortcut associated with a tag cannot be searched on a connected site when searching is disabled."
	@priority = "5"
	test SearchForUnsearchableTag {
		property test.name.skip.portal.instance = "DepotVideoShortcut#SearchForUnsearchableTag";

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Search Bar");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Search Results");

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		VideoShortcut.addCP(
			tagNameList = "depot",
			videoShortcutTitle = "Test Vimeo Video",
			videoURL = "https://vimeo.com/483035084");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		DepotNavigator.openDepotAdmin();

		Depot.configureSearch(
			depotName = "Test Depot Name",
			item = "Make Unsearchable",
			siteList = "Site Name",
			tableEntryText = "No");

		Navigator.gotoSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		Search.searchCP(searchTerm = "depot");

		SearchResultPortlet.viewNoSearchResults(searchTerm = "depot");
	}

	@description = "This ensures that a depot external video cannot be viewed from the disconnected site."
	@priority = "4"
	test ViewFromDisconnectedSite {
		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		VideoShortcut.addCP(
			videoShortcutTitle = "Test YouTube Video",
			videoURL = "https://www.youtube.com/watch?v=HOdbzGCI5ME");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.disconnectSite(siteList = "Site Name");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "site-name");

		BlogsNavigator.gotoAddEntry();

		BlogsEntry.addTitle(entryTitle = "Blogs Entry Title");

		ItemSelector.gotoItemSelectorViaAlloyEditor(video = "true");

		ItemSelector.configureFilterViaSelectFile(
			filterBy = "Everywhere",
			navTab = "Documents and Media");

		AssertElementNotPresent(
			key_card = "Test YouTube Video",
			locator1 = "Card#CARD_TITLE");
	}

	@description = "This ensures that a depot video shortcut can be used through Related Assets on a connected site."
	@priority = "3"
	test ViewInRelatedAssets {
		property test.name.skip.portal.instance = "DepotVideoShortcut#ViewInRelatedAssets";

		Navigator.gotoSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Asset Publisher");

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		VideoShortcut.addCP(
			videoShortcutTitle = "Test YouTube Video",
			videoURL = "https://www.youtube.com/watch?v=HOdbzGCI5ME");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		RelatedAssets.selectAsset(
			assetTitle = "Test YouTube Video",
			assetType = "External Video Shortcut",
			depotName = "Test Depot Name");

		PortletEntry.publish();

		Navigator.openSiteURL(siteName = "Site Name");

		AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Related Assets");

		Navigator.openSiteURL(siteName = "Site Name");

		AssetPublisherPortlet.viewPG(
			assetContent = "WC WebContent Content",
			assetTitle = "WC WebContent Title",
			relatedAssetTitle = "Test YouTube Video");

		Click(
			key_relatedAssetTitle = "Test YouTube Video",
			locator1 = "AssetRelatedAssets#ASSET_TITLE");

		Pause(locator1 = "3000");

		AssertVisible(
			key_videoShortcutPreview = "https://www.youtube.com",
			locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_PREVIEW");

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

		AssertElementPresent(locator1 = "Button#YOUTUBE_PLAY");
	}

}