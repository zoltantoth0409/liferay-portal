@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();

			Depot.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This ensures that the documents with document type can be selected through Collections in a depot."
	@priority = "5"
	test AddDocTypeDocumentsManually {
		for (var i : list "1,2") {
			DepotNavigator.openDepotAdmin();

			DepotNavigator.gotoDepotPortlet(
				depotName = "Test Depot Name",
				portlet = "Documents and Media");

			DMNavigator.gotoDocumentType();

			DMDocumentType.add(
				dmDocumentTypeDescription = "DM Document Type Description",
				dmDocumentTypeFieldNames = "Text",
				dmDocumentTypeName = "DM Document Type Name ${i}");
		}

		for (var i : list "1,2") {
			DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

			DMDocument.addCP(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "DM Document Title ${i}",
				dmDocumentTypeName = "DM Document Type Name 2");
		}

		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Manual Asset List",
			assetType = "Web Content Folder,Basic Document,Basic Web Content,Documents Folder,DM Document Type Name 1,DM Document Type Name 2",
			itemType = "All Types",
			manualSelection = "true");

		for (var i : list "1,2") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "DM Document Title ${i}",
				assetType = "DM Document Type Name 2",
				itemSubtype = "DM Document Type Name 2",
				itemType = "Document");
		}

		for (var documentName : list "DM Document Title 1,DM Document Title 2") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "${documentName}",
				assetType = "Document");
		}
	}

	@description = "This ensures that the scope can be changed through dynamic selection in a depot."
	@priority = "5"
	test ChangeScope {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC Depot Title");

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name",
			title = "WC Site Title");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.addDynamicSelection(assetListTitle = "Depot Dynamic Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name",
			selectedAsset = "All Types");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.removeScope(
			assetListTitle = "Depot Dynamic Asset List",
			scopeName = "Current Asset Library");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Site Title",
			assetType = "Web Content Article");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Depot Title",
			assetType = "Web Content Article");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			defaultSite = "Current Asset Library (Test Depot Name)");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.removeScope(
			assetListTitle = "Depot Dynamic Asset List",
			scopeName = "Test Site Name");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Depot Title",
			assetType = "Web Content Article");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Site Title",
			assetType = "Web Content Article");
	}

	@description = "This ensures that collections from a site can select the basic web content stored in a depot."
	@priority = "5"
	test CreateSetOnSiteUsingDepotWC {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "site-name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Site Manual Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "site-name");

		AssetListsAdmin.selectAsset(
			assetListName = "Site Manual Asset List",
			assetName = "WC WebContent Title",
			assetType = "Basic Web Content",
			depotName = "Test Depot Name",
			itemSubtype = "Basic Web Content",
			itemType = "Web Content Article");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssetPublisherPortlet.selectAssetList(assetListName = "Site Manual Asset List");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssertTextEquals(
			key_assetTitle = "WC WebContent Title",
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "WC WebContent Title");
	}

	@description = "This ensures that the asset types will not be displayed when disabling the applications."
	@priority = "5"
	test DisableAllAssetTypes {
		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Documents and Media,Web Content",
			status = "Disabled");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			allAssetsDisabled = "ture",
			assetListName = "Depot Manual Asset List",
			manualSelection = "true");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			allAssetsDisabled = "true",
			assetListName = "Depot Dynamic Asset List",
			dynamicSelection = "true");
	}

	@description = "This ensures that user can not select the collection in depot after disabling Collections."
	@priority = "5"
	test DisableCollections {
		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		JSONGroup.addGroup(groupName = "Site Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Collections",
			status = "Disabled");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		Portlet.gotoPortletOptions(
			portletName = "Asset Publisher",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Navigator.gotoNavTab(navTab = "Asset Selection");

		Panel.expandPanel(panel = "Asset Selection");

		Click(locator1 = "APConfiguration#ASSET_SELECTION_ASSET_LIST");

		Panel.expandPanel(panel = "Select Collection");

		Button.clickSelect();

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#ASSET_LIST_DIALOG");

		AssertClick(
			key_breadcrumbName = "Sites and Libraries",
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
			value1 = "Sites and Libraries");

		AssertClick(
			key_groupTab = "Asset Library",
			locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
			value1 = "Asset Library");

		Click(
			key_card = "Test Depot Name",
			locator1 = "Card#CARD_TITLE");

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "Info:This application is not supported for Asset Libraries. Please, go back to scope selection.");
	}

	@description = "This ensures that only the asset types related to web content will be displayed after disabling DM."
	@priority = "5"
	test DisableDM {
		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Documents and Media",
			status = "Disabled");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Manual Asset List",
			assetType = "Web Content Folder,Basic Web Content,WC Structure Name",
			itemType = "All Types",
			manualSelection = "true");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Dynamic Asset List",
			assetType = "Web Content Folder,Web Content Article",
			dynamicSelection = "true");
	}

	@description = "This ensures that only the asset types related to DM will be displayed after disabling WC."
	@priority = "5"
	test DisableWC {
		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Text",
			dmDocumentTypeName = "DM Document Type Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Web Content",
			status = "Disabled");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Manual Asset List",
			assetType = "Basic Document,Documents Folder,DM Document Type Name",
			itemType = "All Types",
			manualSelection = "true");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Dynamic Asset List",
			assetType = "Document,Documents Folder",
			dynamicSelection = "true");
	}

	@description = "This ensures that the image is displayed by AP through Collections in a depot."
	@priority = "5"
	test DisplayImageInAP {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document Title",
			groupName = "Test Depot Name",
			mimeType = "image/jpeg",
			sourceFileName = "Document_2.jpeg");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.addManualSelection(
			assetListTitle = "Depot Manual Asset List",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.selectAsset(
			assetListName = "Depot Manual Asset List",
			assetName = "Document Title",
			assetType = "Basic Document",
			itemSubtype = "Basic Document",
			itemType = "Document");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssetPublisherPortlet.selectAssetList(
			assetListName = "Depot Manual Asset List",
			depotName = "Test Depot Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssertTextEquals(
			key_assetTitle = "Document Title",
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "Document Title");
	}

	@description = "This ensures that tagged the web content is displayed by AP through dynamic selection in a depot."
	@priority = "5"
	test DisplayWebContentWithTagInAP {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document Title",
			groupName = "Test Depot Name",
			mimeType = "application/msword",
			sourceFileName = "Document_2.doc");

		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithTagCP(
			tagNameList = "tag1",
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.addDynamicSelection(
			assetListTitle = "Depot Dynamic Asset List",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Dynamic Asset List",
			type = "Dynamic");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");

		AssetListsAdmin.addTagsFilter(tagNameList = "tag1");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssetPublisherPortlet.selectAssetList(
			assetListName = "Depot Dynamic Asset List",
			depotName = "Test Depot Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssertTextEquals(
			key_assetTitle = "Web Content Title",
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "Web Content Title");
	}

	@description = "This ensures that images are excluded with the dynamic selection that does not contain the keyword "Image" in a depot."
	@priority = "5"
	test ExcludeImagesDynamically {
		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		for (var i : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document Text Title ${i}",
				groupName = "Test Depot Name",
				mimeType = "application/msword",
				sourceFileName = "Document_${i}.doc");
		}

		for (var i : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document Image Title ${i}",
				groupName = "Test Depot Name",
				mimeType = "image/png",
				sourceFileName = "Document_${i}.png");
		}

		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.gotoEntry(assetListName = "Depot Dynamic Asset List");

		AssetListsAdmin.addKeywordsFilter(
			keyWords = "Image",
			queryAndOperator = "Any",
			queryContains = "Does Not Contain");

		for (var i : list "1,2") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "Document Text Title ${i}",
				assetType = "Document");
		}
	}

	@description = "This ensures that the asset can be removed in Collections."
	@priority = "4"
	test RemoveAssetFromContentSet {
		for (var i : list "1,2,3") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Test Depot Name",
				site = "false",
				title = "WC WebContent Title ${i}");
		}

		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "WC WebContent Title ${i}",
				assetType = "Basic Web Content",
				itemSubtype = "Basic Web Content",
				itemType = "Web Content Article");
		}

		AssetListsAdmin.deleteManualSelectionAssets(assetsList = "WC WebContent Title 2");

		for (var i : list "1,3") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Manual Asset List",
			assetTitle = "WC WebContent Title 2",
			assetType = "Web Content Article");
	}

	@description = "This ensures that only web contents configured to show with dynamic selection are displayed in a depot."
	@priority = "4"
	test SelectAllWebContentDynamically {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document Title",
			groupName = "Test Depot Name",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		for (var i : list "1,2,3") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Test Depot Name",
				site = "false",
				title = "WC WebContent Title ${i}");
		}

		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Dynamic Asset List",
			assetType = "Web Content Folder,Web Content Article,Document,Documents Folder",
			dynamicSelection = "true");

		AssetListsAdmin.configureAssetEntryType(selectedAsset = "All Types");

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "Document Title",
			assetType = "Document");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "Document Title",
			assetType = "Document");
	}

	@description = "This ensures that an image can be selected from other site scope through dynamic selection that does not contain the keyword."
	@priority = "5"
	test SelectDocumentFromOtherScopeDynamically {
		JSONGroup.addGroup(groupName = "Test Site Name");

		for (var i : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${i}.doc",
				groupName = "Test Site Name",
				mimeType = "application/msword",
				sourceFileName = "Document_${i}.doc");
		}

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Image Title",
			groupName = "Test Depot Name",
			mimeType = "image/jpeg",
			sourceFileName = "Document_2.jpeg");

		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name",
			selectedAsset = "All Types");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.addKeywordsFilter(
			keyWords = "doc",
			queryAndOperator = "Any",
			queryContains = "Does Not Contain");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "DM Image Title",
			assetType = "Document");

		for (var i : list "1,2") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewNoContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "Document_${i}.doc",
				assetType = "Document");
		}
	}

	@description = "This ensures that multiple documents can be selected through Collections in a depot."
	@priority = "5"
	test SelectMultipleDocumentsManually {
		for (var documentName : list "Document_1.doc,Document_2.jpg,Document_3.txt") {
			var extension = StringUtil.extractLast("${documentName}", ".");

			if ("${extension}" == "doc") {
				var mimeType = "application/msword";
			}
			else if ("${extension}" == "jpg") {
				var mimeType = "image/jpg";
			}
			else if ("${extension}" == "txt") {
				var mimeType = "text/plain";
			}

			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "${documentName}",
				groupName = "Test Depot Name",
				mimeType = "${mimeType}",
				sourceFileName = "${documentName}");
		}

		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		for (var documentName : list "Document_1.doc,Document_2.jpg,Document_3.txt") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "${documentName}",
				assetType = "Basic Document",
				itemSubtype = "Basic Document",
				itemType = "Document");
		}

		for (var documentName : list "Document_1.doc,Document_2.jpg,Document_3.txt") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "${documentName}",
				assetType = "Document");
		}
	}

	@description = "This ensures that multiple web content can be selected through Collections in a depot."
	@priority = "5"
	test SelectMultipleWebContentManually {
		for (var i : list "1,2,3") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Test Depot Name",
				site = "false",
				title = "WC WebContent Title ${i}");
		}

		JSONAssetlist.addManualAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Manual Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "WC WebContent Title ${i}",
				assetType = "Basic Web Content",
				itemSubtype = "Basic Web Content",
				itemType = "Web Content Article");
		}

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that web contents from all the scopes can be selected through dynamic selection in a depot."
	@priority = "4"
	test SelectWCFromAllScopesDynamically {
		for (var i : list "1,2") {
			JSONGroup.addGroup(groupName = "Test Site Name ${i}");
		}

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title 1");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name 1",
			title = "WC WebContent Title 2");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name 2",
			title = "WC WebContent Title 3");

		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name 1",
			selectedAsset = "All Types");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name 2");

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that a web content can be selected from other site scope through dynamic selection that does not contain the keyword."
	@priority = "5"
	test SelectWCFromOtherScopeDynamically {
		JSONGroup.addGroup(groupName = "Test Site Name");

		for (var i : list "1 2,2 3,3 4") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Test Site Name",
				title = "WC WebContent Title ${i}");
		}

		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Depot Name",
			site = "false",
			title = "Depot Dynamic Asset List");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name",
			selectedAsset = "All Types");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.addKeywordsFilter(
			keyWords = "3",
			queryAndOperator = "Any",
			queryContains = "Does Not Contain");

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC WebContent Title 1 2",
			assetType = "Web Content Article");

		for (var i : list "2 3,3 4") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewNoContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that the web content created by the structure can be selected via searching for the corresponding structure name."
	@priority = "5"
	test SelectWebContentWithStructure {
		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		for (var i : list "1,2") {
			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

			WebContent.addWithStructureCP(
				webContentText = "WC WebContent Text ${i}",
				webContentTitle = "WC WebContent Title ${i}");

			PortletEntry.publish();
		}

		DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Depot Manual Asset List");

		for (var i : list "1,2") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "WC WebContent Title ${i}",
				assetType = "WC Structure Name",
				itemSubtype = "WC Structure Name",
				itemType = "Web Content Article");
		}

		for (var i : list "1,2") {
			DepotNavigator.openDepotAssetListsAdmin(depotName = "Test Depot Name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that Collections can be set in a depot."
	@priority = "5"
	test Smoke {
		property portal.acceptance = "true";
		property testray.component.names = "Asset Lists";

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Collections");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_TAGLIB_TITLE",
			value1 = "No Collections yet.");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_TAGLIB_DESCRIPTION",
			value1 = "Fortunately, it is very easy to add new ones.");

		LexiconEntry.gotoAdd();

		MenuItem.viewPresent(menuItem = "Manual Collection");

		MenuItem.viewPresent(menuItem = "Dynamic Collection");

		Refresh();

		Click(locator1 = "Message#EMPTY_INFO_TAGLIB");

		Click.waitForMenuToggleJSClick(locator1 = "Dropdown#EMPTY_MESSAGE_DROPDOWN");

		AssertElementPresent(
			key_menuItem = "Manual Collection",
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM");

		AssertElementPresent(
			key_menuItem = "Dynamic Collection",
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM");
	}

}