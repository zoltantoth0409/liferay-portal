import aQute.bnd.osgi.Constants
import aQute.bnd.osgi.Jar
import aQute.bnd.osgi.Resource

import java.util.jar.Manifest

task jarManifest

dependencies {
	compileInclude group: "com.fasterxml", name: "aalto-xml", version: "0.9.10"
	compileInclude group: "com.fasterxml", name: "classmate", version: "1.4.0"
	compileInclude group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-xml", version: "2.9.8"
	compileInclude group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-yaml", version: "2.9.8"
	compileInclude group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jdk8", version: "2.9.8"
	compileInclude group: "com.fasterxml.jackson.jaxrs", name: "jackson-jaxrs-xml-provider", version: "2.9.8"
	compileInclude group: "com.google.guava", name: "guava", version: "20.0"
	compileInclude group: "com.graphql-java", name: "graphql-java", version: "8.0"
	compileInclude group: "com.graphql-java", name: "graphql-java-servlet", version: "6.1.3"
	compileInclude group: "com.graphql-java", name: "java-dataloader", version: "2.1.1"
	compileInclude group: "commons-codec", name: "commons-codec", version: "1.10"
	compileInclude group: "io.swagger.core.v3", name: "swagger-core", version: "2.0.6"
	compileInclude group: "io.swagger.core.v3", name: "swagger-integration", version: "2.0.6"
	compileInclude group: "io.swagger.core.v3", name: "swagger-jaxrs2", version: "2.0.6"
	compileInclude group: "io.swagger.core.v3", name: "swagger-models", version: "2.0.6"
	compileInclude group: "org.antlr", name: "antlr4-runtime", version: "4.7.1"
	compileInclude group: "org.apache.commons", name: "commons-lang3", version: "3.8"
	compileInclude group: "org.hibernate.validator", name: "hibernate-validator", version: "6.0.15.Final"
	compileInclude group: "org.javassist", name: "javassist", version: "3.23.1-GA"
	compileInclude group: "org.reactivestreams", name: "reactive-streams", version: "1.0.2"
	compileInclude group: "org.reflections", name: "reflections", version: "0.9.11"
	compileInclude group: "org.yaml", name: "snakeyaml", version: "1.23"

	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.9.8"
	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.9.8"
	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.8"
	compileOnly group: "com.fasterxml.jackson.jaxrs", name: "jackson-jaxrs-json-provider", version: "2.9.8"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "commons-fileupload", name: "commons-fileupload", version: "1.3.2"
	compileOnly group: "io.github.graphql-java", name: "graphql-java-annotations", version: "5.4"
	compileOnly group: "io.swagger.core.v3", name: "swagger-annotations", version: "2.0.5"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "javax.validation", name: "validation-api", version: "2.0.1.Final"
	compileOnly group: "javax.ws.rs", name: "javax.ws.rs-api", version: "2.1"
	compileOnly group: "org.apache.cxf", name: "cxf-core", version: "3.2.5"
	compileOnly group: "org.apache.cxf", name: "cxf-rt-frontend-jaxrs", version: "3.2.5"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.jaxrs", version: "1.0.0"
	compileOnly project(":apps:portal-odata:portal-odata-api")
	compileOnly project(":apps:portal-vulcan:portal-vulcan-api")
	compileOnly project(":apps:static:osgi:osgi-util")
	compileOnly project(":core:osgi-service-tracker-collections")
	compileOnly project(":core:petra:petra-string")

	testCompile group: "org.hamcrest", name: "hamcrest-all", version: "1.3"
}

deployDependencies {
	from configurations.compileOnly

	include "swagger-annotations-*.jar"
	include "validation-api-*.jar"
}

jar {
	finalizedBy jarManifest
}

jarManifest {
	doLast {
		String serviceComponent = _getServiceComponent(jar.archivePath)

		ant.jar(destfile: jar.archivePath, update: true) {
			delegate.manifest {
				attribute(name: Constants.SERVICE_COMPONENT, value: serviceComponent)
			}
		}
	}
}

private String _getServiceComponent(File jarFile) {
	Jar jar = new Jar(jarFile)

	jar.withCloseable {
		Resource resource = jar.getResource("META-INF/MANIFEST.MF")

		Manifest manifest = new Manifest(resource.openInputStream())

		List<String> serviceComponents = []

		manifest.getMainAttributes().getValue(Constants.SERVICE_COMPONENT).split(",").each {
			if (!it.startsWith("OSGI-INF/graphql.")) {
				serviceComponents.add(it)
			}
		}

		return serviceComponents.join(",")
	}
}