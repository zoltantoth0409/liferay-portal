import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildAlloyEditor(type: Copy)
task buildCreateReactClass(type: Copy)

File createReactClassDestinationDir = file("classes/META-INF/resources/js")
File editorDestinationDir = file("tmp/META-INF/resources")
File editorSrcDir = file("src/main/resources/META-INF/resources")

buildAlloyEditor {
	dependsOn npmInstall

	File alloyEditorDestinationDir = new File(editorDestinationDir, "alloyeditor")
	File alloyEditorSrcDir = new File(editorSrcDir, "_diffs")

	doFirst {
		delete alloyEditorDestinationDir
	}

	doLast {
		FileTree alloyEditorJsFileTree = fileTree(dir: alloyEditorDestinationDir, include : "alloy-editor*.js")
		FileTree buttonsJsFileTree = fileTree(dir: new File(alloyEditorSrcDir, "buttons"), include: "**/*.js")
		FileTree selectionsJsFileTree = fileTree(dir: new File(alloyEditorSrcDir, "selections"), include: "**/*.js")

		alloyEditorJsFileTree.each {
			File alloyEditorJsFile ->

			File liferayAlloyEditorJsFile = new File(alloyEditorDestinationDir, "liferay-" + alloyEditorJsFile.name)

			FileUtil.concatenate(liferayAlloyEditorJsFile, files(alloyEditorJsFile, buttonsJsFileTree, selectionsJsFileTree))
		}
	}

	duplicatesStrategy = "exclude"

	from {
		fileTree(dir: "node_modules")
	} {
		eachFile new StripPathSegmentsAction(3)
		include "alloyeditor/dist/alloy-editor/"
	}

	from alloyEditorSrcDir
	includeEmptyDirs = false
	into alloyEditorDestinationDir
}

buildCreateReactClass {
	dependsOn buildAlloyEditor

	filter {
		String line ->

		line.replace('exports.createReactClass=e(require("react")):t.createReactClass=e(t.React)}(this,', 'exports.createClass=e(require("react")):t.React.createClass=e(t.React)}(AlloyEditor,')
	}

	from npmInstall.nodeModulesDir
	include "create-react-class/create-react-class.min.js"
	includeEmptyDirs = false
	into createReactClassDestinationDir
}

classes {
	dependsOn buildAlloyEditor
	dependsOn buildCreateReactClass
}

clean {
	delete "tmp"
}

configJSModules {
	enabled = false
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.java", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "default"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "2.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly project(":apps:collaboration:item-selector:item-selector-api")
	compileOnly project(":apps:collaboration:item-selector:item-selector-criteria-api")
	compileOnly project(":apps:foundation:frontend-taglib:frontend-taglib")
	compileOnly project(":apps:foundation:petra:petra-encryptor")
	compileOnly project(":apps:web-experience:layout:layout-item-selector-api")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-string")
}

transpileJS {
	enabled = false
}