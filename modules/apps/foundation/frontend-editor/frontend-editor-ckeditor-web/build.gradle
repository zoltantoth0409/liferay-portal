import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction
import com.liferay.gradle.util.tasks.ConcatenateTask

configurations {
	ckEditor
}

task buildCKEditor(type: Copy)
task buildCKEditorBBCode(type: ConcatenateTask)
task buildCKEditorScayt(type: Copy)
task buildCKEditorWsc(type: Copy)

String ckEditorVersion = "4.6.0"
String ckEditorVersionLfr = "4.6.0.1"

String ckEditorScaytUrl = "http://download.ckeditor.com/scayt/releases/scayt_${ckEditorVersion}.zip"
String ckEditorWscUrl = "http://download.ckeditor.com/wsc/releases/wsc_${ckEditorVersion}.zip"

File editorDestinationDir = file("tmp/META-INF/resources")
File editorSrcDir = file("src/main/resources/META-INF/resources")

buildCKEditor {
	File ckEditorDestinationDir = new File(editorDestinationDir, "ckeditor")

	doFirst {
		delete ckEditorDestinationDir
	}

	duplicatesStrategy = "exclude"

	from {
		zipTree(configurations.ckEditor.singleFile)
	} {
		eachFile new StripPathSegmentsAction(5)

		exclude "META-INF/resources/webjars/ckeditor/${ckEditorVersionLfr}/ckeditor/"
		exclude "META-INF/resources/webjars/ckeditor/${ckEditorVersionLfr}/samples/"
		exclude "META-INF/resources/webjars/ckeditor/${ckEditorVersionLfr}/gulpfile.js"
	}

	from new File(editorSrcDir, "_diffs")
	includeEmptyDirs = false
	into ckEditorDestinationDir
}

buildCKEditorBBCode {
	File bbCodeDir = new File(editorSrcDir, "_diffs/plugins/bbcode")

	destinationFile = new File(bbCodeDir, "bbcode_parser.js")

	ext {
		autoClean = false
	}

	sourceFiles new File(bbCodeDir, "bbcode_utils.js")
	sourceFiles new File(bbCodeDir, "lexer.js")
	sourceFiles new File(bbCodeDir, "parser.js")
	sourceFiles new File(bbCodeDir, "converter.js")
}

buildCKEditorScayt {
	dependsOn buildCKEditor

	from {
		File ckEditorScaytFile = FileUtil.get(project, ckEditorScaytUrl)

		zipTree(ckEditorScaytFile)
	}

	into new File(editorDestinationDir, "ckeditor/plugins")
}

buildCKEditorWsc {
	dependsOn buildCKEditor

	from {
		File ckEditorWscFile = FileUtil.get(project, ckEditorWscUrl)

		zipTree(ckEditorWscFile)
	}

	into new File(editorDestinationDir, "ckeditor/plugins")
}

classes {
	dependsOn buildCKEditor, buildCKEditorScayt, buildCKEditorWsc
}

clean {
	delete "tmp"
}

dependencies {
	ckEditor group: "com.liferay.webjars", name: "com.liferay.webjars.ckeditor", version: ckEditorVersionLfr

	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "default"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "2.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly project(":apps:collaboration:message-boards:message-boards-api")
	compileOnly project(":apps:foundation:frontend-editor:frontend-editor-api")
	compileOnly project(":apps:foundation:frontend-editor:frontend-editor-taglib")
	compileOnly project(":apps:foundation:frontend-taglib:frontend-taglib")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-string")
}