import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction
import com.liferay.gradle.util.tasks.ConcatenateTask

task buildCKEditorBBCode(type: ConcatenateTask)
task buildCKEditor(type: Copy)

String ckEditorVersion = "4.6.2"

File editorSrcDir = file("src/main/resources/META-INF/resources")

buildCKEditor {
	File ckEditorDestinationDir = new File(sourceSets.main.output.resourcesDir, "META-INF/resources/ckeditor")

	doFirst {
		delete ckEditorDestinationDir
	}

	duplicatesStrategy = "exclude"

	from {
		zipTree(FileUtil.get(project, "https://github.com/ckeditor/ckeditor-releases/archive/full/${ckEditorVersion}.zip"))
	} {
		eachFile new StripPathSegmentsAction(1)

		exclude "ckeditor-releases-full-${ckEditorVersion}/*.json"
		exclude "ckeditor-releases-full-${ckEditorVersion}/samples/"
	}

	from new File(editorSrcDir, "_diffs")
	includeEmptyDirs = false
	into ckEditorDestinationDir
}

buildCKEditorBBCode {
	File bbCodeDir = new File(editorSrcDir, "_diffs/plugins/bbcode")

	destinationFile = new File(bbCodeDir, "bbcode_parser.js")

	ext {
		autoClean = false
	}

	sourceFiles new File(bbCodeDir, "bbcode_utils.js")
	sourceFiles new File(bbCodeDir, "lexer.js")
	sourceFiles new File(bbCodeDir, "parser.js")
	sourceFiles new File(bbCodeDir, "converter.js")
}

classes {
	dependsOn buildCKEditor
}

dependencies {
	provided group: "com.liferay", name: "com.liferay.frontend.taglib", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	provided group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "2.0.0"
	provided group: "javax.portlet", name: "portlet-api", version: "2.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
}