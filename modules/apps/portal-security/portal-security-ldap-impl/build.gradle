import com.liferay.gradle.util.FileUtil

import org.apache.tools.ant.filters.FixCrLfFilter

apply plugin: "antlr"

String parserDirName = "com/liferay/portal/security/ldap/internal/validator/parser"

cleanGenerateGrammarSource {
	delete "src/main/java/" + parserDirName
	delete "src/main/resources/" + parserDirName
}

compileJava {
	dependsOn.remove "generateGrammarSource"
}

dependencies {
	antlr group: "org.antlr", name: "antlr", version: "3.0.1"

	compileInclude group: "antlr", name: "antlr", version: "2.7.7"
	compileInclude group: "org.antlr", name: "antlr", version: "3.0.1"
	compileInclude group: "org.antlr", name: "stringtemplate", version: "3.0"

	compileOnly group: "com.liferay", name: "com.liferay.petra.lang", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.configuration.metatype.api", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.configuration.persistence.api", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.instance.lifecycle.api", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.security.exportimport.api", version: "4.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.security.ldap.api", version: "2.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "4.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "4.34.0"
	compileOnly group: "commons-lang", name: "commons-lang", version: "2.6"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.1"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.cm", version: "1.5.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
}

generateGrammarSource {
	File grammarFile = file("src/main/antlr/${parserDirName}/LDAPFilter.g")

	deleteAllActions()

	dependsOn cleanGenerateGrammarSource

	doFirst {
		javaexec {
			args "-o", FileUtil.relativize(generateGrammarSource.outputDirectory, grammarFile.parentFile)
			args grammarFile.name

			classpath = generateGrammarSource.antlrClasspath
			main = "org.antlr.Tool"
			maxHeapSize = generateGrammarSource.maxHeapSize
			workingDir = grammarFile.parentFile
		}
	}

	doLast {
		copy {
			eachFile {
				if (name in ["LDAPFilterLexer.java", "LDAPFilterParser.java"]) {
					String javaClass = name[0 .. -6]

					filter {
						String line ->

						line.replace "public class ${javaClass}", """\
@SuppressWarnings(\"all\")
public class ${javaClass}"""
					}
				}
			}

			filter(FixCrLfFilter, javafiles: true, tab: FixCrLfFilter.AddAsisRemove.newInstance("add"))

			from outputDirectory
			include "**/*.java"
			into "src/main/java/" + parserDirName
		}

		copy {
			from outputDirectory
			exclude "**/*.java"
			into "src/main/resources/" + parserDirName
		}
	}

	ext {
		autoClean = false
	}

	maxHeapSize = "1512m"
	outputDirectory = new File(temporaryDir, "generated")
}

liferayOSGi {
	expandCompileInclude = true
}