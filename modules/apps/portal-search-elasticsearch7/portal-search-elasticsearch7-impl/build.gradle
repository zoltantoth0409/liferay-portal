import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.StringUtil

task cleanSidecar
task deploySidecar(type: Copy)

String elasticsearchVersion = "7.9.0"
String luceneVersion = "8.6.0"
String sidecarElasticsearchVersion = file('src/main/resources/META-INF/sidecar.version').text

String elasticsearchDownloadURL = "https://artifacts.elastic.co/downloads/elasticsearch/elasticsearch-oss-${sidecarElasticsearchVersion}-no-jdk-linux-x86_64.tar.gz"

File elasticsearchDistributionFile = file("${buildDir}/${elasticsearchDownloadURL.drop(elasticsearchDownloadURL.lastIndexOf('/') + 1)}")

File sidecarDeployDir = file("${project.'liferay.home'}/elasticsearch" + sidecarElasticsearchVersion.substring(0, 1))

List<String> sidecarPluginURLs = [
	"https://artifacts.elastic.co/downloads/elasticsearch-plugins/analysis-icu/analysis-icu-${sidecarElasticsearchVersion}.zip",
	"https://artifacts.elastic.co/downloads/elasticsearch-plugins/analysis-kuromoji/analysis-kuromoji-${sidecarElasticsearchVersion}.zip",
	"https://artifacts.elastic.co/downloads/elasticsearch-plugins/analysis-smartcn/analysis-smartcn-${sidecarElasticsearchVersion}.zip",
	"https://artifacts.elastic.co/downloads/elasticsearch-plugins/analysis-stempel/analysis-stempel-${sidecarElasticsearchVersion}.zip"
]

cleanSidecar {
	doLast {
		delete file("${buildDir}/elasticsearch-" + sidecarElasticsearchVersion)
	}
}

dependencies {
	compileInclude group: "com.carrotsearch", name: "hppc", version: "0.8.1"
	compileInclude group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-cbor", version: "2.10.4"
	compileInclude group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-smile", version: "2.10.4"
	compileInclude group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-yaml", version: "2.10.4"
	compileInclude group: "com.google.code.gson", name: "gson", version: "2.8.4"
	compileInclude group: "commons-codec", name: "commons-codec", version: "1.11"
	compileInclude group: "commons-io", name: "commons-io", version: "2.6"
	compileInclude group: "commons-lang", name: "commons-lang", version: "2.6"
	compileInclude group: "commons-logging", name: "commons-logging", version: "1.2"
	compileInclude group: "joda-time", name: "joda-time", version: "2.10.4"
	compileInclude group: "log4j", name: "log4j", version: "1.2.17"
	compileInclude group: "org.apache.commons", name: "commons-compress", version: "1.19"
	compileInclude group: "org.apache.httpcomponents", name: "httpasyncclient", version: "4.1.4"
	compileInclude group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.10"
	compileInclude group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.12"
	compileInclude group: "org.apache.httpcomponents", name: "httpcore-nio", version: "4.4.12"
	compileInclude group: "org.apache.logging.log4j", name: "log4j-api", version: "2.13.3"
	compileInclude group: "org.apache.logging.log4j", name: "log4j-core", version: "2.13.3"
	compileInclude group: "org.apache.lucene", name: "lucene-core", version: luceneVersion
	compileInclude group: "org.apache.lucene", name: "lucene-highlighter", version: luceneVersion
	compileInclude group: "org.apache.lucene", name: "lucene-join", version: luceneVersion
	compileInclude group: "org.apache.lucene", name: "lucene-queries", version: luceneVersion
	compileInclude group: "org.apache.lucene", name: "lucene-queryparser", version: luceneVersion
	compileInclude group: "org.apache.lucene", name: "lucene-sandbox", version: luceneVersion
	compileInclude group: "org.apache.lucene", name: "lucene-suggest", version: luceneVersion
	compileInclude group: "org.elasticsearch", name: "elasticsearch", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch", name: "elasticsearch-cli", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch", name: "elasticsearch-core", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch", name: "elasticsearch-geo", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch", name: "elasticsearch-x-content", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch.client", name: "elasticsearch-rest-client", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch.client", name: "elasticsearch-rest-high-level-client", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch.plugin", name: "lang-mustache-client", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch.plugin", name: "parent-join-client", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch.plugin", name: "percolator-client", version: elasticsearchVersion
	compileInclude group: "org.elasticsearch.plugin", name: "rank-eval-client", version: elasticsearchVersion
	compileInclude group: "org.locationtech.jts", name: "jts-core", version: "1.15.0"
	compileInclude group: "org.locationtech.spatial4j", name: "spatial4j", version: "0.7"
	compileInclude group: "org.ow2.asm", name: "asm-debug-all", version: "5.1"
	compileInclude group: "org.yaml", name: "snakeyaml", version: "1.26"

	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.10.3"
	compileOnly group: "com.ibm.icu", name: "icu4j", version: "61.1"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "commons-collections", name: "commons-collections", version: "3.2.2"
	compileOnly group: "org.codehaus.woodstox", name: "stax2-api", version: "3.1.4"
	compileOnly group: "org.codehaus.woodstox", name: "woodstox-core-asl", version: "4.4.1"
	compileOnly group: "org.osgi", name: "org.osgi.annotation.versioning", version: "1.1.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.cm", version: "1.5.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component", version: "1.3.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly project(":apps:configuration-admin:configuration-admin-api")
	compileOnly project(":apps:portal-search-elasticsearch7:portal-search-elasticsearch7-api")
	compileOnly project(":apps:portal-search-elasticsearch7:portal-search-elasticsearch7-spi")
	compileOnly project(":apps:portal-search:portal-search-api")
	compileOnly project(":apps:portal-search:portal-search-engine-adapter-api")
	compileOnly project(":apps:portal-search:portal-search-spi")
	compileOnly project(":apps:static:osgi:osgi-util")
	compileOnly project(":apps:static:portal-configuration:portal-configuration-metatype-api")
	compileOnly project(":apps:static:portal-configuration:portal-configuration-persistence-api")
	compileOnly project(":apps:static:portal:portal-upgrade-api")
	compileOnly project(":core:petra:petra-concurrent")
	compileOnly project(":core:petra:petra-function")
	compileOnly project(":core:petra:petra-io")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-memory")
	compileOnly project(":core:petra:petra-nio")
	compileOnly project(":core:petra:petra-process")
	compileOnly project(":core:petra:petra-reflect")
	compileOnly project(":core:petra:petra-string")

	testCompile group: "biz.aQute.bnd", name: "biz.aQute.bnd.annotation", version: "4.2.0"
	testCompile group: "com.liferay.portal", name: "com.liferay.util.java", version: "default"
	testCompile group: "commons-configuration", name: "commons-configuration", version: "1.10"
	testCompile group: "javax.portlet", name: "portlet-api", version: "3.0.1"
	testCompile group: "log4j", name: "log4j", version: "1.2.17"
	testCompile group: "net.java.dev.jna", name: "jna", version: "4.1.0"
	testCompile group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	testCompile group: "org.apache.tika", name: "tika-core", version: "1.24.1"
	testCompile group: "org.jabsorb", name: "jabsorb", version: "1.3.1"
	testCompile group: "org.jodd", name: "jodd-bean", version: "3.6.4"
	testCompile group: "org.jodd", name: "jodd-json", version: "3.6.4"
	testCompile group: "org.slf4j", name: "slf4j-api", version: "1.7.26"
	testCompile project(":apps:portal-search:portal-search")
	testCompile project(":apps:portal-search:portal-search-test-util")
	testCompile project(":core:osgi-service-tracker-collections")
	testCompile project(":core:petra:petra-sql-dsl-api")
	testCompile project(":core:registry-api")
}

deploy {
	finalizedBy deploySidecar
}

deployDependencies {
	from configurations.compileOnly

	include "stax2-api-*.jar"
	include "woodstox-core-asl-*.jar"

	rename(/stax2-api-(.+)\.jar/, "org.codehaus.stax2" + renameSuffix)
	rename(/woodstox-core-asl-(.+)\.jar/, "com.ctc.wstx" + renameSuffix)
}

deploySidecar {
	dependsOn cleanSidecar

	description = "Assembles the Elasticsearch sidecar and deploys it to Liferay."
	group = "build"

	destinationDir = sidecarDeployDir

	enabled = !destinationDir.exists()

	ext {
		autoClean = false
	}

	from {
		file("${buildDir}/elasticsearch-" + sidecarElasticsearchVersion)
	}

	doFirst {
		FileUtil.get(project, elasticsearchDownloadURL, elasticsearchDistributionFile, false, true)

		project.copy {
			fileMode = 0777
			includeEmptyDirs = false

			exclude "elasticsearch-${sidecarElasticsearchVersion}/modules/ingest-geoip/**"
			include "elasticsearch-${sidecarElasticsearchVersion}/lib/**"
			include "elasticsearch-${sidecarElasticsearchVersion}/modules/**"

			into buildDir
			from tarTree(elasticsearchDistributionFile)
		}
	}

	doLast {
		logger.lifecycle "Deployed Elasticsearch Sidecar: {}", destinationDir
	}
}

test {
	dependsOn {
		configurations.compileOnly

		deploySidecar {
			destinationDir = file("tmp/sidecar-elasticsearch")
			enabled = true
		}
	}

	forkEvery = 0

	doFirst {
		File libProcessExecutorDir = file("tmp/lib-process-executor")

		if (!libProcessExecutorDir.exists()) {
			libProcessExecutorDir.mkdir()
		}

		project.copy {
			include "com.liferay.petra.*.jar"

			configurations.compileOnly.allDependencies.each {
				rename "-${it.version}", ""
			}

			into libProcessExecutorDir.getPath()
			from configurations.compileOnly
		}
	}
}

_createPluginTasks(sidecarElasticsearchVersion, sidecarPluginURLs)

private void _createPluginTasks(String sidecarElasticsearchVersion, List<String> sidecarPluginURLs) {
	sidecarPluginURLs.each {
		String url ->

		String fileName = url.substring(url.lastIndexOf("/") + 1)

		String suffix = fileName.replaceAll(/[\.-](\w)/) {
			return it[1].toUpperCase()
		}

		Task installPluginTask = tasks.create(name: "installPlugin" + StringUtil.capitalize(suffix)) {
			doLast {
				FileUtil.get(project, url, new File(project.buildDir, fileName), false, true)

				String dirName = fileName.take(fileName.lastIndexOf("-" + sidecarElasticsearchVersion))

				project.copy {
					fileMode = 0777
					includeEmptyDirs = false

					into new File(buildDir, "elasticsearch-${sidecarElasticsearchVersion}/plugins/${dirName}")
					from zipTree(new File(project.buildDir, fileName))
				}
			}
		}

		deploySidecar.dependsOn installPluginTask

		installPluginTask.mustRunAfter cleanSidecar
	}
}