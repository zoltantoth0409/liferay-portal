import com.liferay.gradle.util.copy.StripPathSegmentsAction

task copyJAMWiki(type: Copy)

String jamWikiVersion = "1.0.7"

copyJAMWiki {
	eachFile new StripPathSegmentsAction(5)
	from zipTree("jamwiki-${jamWikiVersion}-src.zip")

	include "jamwiki-${jamWikiVersion}/jamwiki-war/src/main/resources/ApplicationResources*.properties"
	include "jamwiki-${jamWikiVersion}/jamwiki-war/src/main/resources/jamwiki-configuration.dtd"
	include "jamwiki-${jamWikiVersion}/jamwiki-war/src/main/resources/jamwiki-configuration.xml"

	includeEmptyDirs = false
	into sourceSets.main.output.resourcesDir
}

dependencies {
	compileInclude group: "com.liferay", name: "org.jamwiki", version: jamWikiVersion

	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "default"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "2.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly project(":apps:collaboration:wiki:wiki-api")
	compileOnly project(":core:petra:petra-function")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-string")
}

processResources {
	dependsOn copyJAMWiki
}