@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Friendly URL";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Source Page");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "This ensures that a user is redirected to the updated friendlyURL when he accesses the former friendlyURL."
	@priority = "5"
	test RedirectToUpdatedFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Source Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/en/web/test-site-name/new-test-page");

		AssertVisible(
			key_pageName = "Test Source Page",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This ensures that a user is redirected to the updated localized friendlyURL when he accesses the former localized friendlyURL."
	@priority = "5"
	test RedirectToUpdatedLocalizedFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Source Page");

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "es_ES",
			localizedFriendlyURL = "/pagina-de-prueba");

		PortletEntry.inputName(name = "Pagina de prueba");

		Button.clickSave();

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "es_ES",
			localizedFriendlyURL = "/nueva-pagina-de-prueba");

		Button.clickSave();

		Navigator.openSpecificURL(url = "${portalURL}/es/web/test-site-name/pagina-de-prueba");

		AssertLocation(value1 = "${portalURL}/es/web/test-site-name/nueva-pagina-de-prueba");

		AssertVisible(
			key_pageName = "Pagina de prueba",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This ensures that the old friendlyURL can be removed from the history."
	@priority = "5"
	test RemoveOldFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Page.gotoFriendlyURLHistory();

		Page.removeOldFriendlyURL(oldFriendlyURL = "/test-page");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page");

		AssertTextPresent(value1 = "The requested resource could not be found.");
	}

	@description = "This ensures that the old friendlyURL can be restored from the history."
	@priority = "5"
	test RestoreOldFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Page.gotoFriendlyURLHistory();

		Page.restoreOldFriendlyURL(
			activeURL = "/new-test-page",
			friendlyURLEdit = "/test-page",
			oldFriendlyURL = "/test-page",
			restoredURL = "/test-page");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page");

		AssertLocation(value1 = "${portalURL}/web/test-site-name/test-page");
	}

	@description = "This ensures that accessing an old friendlyURL in the default language after publishing the new friendly URL will redirect the user to the new URL."
	@priority = "5"
	test StagingFriendlyURLRedirect {
		var portalURL = PropsUtil.get("portal.url");

		Staging.openStagingAdmin(siteURLKey = "test-site-name");

		Staging.activateStagingCP(siteName = "Test Site Name");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Source Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Navigator.gotoStagedSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/en/web/test-site-name/new-test-page");

		AssertVisible(
			key_pageName = "Test Source Page",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This ensures that accessing an old localized friendlyURL after publishing the new friendly URL will redirect the user to the new URL."
	@priority = "5"
	test StagingLocalizedFriendlyURLRedirect {
		var portalURL = PropsUtil.get("portal.url");

		Staging.openStagingAdmin(siteURLKey = "test-site-name");

		Staging.activateStagingCP(siteName = "Test Site Name");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Source Page");

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "es_ES",
			localizedFriendlyURL = "/pagina-de-prueba");

		PortletEntry.inputName(name = "Pagina de prueba");

		Button.clickSave();

		Navigator.gotoStagedSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Source Page");

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "es_ES",
			localizedFriendlyURL = "/nueva-pagina-de-prueba");

		Button.clickSave();

		Navigator.gotoStagedSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		Navigator.openSpecificURL(url = "${portalURL}/es/web/test-site-name/pagina-de-prueba");

		AssertLocation(value1 = "${portalURL}/es/web/test-site-name/nueva-pagina-de-prueba");

		AssertVisible(
			key_pageName = "Pagina de prueba",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This ensures that both the active URL and old friendlyURL can be viewed from the history."
	@priority = "5"
	test ViewFriendlyURLs {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Page.gotoFriendlyURLHistory();

		Page.viewActiveURL(activeURL = "/new-test-page");

		AssertElementNotPresent(
			key_activeURL = "/test-page",
			locator1 = "SitePages#FRIENDLY_URL_ACTIVE_URL");

		Page.viewOldFriendlyURL(oldFriendlyURL = "/test-page");

		AssertElementNotPresent(
			key_oldFriendlyURL = "/new-test-page",
			locator1 = "SitePages#FRIENDLY_URL_HISTORY_ITEM");
	}

	@description = "This test covers LPS-116068. It ensures that both the active URL and old friendlyURL can be viewed from the history of a default page."
	@priority = "5"
	test ViewFriendlyURLsViaDefaultPage {
		SitePages.openPagesAdmin(siteURLKey = "guest");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Search");

		Page.editFriendlyURL(friendlyURLEdit = "/new-search-page");

		Page.gotoFriendlyURLHistory();

		Page.viewActiveURL(activeURL = "/new-search-page");

		AssertElementNotPresent(
			key_activeURL = "/search",
			locator1 = "SitePages#FRIENDLY_URL_ACTIVE_URL");

		Page.viewOldFriendlyURL(oldFriendlyURL = "/search");

		AssertElementNotPresent(
			key_oldFriendlyURL = "/new-search-page",
			locator1 = "SitePages#FRIENDLY_URL_HISTORY_ITEM");
	}

	@description = "This ensures that both the localized active URL and old friendlyURL can be viewed from the history."
	@priority = "5"
	test ViewLocalizedFriendlyURLs {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "es_ES",
			localizedFriendlyURL = "/pagina-de-prueba");

		PortletEntry.inputName(name = "Pagina de prueba");

		Button.clickSave();

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "es_ES",
			localizedFriendlyURL = "/nueva-pagina-de-prueba");

		Button.clickSave();

		Page.gotoFriendlyURLHistory();

		PortletEntry.changeLocale(
			friendlyURLHistory = "true",
			localizedFriendlyURL = "es_ES");

		Page.viewActiveURL(activeURL = "/nueva-pagina-de-prueba");

		AssertElementNotPresent(
			key_activeURL = "/pagina-de-prueba",
			locator1 = "SitePages#FRIENDLY_URL_ACTIVE_URL");

		Page.viewOldFriendlyURL(oldFriendlyURL = "/pagina-de-prueba");

		AssertElementNotPresent(
			key_oldFriendlyURL = "/nueva-pagina-de-prueba",
			locator1 = "SitePages#FRIENDLY_URL_HISTORY_ITEM");
	}

	@description = "This ensures that the multiple old friendlyURLs can be viewed from the history. This test also covers the first old friendlyURL is the last one."
	@priority = "5"
	test ViewMultipleOldFriendlyURLs {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page1");

		Page.gotoFriendlyURLHistory();

		Page.viewActiveURL(activeURL = "/new-test-page1");

		Page.viewOldFriendlyURL(
			lastOldFriendlyURL = "/test-page",
			oldFriendlyURL = "/new-test-page");
	}

	@description = "This ensures that both the active URL and the old friendlyURL can be viewed by default language from the history after localization without friendlyURL."
	@priority = "5"
	test ViewURLsAfterLocalizationWithoutFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page");

		Page.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		PortletEntry.changeLocale(
			fieldName = "Friendly URL",
			locale = "es-ES");

		PortletEntry.inputName(name = "Pagina de prueba");

		Button.clickSave();

		PortletEntry.changeLocale(
			fieldName = "Friendly URL",
			locale = "es-ES");

		Page.gotoFriendlyURLHistory();

		Page.viewActiveURL(activeURL = "/new-test-page");

		Page.viewOldFriendlyURL(oldFriendlyURL = "/test-page");
	}

}