definition {

	macro configureOpenGraph {
		if (isSet(customTitle)) {
			var checkboxName = "Use Custom Title";

			if (IsNotChecked(locator1 = "Checkbox#ANY_CHECKBOX")) {
				Click(locator1 = "Checkbox#ANY_CHECKBOX");
			}

			Type(
				locator1 = "TextInput#OPEN_GRAPH_CUSTOM_TITLE",
				value1 = "${customTitle}");
		}

		if (isSet(customDescription)) {
			var checkboxName = "Use Custom Description";

			if (IsNotChecked(locator1 = "Checkbox#ANY_CHECKBOX")) {
				Click(locator1 = "Checkbox#ANY_CHECKBOX");
			}

			Type(
				key_text = "openGraphDescription",
				locator1 = "TextArea#ANY",
				value1 = "${customDescription}");
		}
		if (isSet(customImage)) {
			var key_uploadFileName = "${uploadFileName}";

			Click(locator1 = "Button#SELECT_OPEN_GRAPH_IMAGE");

			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

			AssertElementPresent(locator1 = "ItemSelector#SELECT_FILE_UPLOAD_IMAGE");

			UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
				locator1 = "TextInput#FILE",
				value1 = "${uploadFileName}");

			SelectFrameTop(value1 = "relative=top");

			Button.click(button = "Save");

			Click(locator1 = "ControlMenu#EDIT_COG");

			Navigator.gotoNavTab(navTab = "Open Graph");

			AssertElementPresent(
				key_dmDocumentURL = "${uploadFileName}",
				locator1 = "TextInput#OPEN_GRAPH_CUSTOM_IMAGE_INPUT");
		}

		Button.click(button = "Save");
	}

	macro viewDefaultConfiguration {
		task ("Assert open graph title") {
			AssertTextEquals(
				key_title = "Open Graph",
				locator1 = "Header#H2_TITLE",
				value1 = "Open Graph");
		}

		task ("Assert custom title configuration") {
			AssertTextEquals.assertPartialText(
				key_fieldName = "openGraphTitleEnabled",
				locator1 = "FormFields#CHECKBOX_LABEL",
				value1 = "Use Custom Title");

			AssertElementPresent(locator1 = "TextInput#OPEN_GRAPH_CUSTOM_TITLE");
		}

		task ("Assert custom description configuration") {
			AssertTextEquals.assertPartialText(
				key_fieldName = "openGraphDescriptionEnabled",
				locator1 = "FormFields#CHECKBOX_LABEL",
				value1 = "Use Custom Description");

			AssertElementPresent(
				key_text = "openGraphDescription",
				locator1 = "TextArea#ANY");
		}

		task ("Assert image configuration") {
			AssertTextEquals(
				key_fieldLabel = "Image",
				locator1 = "FormFields#LABEL",
				value1 = "Image");

			AssertElementPresent(locator1 = "Button#SELECT_OPEN_GRAPH_IMAGE");
		}
	}

	macro viewNoOpenGraphProperties {
		for (var ogProperty : list "${ogPropertyList}" ) {
			AssertHTMLSourceTextNotPresent(value1 = "${ogProperty}");
		}
	}

	macro viewOpenGraphProperties {
		for (var ogProperty : list "${ogPropertyList}" ) {
			AssertHTMLSourceTextPresent(value1 = "${ogProperty}");
		}
	}

}