definition {

	macro addMultipleTags {
		for (var customTag : list "${customTagsList}") {
			Click(locator1 = "Icon#PLUS_LAST");

			Type(
				locator1 = "TextInput#CUSTOM_META_TAGS_LAST_PROPERTY",
				value1 = "${customTag} property");

			Type(
				locator1 = "TextInput#CUSTOM_META_TAGS_LAST_CONTENT",
				value1 = "${customTag} content");
		}

		Button.click(button = "Save");
	}

	macro addTag {
		Type(
			locator1 = "TextInput#CUSTOM_META_TAGS_PROPERTY",
			value1 = "${customProperty}");

		Type(
			locator1 = "TextInput#CUSTOM_META_TAGS_CONTENT",
			value1 = "${customContent}");

		Button.click(button = "Save");
	}

	macro changeLocale {
		Click.clickNoMouseOver(locator1 = "Button#LOCALIZATION_GENERIC");

		Click(
			key_locale = StringUtil.replace("${locale}", "-", "_"),
			locator1 = "Translation#LOCALIZATION_NAME");
	}

	macro editMultipleTags {
		if (isSet(newCustomProperty)) {
			for (var customProperty : list "${customPropertiesList}") {
				Type(
					key_customProperty = "${customProperty}",
					locator1 = "TextInput#CUSTOM_META_TAGS_PROPERTY_EDITOR",
					value1 = "${newCustomProperty}");
			}

			Button.click(button = "Save");
		}

		if (isSet(newCustomContent)) {
			for (var customContent : list "${customContentsList}") {
				Type(
					key_customContent = "${customContent}",
					locator1 = "TextInput#CUSTOM_META_TAGS_CONTENT_EDITOR",
					value1 = "${newCustomContent}");
			}

			Button.click(button = "Save");
		}
	}

	macro editTag {
		if (isSet(newCustomProperty)) {
			Type(
				key_customProperty = "${customProperty}",
				locator1 = "TextInput#CUSTOM_META_TAGS_PROPERTY_EDITOR",
				value1 = "${newCustomProperty}");
		}

		if (isSet(newCustomContent)) {
			Type(
				key_customContent = "${customContent}",
				locator1 = "TextInput#CUSTOM_META_TAGS_CONTENT_EDITOR",
				value1 = "${newCustomContent}");
		}

		Button.click(button = "Save");
	}

	macro removeTag {
		Click(
			key_tagNum = "${tagNum}",
			locator1 = "Icon#DELETE_CUSTOM_META_TAG");

		Button.click(button = "Save");
	}

	macro viewNoTags {
		for (var customMetaTag : list "${customMetaTagsList}" ) {
			AssertHTMLSourceTextNotPresent(value1 = "${customMetaTag}");
		}
	}

	macro viewTags {
		for (var customMetaTag : list "${customMetaTagsList}" ) {
			AssertHTMLSourceTextPresent(value1 = "${customMetaTag}");
		}
	}

}