@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "SEO";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a Page via JSON") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This ensures that the user can customize OG tags."
	@priority = "5"
	test CustomizeMetaTags {
		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		Navigator.gotoNavTab(navTab = "Open Graph");

		OpenGraph.configureOpenGraph(
			customDescription = "This is a custom description",
			customImage = "true",
			customTitle = "This is a custom title",
			pageName = "Test Page Name",
			uploadFileName = "Document_1.jpg");

		var siteID = Site.getSiteId();

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		User.viewLoggedOutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page-name");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:title" content="This is a custom title","og:description" content="This is a custom description","og:image" content="${portalURL}/documents/${siteID}/0/Document_1.jpg''');
	}

	@description = "This ensures that the user can customize OG tags with escape characters."
	@priority = "4"
	test CustomizeMetaTagsWithEscapeCharacters {
		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		Navigator.gotoNavTab(navTab = "Open Graph");

		OpenGraph.configureOpenGraph(
			customDescription = '''description@#$%^&amp;*()~`1234567890''',
			customTitle = '''@#$%^&amp;*()~`1234567890title''');

		User.logoutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page-name");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:title" content="@#$%^&amp;*()~`1234567890title","og:description" content="description@#$%^&amp;*()~`1234567890"''');
	}

	@description = "This ensures that OG tags will not appear when Open Graph is disabled."
	@priority = "5"
	test DisableConfiguration {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "OpenGraph#DisableConfiguration";

		var portalURL = PropsUtil.get("portal.url");

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Pages",
			configurationName = "SEO",
			configurationScope = "Virtual Instance Scope");

		FormFields.disableCheckbox(fieldName = "Enable Open Graph");

		SystemSettings.saveConfiguration();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		AssertElementNotPresent(
			key_tab = "Open Graph",
			locator1 = "NavTab#TAB_LINK");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings",
			site = "Test Site Name");

		AssertElementNotPresent(
			key_panel = "open graph",
			locator1 = "Panel#PANEL_COLLAPSED");

		User.logoutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page-name");

		OpenGraph.viewNoOpenGraphProperties(ogPropertyList = '''property="og:title",property="og:site_name",property="og:url",property="og:locale"''');
	}

	@description = "This ensures that OG configuration is properly disabled at the site level."
	@priority = "5"
	test DisableConfigurationAtSiteLevel {
		property test.name.skip.portal.instance = "OpenGraph#DisableConfigurationAtSiteLevel";

		ApplicationsMenu.gotoSite(site = "Test Site Name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings",
			site = "Test Site Name");

		OpenGraph.disableOpenGraphAtSiteLevel();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		AssertElementNotPresent(
			key_tab = "Open Graph",
			locator1 = "NavTab#TAB_LINK");

		JSONGroup.addGroup(groupName = "Test Site Name 2");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name 2",
			layoutName = "Test Page Name 2");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name-2");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name 2");

		Navigator.gotoNavTab(navTab = "Open Graph");

		OpenGraph.viewDefaultConfiguration();
	}

	@description = "This ensures that the user can edit existing OG tags."
	@priority = "5"
	test EditMetaTagCustomizations {
		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		Navigator.gotoNavTab(navTab = "Open Graph");

		OpenGraph.configureOpenGraph(
			customDescription = "This is a custom description",
			customImage = "true",
			customTitle = "This is a custom title",
			pageName = "Test Page Name",
			uploadFileName = "Document_1.jpg");

		OpenGraph.configureOpenGraph(
			customDescription = "This is a custom description edit",
			customImage = "true",
			customTitle = "This is a custom title edit",
			pageName = "Test Page Name",
			uploadFileName = "Document_2.jpg");

		var siteID = Site.getSiteId();

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		User.viewLoggedOutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page-name");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:title" content="This is a custom title edit","og:description" content="This is a custom description edit","og:image" content="${portalURL}/documents/${siteID}/0/Document_2''');
	}

	@description = "This test ensures that user can map the blogs with open graph meta tags for display pages."
	@priority = "5"
	test MapBlogsToDisplayPage {
		property test.name.skip.portal.instance = "OpenGraph#MapBlogsToDisplayPage";

		var portalURL = PropsUtil.get("portal.url");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.addDisplayPage(
			contentType = "Blogs Entry",
			displayPageName = "Display Page Template Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		PageTemplatesAdmin.gotoPageTemplateEllipsisItem(
			item = "Configure",
			pageTemplateName = "Display Page Template Name");

		Navigator.gotoNavNested(navNested = "Open Graph");

		OpenGraph.viewMappedField(
			mappedField = "Title",
			mappedFieldValue = "Blogs Entry: Title");

		OpenGraph.viewMappedField(
			mappedField = "Description",
			mappedFieldValue = "Blogs Entry: Description");

		OpenGraph.viewMappedField(
			mappedField = "Image",
			mappedFieldValue = "-- Unmapped --");

		OpenGraph.viewMappedField(
			mappedField = "Alt",
			mappedFieldValue = "-- Unmapped --");

		OpenGraph.mapEditableTextToField(
			field = "Subtitle",
			mappedField = "Title");

		OpenGraph.mapEditableTextToField(
			field = "Content",
			mappedField = "Description");

		OpenGraph.mapEditableTextToField(
			field = "Author Profile Image",
			mappedField = "Image");

		OpenGraph.mapEditableTextToField(
			field = "Author Name",
			mappedField = "Image Alt Description");

		Button.clickSave();

		OpenGraph.viewMappedField(
			mappedField = "Title",
			mappedFieldValue = "Blogs Entry: Subtitle");

		OpenGraph.viewMappedField(
			mappedField = "Description",
			mappedFieldValue = "Blogs Entry: Content");

		OpenGraph.viewMappedField(
			mappedField = "Image",
			mappedFieldValue = "Blogs Entry: Author Profile Image");

		OpenGraph.viewMappedField(
			mappedField = "Alt",
			mappedFieldValue = "Blogs Entry: Author Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Template Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Display Page Content");

		Button.clickPublish();

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entrySubtitle = "Blogs Entry Subtitle",
			entryTitle = "Blogs Entry Title",
			groupName = "Test Site Name");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "test-site-name");

		BlogsNavigator.gotoEditCP(entryTitle = "Blogs Entry Title");

		WebContent.editDisplayPage(pageName = "Display Page Template Name");

		PortletEntry.publish();

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		User.viewLoggedOutPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/b/blogs-entry-title");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:title" content="Blogs Entry Subtitle","og:description" content="Blogs Entry Content"''');

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:image:alt" content="Test Test","og:image" content="${portalURL}/image/user_portrait''');
	}

	@description = "This test ensures that user can map the document with open graph meta tags for display pages."
	@priority = "5"
	test MapDocumentToDisplayPage {
		property test.name.skip.portal.instance = "OpenGraph#MapDocumentToDisplayPage";

		var portalURL = PropsUtil.get("portal.url");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.addDisplayPage(
			contentType = "Document",
			displayPageName = "Display Page Template Name Test",
			subType = "Basic Document");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		PageTemplatesAdmin.gotoPageTemplateEllipsisItem(
			item = "Configure",
			pageTemplateName = "Display Page Template Name Test");

		Navigator.gotoNavNested(navNested = "Open Graph");

		OpenGraph.viewMappedField(
			mappedField = "Title",
			mappedFieldValue = "Document: Title");

		OpenGraph.viewMappedField(
			mappedField = "Description",
			mappedFieldValue = "Document: Description");

		OpenGraph.viewMappedField(
			mappedField = "Image",
			mappedFieldValue = "-- Unmapped --");

		OpenGraph.viewMappedField(
			mappedField = "Alt",
			mappedFieldValue = "-- Unmapped --");

		OpenGraph.mapEditableTextToField(
			field = "File Name",
			mappedField = "Title");

		OpenGraph.mapEditableTextToField(
			field = "MIME Type",
			mappedField = "Description");

		OpenGraph.mapEditableTextToField(
			field = "Preview Image",
			mappedField = "Image");

		OpenGraph.mapEditableTextToField(
			field = "Author Name",
			mappedField = "Image Alt Description");

		Button.clickSave();

		OpenGraph.viewMappedField(
			mappedField = "Title",
			mappedFieldValue = "Document: File Name");

		OpenGraph.viewMappedField(
			mappedField = "Description",
			mappedFieldValue = "Document: MIME Type");

		OpenGraph.viewMappedField(
			mappedField = "Image",
			mappedFieldValue = "Document: Preview Image");

		OpenGraph.viewMappedField(
			mappedField = "Alt",
			mappedFieldValue = "Document: Author Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Template Name Test");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Display Page Content");

		Button.clickPublish();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.jpg",
			dmDocumentTitle = "Document_1.jpg");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

		var dmFileEntryID = DMDocument.getFileEntryID(dmDocumentTitle = "Document_1.jpg");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Document_1.jpg");

		WebContent.editDisplayPage(pageName = "Display Page Template Name Test");

		PortletEntry.publish();

		var siteID = Site.getSiteId();

		User.logoutPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/d/${dmFileEntryID}");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:title" content="Document_1.jpg","og:description" content="image/jpeg"''');

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:image:alt" content="Test Test","og:image" content="${portalURL}/documents/${siteID}/0/Document_1''');
	}

	@description = "This test ensures that image meta tags are displayed with the site image and alt description in the HTML when the image fields are not mapped in the display page."
	@priority = "5"
	test MapSiteImageToDisplayPage {
		property test.name.skip.portal.instance = "OpenGraph#MapSiteImageToDisplayPage";

		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		Panel.expandPanel(panel = "Open Graph");

		OpenGraph.configureOpenGraph(
			altDescription = "This is image alternative description",
			customImage = "true",
			uploadFileName = "Document_1.jpg");

		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Blogs Entry",
			displayPageTemplateEntryName = "Display Page Template Name",
			groupName = "Test Site Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		PageTemplatesAdmin.gotoPageTemplateEllipsisItem(
			item = "Configure",
			pageTemplateName = "Display Page Template Name");

		Navigator.gotoNavNested(navNested = "Open Graph");

		OpenGraph.viewMappedField(
			mappedField = "Image",
			mappedFieldValue = "-- Unmapped --");

		OpenGraph.viewMappedField(
			mappedField = "Alt",
			mappedFieldValue = "-- Unmapped --");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Template Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Display Page Content");

		Button.clickPublish();

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entrySubtitle = "Blogs Entry Subtitle",
			entryTitle = "Blogs Entry Title",
			groupName = "Test Site Name");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "test-site-name");

		BlogsNavigator.gotoEditCP(entryTitle = "Blogs Entry Title");

		WebContent.editDisplayPage(pageName = "Display Page Template Name");

		PortletEntry.publish();

		var siteID = Site.getSiteId();

		User.logoutPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/b/blogs-entry-title");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:image:alt" content="This is image alternative description","og:image" content="${portalURL}/documents/${siteID}/0/Document_1''');
	}

	@description = "This test ensures that user can map the web content with open graph meta tags for display pages."
	@priority = "5"
	test MapWebContentToDisplayPage {
		property test.name.skip.portal.instance = "OpenGraph#MapWebContentToDisplayPage";

		var portalURL = PropsUtil.get("portal.url");

		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Web Content Article",
			displayPageTemplateEntryName = "Display Page Template Name",
			groupName = "Test Site Name",
			subType = "Basic Web Content");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		PageTemplatesAdmin.gotoPageTemplateEllipsisItem(
			item = "Configure",
			pageTemplateName = "Display Page Template Name");

		Navigator.gotoNavNested(navNested = "Open Graph");

		OpenGraph.viewMappedField(
			mappedField = "Title",
			mappedFieldValue = "Basic Web Content: Title");

		OpenGraph.viewMappedField(
			mappedField = "Description",
			mappedFieldValue = "Basic Web Content: Description");

		OpenGraph.viewMappedField(
			mappedField = "Image",
			mappedFieldValue = "-- Unmapped --");

		OpenGraph.viewMappedField(
			mappedField = "Alt",
			mappedFieldValue = "-- Unmapped --");

		OpenGraph.mapEditableTextToField(
			field = "Last Editor Name",
			mappedField = "Title");

		OpenGraph.mapEditableTextToField(
			field = "Content",
			mappedField = "Description");

		OpenGraph.mapEditableTextToField(
			field = "Small Image",
			mappedField = "Image");

		OpenGraph.mapEditableTextToField(
			field = "Author Name",
			mappedField = "Image Alt Description");

		Button.clickSave();

		OpenGraph.viewMappedField(
			mappedField = "Title",
			mappedFieldValue = "Basic Web Content: Last Editor Name");

		OpenGraph.viewMappedField(
			mappedField = "Description",
			mappedFieldValue = "Basic Web Content: Content");

		OpenGraph.viewMappedField(
			mappedField = "Image",
			mappedFieldValue = "Basic Web Content: Small Image");

		OpenGraph.viewMappedField(
			mappedField = "Alt",
			mappedFieldValue = "Basic Web Content: Author Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Template Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Display Page Content");

		Button.clickPublish();

		JSONWebcontent.addWebContent(
			content = "Web Content Content",
			groupName = "Test Site Name",
			title = "Web Content Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		WebContent.editDisplayPage(pageName = "Display Page Template Name");

		WebContent.addFeaturedImageFromURL(imageSourceURL = "https://dummyimage.com/600x400/000/fff.jpg");

		PortletEntry.publish();

		User.logoutPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/web-content-title");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:title" content="Test Test","og:description" content="Web Content Content"''');

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:image:alt" content="Test Test","og:image" content="https://dummyimage.com/600x400/000/fff.jpg"''');
	}

	@description = "This ensures that the user can reset to the default OG title tag."
	@priority = "5"
	test RevertCustomTitle {
		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		Navigator.gotoNavTab(navTab = "Open Graph");

		OpenGraph.configureOpenGraph(customTitle = "This is a custom title");

		User.logoutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page-name");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''property="og:title" content="This is a custom title"''');

		User.firstLoginPG();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		Navigator.gotoNavTab(navTab = "Open Graph");

		Click(
			checkboxName = "Use Custom Title",
			locator1 = "Checkbox#ANY_CHECKBOX");

		Button.click(button = "Save");

		User.logoutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page-name");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''property="og:title" content="Test Page Name - Test Site Name''');
	}

	@description = "This ensures that OG tags will appear when Open Graph is enabled."
	@priority = "5"
	test Smoke {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Test Page Name");

		Navigator.gotoNavTab(navTab = "Open Graph");

		OpenGraph.viewDefaultConfiguration();

		User.logoutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page-name");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''property="og:title",property="og:site_name",property="og:url",property="og:locale"''');

		OpenGraph.viewNoOpenGraphProperties(ogPropertyList = '''property="og:image",property="og:description"''');
	}

	@description = "This ensures that meta tags can be viewed on a new page."
	@priority = "5"
	test ViewNewPageWithMetaTags {
		var portalURL = PropsUtil.get("portal.url");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(
			customDescription = "This is a new description",
			customTitle = "This is a new title",
			openGraph = "true",
			pageName = "Test New Page");

		User.logoutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-new-page");

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:title" content="This is a new title","og:description" content="This is a new description"''');

		OpenGraph.viewOpenGraphProperties(ogPropertyList = '''"og:url" content="${portalURL}/web/test-site-name/test-new-page","og:locale" content="en_US","og:locale:alternate" content="en_US","og:site_name" content="Test Site Name"''');
	}

}