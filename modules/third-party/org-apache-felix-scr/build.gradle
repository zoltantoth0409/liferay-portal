import com.liferay.gradle.plugins.patcher.PatchTask

apply plugin: "com.liferay.patcher"

task patch(type: PatchTask)

bundle {
	instruction "-include", '-${.}/classes/META-INF/MANIFEST.MF,${.}/bnd.bnd'
}

copyPatchOriginalLibClasses {
	doLast {
		File file = file("classes/META-INF/MANIFEST.MF")

		if (file.exists()) {
			file.text = file.text.replace("org.osgi.service.log;version=\"[1.3,2)\"", "org.osgi.service.log;version=\"[1.4,2)\"")
		}
	}
}

dependencies {
	compileOnly group: "org.apache.felix", name: "org.apache.felix.configadmin", transitive: false, version: "1.9.8"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.scr", transitive: false, version: "2.1.14"
	compileOnly group: "org.osgi", name: "org.osgi.service.log", version: "1.4.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.metatype", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
}

liferay {
	deployDir = file("${liferayHome}/osgi/static")
}

patch {
	ext {
		autoClean = false
	}

	fileNames "org/apache/felix/scr/impl/ComponentRegistry.java"
	fileNames "org/apache/felix/scr/impl/config/ScrMetaTypeProvider.java"

	originalLibModuleName = "org.apache.felix.scr"
}