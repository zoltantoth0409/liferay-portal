import com.liferay.gradle.util.GradleUtil
import com.liferay.gradle.util.Validator

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript
}

task checkoutDXPScript
task removeDXPScript

String gitBranchName = "master-private"
String gitRepositoryURL = "git@github.com:liferay/liferay-portal-ee.git"

File gitRepositoryDir = new File(projectDir.parentFile, "git-clone-profile-dxp")

checkoutDXPScript {
	dependsOn removeDXPScript

	doLast {
		if (System.getenv("JENKINS_HOME")) {
			logger.lifecycle "git clone --branch={} --depth=1 --no-checkout --single-branch {} {}", gitBranchName, "git@github-dev.liferay.com:liferay/liferay-portal-ee.git", gitRepositoryDir.name

			exec {
				commandLine "git", "clone", "--branch=" + gitBranchName, "--depth=1", "--no-checkout", "--single-branch", "git@github-dev.liferay.com:liferay/liferay-portal-ee.git", gitRepositoryDir.name
				workingDir = projectDir.parentFile
			}

			ByteArrayOutputStream outputByteArrayOutputStream = new ByteArrayOutputStream()

			exec {
				commandLine "git", "rev-parse", gitBranchName
				standardOutput = outputByteArrayOutputStream
				workingDir = gitRepositoryDir
			}

			String githubDevGitHash = outputByteArrayOutputStream.toString()

			outputByteArrayOutputStream = new ByteArrayOutputStream()

			exec {
				commandLine "git", "ls-remote", gitRepositoryURL, gitBranchName
				standardOutput = outputByteArrayOutputStream
				workingDir = projectDir.parentFile
			}

			String githubGitOutput = outputByteArrayOutputStream.toString()

			String githubGitHash = githubGitOutput.replaceAll("([0-9a-f]+).*", "\$1")

			if (!githubGitHash.equals(githubDevGitHash)) {
				delete gitRepositoryDir
			}
		}

		if (!gitRepositoryDir.exists()) {
			logger.lifecycle "git clone --branch={} --depth=1 --no-checkout --single-branch {} {}", gitBranchName, gitRepositoryURL, gitRepositoryDir.name

			exec {
				commandLine "git", "clone", "--branch=" + gitBranchName, "--depth=1", "--no-checkout", "--single-branch", gitRepositoryURL, gitRepositoryDir.name
				workingDir = projectDir.parentFile
			}
		}

		exec {
			commandLine "git", "log", "--no-decorate", "-1"
			workingDir = gitRepositoryDir
		}

		exec {
			commandLine "git", "checkout", "refs/heads/" + gitBranchName, "--", "build-working-dir.xml", "working.dir.properties"
			workingDir = gitRepositoryDir
		}

		ant.move(file: new File(gitRepositoryDir, "build-working-dir.xml"), todir: projectDir.parentFile)
		ant.move(file: new File(gitRepositoryDir, "working.dir.properties"), todir: projectDir.parentFile)

		delete gitRepositoryDir
	}
}

removeDXPScript {
	doLast {
		delete new File(projectDir.parentFile, "build-working-dir.xml"), new File(projectDir.parentFile, "working.dir.properties")
	}
}