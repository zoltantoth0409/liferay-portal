@component-name = "portal-workflow-ee"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property test.run.environment = "EE";
	property testray.main.component.name = "Kaleo Designer";

	var kfFieldSetName = "New Kaleo Field Set";
	var workflowAssetType = "Process";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Workflow.gotoEditWorkflowDefinition(workflowDefinitionTitle = "Single Approver");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "5"
	test DeleteTaskNode {
		property test.name.skip.portal.instance = "KaleoDesignerEditSingleApprover#DeleteTaskNode";

		PortletEntry.inputTitle(title = "Edited Workflow Definition");

		KaleoDesigner.selectNode(nodeNodeLabel = "update");

		KaleoDesigner.deleteNode(nodeNodeLabel = "update");

		PortletEntry.update();

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Workflow.viewDefinitionPublished(
			workflowDefinitionName = "Single Approver",
			workflowDefinitionTitle = "Edited Workflow Definition",
			workflowDefinitionVersionNumber = "2");

		Workflow.gotoEditWorkflowDefinition(workflowDefinitionTitle = "Edited Workflow Definition");

		KaleoDesigner.viewNodeNotPresent(nodeNodeLabel = "update");
	}

	@priority = "4"
	test ViewInvalidSourceXMLNotRemainsAfterTabSwitch {
		KaleoDesigner.editSourceXML(
			kdSourceXML = "INVALID",
			line = "9");

		KaleoDesigner.viewViewTab();

		KaleoDesigner.viewSourceXMLLine(
			invalid = "true",
			kdSourceXML = "INVALID",
			line = "9");
	}

	@description = "This is a use case for LPS-46657."
	@priority = "4"
	test ViewNodeNotificationRecipientPersistsAfterSwitchingTabs {
		property test.name.skip.portal.instance = "KaleoDesignerEditSingleApprover#ViewNodeNotificationRecipientPersistsAfterSwitchingTabs";

		KaleoDesigner.selectNode(nodeNodeLabel = "review");

		KaleoDesigner.editNodeNotifications(
			nodeNotificationsDescription = "Review Notification Description",
			nodeNotificationsName = "Review Notification",
			nodeNotificationsRecipientType = "Role Type",
			roleTitle = "Administrator",
			roleType = "Regular");

		KaleoDesigner.viewSourceXMLTab();

		KaleoDesigner.viewViewTab();

		KaleoDesigner.selectNode(nodeNodeLabel = "review");

		KaleoDesigner.viewNodeNotifications(
			nodeNotificationsName = "Review Notification",
			nodeNotificationsRecipientType = "Role Type",
			roleTitle = "Administrator",
			roleType = "Regular");
	}

	@description = "This is a use case for LPS-46605."
	@priority = "3"
	test ViewUserNotificationOption {
		KaleoDesigner.selectNode(nodeNodeLabel = "review");

		KaleoDesigner.viewNodeNotifications(
			nodeNotificationsName = "Review Notification",
			nodeNotificationsType = "User Notification");
	}

}