@component-name = "portal-workflow-ee"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property test.run.environment = "EE";
	property testray.main.component.name = "Kaleo Forms Admin";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Workflow.uploadDefinition(
			workflowDefinitionFile = "workflow_definition_xss.xml",
			workflowDefinitionTitle = '''<script>alert(123);</script>''');

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Kaleo Forms Admin");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			KaleoFormsAdmin.tearDownProcess();

			KaleoFormsAdmin.deleteFieldSet();

			Page.tearDownCP();

			Workflow.tearDownWorkflowDefinitions();
		}
	}

	@priority = "4"
	test AddProcess {
		property test.name.skip.portal.instance = "KaleoFormsXSS#AddProcess";

		KaleoFormsAdmin.addFieldSet(
			kfFieldSetName = "New Kaleo Field Set",
			kfProcessName = "Kaleo Forms Process");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "Text",
			fieldName = "Text");

		KaleoFormsAdmin.saveFieldSet();

		KaleoFormsAdmin.addProcess();

		KaleoFormsAdmin.addProcessDetails(
			kfProcessDescription = "Kaleo Forms Process Description",
			kfProcessName = "Kaleo Forms Process");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.chooseFieldSet(
			kfFieldSetName = "New Kaleo Field Set",
			kfProcessName = "Kaleo Forms Process");

		KaleoFormsAdmin.next();

		AssertAlertNotPresent();

		var actualScript = '''<script>alert(123);</script>''';

		var escapedScript = '''&lt;script&gt;alert(123);&lt;/script&gt;''';

		AssertHTMLSourceTextNotPresent(value1 = "${actualScript}");

		AssertHTMLSourceTextPresent(value1 = "${escapedScript}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Choose",
			rowEntry = "alert");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.assignForm(
			kfProcessName = "Kaleo Forms Process",
			workflowTask = "StartNode");

		KaleoFormsAdmin.addForm(kfFormName = "Created Task Form");

		KaleoFormsAdmin.saveForm();

		KaleoFormsAdmin.chooseForm(kfFormName = "Created Task Form");

		KaleoFormsAdmin.saveProcess(kfProcessName = "Kaleo Forms Process");
	}

}