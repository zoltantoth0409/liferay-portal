import groovy.time.TimeCategory
import groovy.time.TimeDuration

import java.util.regex.Matcher

import org.gradle.util.GUtil
import org.gradle.util.VersionNumber

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

task buildChangelogs
task recordCommits

buildChangelogs {
	dependsOn recordCommits

	description = "Builds the changelogs for each published version in `CHANGELOG.markdown`."
	group = "changelogs"

	doLast {
		assert project.properties["projectName"] : "Please provide a value for the '-PprojectName' property"

		String pluginFullName = GUtil.loadProperties(new File("sdk/${projectName}/bnd.bnd"))["Bundle-Name"]

		String text = "# ${pluginFullName} Change Log"

		String date = ""
		String rangeEnd = ""
		String rangeStart = ""
		String version = ""

		File changelogPropertiesFile = file("sdk/" + projectName + "/CHANGELOG.properties")

		changelogPropertiesFile.getText("UTF-8").eachLine {
			String recordCommitLine ->

			Matcher matcher = recordCommitLine =~ /(\d+\.\d+\.\d+):([a-f0-9]+)\.\.([a-f0-9]+)/

			if (matcher.find()) {
				rangeEnd = matcher.group(3)
				rangeStart = matcher.group(2)
				version = matcher.group(1)
			}

			ByteArrayOutputStream standardOutputByteArrayOutputStream = new ByteArrayOutputStream()

			exec {
				commandLine "git", "--no-pager", "log", "--follow", "--pretty=format:%H:%cd:%s", "--date=short", "-p", "${rangeStart}..${rangeEnd}", "--", "modules/sdk/${projectName}/build.gradle"
				standardOutput = standardOutputByteArrayOutputStream
				workingDir = projectDir.parentFile
			}

			String gitLogOutput = standardOutputByteArrayOutputStream.toString()

			String changelogItem = ""
			String ticket = ""

			gitLogOutput.eachLine {
				String gitLogLine ->

				matcher = gitLogLine =~ /[a-f0-9]+:(.+):([A-Z]+-\d+).*/

				if (matcher.find()) {
					date = matcher.group(1)
					ticket = matcher.group(2)
				}

				matcher = gitLogLine =~ /\+\s*.+ group: .*, name: \"(.+)", version: \"(\d+\.\d+\.\d+)\"/

				if (matcher.find()) {
					String dependencyName = matcher.group(1)
					String dependencyVersion = matcher.group(2)

					changelogItem = changelogItem + """\
						|
						|- [${ticket}]: Update the ${dependencyName} dependency to version ${dependencyVersion}.""".stripMargin()

					changelogItem = changelogItem.replaceAll(/(\n(?=.{80,}).{1,80})[ \t]+(.*)/) {
						String all, String s1, String s2 ->

						"${s1}\n${s2}"
					}
				}
			}

			if (changelogItem.length() > 0) {
				changelogItem = """\
					|
					|
					|## ${version} - ${date}
					|
					|### Changed""".stripMargin() + changelogItem

				text = text + changelogItem
			}
		}

		File changelogMarkdownFile = file("sdk/" + projectName + "/CHANGELOG.markdown")

		changelogMarkdownFile.text = text

		logger.lifecycle "Updated '{}'", changelogMarkdownFile
	}
}

recordCommits {
	description = "Records the commit ID for each published version in `CHANGELOG.properties`."
	group = "changelogs"

	doLast {
		assert project.properties["projectName"] : "Please provide a value for the '-PprojectName' property"

		ByteArrayOutputStream standardOutputByteArrayOutputStream = new ByteArrayOutputStream()

		exec {
			commandLine "git", "--no-pager", "log", "--date=rfc2822", "--follow", "--format=fuller", "-p", "--", "modules/sdk/" + projectName + "/"
			standardOutput = standardOutputByteArrayOutputStream
			workingDir = projectDir.parentFile
		}

		String gitLogOutput = standardOutputByteArrayOutputStream.toString()

		List<String> commits = gitLogOutput.split("\ncommit ")

		String lastVersionCommitId = null
		VersionNumber lastVersionNumber = null
		Map<VersionNumber, String> recordCommitIDs = [:]

		for (int i = 0; i < (commits.size() - 1); i++) {
			String commit = commits.get(i)

			String[] lines = commit.split("\n", 8)

			VersionNumber publishedVersionNumber = _getPublishedVersionNumber(lines[7], projectName)

			if ((publishedVersionNumber != null) && !recordCommitIDs.containsKey(publishedVersionNumber) && (publishedVersionNumber.major > 0) && ((lastVersionNumber == null) || (publishedVersionNumber.compareTo(lastVersionNumber) < 0))) {
				String commitId = lines[0]

				String commitDateString = lines[4]

				Date commitDate = Date.parse("E, d MMM yyyy H:m:s Z", commitDateString.substring(12))

				for (int j = i - 1; j >= 0; j--) {
					String previousCommit = commits.get(j)

					lines = previousCommit.split("\n", 8)

					String previousCommitMessage = lines[6]

					if (!previousCommitMessage.contains("prep next") || (_getPublishedVersionNumber(lines[7], projectName) != null)) {
						break
					}

					String previousCommitDateString = lines[4]

					Date previousCommitDate = Date.parse("E, d MMM yyyy H:m:s Z", previousCommitDateString.substring(12))

					TimeDuration duration = TimeCategory.minus(previousCommitDate, commitDate)

					if (duration.toMilliseconds() > 1800000) {
						break
					}

					commitId = lines[0]
				}

				if (lastVersionNumber != null) {
					recordCommitIDs.put(lastVersionNumber, commitId + ".."  + lastVersionCommitId)
				}

				lastVersionNumber = publishedVersionNumber
				lastVersionCommitId = commitId
			}
		}

		File changelogPropertiesFile = file("sdk/" + projectName + "/CHANGELOG.properties")

		changelogPropertiesFile.text = recordCommitIDs.collect {
			VersionNumber versionNumber, String commitIds ->

			"${versionNumber}:${commitIds}"
		} join "\n"

		logger.lifecycle "Updated '{}'", changelogPropertiesFile
	}
}

private VersionNumber _getPublishedVersionNumber(String diff, String projectName) {
	int x = diff.indexOf("/" + projectName + "/bnd.bnd");

	if (x == -1) {
		return null
	}

	int y = diff.indexOf("-Bundle-Version: ", x)

	if (y == -1) {
		return null
	}

	return VersionNumber.parse(diff.substring(y + 17, diff.indexOf("\n", y)))
}