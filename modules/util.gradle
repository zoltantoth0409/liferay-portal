import aQute.bnd.osgi.Constants

import com.liferay.gradle.plugins.defaults.tasks.CopyIvyDependenciesTask
import com.liferay.gradle.plugins.node.internal.util.FileUtil
import com.liferay.gradle.plugins.node.tasks.ExecutePackageManagerTask
import com.liferay.gradle.plugins.source.formatter.FormatSourceTask
import com.liferay.gradle.plugins.util.PortalTools
import com.liferay.gradle.util.OSDetector
import com.liferay.gradle.util.StringUtil

import groovy.io.FileType
import groovy.io.FileVisitResult

import java.nio.charset.StandardCharsets
import java.nio.file.Files

import java.util.regex.Matcher

import org.gradle.util.GUtil

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript
}

apply plugin: "com.liferay.node"
apply plugin: "com.liferay.node.defaults"
apply plugin: "com.liferay.source.formatter"
apply plugin: "com.liferay.source.formatter.defaults"

buildDir = new File(rootDir.parentFile, "build")

task appendHotfixQualifier
task formatSourceCurrentBranch(type: FormatSourceTask)
task formatSourceLatestAuthor(type: FormatSourceTask)
task formatSourceLocalChanges(type: FormatSourceTask)
task printDependencyPath
task removeHotfixQualifier
task setUpLibs
task setUpPortalTools
task setUpYarn
task wrapper(type: Wrapper)
task wrapperSubrepo(type: Wrapper)
task yarnCheckFormat
task yarnFormat
task yarnLock

FileTree libDependenciesFileTree = fileTree(dir: "../lib", include: "*/dependencies.properties")

Map<File, Properties> libDependenciesMap = libDependenciesFileTree.collectEntries {
	Properties properties = GUtil.loadProperties(it)

	[it.parentFile, properties]
}

appendHotfixQualifier {
	doLast {
		String dirNamesString = System.getProperty("patcher.hotfix.dirs")
		String qualifier = System.getProperty("patcher.hotfix.qualifier")

		for (String dirName : dirNamesString.split(",")) {
			File bndBndFile = new File(new File(projectDir, dirName), "bnd.bnd")

			if (bndBndFile.exists()) {
				Properties properties = GUtil.loadProperties(bndBndFile)

				String version = properties.getProperty(Constants.BUNDLE_VERSION)

				if ((version != null) && (version.indexOf(qualifier) == -1)) {
					String newLine = Constants.BUNDLE_VERSION + ": " + version + qualifier
					String oldLine = bndBndFile.filterLine {
						it =~ "^" + Constants.BUNDLE_VERSION + ".*"
					}

					bndBndFile.text = bndBndFile.text.replace(oldLine.trim(), newLine)

					logger.lifecycle "Appended {}:{}", project.relativePath(bndBndFile), qualifier
				}
			}
		}
	}
}

formatSourceCurrentBranch {
	description = "Runs Liferay Source Formatter to format the project files that are added or modified in the current Git branch."
	formatCurrentBranch = true
}

formatSourceLatestAuthor {
	description = "Runs Liferay Source Formatter to format the project files that are added or modified in the latest Git commits of the same author."
	formatCurrentBranch = true
}

formatSourceLocalChanges {
	description = "Runs Liferay Source Formatter to format the unstaged project files."
	formatLocalChanges = true
}

node {
	nodeVersion = "8.10.0"
	npmVersion = "5.7.1"
}

printDependencyPath {
	doLast {
		assert project.properties["dependencyNotation"] : "Please provide a value for the '-PdependencyNotation' property"

		Dependency dependency = dependencies.create dependencyNotation

		Configuration configuration = configurations.detachedConfiguration dependency

		configuration.transitive = false

		logger.quiet configuration.singleFile.absolutePath
	}
}

removeHotfixQualifier {
	doLast {
		String dirNamesString = System.getProperty("patcher.hotfix.dirs")
		String qualifier = System.getProperty("patcher.hotfix.qualifier")

		for (String dirName : dirNamesString.split(",")) {
			File bndBndFile = new File(new File(projectDir, dirName), "bnd.bnd")

			if (bndBndFile.exists()) {
				Properties properties = GUtil.loadProperties(bndBndFile)

				String version = properties.getProperty(Constants.BUNDLE_VERSION)

				if ((version != null) && (version.indexOf(qualifier) != -1)) {
					bndBndFile.text = bndBndFile.text.replace(version, version.minus(qualifier))

					logger.lifecycle "Removed {}:{}", project.relativePath(bndBndFile), qualifier
				}
			}
		}
	}
}

repositories {
	mavenLocal()

	File tmpMavenRepositoryDir = file("../.m2-tmp")

	if (tmpMavenRepositoryDir.exists()) {
		maven {
			url tmpMavenRepositoryDir
		}
	}

	maven {
		url System.properties["repository.url"] ?: "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

setUpYarn {
	String dirNamesString = System.getProperty("patcher.hotfix.dirs")
	String qualifier = System.getProperty("patcher.hotfix.qualifier")

	if (System.getenv("JENKINS_HOME") && dirNamesString?.trim() && qualifier?.trim()) {
		dependsOn removeHotfixQualifier
		finalizedBy appendHotfixQualifier
	}

	doFirst {
		String buildBinariesCacheDir = project.findProperty("build.binaries.cache.dir")

		if (buildBinariesCacheDir) {
			File cacheDir = new File(rootDir.parentFile, buildBinariesCacheDir)

			if (cacheDir.exists()) {
				File fromDir = new File(cacheDir, ".yarn/offline-cache")
				File intoDir = new File(rootDir, "node_modules_cache")

				logger.lifecycle "Copying {} into {}", fromDir, intoDir

				copy {
					from fromDir
					into intoDir
				}
			}
		}
	}
}

wrapper {
	File wrapperDir = projectDir.parentFile

	distributionUrl = "../../tools/gradle-${gradle.gradleVersion}.LIFERAY-PATCHED-3-bin.zip"
	dependsOn wrapperSubrepo

	doLast {
		File gradlewFile = new File(wrapperDir, "gradlew")

		ant.replace(
			failOnNoReplacements: true,
			file: gradlewFile,
			token: '# Escape application args',
			value: '''\
# Escape application args
DEFAULT_JVM_OPTS="$DEFAULT_JVM_OPTS \\\"-Dgradle.user.home=$APP_HOME/.gradle\\\""'''
		)

		ant.replace(
			failOnNoReplacements: true,
			file: gradlewFile,
			token: 'org.gradle.wrapper.GradleWrapperMain "$APP_ARGS"',
			value: 'org.gradle.wrapper.GradleWrapperMain "--init-script=$APP_HOME/modules/init.gradle" "$APP_ARGS"'
		)

		File gradlewBatFile = new File(wrapperDir, "gradlew.bat")

		ant.replace(
			failOnNoReplacements: true,
			file: gradlewBatFile,
			token: '@rem Setup the command line',
			value: '''\
@rem Setup the command line
set DEFAULT_JVM_OPTS=%DEFAULT_JVM_OPTS% "-Dgradle.user.home=%APP_HOME%\\.gradle"'''
		)

		ant.replace(
			failOnNoReplacements: true,
			file: gradlewBatFile,
			token: 'org.gradle.wrapper.GradleWrapperMain %CMD_LINE_ARGS%',
			value: 'org.gradle.wrapper.GradleWrapperMain "--init-script=%APP_HOME%\\modules\\init.gradle" %CMD_LINE_ARGS%'
		)
	}

	jarFile = new File(wrapperDir, "gradle/wrapper/gradle-wrapper.jar")
	scriptFile = new File(wrapperDir, "gradlew")
}

wrapperSubrepo {
	File wrapperDir = new File(projectDir.parentFile, "tools/subrepo")

	doLast {
		File gradlewFile = new File(wrapperDir, "gradlew")

		ant.replace(
			failOnNoReplacements: true,
			file: gradlewFile,
			token: '# Escape application args',
			value: '''\
# Escape application args
DEFAULT_JVM_OPTS="$DEFAULT_JVM_OPTS \\\"-Dgradle.user.home=$APP_HOME/.gradle\\\""'''
		)

		File gradlewBatFile = new File(wrapperDir, "gradlew.bat")

		ant.replace(
			failOnNoReplacements: true,
			file: gradlewBatFile,
			token: '@rem Setup the command line',
			value: '''\
@rem Setup the command line
set DEFAULT_JVM_OPTS=%DEFAULT_JVM_OPTS% "-Dgradle.user.home=%APP_HOME%\\.gradle"'''
		)
	}

	jarFile = new File(wrapperDir, "gradle/wrapper/gradle-wrapper.jar")
	scriptFile = new File(wrapperDir, "gradlew")
}

tasks.withType(FormatSourceTask) {
	baseDirName = "../"
	group = "formatting"
	showStatusUpdates = true
}

_createSetUpLibTasks(libDependenciesMap)
_createSetUpPortalToolTasks()
_createYarnTasks()

private String _camelCase(String dirName) {
	String suffix = dirName.replaceAll(/\.(\w)/) {
		String s = it[1]

		s.toUpperCase()
	}

	return StringUtil.capitalize(suffix)
}

private void _createSetUpLibTasks(Map<File, Properties> libDependenciesMap) {
	libDependenciesMap.each {
		File dir, Properties properties ->

		String libSuffix = StringUtil.capitalize(dir.name)

		Configuration libConfiguration = configurations.create("lib" + libSuffix)

		libConfiguration.transitive = false

		Task libTask = tasks.create(name: "setUpLib" + libSuffix, type: Copy) {
			from libConfiguration
			into dir
		}

		for (String fileTitle in properties.stringPropertyNames()) {
			String dependencyNotation = properties.getProperty(fileTitle)

			def dependency = dependencies.add(libConfiguration.name, dependencyNotation)

			DependencyArtifact dependencyArtifact = dependency.artifacts.find()

			if (dependencyArtifact) {
				libTask.rename "${dependency.name}-${dependency.version}-${dependencyArtifact.classifier}.jar", "${fileTitle}.jar"
			}
			else {
				libTask.rename "${dependency.name}-${dependency.version}.jar", "${fileTitle}.jar"
			}
		}

		setUpLibs.dependsOn libTask
	}
}

private void _createSetUpPortalToolTasks() {
	FileTree ivyXmlFileTree = fileTree(dir: "../tools/sdk", includes: ["ivy.xml", "dependencies/*/ivy.xml"])

	XmlSlurper xmlSlurper = new XmlSlurper()

	ivyXmlFileTree.each {
		File ivyXmlFile ->

		File portalToolDir = ivyXmlFile.parentFile

		CopyIvyDependenciesTask copyIvyDependenciesTask = tasks.create(name: "setUp" + _camelCase(portalToolDir.name), type: CopyIvyDependenciesTask) {
			configuration.exclude group: "com.liferay.portal"

			dependencyTransformClosure = {
				Map<String, Object> dependencyNotation ->

				String version = PortalTools.getVersion(project, dependencyNotation["name"])

				if (version) {
					dependencyNotation["version"] = version
				}

				dependencyNotation
			}

			if (ivyXmlFile.parentFile.name != "sdk") {
				doFirst {
					delete destinationDir
				}
			}

			inputFile = ivyXmlFile
			into new File(portalToolDir, "lib")

			outputs.upToDateWhen {
				false
			}
		}

		setUpPortalTools.dependsOn copyIvyDependenciesTask
	}
}

private void _createYarnTasks() {
	FileTree packageJSONFileTree = fileTree(dir: rootDir, includes: ["package.json", "private/package.json", "apps/*/package.json", "private/apps/*/package.json"])

	packageJSONFileTree.each {
		File packageJSONFile ->

		if (packageJSONFile.text.contains("\"workspaces\":")) {
			Task yarnTask = tasks.create(name: "setUpYarn" + _camelCase(packageJSONFile.parentFile.name), type: ExecutePackageManagerTask) {
				args "install"
				args "--frozen-lockfile"

				scriptFile = new File(rootDir, "yarn-1.13.0.js")
				workingDir = packageJSONFile.parentFile

				doFirst {
					String registry = project.findProperty("nodejs.npm.ci.registry")

					if (registry) {
						File yarnLockFile = new File(packageJSONFile.parentFile, "yarn.lock")

						if (yarnLockFile.exists()) {
							logger.lifecycle "Using registry {}", registry

							yarnLockFile.text = yarnLockFile.text.replaceAll("https://registry.yarnpkg.com", registry)
						}
					}
				}
			}

			appendHotfixQualifier.mustRunAfter yarnTask
			setUpYarn.finalizedBy yarnTask

			yarnTask = tasks.create(name: "yarnCheckFormat" + _camelCase(packageJSONFile.parentFile.name), type: ExecutePackageManagerTask) {
				args "checkFormat"

				group = "formatting"
				scriptFile = new File(rootDir, "yarn-1.13.0.js")
				workingDir = packageJSONFile.parentFile
			}

			yarnCheckFormat.finalizedBy yarnTask

			yarnTask = tasks.create(name: "yarnFormat" + _camelCase(packageJSONFile.parentFile.name), type: ExecutePackageManagerTask) {
				args "format"

				group = "formatting"
				scriptFile = new File(rootDir, "yarn-1.13.0.js")
				workingDir = packageJSONFile.parentFile
			}

			yarnFormat.finalizedBy yarnTask

			yarnTask = tasks.create(name: "yarnLock" + _camelCase(packageJSONFile.parentFile.name), type: ExecutePackageManagerTask) {
				args "install"

				scriptFile = new File(rootDir, "yarn-1.13.0.js")
				workingDir = packageJSONFile.parentFile
			}

			yarnLock.finalizedBy yarnTask
		}
	}
}