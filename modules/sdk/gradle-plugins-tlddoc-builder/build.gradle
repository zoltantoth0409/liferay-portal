apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)

String gradleVersion = "5.6.4"

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.39"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions gradleVersion
}

pluginBundle {
	plugins {
		appTLDDocBuilderPlugin {
			description = "The App TLDDoc Builder lets you generate the tag library documentation as a single, combined HTML document for an application that spans different subprojects, each one representing a different component of the same application."
			displayName = "Liferay Gradle Plugins App TLDDoc Builder"
		}

		tLDDocBuilderPlugin {
			displayName = "Liferay Gradle Plugins TLDDoc Builder"
		}
	}

	tags = ["java", "tld"]
}