import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "org.ysb33r.gradletest"

task copyDistBundleZipPluginsSdk(type: Copy)
task copyGradleTestDependencies(type: Copy)
task copyHotFixZip(type: Copy)
task copyRepo(type: Copy)
task downloadHotFixZip(type: Download)
task downloadPluginsSdk(type: Download)

String bundleUrl = "https://releases-cdn.liferay.com/portal/7.1.2-ga3/liferay-ce-portal-tomcat-7.1.2-ga3-20190107144105508.tar.gz"
File gradleTestDependenciesDir = new File(buildDir, "gradle-test-dependencies")
String gradleVersion = "5.6.4"
String hotFixZipUrl = "https://dl.bintray.com/gamerson/liferay-ide-files/zips/liferay-hotfix-1072-7210.zip"
String pluginsSdkUrl = "https://repository-cdn.liferay.com/nexus/service/local/repositories/liferay-public-releases/content/com/liferay/portal/com.liferay.portal.plugins.sdk/1.0.22/com.liferay.portal.plugins.sdk-1.0.22-withdependencies.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.1.2-ga3/liferay-ce-portal-tomcat-7.1.2-ga3-20190107144105508.tar.gz"
	pluginsSdkUrl = "https://repository-cdn.liferay.com/nexus/service/local/repositories/liferay-public-releases/content/com/liferay/portal/com.liferay.portal.plugins.sdk/1.0.22/com.liferay.portal.plugins.sdk-1.0.22-withdependencies.zip"
}

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

cleanCopyLibs {
	enabled = false
}

copyDistBundleZipPluginsSdk {
	ext {
		autoClean = false
	}

	dependsOn downloadPluginsSdk
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(downloadPluginsSdk.dest)
	}

	includeEmptyDirs = false
	into "src/gradleTest/distBundleZip/plugins-sdk"
}

copyHotFixZip {
	ext {
		autoClean = false
	}

	dependsOn downloadHotFixZip

	from {
		downloadHotFixZip.dest
	}

	into "src/gradleTest/startDockerContainerWithPatch/configs/common/patching"
}

copyGradleTestDependencies {
	from configurations.compile
	from jar
	into gradleTestDependenciesDir
}

copyRepo {
	from "src/gradleTest/repo"
	into new File(buildDir, "gradleTest/repo")
}

dependencies {
	compile group: "com.bmuschko", name: "gradle-docker-plugin", version: "3.6.2"
	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
	compile group: "com.liferay", name: "com.liferay.ant.bnd", version: "3.2.6"
	compile(group: "com.liferay", name: "com.liferay.gradle.plugins", version: "11.0.16") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	compile(group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "2.2.31") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.target.platform", version: "2.1.24"
	compile(group: "com.liferay", name: "com.liferay.gradle.plugins.theme.builder", version: "2.0.13") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	compile(group: "com.liferay", name: "com.liferay.portal.tools.bundle.support", version: "3.7.1") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	compile group: "de.undercouch", name: "gradle-download-task", version: "3.2.0"

	compileInclude files("lib/com.liferay.workspace.bundle.url.codec-1.0.0.jar")

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

downloadHotFixZip {
	dest new File(buildDir, "liferay-hotfix-1072-7210.zip")
	src hotFixZipUrl
}

downloadPluginsSdk {
	dest new File(buildDir, "plugins-sdk.zip")
	src pluginsSdkUrl
}

gradleTest {
	dependsOn copyDistBundleZipPluginsSdk
	dependsOn copyGradleTestDependencies
	dependsOn copyHotFixZip
	dependsOn copyRepo
	dependsOn jar

	deprecationMessagesAreFailures = false

	gradleArguments "--project-prop", "liferay.workspace.bundle.url=${bundleUrl}"
	gradleArguments "--project-prop", "pluginClasspathDir=${uri(gradleTestDependenciesDir)}"

	versions gradleVersion
}

liferayOSGi {
	expandCompileInclude = true
}