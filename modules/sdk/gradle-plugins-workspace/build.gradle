import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "org.ysb33r.gradletest"

task copyDistBundleZipPluginsSdk(type: Copy)
task copyGradleTestDependencies(type: Copy)
task copyRepo(type: Copy)
task downloadPluginsSdk(type: Download)

String bundleUrl = "https://releases-cdn.liferay.com/portal/7.1.0-ga1/liferay-ce-portal-tomcat-7.1.0-ga1-20180703012531655.zip"
String pluginsSdkUrl = "https://repository-cdn.liferay.com/nexus/service/local/repositories/liferay-public-releases/content/com/liferay/portal/com.liferay.portal.plugins.sdk/1.0.19/com.liferay.portal.plugins.sdk-1.0.19-withdependencies.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.1.0-ga1/liferay-ce-portal-tomcat-7.1.0-ga1-20180703012531655.zip"
	pluginsSdkUrl = "https://repository-cdn.liferay.com/nexus/service/local/repositories/liferay-public-releases/content/com/liferay/portal/com.liferay.portal.plugins.sdk/1.0.19/com.liferay.portal.plugins.sdk-1.0.19-withdependencies.zip"
}

copyDistBundleZipPluginsSdk {
	dependsOn downloadPluginsSdk
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(downloadPluginsSdk.dest)
	}

	includeEmptyDirs = false
	into "src/gradleTest/distBundleZip/plugins-sdk"
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

copyRepo {
	from "src/gradleTest/repo"
	into new File(buildDir, "gradleTest/repo")
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.plugins", version: "3.13.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "2.2.5"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.target.platform", version: "1.1.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.theme.builder", version: "2.0.4"
	compile group: "com.liferay", name: "com.liferay.portal.tools.bundle.support", version: "3.2.6"
	compile group: "de.undercouch", name: "gradle-download-task", version: "3.2.0"

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))
}

downloadPluginsSdk {
	dest new File(buildDir, "plugins-sdk.zip")
	src pluginsSdkUrl
}

gradleTest {
	dependsOn copyDistBundleZipPluginsSdk
	dependsOn copyGradleTestDependencies
	dependsOn copyRepo
	dependsOn jar

	gradleArguments "--project-prop", "liferay.workspace.bundle.url=${bundleUrl}"
	gradleArguments "--project-prop", "pluginClasspathDir=${uri(jar.destinationDir)}"

	versions "2.14.1", "3.0", "3.1", "3.2.1", "3.3", "3.5", "3.5.1"
}