import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "org.ysb33r.gradletest"

task copyDistBundleZipPluginsSdk(type: Copy)
task copyGradleTestDependencies(type: Copy)
task downloadPluginsSdk(type: Download)

String bundleUrl = "https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"
String pluginsSdkUrl = "https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.4-ga5/liferay-plugins-sdk-7.0-1.0.13-20171018150113838.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"
	pluginsSdkUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.4-ga5/liferay-plugins-sdk-7.0-1.0.13-20171018150113838.zip"
}

copyDistBundleZipPluginsSdk {
	dependsOn downloadPluginsSdk
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(downloadPluginsSdk.dest)
	}

	includeEmptyDirs = false
	into "src/gradleTest/distBundleZip/plugins-sdk"
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.plugins", version: "3.8.4"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "2.2.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.theme.builder", version: "2.0.3"
	compile group: "com.liferay", name: "com.liferay.portal.tools.bundle.support", version: "3.2.1"
	compile group: "de.undercouch", name: "gradle-download-task", version: "3.2.0"

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))
}

downloadPluginsSdk {
	dest new File(buildDir, "plugins-sdk.zip")
	src pluginsSdkUrl
}

gradleTest {
	dependsOn copyDistBundleZipPluginsSdk
	dependsOn copyGradleTestDependencies
	dependsOn jar

	gradleArguments "--project-prop", "liferay.workspace.bundle.url=${bundleUrl}"
	gradleArguments "--project-prop", "pluginClasspathDir=${uri(jar.destinationDir)}"

	versions "2.14.1", "3.0", "3.1", "3.2.1", "3.3"
}