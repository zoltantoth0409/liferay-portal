apply plugin: "com.liferay.lang.builder"
apply plugin: "java"

task runGradleTest

buildLang {
	translateSubscriptionKey = project.properties["microsoft.translator.subscription.key"]
}

repositories {
	maven {
		url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
	}
}

runGradleTest {
	dependsOn buildLang

	doLast {
		FileTree languagePropertiesFileTree = fileTree(dir: "src/main/resources/content", include: "Language*.properties")

		assert !languagePropertiesFileTree.empty

		languagePropertiesFileTree.each {
			File languagePropertiesFile ->

			Properties languageProperties = new Properties()

			languagePropertiesFile.withInputStream {
				languageProperties.load it
			}

			if (languagePropertiesFile.name == "Language_en.properties") {
				assert languageProperties.getProperty("add-and-customize-borders-around-this-widget") == "Add and customize borders around This widget"
			}

			assert languageProperties.getProperty("hello-lang-builder")
		}
	}
}