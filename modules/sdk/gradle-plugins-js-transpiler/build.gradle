apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)

String gradleVersion = "5.6.4"

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.node", version: "7.2.1"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions gradleVersion
}

pluginBundle {
	plugins {
		jSTranspilerBasePlugin {
			description = "The JS Transpiler Base Gradle plugin lets you use Gradle dependencies (such as external module or project dependencies) in Node.js scripts."
			displayName = "Liferay Gradle Plugins JS Transpiler Base"
		}

		jSTranspilerPlugin {
			displayName = "Liferay Gradle Plugins JS Transpiler"
		}
	}

	tags = ["closure", "ecmascript", "es6", "javascript", "js", "metal", "metaljs", "node", "nodejs", "soy"]
}