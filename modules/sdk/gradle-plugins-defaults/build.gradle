import com.liferay.gradle.util.copy.ReplaceLeadingPathAction

String gradleVersion = "5.6.4"
String pluginsSDKVersion = project.properties["plugins.sdk.version"]

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

configurations {
	pluginsSDK {
		transitive = false
	}
}

dependencies {
	compile group: "com.github.jk1", name: "gradle-license-report", version: "1.8"
	compile group: "com.gradle.publish", name: "plugin-publish-plugin", version: "0.9.9"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins", version: "4.5.27"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.app.javadoc.builder", version: "1.2.2"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.baseline", version: "2.2.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.cache", version: "1.0.16"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.1.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.dependency.checker", version: "1.0.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.go", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.jsdoc", version: "2.0.46"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.lang.merger", version: "1.1.2"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.patcher", version: "1.0.17"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "2.2.19"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.whip", version: "1.0.7"
	compile group: "com.netflix.nebula", name: "gradle-extra-configurations-plugin", version: "3.1.0"
	compile group: "org.apache.aries.jmx", name: "org.apache.aries.jmx.api", version: "1.1.0"
	compile group: "org.springframework.boot", name: "spring-boot-gradle-plugin", version: "1.5.7.RELEASE"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))

	pluginsSDK group: "com.liferay.portal", name: "com.liferay.portal.plugins.sdk", version: pluginsSDKVersion
}

processResources {
	String destinationDirName = "com/liferay/gradle/plugins/defaults/dependencies"
	String zipDirName = "com.liferay.portal.plugins.sdk-${pluginsSDKVersion}/dependencies/net.sourceforge.pmd/rulesets/java"

	into(destinationDirName) {
		eachFile new ReplaceLeadingPathAction("${destinationDirName}/${zipDirName}": destinationDirName)

		from {
			zipTree(configurations.pluginsSDK.singleFile)
		}

		include "${zipDirName}/standard-rules.xml"
		includeEmptyDirs = false
	}
}