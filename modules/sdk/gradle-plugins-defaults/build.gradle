import com.liferay.gradle.util.copy.ReplaceLeadingPathAction

apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)

String gradleVersion = "5.6.4"
String pluginsSDKVersion = project.properties["plugins.sdk.version"]

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

configurations {
	pluginsSDK {
		transitive = false
	}
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.github.jk1", name: "gradle-license-report", version: "1.8"
	compile group: "com.gradle.publish", name: "plugin-publish-plugin", version: "0.11.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins", version: "11.0.20"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.app.javadoc.builder", version: "1.2.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.baseline", version: "6.0.2"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.cache", version: "1.0.17"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.1.4"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.dependency.checker", version: "1.0.4"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.go", version: "1.0.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.jsdoc", version: "2.0.54"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.lang.merger", version: "2.0.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.patcher", version: "1.0.18"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "2.2.31"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.whip", version: "1.0.8"
	compile group: "com.netflix.nebula", name: "gradle-extra-configurations-plugin", version: "3.1.0"
	compile group: "gradle.plugin.com.github.spotbugs.snom", name: "spotbugs-gradle-plugin", version: "4.0.0"
	compile group: "org.apache.aries.jmx", name: "org.apache.aries.jmx.api", version: "1.1.0"
	compile group: "org.springframework.boot", name: "spring-boot-gradle-plugin", version: "1.5.7.RELEASE"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))

	pluginsSDK group: "com.liferay.portal", name: "com.liferay.portal.plugins.sdk", version: pluginsSDKVersion
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions gradleVersion
}

processResources {
	String destinationDirName = "com/liferay/gradle/plugins/defaults/dependencies"
	String zipDirName = "com.liferay.portal.plugins.sdk-${pluginsSDKVersion}/dependencies/net.sourceforge.pmd/rulesets/java"

	into(destinationDirName) {
		eachFile new ReplaceLeadingPathAction("${destinationDirName}/${zipDirName}": destinationDirName)

		from {
			zipTree(configurations.pluginsSDK.singleFile)
		}

		include "${zipDirName}/standard-rules.xml"
		includeEmptyDirs = false
	}
}