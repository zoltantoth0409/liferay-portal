apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)

String gradleVersion = "5.6.4"

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.39"

	compile(group: "com.liferay", name: "com.liferay.portal.tools.soy.builder", version: "3.0.4") {
		exclude group: "com.google.template", module: "soy"
		exclude group: "org.codehaus.plexus"
		exclude group: "org.sonatype.plexus"
	}

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions gradleVersion
}

pluginBundle {
	plugins {
		soyTranslationPlugin {
			description = "The Soy Translation Gradle plugin lets you use a custom localization mechanism in the generated .soy.js files"
			displayName = "Liferay Gradle Plugins Soy Translation"
		}

		soyPlugin {
			displayName = "Liferay Gradle Plugins Soy"
		}
	}

	tags = ["javascript", "js", "soy"]
}