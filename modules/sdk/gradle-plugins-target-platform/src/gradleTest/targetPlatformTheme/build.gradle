buildscript {
	dependencies {
		classpath(group: "com.liferay", name: "com.liferay.gradle.plugins", version: "4.4.85") {
			exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
		}
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.theme.builder", version: "2.0.10"
	}

	repositories {
		File dir = rootProject.projectDir

		while (dir != null) {
			File tmpMavenRepositoryDir = new File(dir, ".m2-tmp")

			if (tmpMavenRepositoryDir.exists()) {
				maven {
					url tmpMavenRepositoryDir
				}
			}

			dir = dir.getParentFile();
		}

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "com.liferay.portal.tools.theme.builder"
apply plugin: "com.liferay.target.platform"

task runGradleTest

dependencies {
	parentThemes group: "com.liferay", name: "com.liferay.frontend.theme.styled"

	portalCommonCSS group: "com.liferay", name: "com.liferay.frontend.css.common"

	targetPlatformBoms group: "com.liferay.portal", name: "release.portal.bom", version: "7.1.3"
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

runGradleTest {
	doLast {
		ResolvedArtifact resolvedArtifact = configurations.parentThemes.resolvedConfiguration.resolvedArtifacts.find {
			it.name == "com.liferay.frontend.theme.styled"
		}

		assert resolvedArtifact.file.name.contains("3.0.16")

		resolvedArtifact = configurations.portalCommonCSS.resolvedConfiguration.resolvedArtifacts.find {
			it.name == "com.liferay.frontend.css.common"
		}

		assert resolvedArtifact.file.name.contains("3.0.1")
	}
}