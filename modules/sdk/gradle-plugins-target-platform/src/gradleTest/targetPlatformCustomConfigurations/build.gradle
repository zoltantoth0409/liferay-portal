buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.service.builder", version: "3.0.2"
	}

	repositories {
		File dir = rootProject.projectDir

		while (dir != null) {
			File tmpMavenRepositoryDir = new File(dir, ".m2-tmp")

			if (tmpMavenRepositoryDir.exists()) {
				maven {
					url tmpMavenRepositoryDir
				}
			}

			dir = dir.getParentFile();
		}

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "com.liferay.portal.tools.service.builder"
apply plugin: "com.liferay.target.platform"
apply plugin: "java"

task runGradleTest

dependencies {
	serviceBuilder group: "com.liferay", name: "com.liferay.portal.tools.service.builder", version: "1.0.316"

	targetPlatformBoms group: "com.liferay.portal", name: "release.portal.bom", version: "7.1.3"
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

runGradleTest {
	doLast {
		ResolvedArtifact qdoxResolvedArtifact = configurations.serviceBuilder.resolvedConfiguration.resolvedArtifacts.find {
			it.name.contains("qdox")
		}

		assert qdoxResolvedArtifact.file.name.contains("2.0")
	}
}