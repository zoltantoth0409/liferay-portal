buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins", version: "4.4.79"
	}

	repositories {
		File dir = rootProject.projectDir

		while (dir != null) {
			File tmpMavenRepositoryDir = new File(dir, ".m2-tmp")

			if (tmpMavenRepositoryDir.exists()) {
				maven {
					url tmpMavenRepositoryDir
				}
			}

			dir = dir.getParentFile();
		}

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "com.liferay.osgi.ext.plugin"
apply plugin: "com.liferay.target.platform"

task runGradleTest

dependencies {
	originalModule group: "com.liferay", name: "com.liferay.login.web"

	targetPlatformBoms group: "com.liferay.portal", name: "release.portal.bom", version: "7.1.3"
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

runGradleTest {
	doLast {
		ResolvedArtifact resolvedArtifact = configurations.originalModule.resolvedConfiguration.resolvedArtifacts.find {
			it.name == "com.liferay.login.web"
		}

		assert resolvedArtifact.file.name.contains("3.0.26")
	}
}