apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)

String gradleVersion = "5.6.4"

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.node", version: "7.2.1"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions gradleVersion
}

pluginBundle {
	plugins {
		appJSDocPlugin {
			description = "The App JSDoc Gradle plugin lets you generate JavaScript documentation as a single, combined HTML document for an application that spans different subprojects, each one representing a different component of the same application."
			displayName = "Liferay Gradle Plugins App JSDoc"
		}

		jSDocPlugin {
			displayName = "Liferay Gradle Plugins JSDoc"
		}
	}

	tags = ["docs", "documentation", "javascript", "js", "jsdoc", "node", "nodejs"]
}