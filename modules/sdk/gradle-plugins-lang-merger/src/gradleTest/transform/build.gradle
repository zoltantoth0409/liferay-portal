buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.lang.builder", version: "latest.release"
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

task runGradleTest

subprojects {
	apply plugin: "com.liferay.lang.builder"
	apply plugin: "java"

	buildLang {
		translateSubscriptionKey = project.properties["microsoft.translator.subscription.key"]
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

project(":sample-web") {
	apply plugin: "com.liferay.lang.merger"

	mergeLang {
		setting("../sample-foo/src/main/resources/content") {
			transformKeys = [
				"hello-foo": "hello-web",
				"hello-world": "hello-world"
			]
		}

		sourceDirs = [
			"../sample-foo/src/main/resources/content",
			"../sample-lang/src/main/resources/content",
			"src/main/resources/content"
		]
	}
}

runGradleTest {
	Project webProject = project(":sample-web")

	dependsOn webProject.tasks.classes

	doLast {
		FileTree languagePropertiesFileTree = fileTree(dir: webProject.sourceSets.main.output.resourcesDir, include: "content/Language*.properties")

		assert !languagePropertiesFileTree.empty

		languagePropertiesFileTree.each {
			File languagePropertiesFile ->

			Properties languageProperties = new Properties()

			languagePropertiesFile.withInputStream {
				languageProperties.load it
			}

			assert !languageProperties.getProperty("hello-foo")

			assert languageProperties.getProperty("hello-web")
			assert languageProperties.getProperty("hello-world")
			assert languageProperties.getProperty("sample-lang")
			assert languageProperties.getProperty("sample-web")
		}
	}
}