import com.liferay.gradle.plugins.defaults.tasks.ReplaceRegexTask

apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)
task updateReadme(type: ReplaceRegexTask)

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.31"

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi25], dir: new File(rootProject.buildDir, "gradle-2.5"))
	compileOnly group: "com.liferay", name: "com.liferay.source.formatter", version: "1.0.746"
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions "2.5", "2.6", "2.7", "2.8", "2.9", "2.10", "2.11", "2.12", "2.13", "2.14.1", "3.0", "3.1", "3.2.1", "3.3", "3.5", "3.5.1"
}

pluginBundle {
	tags = ["java", "style"]
}

updateReadme {
	match(/group: "com\.liferay", name: "com\.liferay\.gradle\.plugins\.source\.formatter", version: "(\d.+)"/, "README.markdown")

	onlyIf {
		!project.version.endsWith("-SNAPSHOT")
	}

	replacement = {
		project.version
	}
}

uploadArchives {
	dependsOn updateReadme
}