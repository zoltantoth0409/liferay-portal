import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

import eu.emundo.gradle.sevenz.UnSevenZ

buildscript {
	dependencies {
		classpath group: "gradle.plugin.eu.emundo", name: "7z-gradle-plugin", transitive: false, version: "1.0.1"
		classpath group: "org.apache.commons", name: "commons-compress", version: "1.12"
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "de.undercouch.download"
apply plugin: "eu.emundo.sevenz"
apply plugin: "org.ysb33r.gradletest"

task copyBundle(type: Copy)
task copyGradleTestDependencies(type: Copy)
task downloadBundle(type: Download)
task unzipBundle(type: UnSevenZ)

String bundleUrl = "https://releases-cdn.liferay.com/portal/snapshot-7.1.x/201810131240/liferay-portal-tomcat-7.1.x.7z"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/snapshot-7.1.x/201810131240/liferay-portal-tomcat-7.1.x.7z"
}

copyBundle {
	dependsOn unzipBundle

	eachFile new StripPathSegmentsAction(1)

	from {
		unzipBundle.outputDir
	}

	includeEmptyDirs = false
	into new File(buildDir, "bundle")
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.ant.bnd", version: "2.0.57"
	compile group: "com.liferay", name: "com.liferay.gogo.shell.client", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.alloy.taglib", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.css.builder", version: "2.2.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.db.support", version: "1.0.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.gulp", version: "2.0.34"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.jasper.jspc", version: "2.0.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.javadoc.formatter", version: "1.0.22"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.js.module.config.generator", version: "2.1.35"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.js.transpiler", version: "2.4.14"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.lang.builder", version: "3.0.2"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.service.builder", version: "2.1.58"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "2.3.277"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.soy", version: "3.1.6"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.test.integration", version: "2.3.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.tld.formatter", version: "1.0.7"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.tlddoc.builder", version: "1.3.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.upgrade.table.builder", version: "2.0.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.wsdd.builder", version: "1.0.11"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.wsdl.builder", version: "2.0.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.xml.formatter", version: "1.0.9"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.xsd.builder", version: "1.0.5"
	compile group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.31"

	compile(group: "org.dm.gradle", name: "gradle-bundle-plugin", version: "0.9.0") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bndlib"
	}

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))
}

downloadBundle {
	dest new File(buildDir, "bundle.7z")
	onlyIfModified true
	src bundleUrl
}

gradleTest {
	dependsOn copyBundle
	dependsOn copyGradleTestDependencies
	dependsOn jar
	dependsOn testClasses

	gradleArguments "--project-prop", "app.server.parent.dir=" + FileUtil.getAbsolutePath(copyBundle.destinationDir)

	versions "2.14.1", "3.0", "3.1", "3.2.1", "3.3"
}

unzipBundle {
	dependsOn downloadBundle

	outputDir = new File(buildDir, "tmp/bundle7z")
	sourceFile = downloadBundle.dest
}