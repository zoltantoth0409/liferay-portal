import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "org.ysb33r.gradletest"

task copyBundle(type: Copy)
task copyGradleTestDependencies(type: Copy)
task downloadBundle(type: Download)

String bundleUrl = "https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"
}

copyBundle {
	dependsOn downloadBundle
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(downloadBundle.dest)
	}

	includeEmptyDirs = false
	into new File(buildDir, "bundle")
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.ant.bnd", version: "2.0.45"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.alloy.taglib", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.css.builder", version: "2.2.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.db.support", version: "1.0.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.gulp", version: "2.0.25"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.jasper.jspc", version: "2.0.3"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.javadoc.formatter", version: "1.0.20"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.js.module.config.generator", version: "2.1.26"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.js.transpiler", version: "2.4.5"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.lang.builder", version: "2.1.8"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.service.builder", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "2.3.86"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.soy", version: "3.1.4"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.test.integration", version: "2.2.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.tld.formatter", version: "1.0.7"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.tlddoc.builder", version: "1.3.0"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.upgrade.table.builder", version: "2.0.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.wsdd.builder", version: "1.0.11"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.wsdl.builder", version: "2.0.1"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.xml.formatter", version: "1.0.9"
	compile group: "com.liferay", name: "com.liferay.gradle.plugins.xsd.builder", version: "1.0.5"
	compile group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.29"
	compile group: "org.dm.gradle", name: "gradle-bundle-plugin", version: "0.9.0"

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))
}

downloadBundle {
	dest new File(buildDir, "bundle.zip")
	onlyIfModified true
	src bundleUrl
}

gradleTest {
	dependsOn copyBundle
	dependsOn copyGradleTestDependencies
	dependsOn jar
	dependsOn testClasses

	gradleArguments "--project-prop", "app.server.parent.dir=" + FileUtil.getAbsolutePath(copyBundle.destinationDir)

	versions "2.14.1", "3.0", "3.1", "3.2.1", "3.3"
}